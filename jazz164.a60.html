<html><head><meta charset="utf-8"></head><body><pre><u>begin</u> <u>comment</u> JAZ164, R743, Outer Planets;
   <u>integer</u> k,t; <u>real</u> a,k2,x; <u>Boolean</u> fi;
   <u>array</u> y,ya,z,za[1:15],m[0:5],e[1:60],d[1:33];
   <u>array</u> ownd[1:5,1:5],ownr[1:5];
   <u>real</u> <u>procedure</u> f(k); <u>integer</u> k;
   <u>begin</u> <u>integer</u> i,j,i3,j3; <u>real</u> p;
      <u>if</u> k ≠ 1 <u>then</u> <u>goto</u> A;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         <u>begin</u> i3 ≔ 3×i;
            <u>for</u> j ≔ i+1 <u>step</u> 1 <u>until</u> 5 <u>do</u>
               <u>begin</u> j3 ≔ 3×j;
                  p ≔ (y[i3-2] - y[j3-2])⭡2 + (y[i3-1] - y[j3-1])⭡2 +
                    (y[i3] - y[j3])⭡2;
                  ownd[i,j] ≔ ownd[j,i] ≔ 1/p/sqrt(p)
               <u>end</u>
         <u>end</u> ;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> 5 <u>do</u>
         <u>begin</u> i3 ≔ 3×i; ownd[i,i] ≔ 0;
            p ≔ y[i3-2]⭡2 + y[i3-1]⭡2 + y[i3]⭡2;
            ownr[i] ≔ 1/p/sqrt(p)
         <u>end</u> ;
      A: i ≔ (k - 1) ÷ 3 + 1;
      f ≔ k2 × (- m[0] × y[k] × ownr[i] +
        SUM(j,1,5,m[j]×((y[3×(j-i)+k]-y[k])×ownd[i,j]-y[3×(j-i)+k]×ownr[j])))
   <u>end</u> f;
   <u>procedure</u> RK3n(x,a,b,y,ya,z,za,fxyj,j,e,d,fi,n);
      <u>value</u> b,fi,n; <u>integer</u> j,n; <u>real</u> x,a,b,fxyj;
        <u>Boolean</u> fi; <u>array</u> y,ya,z,za,e,d;
   <u>begin</u> <u>integer</u> jj;
      <u>real</u> xl,h,hmin,int,hl,absh,fhm,discry,discrz,toly,tolz,mu,mu1,fhy,fhz;
      <u>Boolean</u> last,first,reject;
      <u>array</u> yl,zl,k0,k1,k2,k3,k4,k5[1:n],ee[1:4×n];
      <u>if</u> fi
        <u>then</u> <u>begin</u> d[3] ≔ a;
         <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
            <u>begin</u> d[jj+3] ≔ ya[jj]; d[n+jj+3] ≔ za[jj] <u>end</u>
      <u>end</u> ;
      d[1] ≔ 0; xl ≔ d[3];
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
         <u>begin</u> yl[jj] ≔ d[jj+3]; zl[jj] ≔ d[n+jj+3] <u>end</u> ;
      <u>if</u> fi <u>then</u> d[2] ≔ b - d[3];
      absh ≔ h ≔ abs(d[2]);
      <u>if</u> b - xl &lt; 0 <u>then</u> h ≔ - h;
      int ≔ abs(b - xl); hmin ≔ int × e[1] + e[2];
      <u>for</u> jj ≔ 2 <u>step</u> 1 <u>until</u> 2×n <u>do</u>
         <u>begin</u> hl ≔ int × e[2×jj-1] + e[2×jj];
            <u>if</u> hl &lt; hmin <u>then</u> hmin ≔ hl
         <u>end</u> ;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> 4×n <u>do</u> ee[jj] ≔ e[jj]/int;
      first ≔ reject ≔ <u>true</u> ;
      <u>if</u> fi
        <u>then</u> <u>begin</u> last ≔ <u>true</u> ; <u>goto</u> step <u>end</u> ;
      test: absh ≔ abs(h);
      <u>if</u> absh &lt; hmin
        <u>then</u> <u>begin</u> h ≔ <u>if</u> h &gt; 0 <u>then</u> hmin <u>else</u> - hmin;
         absh ≔ hmin
      <u>end</u> ;
      <u>if</u> h &gt; b - xl ≡ h &gt; 0
        <u>then</u> <u>begin</u> d[2] ≔ h; last ≔ <u>true</u> ;
         h ≔ b - xl; absh ≔ abs(h)
      <u>end</u>
      <u>else</u> last ≔ <u>false</u> ;
      step: <u>if</u> reject
        <u>then</u> <u>begin</u> x ≔ xl;
         <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
              y[jj] ≔ yl[jj];
         <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
              k0[j] ≔ fxyj × h
      <u>end</u>
      <u>else</u> <u>begin</u> fhy ≔ h/hl;
         <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
              k0[jj] ≔ k5[jj] × fhy
      <u>end</u> ;
      x ≔ xl + ·27639 32022 50021 × h;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           y[jj] ≔ yl[jj] + (zl[jj] × ·27639 32022 50021 +
           k0[jj] × ·03819 66011 25011) × h;
      <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u> k1[j] ≔ fxyj × h;
      x ≔ xl + ·72360 67977 49979 × h;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           y[jj] ≔ yl[jj] + (zl[jj] × ·72360 67977 49979 +
           k1[jj] × ·26180 33988 74989) × h;
      <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u> k2[j] ≔ fxyj × h;
      x ≔ xl + h × ·5;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           y[jj] ≔ yl[jj] + (zl[jj] × ·5 +
           k0[jj] × ·04687 5 +
           k1[jj] × ·07982 41558 39840 -
           k2[jj] × ·00169 91558 39840) × h;
      <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u> k4[j] ≔ fxyj × h;
      x ≔ <u>if</u> last <u>then</u> b <u>else</u> xl + h;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           y[jj] ≔ yl[jj] + (zl[jj] +
           k0[jj] × ·30901 69943 74947 +
           k2[jj] × ·19098 30056 25053) × h;
      <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u> k3[j] ≔ fxyj × h;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           y[jj] ≔ yl[jj] + (zl[jj] +
           k0[jj] × ·08333 33333 33333 +
           k1[jj] × ·30150 28323 95825 +
           k2[jj] × ·11516 38342 70842) × h;
      <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u> k5[j] ≔ fxyj × h;
      reject ≔ <u>false</u> ; fhm ≔ 0;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
         <u>begin</u>
            discry ≔ abs((- k0[jj] × ·5 + k1[jj] × 1·80901 69943 74947 +
              k2[jj] × ·69098 30056 25053 - k4[jj] × 2) × h);
            discrz ≔ abs((k0[jj] - k3[jj]) × 2 - (k1[jj] + k2[jj]) × 10 +
              k4[jj] × 16 + k5[jj] × 4);
            toly ≔ absh × (abs(zl[jj]) × ee[2×jj-1] + ee[2×jj]);
            tolz ≔ abs(k0[jj]) × ee[2×(jj+n)-1] + absh × ee[2×(jj+n)];
            reject ≔ discry &gt; toly ∨ discrz &gt; tolz ∨ reject;
            fhy ≔ discry/toly; fhz ≔ discrz/tolz;
            <u>if</u> fhz &gt; fhy <u>then</u> fhy ≔ fhz;
            <u>if</u> fhy &gt; fhm <u>then</u> fhm ≔ fhy
         <u>end</u> ;
      mu ≔ 1/(1 + fhm) + ·45;
      <u>if</u> reject
        <u>then</u> <u>begin</u> <u>if</u> absh &lt; hmin
           <u>then</u> <u>begin</u> d[1] ≔ d[1] + 1;
            <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
               <u>begin</u> y[jj] ≔ yl[jj];
                  z[jj] ≔ zl[jj]
               <u>end</u> ;
            first ≔ <u>true</u> ; <u>goto</u> next
         <u>end</u> ;
         h ≔ mu × h; <u>goto</u> test
      <u>end</u> rej;
      <u>if</u> first
        <u>then</u> <u>begin</u> first ≔ <u>false</u> ; hl ≔ h; h ≔ mu × h;
         <u>goto</u> acc
      <u>end</u> ;
      fhy ≔ mu × h/hl + mu - mu1; hl ≔ h; h ≔ fhy × h;
      acc: mu1 ≔ mu;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           z[jj] ≔ zl[jj] + (k0[jj] + k3[jj]) × ·08333 33333 33333 +
           (k1[jj] + k2[jj]) × ·41666 66666 66667;
      next: <u>if</u> b ≠ x
        <u>then</u> <u>begin</u> xl ≔ x;
         <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
            <u>begin</u> yl[jj] ≔ y[jj]; zl[jj] ≔ z[jj] <u>end</u> ;
         <u>goto</u> test
      <u>end</u> ;
      <u>if</u> ¬ last <u>then</u> d[2] ≔ h;
      d[3] ≔ x;
      <u>for</u> jj ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
         <u>begin</u> d[jj+3] ≔ y[jj]; d[n+jj+3] ≔ z[jj] <u>end</u>
   <u>end</u> RK3n;
   <u>procedure</u> TYP(x); <u>array</u> x;
   <u>begin</u> <u>integer</u> k;
      NLCR; PRINTTEXT(“T = ”); ABSFIXT(7,1,t+a); NLCR; NLCR;
      <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> 5 <u>do</u>
         <u>begin</u> <u>if</u> k=1 <u>then</u> PRINTTEXT(“J ”) <u>else</u>
            <u>if</u> k=2 <u>then</u> PRINTTEXT(“S ”) <u>else</u>
            <u>if</u> k=3 <u>then</u> PRINTTEXT(“U ”) <u>else</u>
            <u>if</u> k=4 <u>then</u> PRINTTEXT(“N ”) <u>else</u>
              PRINTTEXT(“P ”);
            FIXT(2,9,x[3×k-2]); FIXT(2,9,x[3×k-1]); FIXT(2,9,x[3×k]);
            NLCR
         <u>end</u>
   <u>end</u> TYP;
   <u>real</u> <u>procedure</u> SUM(i,a,b,xi);
      <u>value</u> b; <u>integer</u> i,a,b; <u>real</u> xi;
   <u>begin</u> <u>real</u> s;
      s ≔ 0;
      <u>for</u> i ≔ a <u>step</u> 1 <u>until</u> b <u>do</u> s ≔ s + xi;
      SUM ≔ s
   <u>end</u> SUM;
   a ≔ read;
   <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> 15 <u>do</u>
      <u>begin</u> ya[k] ≔ read; za[k] ≔ read <u>end</u> ;
   <u>for</u> k ≔ 0 <u>step</u> 1 <u>until</u> 5 <u>do</u> m[k] ≔ read;
   k2 ≔ read; e[1] ≔ read;
   <u>for</u> k ≔ 2 <u>step</u> 1 <u>until</u> 60 <u>do</u> e[k] ≔ e[1];
   NLCR; PRINTTEXT(“JAZ164, R743, Outer Planets”); NLCR; NLCR;
   <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> 15 <u>do</u>
      <u>begin</u> FLOT(12,2,ya[k]); FLOT(12,2,za[k]); NLCR <u>end</u> ;
   <u>for</u> k ≔ 0 <u>step</u> 1 <u>until</u> 5 <u>do</u>
      <u>begin</u> NLCR; FLOT(12,2,m[k]) <u>end</u> ;
   NLCR; NLCR; FLOT(12,2,k2);
   NLCR; NLCR; PRINTTEXT(“eps = ”); FLOT(2,2,e[1]); NLCR;
   t ≔ 0; TYP(ya); fi ≔ <u>true</u> ;
   <u>for</u> t ≔ 500,1000 <u>do</u>
      <u>begin</u> RK3n(x,0,t,y,ya,z,za,f(k),k,e,d,fi,15);
         fi ≔ <u>false</u> ; TYP(y)
      <u>end</u>
<u>end</u>
</pre></body></html>
