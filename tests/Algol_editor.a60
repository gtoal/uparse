b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ ALGOL editor. The data tape must contain the
  following control numbers: 1. numofpr: the number of programs to
  be edited, 2, width: the maximal number of positions on a line,
  3. page: the number of lines per type area, 4. rest: the number
  of blank lines between two pages, 5. n: the upper index of the
  i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ pos (see 6.), 6. pos[1:n]: n numbers which
  regulate the indentations;
  i̲n̲t̲e̲g̲e̲r̲ symbol, i, s, ih, sh, breaki, breaks, tabstop, level,
  arlevel, stringlevel, line, comm, decl, lab, proc, pointer, a, b,
  c, h, k, w, zz, nop, numofpr, width, page, rest, n;
  b̲o̲o̲l̲e̲a̲n̲ booll, boolb, boolc;
  i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ buffer[1:2000], stock[1:40], proclevel[0:10];
  numofpr:= read; width:= read; page:= read; rest:= read; n:= read;
  b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ pos[0:n];
  
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ sym(n); v̲a̲l̲u̲e̲ n; i̲n̲t̲e̲g̲e̲r̲ n;
      b̲e̲g̲i̲n̲ PUSYM(n); PRSYM(n) e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ space(n); v̲a̲l̲u̲e̲ n; i̲n̲t̲e̲g̲e̲r̲ n;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ i;
         f̲o̲r̲ i:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ n d̲o̲ sym(93)
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ tabspace(n); v̲a̲l̲u̲e̲ n; i̲n̲t̲e̲g̲e̲r̲ n;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ p, q;
         p:= n ÷ 8; q:= n - p × 8;
         f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ p d̲o̲ sym(118); space(q)
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ punchline(border); v̲a̲l̲u̲e̲ border; i̲n̲t̲e̲g̲e̲r̲ border;
      b̲e̲g̲i̲n̲ tabspace(tabstop - (i̲f̲ boolb ∧ booll t̲h̲e̲n̲ (pos[b] + (i̲f̲
         level ≤ n t̲h̲e̲n̲ pos[b - 1] e̲l̲s̲e̲ pos[n])) e̲l̲s̲e̲ i̲f̲ booll ∨
         boolb t̲h̲e̲n̲ pos[b] e̲l̲s̲e̲ 0)); booll:= boolb:= f̲a̲l̲s̲e̲;
         f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ border d̲o̲ sym(buffer[a]); newline
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ newline;
      b̲e̲g̲i̲n̲ sym(119); line:= line + 1; i̲f̲ line ≥ page t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ RUNOUT;
            f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ rest d̲o̲ sym(119); line:= 0
         e̲n̲d̲
      e̲n̲d̲;
      
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ punchbuffer;
      b̲e̲g̲i̲n̲ punchline(i - 1); i:= 1; s:= tabstop; breaki:= breaks:= 0
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ restbuffer(n); v̲a̲l̲u̲e̲ n; i̲n̲t̲e̲g̲e̲r̲ n;
      b̲e̲g̲i̲n̲ i:= i - n - 1;
         f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ i d̲o̲ buffer[a] := buffer[n + a];
         i:= i + 1
      e̲n̲d̲;
      

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ label;
      b̲e̲g̲i̲n̲ i̲f̲ i < pos[b] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ a:= i s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ pos[b] d̲o̲ buffer[a] := 93;
            i:= pos[b] + 1
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ buffer[i] := 93; i:= i + 1 e̲n̲d̲;
         lab:= 0
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ breakbuffer;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ n;
         i̲f̲ breaki > 0 ∧ zz = 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ punchline(breaki); restbuffer(breaki);
            s:= s - breaks + tabstop; i̲f̲ lab = 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ label; s:= i + tabstop e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ a:= i - 2 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲ i̲f̲ buffer[a] = 93
            t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ n:= a; g̲o̲t̲o̲ break1 e̲n̲d̲;
            g̲o̲t̲o̲ break2;
         break1: punchline(n); restbuffer(n);
         break2: i̲f̲ lab = 1 t̲h̲e̲n̲ label; s:= i + tabstop;
            i̲f̲ s ≥ width t̲h̲e̲n̲ emergency
         e̲n̲d̲;
         breaki:= breaks:= 0
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ stockbuffer(spacesbefore, spacesafter, c);
      v̲a̲l̲u̲e̲ spacesbefore, spacesafter, c;
      i̲n̲t̲e̲g̲e̲r̲ spacesbefore, spacesafter, c;
      b̲e̲g̲i̲n̲ f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ spacesbefore d̲o̲ buffer[i + a -
         1]:=93; i:= i + spacesbefore; s:= s + spacesbefore;
         f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ c d̲o̲ buffer[i + a - 1] := stock[a];
         i:= i + c; s:= s + c ÷ 2;
         f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ spacesafter d̲o̲ buffer[i + a - 1]:= 93;
         i:= i + spacesafter; s:= s + spacesafter;
         i̲f̲ comm = 0 t̲h̲e̲n̲ g̲o̲t̲o̲ start1
      e̲n̲d̲;


      p̲r̲o̲c̲e̲d̲u̲r̲e̲ semicolon;
      b̲e̲g̲i̲n̲ buffer[i] := 91; i:= i + 1; zz:= 0; punchbuffer;
         i̲f̲ level = proclevel[pointer] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ RUNOUT; newline; pointer:= pointer - 1;
            proc:= i̲f̲ pointer = 0 t̲h̲e̲n̲ - 1 e̲l̲s̲e̲ 1
         e̲n̲d̲;
         g̲o̲t̲o̲ start
      e̲n̲d̲;
      
      i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ undsym;
      b̲e̲g̲i̲n̲
      undl: symbol:= RESYM; i̲f̲ symbol = 126 t̲h̲e̲n̲ g̲o̲t̲o̲ undl;
         undsym:= symbol
      e̲n̲d̲;
      
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ emergency;
      b̲e̲g̲i̲n̲ ih:= i; sh:= s;
         f̲o̲r̲ a:= ih s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
         b̲e̲g̲i̲n̲ ih:= ih - 1; sh:= sh - 1;
            i̲f̲ buffer[a] = 87 ∧ sh < width t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ punchline(ih + 1); restbuffer(ih + 1);
               s:= i + tabstop; breaki:= breaks:= 0; g̲o̲t̲o̲ endem
            e̲n̲d̲;
            k:= a
         e̲n̲d̲;
         i̲f̲ k = 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ sym(119); PRINTTEXT(
{
the chosen indentations are too large with respect to
the maximal number of positions on the line:}
            ); EXIT
         e̲n̲d̲;
      endem:
      e̲n̲d̲;

  begin of program: f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ n d̲o̲ pos[a] := read;
      nop:= 0;
  repeat: i:= s:= line:= 1; booll:= boolb:= boolc:= f̲a̲l̲s̲e̲;
      breaki:= breaks:= tabstop:= level:= arlevel:= stringlevel:=
      comm:= decl:= lab:= b:= pointer:= zz:= proclevel[0]:=
      pos[0]:= 0; proc:= - 1; RUNOUT; sym(119);
  start: symbol:= RESYM;
      i̲f̲ i > 1 ∧ (symbol = 93 ∨ symbol = 119) t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ buffer[i - 1] > 63 t̲h̲e̲n̲ g̲o̲t̲o̲ start e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ symbol:= RESYM w̲h̲i̲l̲e̲ symbol = 93 ∨ symbol = 119
            d̲o̲ ; i̲f̲ symbol < 63 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ buffer[i]:= 93; buffer[i + 1]:= symbol; i:= i + 2;
               s:= s + 2; g̲o̲t̲o̲ start
            e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲;
  start1: i̲f̲ symbol = 93 ∨ symbol = 118 ∨ symbol = 119 t̲h̲e̲n̲ g̲o̲t̲o̲
      start; i̲f̲ s ≥ width t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ breakbuffer; g̲o̲t̲o̲ start1 e̲n̲d̲;
      i̲f̲ symbol < 64 ∨ symbol = 76 ∨ symbol = 88 ∨ symbol = 89 ∨
      symbol= 98 ∨ symbol = 99 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ digits, letters, ¬, ., ⏨, (, );
         buffer[i]:= symbol; i:= i + 1; s:= s + 1; g̲o̲t̲o̲ start
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ symbol = 64 ∨ symbol = 65 ∨ symbol = 66 ∨ symbol = 67
      ∨ symbol = 70 ∨ symbol = 72 ∨ symbol = 74 ∨ symbol = 79 ∨
      symbol = 80 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ +, -, ×, /, =, <, >, ∧, ∨;
         i̲f̲ buffer[i - 1] ≠ 93 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ buffer[i]:= 93; i:= i + 1; s:= s + 1 e̲n̲d̲;
         buffer[i]:= symbol; buffer[i + 1]:= 93; i:= i + 2; s:= s + 2;
         g̲o̲t̲o̲ start
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ symbol = 100 ∨ symbol = 101 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ [, ];
         buffer[i]:= symbol; i:= i + 1; s:= s + 1;
         arlevel:= (i̲f̲ symbol = 100 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ - 1) + arlevel;
         g̲o̲t̲o̲ start
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ symbol = 87 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ ,;
         buffer[i]:= symbol; i̲f̲ arlevel = 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ buffer[i + 1]:= 93; i:= i + 2; s:= s + 2 e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i:= i + 1; s:= s + 1 e̲n̲d̲;
         g̲o̲t̲o̲ start
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ symbol = 91 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ semicolon;
         buffer[i]:= symbol; buffer[i + 1]:= 93; i̲f̲ s < width t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ breaki:= i + 1; breaks:= s + 1 e̲n̲d̲;
         i:= i + 2; s:= s + 2; i̲f̲ decl = 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ proc ≠ 0 t̲h̲e̲n̲ punchbuffer; decl:= 0; g̲o̲t̲o̲ start
         e̲n̲d̲;
         i̲f̲ proc = 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ punchbuffer; RUNOUT; newline;
            proc:= i̲f̲ pointer = 0 t̲h̲e̲n̲ - 1 e̲l̲s̲e̲ 1
         e̲n̲d̲;
         g̲o̲t̲o̲ start
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ symbol = 90 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ colon;
         i̲f̲ arlevel ≠ 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ buffer[i]:= 90; i:= i + 1; s:= s + 1; g̲o̲t̲o̲ start e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ symbol:= RESYM; i̲f̲ symbol = 70 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ buffer[i]:= 90; buffer[i + 1]:= 70;
               buffer[i + 2]:= 93; i:= i + 3; s:= s + 3; g̲o̲t̲o̲ start
            e̲n̲d̲
            e̲l̲s̲e̲
         pardel: i̲f̲ symbol = 93 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ symbol:= RESYM; g̲o̲t̲o̲ pardel e̲n̲d̲;
            i̲f̲ symbol = 98 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ buffer[i]:= 90; buffer[i + 1]:= 93;
               buffer[i + 2]:= 98; i:= i + 3; s:= s + 3; g̲o̲t̲o̲ start
            e̲n̲d̲
            e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ buffer[i]:= 90; i:= i + 1; i̲f̲ level = 0 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ punchbuffer; g̲o̲t̲o̲ start1 e̲n̲d̲;
               lab:= 1;
               f̲o̲r̲ a:= i - 2 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲ i̲f̲ buffer[a] = 126
               ∨ buffer[a] = 91 ∨ buffer[a] = 90 t̲h̲e̲n̲ g̲o̲t̲o̲ labinst;
               label; booll:= t̲r̲u̲e̲; g̲o̲t̲o̲ start1;
            labinst: i̲f̲ buffer[a] = 126 t̲h̲e̲n̲ breaki:= a + 2 e̲l̲s̲e̲
               i̲f̲ buffer[a] = 90 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ a < pos[b] t̲h̲e̲n̲ breaki:= pos[b] e̲l̲s̲e̲
                  breaki:= a + 1
               e̲n̲d̲;
               breakbuffer; booll:= t̲r̲u̲e̲; g̲o̲t̲o̲ start1
            e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ symbol = 127 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ |;
      bar: symbol:= RESYM; i̲f̲ symbol = 127 t̲h̲e̲n̲ g̲o̲t̲o̲ bar;
         i̲f̲ symbol = 80 ∨ symbol = 70 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ ⭡ ≠ ;
            buffer[i]:= 93; buffer[i + 1]:= 127;
            buffer[i + 2]:= symbol; buffer[i + 3]:= 93; i:= i + 4;
            s:= s + 3; g̲o̲t̲o̲ start
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ symbol = 72 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ {;
            buffer[i]:= 127; buffer[i + 1]:= 72; breaki:= i - 1;
            breaks:= s - 1; i:= i + 2; s:= s + 1;
         string: symbol:= RESYM; buffer[i]:= symbol;
            i̲f̲ symbol ≠ 127 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ i:= i + 1; i̲f̲ symbol ≠ 126 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ symbol = 118 t̲h̲e̲n̲ s:= s + 8 e̲l̲s̲e̲ s:= s + 1;
                  i̲f̲ symbol = 119 t̲h̲e̲n̲ line:= line+ 1
               e̲n̲d̲;
               i̲f̲ s > width - 4 ∧ breaks > 0 t̲h̲e̲n̲ breakbuffer;
               g̲o̲t̲o̲ string
            e̲n̲d̲
            e̲l̲s̲e̲
            b̲e̲g̲i̲n̲
            bar1: symbol:= RESYM; i̲f̲ symbol = 127 t̲h̲e̲n̲ g̲o̲t̲o̲ bar1;
               i̲f̲ symbol = 74 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ };
                  buffer[i + 1]:= symbol; i:= i + 2; s:= s + 1;
                  i̲f̲ stringlevel ≠ 0 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ stringlevel:= stringlevel - 1; g̲o̲t̲o̲ string
                  e̲n̲d̲
                  e̲l̲s̲e̲ i̲f̲ s > width - 3 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ f̲o̲r̲ a:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ i - 1 d̲o̲
                     sym(buffer[a]); newline; i:= 1; s:= tabstop
                  e̲n̲d̲;
               g̲o̲t̲o̲ start
            e̲n̲d̲
            e̲l̲s̲e̲ i̲f̲ symbol = 72 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ nested stringquotes;
               stringlevel:= stringlevel + 1;
               buffer[i + 1]:= symbol; i:= i + 2; s:= s + 1;
               g̲o̲t̲o̲ string
            e̲n̲d̲
            e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ buffer[i + 1]:= symbol; i:= i + 2; s:= s + 1;
               g̲o̲t̲o̲ string
            e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ buffer[i]:= 127; i:= i + 1; g̲o̲t̲o̲ start1 e̲n̲d̲
   e̲n̲d̲
   e̲l̲s̲e̲ i̲f̲ symbol = 126 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ _; 
      symbol:= undsym;
      i̲f̲ symbol = 70 ∨ symbol = 72 ∨ symbol = 74 ∨ symbol= 76 ∨
      symbol = 90 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ ≡, ≤, ≥, ⊃, ÷ ;
         buffer[i]:= 93; buffer[i + 1]:= 126;
         buffer[i + 2]:= symbol; buffer[i + 3]:= 93; i:= i + 4;
         s:= s + 3; g̲o̲t̲o̲ start
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ stock[1]:= 126; stock[2]:= symbol; stock[3]:= RESYM;
         f̲o̲r̲ a:= 4 s̲t̲e̲p̲ 2 u̲n̲t̲i̲l̲ 18 d̲o̲
         b̲e̲g̲i̲n̲ stock[a] := undsym; symbol:= stock[a + 1]:= RESYM;
           i̲f̲ symbol ≠ 126 t̲h̲e̲n̲
           b̲e̲g̲i̲n̲ k:= a; g̲o̲t̲o̲ again e̲n̲d̲
         e̲n̲d̲;
      again: i̲f̲ (stock[2] = 15 ∧ stock[4] = 10) ∨ (stock[2] = 29
         ∧ stock[4] = 27) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ f̲a̲l̲s̲e̲, t̲r̲u̲e̲;
            stockbuffer(0, 0, k)
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ stock[2] = 11 ∧ stock[4] = 14 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ begin;
            i̲f̲ i > pos[b] t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ a:= i - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ i - pos[b] d̲o̲ i̲f̲
               buffer[a] ≠ 93 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ c:= a;
                  i̲f̲ buffer[c] = 90 ∧ c < pos[b] t̲h̲e̲n̲ g̲o̲t̲o̲ labeg
                  e̲l̲s̲e̲ g̲o̲t̲o̲ pun
               e̲n̲d̲
            e̲n̲d̲;
         pun: i̲f̲ i ≠ 1 t̲h̲e̲n̲ punchbuffer;
         labeg: comm:= 1; stockbuffer(0, 1, k); comm:= 0;
            i̲f̲ proc = 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ pointer:= pointer + 1;
               proclevel[pointer]:= level; proc:= 1
            e̲n̲d̲;
            level:= level + 1; b:= i̲f̲ level < n t̲h̲e̲n̲ level e̲l̲s̲e̲ n;
            boolb:= t̲r̲u̲e̲; tabstop:=tabstop + pos[b];
            w:= pos[b] - 6; i̲f̲ w > 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ a:= 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ w - 1 d̲o̲ buffer[i + a]:=
               93; i:= i + w; s:= s + w
            e̲n̲d̲;
            g̲o̲t̲o̲ start1
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ stock[2] = 13 ∨ (stock[2] = 28 ∧ stock[4] = 29 ∧
         stock[6] = 14) ∨ stock[2] = 32 ∨ stock[2] = 29 ∨
         stock[2] = 30 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ do, step, while, then, until;
            stockbuffer(1, 1, k)
         e̲n̲d̲
         e̲l̲s̲e̲
      e: i̲f̲ stock[2] = 14 ∧ stock[4] = 21 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ else;
         i̲f̲ zz = 0 t̲h̲e̲n̲ stockbuffer(1, 1, k) e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ zz:= 0; stockbuffer(0, 1, k) e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ stock[2] = 14 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ end;
            i̲f̲ buffer[1] = 126 ∧ buffer[2] = 11 ∧ buffer[4] = 14
            ∧ s < width - 5 ∧ zz = 0 t̲h̲e̲n̲ zz:= 1 e̲l̲s̲e̲ zz:= 0;
            i̲f̲ zz = 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ boolb := f̲a̲l̲s̲e̲; g̲o̲t̲o̲ goon e̲n̲d̲;
            i̲f̲ i ≠ 1 t̲h̲e̲n̲ punchbuffer;
         goon: comm:= 1;
            i̲f̲ zz = 0 t̲h̲e̲n̲ stockbuffer(0, 0, k) e̲l̲s̲e̲
            stockbuffer(1, 0, k); comm:= 0;
            tabstop:= tabstop - pos[b]; level:= level - 1;
            i̲f̲ level = 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ punchbuffer; sym(119); RUNOUT; nop:= nop + 1;
               i̲f̲ nop = numofpr t̲h̲e̲n̲ g̲o̲t̲o̲ end e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ k:= page + rest - 1;
               f̲o̲r̲ a:= line s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ k d̲o̲ sym(119);
               g̲o̲t̲o̲ repeat
            e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲ b:= i̲f̲ level < n t̲h̲e̲n̲ level e̲l̲s̲e̲ n;
         i̲f̲ symbol = 93 ∨ symbol = 118 ∨ symbol = 119 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲
         reject: symbol:= RESYM;
            i̲f̲ symbol = 93 ∨ symbol = 118 ∨ symbol = 119 t̲h̲e̲n̲
            g̲o̲t̲o̲ reject e̲l̲s̲e̲ i̲f̲ symbol = 126 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ stock[1]:= 126;
               f̲o̲r̲ a:= 2 s̲t̲e̲p̲ 2 u̲n̲t̲i̲l̲ 18 d̲o̲
               b̲e̲g̲i̲n̲ stock[a]:= undsym;
                  symbol:= stock[a + 1]:= RESYM;
                  i̲f̲ symbol ≠ 126 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ k:= a; g̲o̲t̲o̲ nextund e̲n̲d̲
               e̲n̲d̲;
            nextund: i̲f̲ stock[2] = 14 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ i ≠ 1 t̲h̲e̲n̲ punchbuffer; zz:= 1; g̲o̲t̲o̲ e
               e̲n̲d̲
               e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ i̲f̲ zz = 1 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ breakbuffer; zz:= 0 e̲n̲d̲;
                  buffer[i]:= 93; i:= i + 1; s:= s + 1;
                  boolc:= t̲r̲u̲e̲; g̲o̲t̲o̲ comm1
               e̲n̲d̲
            e̲n̲d̲
            e̲l̲s̲e̲ i̲f̲ symbol = 91 t̲h̲e̲n̲ semicolon e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ i̲f̲ zz = 1 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ breakbuffer; zz:= 0 e̲n̲d̲;
               buffer[i]:= 93; buffer[i + 1]:= symbol;
               i:= i + 2; s:= s + 2; boolc:= t̲r̲u̲e̲; g̲o̲t̲o̲ comm2
            e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ symbol = 91 t̲h̲e̲n̲ semicolon e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i̲f̲ zz = 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ breakbuffer; zz:= 0 e̲n̲d̲;
            buffer[i]:= symbol; i:= i + 1; s:= s + 1;
            g̲o̲t̲o̲ comm2
         e̲n̲d̲
      e̲n̲d̲
      e̲l̲s̲e̲ i̲f̲ stock[2] = 12 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ comment;
      comm1: comm:= 1; stockbuffer(0, 1, k); comm:= 0;
      nospace: i̲f̲ symbol = 93 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ symbol:= RESYM; g̲o̲t̲o̲ nospace e̲n̲d̲;
         buffer[i]:= symbol; i:= i + 1; s:= s + 1;
      comm2: symbol:= RESYM;
         i̲f̲ symbol ≠ 118 ∧ symbol ≠ 119 ∧ symbol ≠ 93 t̲h̲e̲n̲
         buffer[i]:= symbol e̲l̲s̲e̲ i̲f̲ buffer[i - 1] ≠ 93 t̲h̲e̲n̲
         buffer[i]:= 93 e̲l̲s̲e̲ g̲o̲t̲o̲ comm2; i̲f̲ symbol ≠ 91 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i:= i + 1;
            i̲f̲ symbol ≠ 126 t̲h̲e̲n̲ s:= s + 1 e̲l̲s̲e̲ i̲f̲ boolc t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ buffer[i]:= symbol:= undsym; i:= i + 1;
               s:= s + 1; i̲f̲ symbol ≠ 14 t̲h̲e̲n̲ g̲o̲t̲o̲ comm2 e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ stock[1]:= 126; stock[2]:= 14;
                  symbol:= stock[3]:= RESYM;
                  i̲f̲ symbol = 126 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ f̲o̲r̲ a:= 4 s̲t̲e̲p̲ 2 u̲n̲t̲i̲l̲ 10 d̲o̲
                     b̲e̲g̲i̲n̲ stock[a]:= undsym;
                        symbol:= stock[a + 1]:= RESYM;
                        i̲f̲ symbol ≠ 126 t̲h̲e̲n̲
                        b̲e̲g̲i̲n̲ k:= a; i:= i - 3; s:= s - 2;
                        i̲f̲ stock[4] = 21 ∨ stock[4] = 23
                        t̲h̲e̲n̲
                        b̲e̲g̲i̲n̲ i:= i + 1; punchbuffer;
                           boolc:= f̲a̲l̲s̲e̲;
                           i̲f̲ stock[4] = 21 t̲h̲e̲n̲ zz:= 1;
                           g̲o̲t̲o̲ e
                        e̲n̲d̲
                        e̲l̲s̲e̲
                        b̲e̲g̲i̲n̲ comm:= 1; stockbuffer(0, 0, k);
                           comm:= 0; g̲o̲t̲o̲ comm2
                        e̲n̲d̲
                     e̲n̲d̲
                  e̲n̲d̲
               e̲n̲d̲
               e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ comm:= 1; stockbuffer(0, 0,2);
                  comm:= 0; buffer[i] := symbol; g̲o̲t̲o̲ comm2
               e̲n̲d̲
            e̲n̲d̲
         e̲n̲d̲;
         i̲f̲ s > width t̲h̲e̲n̲ breakbuffer; g̲o̲t̲o̲ comm2
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ i:= i + 1; punchbuffer;
         i̲f̲ level = proclevel[pointer] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ RUNOUT; newline; pointer:= pointer - 1;
            proc:= i̲f̲ pointer = 0 t̲h̲e̲n̲ - 1 e̲l̲s̲e̲ 1
         e̲n̲d̲;
         boolc:= f̲a̲l̲s̲e̲; g̲o̲t̲o̲ start
      e̲n̲d̲
   e̲n̲d̲
   e̲l̲s̲e̲ i̲f̲ stock[2] = 27 ∨ (stock[2] = 18 ∧ stock[4] = 23)
   ∨ stock[2] = 11 ∨ stock[2] = 38 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ real, integer, boolean, Boolean;
      i̲f̲ stock[k +1] = 93 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ a:= k + 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 100 d̲o̲
         b̲e̲g̲i̲n̲ symbol:= RESYM; i̲f̲ symbol ≠ 93 t̲h̲e̲n̲ g̲o̲t̲o̲ dec
         e̲n̲d̲;
      dec: i̲f̲ symbol = 126 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ stock[k + 2]:= 126;
            f̲o̲r̲ a:= k + 4 s̲t̲e̲p̲ 2 u̲n̲t̲i̲l̲ k + 20 d̲o̲
            b̲e̲g̲i̲n̲ stock[a - 1] := undsym;
               symbol:= stock[a]:= RESYM;
               i̲f̲ symbol ≠ 126 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ h:= a; g̲o̲t̲o̲ arproc e̲n̲d̲
            e̲n̲d̲;
         arproc: i̲f̲ stock[k + 3] = 10 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ <type> array;
               decl:= 1; stock[h]:= 93; stockbuffer(0, 0, h)
            e̲n̲d̲
            e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ <type> procedure;
               i̲f̲ i ≠ 1 t̲h̲e̲n̲ punchbuffer; i̲f̲ proc ≠ 0 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ RUNOUT; newline; proc:= 0 e̲n̲d̲;
               decl:= 1; stock[h]:= 93; stockbuffer(0, 0, h)
            e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ stock[k + 2]:= symbol;
            symbol:= stock[k + 3]:= RESYM; decl:= 1;
            stockbuffer(0, 0, k + 2)
         e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ decl:= 1; stockbuffer(0, 1, k) e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ stock[2] = 25 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ procedure;
            i̲f̲ proc ≠ 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ i̲f̲ i ≠ 1 t̲h̲e̲n̲ punchbuffer; RUNOUT; newline;
               proc:= 0
            e̲n̲d̲;
            decl:= 1; stockbuffer(0, 1, k)
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ stock[2] = 15 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ for;
            i̲f̲ i > pos[b] t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ a:= i - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ i - pos[b] d̲o̲ i̲f̲
               buffer[a] ≠ 93 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ c:= a; g̲o̲t̲o̲ non12 e̲n̲d̲;
            non12: i̲f̲ c ≥ 10 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ (buffer[c] = 23 ∧ buffer[c - 1] = 126 ∧
                  buffer[c - 2] = 18) ∨ buffer[c] = 90 t̲h̲e̲n̲ g̲o̲t̲o̲
                  lafor
               e̲n̲d̲;
               i̲f̲ buffer[c] = 90 t̲h̲e̲n̲ g̲o̲t̲o̲ lafor
            e̲n̲d̲;
            i̲f̲ i ≠ 1 t̲h̲e̲n̲ punchbuffer;
         lafor: stockbuffer(0, 1, k)
         e̲n̲d̲
         e̲l̲s̲e̲ i̲f̲ stock[2] = 24 ∨ (stock[2] = 28 ∧ stock[4] = 29)
         ∨ stock[2] = 10 ∨ stock[2] = 28 ∨ stock[2] = 21 ∨
         stock[2] = 31 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ own, string, array, switch, label, value;
            decl:= 1; stockbuffer(0, 1, k)
         e̲n̲d̲
         e̲l̲s̲e̲ stockbuffer(0, 1, k)
       e̲n̲d̲
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ buffer[i] := symbol; i:= i + 1; s:= s + 1; g̲o̲t̲o̲ start e̲n̲d̲;
   end: STOPCODE
   e̲n̲d̲
e̲n̲d̲
                                                                                              
