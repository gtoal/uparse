b̲e̲g̲i̲n̲
	c̲o̲m̲m̲e̲n̲t̲
		CALCULATION OF THE PRIME NUMBERS BETWEEN 2 AND 200
	;

	i̲n̲t̲e̲g̲e̲r̲ NN;

	NN := 200;

	b̲e̲g̲i̲n̲
		c̲o̲m̲m̲e̲n̲t̲ FIRST ALGORITHM (CHECK DIVISION IN A LOOP) ;

		b̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ISPRIME (N);
		v̲a̲l̲u̲e̲ N;
		i̲n̲t̲e̲g̲e̲r̲ N;
		b̲e̲g̲i̲n̲
			b̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EVEN (N);
			v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
				EVEN := ENTIER (N / 2) * 2 = N;
			i̲n̲t̲e̲g̲e̲r̲ I;

			ISPRIME := f̲a̲l̲s̲e̲;
			i̲f̲ EVEN (N) a̲n̲d̲ N n̲o̲t̲e̲q̲u̲a̲l̲ 2 t̲h̲e̲n̲
				g̲o̲t̲o̲ RET;

			f̲o̲r̲ I := 3 s̲t̲e̲p̲ 2 u̲n̲t̲i̲l̲ N d̲i̲v̲ 2 d̲o̲
				i̲f̲ ENTIER (N / I) * I = N t̲h̲e̲n̲
					g̲o̲t̲o̲ RET;
			ISPRIME := t̲r̲u̲e̲;
		    RET:

		e̲n̲d̲;

		i̲n̲t̲e̲g̲e̲r̲ I;

		VPRINT ("FIRST:");

		f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NN d̲o̲
			i̲f̲ ISPRIME (I) t̲h̲e̲n̲
				OUTINTEGER (1, I);
		VPRINT ("\NDONE.")
	e̲n̲d̲;

	b̲e̲g̲i̲n̲
		c̲o̲m̲m̲e̲n̲t̲ SECOND ALGORITHM (SIEVE) ;


		b̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ ARR [2 : NN];
		i̲n̲t̲e̲g̲e̲r̲ I, J;

		VPRINT ("SECOND:");

		f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NN d̲o̲
			ARR [I] := t̲r̲u̲e̲;

		f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NN d̲i̲v̲ 2 d̲o̲
			f̲o̲r̲ J := 2 * I s̲t̲e̲p̲ I u̲n̲t̲i̲l̲ NN d̲o̲
				ARR [J] := f̲a̲l̲s̲e̲;
		
		f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NN d̲o̲
			i̲f̲ ARR [I] t̲h̲e̲n̲
				OUTINTEGER (1, I);
		VPRINT ("\NDONE.")

	e̲n̲d̲
e̲n̲d̲		

