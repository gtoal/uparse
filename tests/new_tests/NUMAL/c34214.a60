c̲o̲d̲e̲ 34214;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ RNK1MIN(N, X, G, H, FUNCT, IN, OUT);
   v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ X, G, H, IN, OUT;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, IT, N2, CNTL, CNTE, EVL, EVLMAX;
   b̲o̲o̲l̲e̲a̲n̲ OK;
   r̲e̲a̲l̲ F, F0, FMIN, MU, DG, DG0, GHG, GS, NRMDELTA, ALFA,
     MACHEPS, RELTOL, ABSTOL, EPS, TOLG, ORTH, AID;
   a̲r̲r̲a̲y̲ V, DELTA, GAMMA, S, P[1:N];

   MACHEPS:= IN[0]; RELTOL:= IN[1]; ABSTOL:= IN[2];
   MU:= IN[3]; TOLG:= IN[4]; FMIN:= IN[5]; IT := 0;
   ALFA:= IN[6]; EVLMAX:= IN[7]; ORTH:= IN[8];
   N2:= N × (N + 1) ÷ 2; CNTL:= CNTE:= 0; i̲f̲ ALFA > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ INIVEC(1, N2, H, 0); INISYMD(1, N, 0, H, ALFA) e̲n̲d̲;
   F:= FUNCT(N, X, G); EVL:= 1; DG:= SQRT(VECVEC(1, N, 0, G, G));
   f̲o̲r̲ I:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        DELTA[I]:= - SYMMATVEC(1, N, I, H, G);
   NRMDELTA:= SQRT(VECVEC(1, N, 0, DELTA, DELTA));
   DG0:= VECVEC(1, N, 0, DELTA, G); OK:= DG0 < 0;
   EPS:= SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL;
   f̲o̲r̲ IT:= IT + 1  w̲h̲i̲l̲e̲
     (NRMDELTA > EPS ∨ DG > TOLG ∨ ¬ OK) ∧ EVL < EVLMAX
     d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ ¬ OK t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ VEC[1:N,1:N], TH[1:N2], EM[0:9];
            EM[0]:= MACHEPS; EM[2]:= AID:= SQRT(MACHEPS × RELTOL);
            EM[4]:= ORTH; EM[6]:= AID × N; EM[8]:= 5;
            CNTE:= CNTE + 1; DUPVEC(1, N2, 0, TH, H);
            EIGSYM1(TH,N,N,V,VEC,EM);
            f̲o̲r̲ I:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ AID:= - TAMVEC(1, N, I, VEC, G);
                  S[I]:= AID × ABS(V[I]); V[I]:= AID × SIGN(V[I])
               e̲n̲d̲;
            f̲o̲r̲ I:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ DELTA[I]:= MATVEC(1, N, I, VEC, S);
                  P[I]:= MATVEC(1, N, I, VEC, V)
               e̲n̲d̲;
            DG0:= VECVEC(1, N, 0, DELTA, G);
            NRMDELTA:= SQRT(VECVEC(1, N, 0, DELTA, DELTA))
         e̲n̲d̲ CALCULATING GREENSTADTS DIRECTION;
         DUPVEC(1, N, 0, S, X); DUPVEC(1, N, 0, V, G);
         i̲f̲ IT > N t̲h̲e̲n̲ ALFA:= 1 e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i̲f̲ IT ≠ 1 t̲h̲e̲n̲ ALFA:= ALFA / NRMDELTA e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ ALFA:= 2 × (FMIN - F) / DG0;
               i̲f̲ ALFA > 1 t̲h̲e̲n̲ ALFA:= 1
            e̲n̲d̲
         e̲n̲d̲;
         ELMVEC(1, N, 0, X, DELTA, ALFA);
         F0:= F; F:= FUNCT(N, X, G); EVL:= EVL +1 ;
         DG:= VECVEC(1, N, 0, DELTA, G);
         i̲f̲ IT = 1 ∨ F0 - F < -MU × DG0 × ALFA t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ I:= EVLMAX - EVL; CNTL:= CNTL +1 ;
            LINEMIN(N, S, DELTA, NRMDELTA, ALFA, G, FUNCT, F0, F,
              DG0, DG, I, f̲a̲l̲s̲e̲, IN); EVL:= EVL + I;
            DUPVEC(1, N, 0, X, S);
         e̲n̲d̲ LINEMINIMIZATION;
         DUPVEC(1, N, 0, GAMMA, G); ELMVEC(1, N, 0, GAMMA, V, -1);
         i̲f̲ ¬ OK t̲h̲e̲n̲ MULVEC(1, N, 0, V, P, -1);
         DG:= DG - DG0; i̲f̲ ALFA ≠ 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MULVEC(1, N, 0, DELTA, DELTA, ALFA);
            MULVEC(1, N, 0, V, V, ALFA);
            NRMDELTA:= NRMDELTA × ALFA; DG:= DG × ALFA
         e̲n̲d̲;
         DUPVEC(1, N, 0, P, GAMMA); ELMVEC(1, N, 0, P, V, 1);
         f̲o̲r̲ I:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              V[I]:= SYMMATVEC(1, N, I, H, GAMMA);
         DUPVEC(1, N, 0, S, DELTA); ELMVEC(1, N, 0, S, V, -1);
         GS:= VECVEC(1, N, 0, GAMMA, S);
         GHG:= VECVEC(1, N, 0, V, GAMMA);
         AID:= DG / GS;
         i̲f̲ VECVEC(1, N, 0, DELTA, P) ⭡ 2 > VECVEC(1, N, 0, P, P)× (ORTH × NRMDELTA) ⭡ 2 t̲h̲e̲n̲ RNK1UPD(H, N, S, 1 / GS)
           e̲l̲s̲e̲ i̲f̲ AID ≥ 0 t̲h̲e̲n̲
           FLEUPD(H, N, DELTA, V, 1 / DG, (1 + GHG / DG) / DG) e̲l̲s̲e̲
           DAVUPD(H, N, DELTA, V, 1 / DG, 1 / GHG);
         f̲o̲r̲ I:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              DELTA[I]:= -SYMMATVEC(1, N, I, H, G);
         ALFA:= NRMDELTA;
         NRMDELTA:= SQRT(VECVEC(1, N, 0, DELTA, DELTA));
         EPS:= SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL;
         DG:= SQRT(VECVEC(1, N, 0, G, G));
         DG0:= VECVEC(1, N, 0, DELTA, G); OK:= DG0 ≤ 0
      e̲n̲d̲ ITERATION;
   OUT[0]:= NRMDELTA; OUT[1]:= DG; OUT[2]:= EVL;
   OUT[3]:= CNTL; OUT[4]:= CNTE; RNK1MIN:= F
e̲n̲d̲ RNK1MIN;

e̲o̲p̲
