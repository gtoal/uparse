c̲o̲d̲e̲ 34134;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LSQORTDEC(A, N, M, AUX, AID, CI); v̲a̲l̲u̲e̲ N, M;
   i̲n̲t̲e̲g̲e̲r̲ N, M; a̲r̲r̲a̲y̲ A, AUX, AID; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ CI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, K, KPIV;
   r̲e̲a̲l̲ BETA, SIGMA, NORM, W, EPS, AKK, AIDK;
   a̲r̲r̲a̲y̲ SUM[1:M];

   NORM:= 0; AUX[3]:= M;
   f̲o̲r̲ K:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
      b̲e̲g̲i̲n̲ W:= SUM[K]:= TAMMAT(1, N, K, K, A, A);
         i̲f̲ W > NORM t̲h̲e̲n̲ NORM:= W
      e̲n̲d̲;
   W:= AUX[5]:= SQRT(NORM); EPS:= AUX[2] × W;
   f̲o̲r̲ K:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
      b̲e̲g̲i̲n̲ SIGMA:= SUM[K]; KPIV:= K;
         f̲o̲r̲ J:= K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
              i̲f̲ SUM[J] > SIGMA t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ SIGMA:= SUM[J]; KPIV:= J e̲n̲d̲;
         i̲f̲ KPIV ≠ K t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ SUM[KPIV]:= SUM[K]; ICHCOL(1, N, K, KPIV, A) e̲n̲d̲;
         CI[K]:= KPIV; AKK:= A[K,K];
         SIGMA:= TAMMAT(K, N, K, K, A, A); W:= SQRT(SIGMA);
         AIDK:= AID[K]:= i̲f̲ AKK < 0 t̲h̲e̲n̲ W e̲l̲s̲e̲ - W;
         i̲f̲ W < EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ AUX[3]:= K - 1; g̲o̲t̲o̲ ENDDEC e̲n̲d̲;
         BETA:= 1 / (SIGMA - AKK × AIDK); A[K,K]:= AKK - AIDK;
         f̲o̲r̲ J:= K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
            b̲e̲g̲i̲n̲ ELMCOL(K, N, J, K, A, A, - BETA × TAMMAT(K, N,
                 K, J, A, A)); SUM[J]:= SUM[J] - A[K,J] ⭡ 2
            e̲n̲d̲
      e̲n̲d̲ FOR K;
   ENDDEC:
e̲n̲d̲ LSQORTDEC;

e̲o̲p̲
