c̲o̲d̲e̲ 35191;
p̲r̲o̲c̲e̲d̲u̲r̲e̲  BESS KA01(A, X, KA, KA1); v̲a̲l̲u̲e̲ A, X;
   r̲e̲a̲l̲ A, X, KA, KA1;
   i̲f̲ A = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲
      BESS K01(X,KA,KA1)
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ F, G, H, PI; i̲n̲t̲e̲g̲e̲r̲ N, NA; b̲o̲o̲l̲e̲a̲n̲ REC, REV;
      PI:= 4 × ARCTAN(1);
      REV:= A < -·5; i̲f̲ REV t̲h̲e̲n̲ A:= -A-1;
      REC:= A ≥  ·5; i̲f̲ REC t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ NA:=ENTIER(A+·5); A:= A - NA e̲n̲d̲;
      i̲f̲ A = ·5 t̲h̲e̲n̲ F:= G:= SQRT(PI / X / 2) × EXP (-X) e̲l̲s̲e̲
      i̲f̲ X < 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲
         r̲e̲a̲l̲ A1, B, C, D, E, P, Q, S;
         B:=X/2;D:=-LN(B);E:=A×D;C:=A×PI;
         C:=i̲f̲ ABS(C)<@-15 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ C/SIN(C);
         S:=i̲f̲ ABS(E)<@-15 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ SINH(E)/E;
         E:=EXP(E);A1:=(E+1/E)/2;G:=RECIP GAMMA(A,P,Q)×E;
         KA:=F:=C×(P×A1+Q×S×D);E:=A×A;
         P:=·5×G×C;Q:=·5/G;C:=1;D:=B×B;KA1:=P;
         f̲o̲r̲ N:=1,N+1 w̲h̲i̲l̲e̲ H/KA+ABS(G)/KA1>@-15 d̲o̲
            b̲e̲g̲i̲n̲ F:=(F×N+P+Q)/(N×N-E);C:=C×D/N;
               P:=P/(N-A);Q:=Q/(N+A);G:=C×(P-N×F);
               H:=C×F;KA:=KA+H;KA1:=KA1+G
            e̲n̲d̲;
         F:=KA;G:=KA1/B
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ EXPON;
         EXPON:= EXP(-X); NONEXP BESS KA01(A, X, KA, KA1);
         F:= EXPON × KA; G:= EXPON × KA1
      e̲n̲d̲;
      i̲f̲ REC t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ X:= 2 / X;
         f̲o̲r̲ N:= 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NA d̲o̲
            b̲e̲g̲i̲n̲ H:= F + (A + N) × X × G; F:= G; G:= H e̲n̲d̲
      e̲n̲d̲;
      i̲f̲ REV t̲h̲e̲n̲ b̲e̲g̲i̲n̲ KA1:= F; KA:= G e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ KA:= F; KA1:= G e̲n̲d̲
   e̲n̲d̲ BESS KA01;
e̲o̲p̲
