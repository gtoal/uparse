c̲o̲d̲e̲ 33012 ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK2(X, A, B, Y, YA, Z, ZA, FXYZ, E, D, FI);
   v̲a̲l̲u̲e̲ B, FI; r̲e̲a̲l̲ X, A, B, Y, YA, Z, ZA, FXYZ; b̲o̲o̲l̲e̲a̲n̲ FI;
     a̲r̲r̲a̲y̲ E, D;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E1, E2, E3, E4, XL, YL, ZL, H, INT, HMIN, HL,
     ABSH, K0, K1, K2, K3, K4, K5, DISCRY, DISCRZ, TOLY,
     TOLZ, MU, MU1, FHY, FHZ;
   b̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3]:= A; D[4]:= YA; D[5]:= ZA e̲n̲d̲;
   D[1]:= 0; XL:= D[3]; YL:= D[4]; ZL:= D[5];
   i̲f̲ FI t̲h̲e̲n̲ D[2]:= B - D[3]; ABSH:= H:= ABS(D[2]);
   i̲f̲ B - XL < 0 t̲h̲e̲n̲ H:= - H; INT:= ABS(B - XL);
   HMIN:= INT × E[1] + E[2]; HL:= INT × E[3] + E[4];
   i̲f̲ HL < HMIN t̲h̲e̲n̲ HMIN:= HL; E1:= E[1] / INT;
   E2:= E[2] / INT; E3:= E[3] / INT; E4:= E[4] / INT;
   FIRST:= t̲r̲u̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST:= t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH:= ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H:= i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN; ABSH:= HMIN
   e̲n̲d̲;
   i̲f̲ H ≥ B - XL ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[2]:= H; LAST:= t̲r̲u̲e̲; H:= B - XL;
      ABSH:= ABS(H)
   e̲n̲d̲
   e̲l̲s̲e̲ LAST:= f̲a̲l̲s̲e̲;
   STEP: X:= XL; Y:= YL; Z:= ZL; K0:= FXYZ × H;
   X:= XL + H / 4·5;
   Y:= YL + (ZL × 18 + K0 × 2) / 81 × H;
   Z:= ZL + K0 / 4·5  ; K1:= FXYZ × H; X:= XL + H / 3;
   Y:= YL + (ZL × 6 + K0) / 18 × H;
   Z:= ZL + (K0 + K1 × 3) / 12; K2:= FXYZ × H;
   X:= XL + H × ·5;
   Y:= YL + (ZL × 8 + K0 + K2) / 16 × H;
   Z:= ZL + (K0 + K2 × 3) / 8; K3:= FXYZ × H;
   X:= XL + H × ·8;
   Y:= YL + (ZL × 100 + K0 × 12 + K3 × 28) / 125 × H;
   Z:= ZL + (K0 × 53 - K1 × 135 + K2 × 126 + K3 × 56)
     / 125; K4:= FXYZ × H; X:= i̲f̲ LAST t̲h̲e̲n̲ B e̲l̲s̲e̲ XL + H;
   Y:= YL + (ZL × 336 + K0 × 21 + K2 × 92 + K4 × 55) /
     336 × H;
   Z:= ZL + (K0 × 133 - K1 × 378 + K2 × 276 + K3 × 112
     + K4 × 25) / 168; K5:= FXYZ × H;
   DISCRY:= ABS(( - K0 × 21 + K2 × 108 - K3 × 112 + K4× 25) / 56 × H);
   DISCRZ:= ABS(K0 × 21 - K2 × 162 + K3 × 224 - K4 ×
     125 + K5 × 42) / 14;
   TOLY:= ABSH × (ABS(ZL) × E1 + E2);
   TOLZ:= ABS(K0) × E3 + ABSH × E4;
   REJECT:= DISCRY > TOLY ∨ DISCRZ > TOLZ;
   FHY:= DISCRY / TOLY; FHZ:= DISCRZ / TOLZ;
   i̲f̲ FHZ > FHY t̲h̲e̲n̲ FHY:= FHZ;
   MU:= 1 / (1 + FHY) + ·45; i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[1]:= D[1] + 1; Y:= YL; Z:= ZL;
         FIRST:= t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H:= MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST:= f̲a̲l̲s̲e̲; HL:= H; H:= MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FHY:= MU × H / HL + MU - MU1; HL:= H; H:= FHY × H;
   ACC: MU1:= MU;
   Y:= YL + (ZL × 56 + K0 × 7 + K2 × 36 - K4 × 15) / 56× HL;
   Z:= ZL + ( - K0 × 63 + K1 × 189 - K2 × 36 - K3 × 112
     + K4 × 50) / 28; K5:= FXYZ × HL;
   Y:= YL + (ZL × 336 + K0 × 35 + K2 × 108 + K4 × 25)
     / 336 × HL;
   Z:= ZL + (K0 × 35 + K2 × 162 + K4 × 125 + K5 × 14)
     / 336;
   NEXT: i̲f̲ B ≠ X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XL:= X; YL:= Y; ZL:= Z; g̲o̲t̲o̲ TEST e̲n̲d̲;
   i̲f̲ ¬LAST t̲h̲e̲n̲ D[2]:= H; D[3]:= X; D[4]:= Y; D[5]:= Z
e̲n̲d̲ RK2;
e̲o̲p̲
