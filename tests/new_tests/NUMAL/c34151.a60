c̲o̲d̲e̲ 34151;
c̲o̲m̲m̲e̲n̲t̲ MCA 2311;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM);
   v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ D, BB, VAL, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K, COUNT;
   r̲e̲a̲l̲ MAX, X, Y, MACHEPS, NORM, RE, MACHTOL, UB, LB, LAMBDA;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ STURM;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ P, I; r̲e̲a̲l̲ F;
      COUNT:= COUNT + 1;
      P:= K; F:= D[1] - X;
      f̲o̲r̲ I:= 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ F ≤ 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ P:= P + 1;
               i̲f̲ P > N t̲h̲e̲n̲ g̲o̲t̲o̲ OUT
            e̲n̲d̲
              e̲l̲s̲e̲ i̲f̲ P < I - 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ LB:= X; g̲o̲t̲o̲ OUT e̲n̲d̲;
            i̲f̲ ABS(F) < MACHTOL t̲h̲e̲n̲
              F:= i̲f̲ F ≤ 0 t̲h̲e̲n̲ - MACHTOL e̲l̲s̲e̲ MACHTOL;
            F:= D[I] - X - BB[I - 1] / F
         e̲n̲d̲;
      i̲f̲ P = N ∨ F ≤ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ X < UB t̲h̲e̲n̲ UB:= X e̲n̲d̲ e̲l̲s̲e̲ LB:= X;
      OUT: STURM:= i̲f̲ P = N t̲h̲e̲n̲ F e̲l̲s̲e̲ (N - P) × MAX
   e̲n̲d̲ STURM;

   MACHEPS:= EM[0]; NORM:= EM[1]; RE:= EM[2];
   MACHTOL:= NORM × MACHEPS; MAX:= NORM / MACHEPS; COUNT:= 0;
   UB:= 1·1 × NORM; LB:= - UB; LAMBDA:= UB;
   f̲o̲r̲ K:= N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ X:= LB; Y:= UB; LB:= -1·1  × NORM;
         ZEROIN(X, Y, STURM, ABS(X) × RE + MACHTOL);
         VAL[K]:= LAMBDA:= i̲f̲ X > LAMBDA t̲h̲e̲n̲ LAMBDA e̲l̲s̲e̲ X;
         i̲f̲ UB > X t̲h̲e̲n̲ UB:= i̲f̲ X > Y t̲h̲e̲n̲ X e̲l̲s̲e̲ Y
      e̲n̲d̲;
   EM[3]:= COUNT
e̲n̲d̲ VALSYMTRI

;e̲o̲p̲
