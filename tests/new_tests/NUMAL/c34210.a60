c̲o̲d̲e̲ 34210;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LINEMIN(N, X, D, ND, ALFA, G, FUNCT, F0, F1, DF0, DF1,
  EVLMAX, STRONGSEARCH, IN); v̲a̲l̲u̲e̲ N, ND, F0, DF0, STRONGSEARCH;
   i̲n̲t̲e̲g̲e̲r̲ N, EVLMAX; b̲o̲o̲l̲e̲a̲n̲ STRONGSEARCH;
     r̲e̲a̲l̲ ND, ALFA, F0, F1, DF0, DF1;
     a̲r̲r̲a̲y̲ X, D, G, IN;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, EVL;
   b̲o̲o̲l̲e̲a̲n̲ NOTININT;
   r̲e̲a̲l̲ F,OLDF,DF,OLDDF,MU,ALFA0,Q,W,Y,Z,RELTOL,ABSTOL
     ,EPS, AID;
   a̲r̲r̲a̲y̲ X0[1:N];

   RELTOL:= IN[1]; ABSTOL:= IN[2]; MU:= IN[3]; EVL:= 0;
   ALFA0:= 0; OLDF:= F0; OLDDF:= DF0; Y:= ALFA; NOTININT:= t̲r̲u̲e̲;
   DUPVEC(1, N, 0, X0, X);
   EPS:= (SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL) / ND;
   Q:= (F1 - F0) / (ALFA × DF0);
   INT: i̲f̲ NOTININT t̲h̲e̲n̲ NOTININT:= DF1 < 0 ∧ Q > MU;
   AID:= ALFA; i̲f̲ DF1 ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ Z:= 3 × (OLDF - F1) / ALFA + OLDDF + DF1;
      W:= SQRT(Z ⭡ 2 - OLDDF × DF1);
      ALFA:= ALFA × (1 - (DF1 + W - Z) / (DF1 - OLDDF + W × 2));
      i̲f̲ ALFA < EPS t̲h̲e̲n̲ ALFA:= EPS e̲l̲s̲e̲
      i̲f̲ AID - ALFA < EPS t̲h̲e̲n̲ ALFA:= AID - EPS
   e̲n̲d̲ CUBIC INTERPOLATION
     e̲l̲s̲e̲ i̲f̲ NOTININT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ ALFA0:= ALFA:= Y; OLDDF:= DF1; OLDF:= F1 e̲n̲d̲
   e̲l̲s̲e̲ ALFA:= 0·5 × ALFA; Y:= ALFA + ALFA0;
   DUPVEC(1, N, 0, X, X0); ELMVEC(1, N, 0, X, D, Y);
   EPS:= (SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL) / ND;
   F:= FUNCT(N, X, G); EVL:= EVL +1 ; DF:= VECVEC(1, N, 0, D, G);
   Q:= (F - F0) / (Y × DF0);
   i̲f̲ (i̲f̲ NOTININT ∨ STRONGSEARCH t̲h̲e̲n̲ t̲r̲u̲e̲ e̲l̲s̲e̲
     Q < MU ∨ Q > 1 - MU) ∧ EVL < EVLMAX t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ NOTININT ∨ DF > 0 ∨ Q < MU t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DF1:= DF; F1:= F e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ ALFA0:= Y; ALFA:= AID - ALFA; OLDDF:= DF; OLDF:= F
      e̲n̲d̲;
      i̲f̲ ALFA > EPS × 2 t̲h̲e̲n̲ g̲o̲t̲o̲ INT
   e̲n̲d̲;
   ALFA:= Y; EVLMAX:= EVL; DF1:= DF; F1:= F
e̲n̲d̲ LINEMIN

;e̲o̲p̲
