<html><head><meta charset="utf-8"></head><body><pre>

algol,<u>n</u>&lt;
Program Pentomino
<u>begin</u>
   <u>comment</u>

   Time: 280782s = 3d 5h 59m 42s

   No buffer:

   Time classic:        428386
   Time turbo:          408163 4.7pct

   Buffer:

   Time classic:        280782
   Time turbo:          251104 10.6pct

   11 solutions
   ;
   <u>integer</u> BOARDX,BOARDY,BOARDX1,BOARDY1,nsolutions;
   <u>Boolean</u> <u>array</u> transformed pieces[1:13,1:8];
   <u>integer</u> <u>array</u> transformedx[1:12,1:8];
   <u>integer</u> <u>array</u> ntransformed[1:12];
   <u>Boolean</u> <u>array</u> used piece[1:12];
   <u>integer</u> ix,iy;
   <u>real</u> <u>procedure</u> clock count;
   <u>code</u> clock count;
   1, 37;
     zl        , grf p−1   ; RF ≔ clock count; stack[p−1] ≔ RF;
   <u>e</u>;

   BOARDX ≔  8;
   BOARDY ≔  9;

   BOARDX1 ≔  BOARDX−1;
   BOARDY1 ≔  BOARDY−1;

   <u>begin</u>
      <u>Boolean</u> <u>array</u> board[0:BOARDY+4];
      <u>Boolean</u> <u>array</u> mask[0:BOARDY1];
      <u>integer</u> <u>array</u> solution board[0:BOARDY1,0:BOARDX1];

      <u>procedure</u> move up left(itransform);
      <u>value</u> itransform;
      <u>integer</u> itransform;
      <u>begin</u>
         <u>integer</u> i;
         <u>for</u> i ≔ i <u>while</u> (<u>integer</u> (transformed pieces[13,itransform]∧
            <u>35</u> 0 <u>5</u> m))=0 <u>do</u>
         transformed pieces[13,itransform] ≔  transformed pieces[13,itransform]
            <u>shift</u> −5;
         <u>for</u> i ≔ i <u>while</u> (<u>integer</u> (transformed pieces[13,itransform]∧
            <u>15</u> 0 <u>5</u> 1 <u>5</u> 1 <u>5</u> 1 <u>5</u> 1 <u>5</u> 1))=0 <u>do</u>
         transformed pieces[13,itransform] ≔  transformed pieces[13,itransform]
            <u>shift</u> −1;
      <u>end</u> move up left;
      <u>procedure</u> rotate cw(dst, src);
      <u>value</u> dst, src;
      <u>integer</u> dst, src;
      <u>begin</u>
         <u>integer</u> i,j;
         <u>Boolean</u> s;
         s ≔  <u>40</u> 0;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         <u>begin</u>
            <u>for</u> j ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
            s ≔  s  ∨ (((transformed pieces[13,src] <u>shift</u> −j×5) ∧
               (<u>40</u> 1 <u>shift</u> i)) <u>shift</u> (4−j−i+i×5))
         <u>end</u>;
         transformed pieces[13,dst] ≔  s;
         move up left(dst)
      <u>end</u> rotate cw;
      <u>procedure</u> mirror(dst, src);
      <u>value</u> dst, src;
      <u>integer</u> dst, src;
      <u>begin</u>
         <u>integer</u> i;
         transformed pieces[13,dst] ≔  <u>40</u> 0;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         transformed pieces[13,dst] ≔  (transformed pieces[13,dst] <u>shift</u> 5)  ∨
            ((transformed pieces[13,src] <u>shift</u> −i×5) ∧ <u>35</u> 0 <u>5</u> m);
         move up left(dst)
      <u>end</u> mirror;
      <u>Boolean</u> <u>procedure</u> compare pieces(ipiece1, itransform1, ipiece2, itransform2);
      <u>value</u> ipiece1, itransform1, ipiece2, itransform2;
      <u>integer</u> ipiece1, itransform1, ipiece2, itransform2;
      <u>begin</u>
         <u>integer</u> i;
         compare pieces ≔  (<u>integer</u> transformed pieces[ipiece1,itransform1]) =
            (<u>integer</u> transformed pieces[ipiece2,itransform2]);
      <u>end</u> compare pieces;
      <u>procedure</u> copy piece(dstpiece, dsttransform, srcpiece, srctransform);
      <u>value</u> dstpiece, dsttransform, srcpiece, srctransform;
      <u>integer</u> dstpiece, dsttransform, srcpiece, srctransform;
      <u>begin</u>
         transformed pieces[dstpiece,dsttransform] ≔ 
            transformed pieces[srcpiece,srctransform]
      <u>end</u> copy piece;

      <u>procedure</u> transform pieces;
      <u>begin</u>
         <u>integer</u> i,ipiece,irotate,imirror,itransformed;
         <u>Boolean</u> piece;

         <u>for</u> ipiece ≔ 1 <u>step</u> 1 <u>until</u> 12 <u>do</u>
         <u>begin</u>
            piece ≔  <u>40</u> 0;
            <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
            piece ≔  piece  ∨ ((<u>Boolean</u> read integer) <u>shift</u> 5×i);
            transformed pieces[13,1] ≔  piece;
            ntransformed[ipiece] ≔  0;
            <u>for</u> irotate ≔ 0 <u>step</u> 1 <u>until</u> 3 <u>do</u>
            <u>begin</u>
               copy piece(13,2,13,1);
               <u>for</u> imirror ≔ 0 <u>step</u> 1 <u>until</u> 1 <u>do</u>
               <u>begin</u>
                  <u>if</u> imirror=0 <u>then</u>
                  copy piece(13,3,13,2)
                  <u>else</u>
                  mirror(3,2);
                  <u>for</u> itransformed ≔ 1 <u>step</u> 1 <u>until</u> ntransformed[ipiece] <u>do</u>
                  <u>begin</u>
                     <u>if</u> compare pieces(ipiece,itransformed,13,3) <u>then</u>
                        <u>go</u>_<u>to</u> duplicate
                  <u>end</u> check for duplicate;
                  ntransformed[ipiece] ≔  ntransformed[ipiece]+1;
                  <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
                  <u>begin</u>
                     <u>if</u> transformed pieces[13,3] <u>shift</u> (−i−1) <u>then</u>
                     <u>begin</u>
                        transformedx[ipiece,ntransformed[ipiece]] ≔  i;
                        <u>go</u>_<u>to</u> found first bit
                     <u>end</u>
                  <u>end</u> look for first bit in first row;
found first bit:
                  copy piece(ipiece,ntransformed[ipiece],13,3);
duplicate:
               <u>end</u> imirror;
               rotate cw(2,1);
               copy piece(13,1,13,2)
            <u>end</u> irotate
         <u>end</u> ipiece
      <u>end</u> transform pieces;
      <u>procedure</u> create board;
      <u>begin</u>
         <u>integer</u> i,j;
         board[0] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 1 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[1] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 1 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[2] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 1 <u>1</u> 0 <u>1</u> 1 <u>1</u> 1 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[3] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 0 <u>1</u> 0 <u>1</u> 1 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 1 <u>1</u> 0 <u>4</u> m;
         board[4] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 1 <u>1</u> 0 <u>4</u> m;
         board[5] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[6] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 1 <u>1</u> 1 <u>1</u> 1 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[7] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[8] ≔  <u>24</u> 0 <u>4</u> m <u>1</u> 1 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>1</u> 0 <u>4</u> m;
         board[9] ≔  <u>40</u> m;
         board[10] ≔  <u>40</u> m;
         board[11] ≔  <u>40</u> m;
         board[12] ≔  <u>40</u> m;
         <u>for</u> i ≔  0 <u>step</u> 1 <u>until</u> BOARDY1 <u>do</u>
         mask[i] ≔  (board[i] <u>shift</u> −4) ∧ <u>32</u> 0 <u>8</u> m;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> BOARDY1 <u>do</u>
         <u>for</u> j ≔ 0 <u>step</u> 1 <u>until</u> BOARDX1 <u>do</u>
         solution board[i,j] ≔  −1
      <u>end</u> create board;
      <u>procedure</u> find first free;
      <u>begin</u>
next:
         <u>if</u> board[iy] <u>shift</u> −(ix+5) <u>then</u>
         <u>begin</u>
            ix ≔ ix+1;
            <u>if</u> ix&GreaterSlantEqual;BOARDX <u>then</u>
            <u>begin</u>
               ix ≔  0;
               iy ≔  iy+1
            <u>end</u> next row;
            <u>go</u>_<u>to</u> next
         <u>end</u> bit is one
      <u>end</u> find first free;
      <u>Boolean</u> <u>procedure</u> piece fit(ix,iy,ipiece,itransform);
      <u>value</u> ix,iy,ipiece,itransform;
      <u>integer</u> ix,iy,ipiece,itransform;
      <u>begin</u>
         <u>integer</u> i;
         piece fit ≔  <u>true</u>;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         <u>begin</u>
            <u>if</u> (<u>integer</u>(board[iy+i]∧
               (((transformed pieces[ipiece,itransform] <u>shift</u> −5×i) ∧
               <u>35</u> 0 <u>5</u> m)<u>shift</u> (ix+4))))≠0 <u>then</u>
            <u>begin</u>
               piece fit ≔  <u>false</u>;
               <u>go</u>_<u>to</u> not fit
            <u>end</u>
         <u>end</u> <u>for</u>;
not fit:
      <u>end</u> piece fit;
      <u>procedure</u> print piece(ipiece,itransform);
      <u>value</u> ipiece,itransform;
      <u>integer</u> ipiece,itransform;
      <u>begin</u>
         <u>Boolean</u> s;
         <u>integer</u> i,j;
         s ≔ transformed pieces[ipiece,itransform];
         writecr;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         <u>begin</u>
            <u>for</u> j ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
            <u>begin</u>
               s ≔ s <u>shift</u> −1;
               write(«d», <u>if</u> s <u>then</u> 1 <u>else</u> 0)
            <u>end</u>;
            writecr
         <u>end</u>;
         i ≔ select(17);
         lyn;
         select(i)
      <u>end</u> print piece;
      <u>procedure</u> print board;
      <u>begin</u>
         <u>integer</u> i,j;
         writecr;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> BOARDY1 <u>do</u>
         <u>begin</u>
            <u>for</u> j ≔ 0 <u>step</u> 1 <u>until</u> BOARDX1 <u>do</u>
            write(«d», <u>if</u> board[i] <u>shift</u> −(j+5) <u>then</u> 1 <u>else</u> 0);
            writecr
         <u>end</u> row;
         lyn
      <u>end</u> print board;
      <u>procedure</u> set piece(ix,iy,ipiece,itransform);
      <u>value</u> ix,iy,ipiece,itransform;
      <u>integer</u> ix,iy,ipiece,itransform;
      <u>begin</u>
         <u>integer</u> i;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         board[iy+i] ≔  board[iy+i]  ∨
            (((transformed pieces[ipiece,itransform] <u>shift</u> −5×i)
            ∧ <u>35</u> 0 <u>5</u> m) <u>shift</u> (ix+4))
      <u>end</u> set piece;
      <u>procedure</u> remove piece(ix,iy,ipiece,itransform);
      <u>value</u> ix,iy,ipiece,itransform;
      <u>integer</u> ix,iy,ipiece,itransform;
      <u>begin</u>
         <u>integer</u> i;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         board[iy+i] ≔  board[iy+i] ∧
            ¬(((transformed pieces[ipiece,itransform] <u>shift</u> −5×i)
            ∧ <u>35</u> 0 <u>5</u> m) <u>shift</u> (ix+4))
      <u>end</u> remove piece;
      <u>procedure</u> set solution(ix,iy,ipiece,itransform);
      <u>value</u> ix,iy,ipiece,itransform;
      <u>integer</u> ix,iy,ipiece,itransform;
      <u>begin</u>
         <u>integer</u> i,j;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         <u>for</u> j ≔ 0 <u>step</u> 1 <u>until</u> 4 <u>do</u>
         <u>begin</u>
            <u>if</u> transformed pieces[ipiece,itransform] <u>shift</u> −(j+1+5×i) <u>then</u>
            solution board[iy+i,ix+j] ≔  ipiece
         <u>end</u>
      <u>end</u> set solution;
      <u>procedure</u> print solution;
      <u>begin</u>
         <u>integer</u> i,j,k;
         writecr;
         write text(«Solution: »);
         write(«dddd», nsolutions);
         writecr;
         writetext(«+−−−»);
         <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> BOARDX1 <u>do</u>
         <u>begin</u>
            <u>if</u> mask[0] <u>shift</u> −j−1 <u>then</u>
            writetext(«XXXX»)
            <u>else</u> <u>if</u> solution board[0,j−1]=
               solution board[0,j] <u>then</u>
            writetext(«−−−−»)
            <u>else</u>
            writetext(«+−−−»)
         <u>end</u> first row;
         <u>if</u> mask[0] <u>shift</u> −BOARDX1−1 <u>then</u>
         writetext(«X»)
         <u>else</u>
         writetext(«+»);
         writecr;
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> BOARDY1 <u>do</u>
         <u>begin</u>
            <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> 2 <u>do</u>
            <u>begin</u>
               writetext(«I   »);
               <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> BOARDX1 <u>do</u>
               <u>begin</u>
                  <u>if</u> mask[i] <u>shift</u> −j−1 <u>then</u>
                  writetext(«XXXX»)
                  <u>else</u> <u>if</u> solution board[i,j−1]=
                     solution board[i,j] <u>then</u>
                  writetext(«    »)
                  <u>else</u>
                  <u>if</u> mask[i] <u>shift</u> −j <u>then</u>
                  writetext(«X   »)
                  <u>else</u>
                  writetext(«I   »)
               <u>end</u>;
               <u>if</u> mask[i] <u>shift</u> −BOARDX1−1 <u>then</u>
               writetext(«X»)
               <u>else</u>
               writetext(«I»);
               writecr
            <u>end</u>;
            <u>if</u> i&lt;BOARDY1 <u>then</u>
            <u>begin</u>
               <u>if</u> solution board[i,0]=
                  solution board[i+1,0] <u>then</u>
               writetext(«I   »)
               <u>else</u>
               writetext(«+−−−»);
               <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> BOARDX1 <u>do</u>
               <u>begin</u>
                  <u>if</u> (mask[i] <u>shift</u> −j−1)  ∨
                       (mask[i+1] <u>shift</u> −j−1) <u>then</u>
                  writetext(«XXXX»)
                  <u>else</u> <u>if</u> solution board[i,j]=
                     solution board[i+1,j] <u>then</u>
                  <u>begin</u>
                     <u>if</u> solution board[i,j−1]=
                        solution board[i+1,j−1] <u>then</u>
                     <u>begin</u>
                        <u>if</u> solution board[i,j] ≠
                           solution board[i,j−1]  ∨
                           solution board[i+1,j] ≠
                           solution board[i+1,j−1] <u>then</u>
                        <u>begin</u>
                           <u>if</u> mask[i] <u>shift</u> −j <u>then</u>
                           writetext(«X   »)
                           <u>else</u>
                           writetext(«I   »)
                        <u>end</u>
                        <u>else</u>
                        writetext(«    »)
                     <u>end</u>
                     <u>else</u>
                     <u>if</u> (mask[i] <u>shift</u> −j)  ∨
                          (mask[i+1] <u>shift</u> −j) <u>then</u>
                     writetext(«X   »)
                     <u>else</u>
                     writetext(«+   »)
                  <u>end</u>
                  <u>else</u>
                  <u>begin</u>
                     <u>if</u> solution board[i,j] =
                        solution board[i,j−1] ∧
                        solution board[i+1,j] =
                        solution board[i+1,j−1] <u>then</u>
                     writetext(«−−−−»)
                     <u>else</u>
                     <u>if</u> (mask[i] <u>shift</u> −j)  ∨
                          (mask[i+1] <u>shift</u> −j) <u>then</u>
                     writetext(«X−−−»)
                     <u>else</u>
                     writetext(«+−−−»)
                  <u>end</u>
               <u>end</u> first row;
               <u>if</u> (mask[i] <u>shift</u> −BOARDX1−1)  ∨
                    (mask[i+1] <u>shift</u> −BOARDX1−1) <u>then</u>
               writetext(«X»)
               <u>else</u> <u>if</u> solution board[i,BOARDX1]=
                  solution board[i+1,BOARDX1] <u>then</u>
               writetext(«I»)
               <u>else</u>
               writetext(«+»);
               writecr
            <u>end</u> not last row
         <u>end</u> each row;
         writetext(«+−−−»);
         <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> BOARDX1 <u>do</u>
         <u>begin</u>
            <u>if</u> mask[BOARDY1] <u>shift</u> −j−1 <u>then</u>
            writetext(«XXXX»)
            <u>else</u> <u>if</u> solution board[BOARDY1,j−1]=
               solution board[BOARDY1,j] <u>then</u>
            writetext(«−−−−»)
            <u>else</u>
            writetext(«+−−−»)
         <u>end</u> first row;
         <u>if</u> mask[BOARDY1] <u>shift</u> −BOARDX1−1 <u>then</u>
         writetext(«X»)
         <u>else</u>
         writetext(«+»);
         writecr;
      <u>end</u> print solution;
      <u>procedure</u> test piece(piece count);
      <u>value</u> piece count;
      <u>integer</u> piece count;
      <u>begin</u>
         <u>integer</u> ipiece,itransform,saveix,saveiy;
         <u>for</u> ipiece ≔ 1 <u>step</u> 1 <u>until</u> 12 <u>do</u>
         <u>begin</u>
            <u>if</u> ¬ used piece[ipiece] <u>then</u>
            <u>begin</u>
               used piece[ipiece] ≔  <u>true</u>;
               <u>for</u> itransform ≔ 1 <u>step</u> 1 <u>until</u> ntransformed[ipiece] <u>do</u>
               <u>begin</u>
                  <u>if</u> piece fit(ix−transformedx[ipiece,itransform],
                     iy,ipiece,itransform) <u>then</u>
                  <u>begin</u>
                     set piece(ix−transformedx[ipiece,itransform],iy,
                        ipiece,itransform);
                     set solution(ix−transformedx[ipiece,itransform],iy,
                        ipiece,itransform);
                     <u>if</u> piece count=11 <u>then</u>
                     <u>begin</u>
                        nsolutions ≔ nsolutions+1;
                        print solution
                     <u>end</u> solution found
                     <u>else</u>
                     <u>begin</u>
                        saveix ≔  ix;
                        saveiy ≔  iy;
                        find first free;
                        test piece(piece count+1);
                        ix ≔  saveix;
                        iy ≔  saveiy
                     <u>end</u> next piece;
                     remove piece(ix−transformedx[ipiece,itransform],iy,
                        ipiece,itransform)
                  <u>end</u> piece fit
               <u>end</u> itransform;
               used piece[ipiece] ≔  <u>false</u>
            <u>end</u> unused piece
         <u>end</u> ipiece
      <u>end</u> test piece;
      <u>procedure</u> solve;
      <u>begin</u>
         <u>integer</u> ipiece;
         <u>for</u> ipiece ≔ 1 <u>step</u> 1 <u>until</u> 12 <u>do</u>
         used piece[ipiece] ≔ <u>false</u>;
         ix ≔ 0;
         iy ≔ 0;
         test piece(0)
      <u>end</u> solve;

      select(16);
      nsolutions ≔ 0;
      transform pieces;
      select(17);
      create board;
      clock count;
      solve;
      writecr;
      write text(«Solutions: »);
      write(«dddd», nsolutions);
      writecr;
      write text(«Time: »);
      write(«dddddd», clock count);
      write text(« sec.»);
      writecr
   <u>end</u>
<u>end</u>;
t&lt;
</pre></body></html>
