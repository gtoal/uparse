<html><head><meta charset="utf-8"></head><body><pre>

algol,<u>n</u>&lt;
<u>begin</u>
   <u>comment</u>

   https://projecteuler.net/problem=112

   Answer:  1587000
   Time:    25518.52s = 7h 5m 18.52s

   No buffer, no f̲o̲r̲:

   Time classic:        30380.22
   Time turbo:          28390.72 6.5pct

   Buffer, no f̲o̲r̲:

   Time classic:        30569.63
   Time turbo:          28580.12 6.5pct

   No buffer, f̲o̲r̲:

   Time classic:        25329.12
   Time turbo:          24158.28 4.6pct

   Buffer, f̲o̲r̲:

   Time classic:        25518.52
   Time turbo:          24347.68 4.6pct

   ;
   <u>real</u> clock;
   <u>real</u> <u>procedure</u> clock count;
   <u>code</u> clock count;
   1, 37;
     zl        , grf p−1   ; RF ≔ clock count; stack[p−1] ≔ RF;
   <u>e</u>;
   <u>boolean</u> <u>procedure</u> bouncy(n);
   <u>value</u> n;
   <u>integer</u> n;
   <u>begin</u>
      <u>integer</u> d,lastd;
      <u>boolean</u> up,down;
      bouncy ≔ up ≔ down ≔ <u>false</u>;
      lastd ≔ n <u>mod</u> 10;
      n ≔ n÷10;
      <u>for</u> n ≔ n <u>while</u> n&gt;0 <u>do</u>
      <u>begin</u>
         d ≔ n <u>mod</u> 10;
         <u>if</u> d&gt;lastd ∨ (d=lastd ∧ up) <u>then</u>
            up ≔ <u>true</u>
         <u>else</u>
         <u>if</u> d&lt;lastd ∨ (d=lastd ∧ down) <u>then</u>
            down ≔ <u>true</u>;
         <u>if</u> up ∧ down <u>then</u>
         <u>begin</u>
            bouncy ≔ <u>true</u>;
            <u>goto</u> finish
         <u>end</u> bouncy;
         n ≔ n÷10;
         lastd ≔ d
      <u>end</u> <u>for</u>;
finish:
   <u>end</u> bouncy;
   <u>integer</u> i,bcount;
   clock count;
   bcount ≔ 0;
   <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> 999999999 <u>do</u>
   <u>begin</u>
      <u>if</u> bouncy(i) <u>then</u> bcount ≔ bcount+1;
      <u>if</u> bcount×100/i&GreaterSlantEqual;99 <u>then</u> <u>goto</u> found
   <u>end</u>;
   writetext(«bad»);
found:
   clock ≔ clock count;
   writecr;
   writeinteger(«p»,bcount);
   writecr;
   writeinteger(«p»,i);
   writecr;
   write(«dddddddd.dd»,clock)
<u>end</u>;
t&lt;
</pre></body></html>
