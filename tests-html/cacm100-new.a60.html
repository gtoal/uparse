<html><head><meta charset="utf-8"></head><body><pre><u>begin</u>
   <u>comment</u> Test rig for fixed versions of CACM Algorithms 100 and 101;

   <u>comment</u> ALGORITHM 100 
     ADD ITEM TO CHAIN-LINKED LIST 
     PHILIP J. KIVIAT 
     United States Steel Corp., Appl. Research Lab., Monroeville, 
     Penn. ;
   <u>procedure</u> inlist(t, info, m, list, n, first, flag, addr, listfull);
      <u>integer</u> n, m, first, flag, t;
        <u>integer</u> <u>array</u> info, list, addr;
        <u>label</u> listfull;
        <u>comment</u> inlist adds the information pair {t,info} to the chain-
        link structured matrix list (i,j), where t is an order key &gt;= 0, and
        info(k) an information vector associated with t. info(k) has
        dimension m, list(i,j) has dimensions (n * (m+3)). flag denotes
        the head and tail of list(i,j), and first contains the address of
        the first (lowest order) entry in list(i,j). addr(k) is a vector
        containing the addresses of available (empty) rows in list(i,j).
        Initialization: list(i,m+2) = flag, for some i &lt;= n. If list(i,j) is
        filled exit is to listfull;
   <u>begin</u>
      <u>integer</u> i, j, k, link1, link2;
      L0:
      <u>if</u> addr[1]=0 <u>then</u>
        <u>go</u> <u>to</u> listfull
        <u>else</u> <u>if</u> addr[n] ≠ 0 <u>then</u>
      <u>begin</u>
         <u>comment</u> Insertion into an empty list;
         i ≔ flag;
         link1 ≔ m + 3;
         link2 ≔ m + 2;
         <u>go</u> <u>to</u> L3
      <u>end</u>;
      <u>comment</u> There is at least one element to compare against;
      i ≔ first;
      L1:
      <u>if</u> list[i, 1]&LessSlantEqual;t <u>then</u> 
        <u>begin</u>
         <u>comment</u> Insert after first item - search forwards;
         link1 ≔ m+2;
         link2 ≔ m+3
      <u>end</u>
      <u>else</u> 
        <u>begin</u>
         <u>comment</u> Insert before first item - search backwards;
         link1 ≔ m+3;
         link2 ≔ m+2 
        <u>end</u>;

      <u>if</u> list[i, link2]≠flag <u>then</u> 
        <u>begin</u>
         <u>comment</u> Check the n̲e̲x̲t̲ element for the insertion point;
         k ≔ i;
         i ≔ list[i, link2];
         <u>if</u>(list[i, 1]&gt;t)<u>then</u>
         <u>begin</u>
            <u>comment</u> Insertion point found;
            <u>go</u> <u>to</u> L4
         <u>end</u>
         <u>else</u>
         <u>begin</u>
            <u>comment</u> Continue the search;
            <u>go</u> <u>to</u> L1
         <u>end</u>
      <u>end</u>
      <u>else</u> 
        <u>begin</u>
         <u>comment</u> Insert a̲f̲t̲e̲r̲ i (depending on search direction).
           Link i into the new element;
         list[i, link2] ≔ addr[1]
      <u>end</u>;
      L3: ;
      <u>comment</u> Insert at one of the ends of the list,
        linking to element i in the opposite direction.
        As a special case, i = flag when inserting into the empty list;
      j ≔ addr[1];
      list[j, link1] ≔ i;
      list[j, link2] ≔ flag;
      <u>if</u> link2=m+2 <u>then</u>
        first ≔ addr[1];
      <u>go</u> <u>to</u> L5;
      L4: ;
      <u>comment</u> Insert between two existing elements;
      j ≔ addr[1];
      list[j, link1] ≔ list[i, link1];
      list[i, link1] ≔ list[k, link2] ≔ addr[1];
      list[j, link2] ≔ i;
      L5:
      list[j, 1] ≔ t;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> m <u>do</u>
           list[j, i+1] ≔ info[i];
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> n-1 <u>do</u>
           addr[i] ≔ addr[i+1];
      addr[n] ≔ 0 
     <u>end</u> inlist ;

   <u>comment</u> ALGORITHM 101 
     REMOVE ITEM FROM CHAIN-LINKED LIST 
     PHILIP J. KIVIAT 
     United States Steel Corp., Appl. Res. Lab., Monroeville, 
     Penn. ;
   <u>procedure</u> outlist(vector, m, list, n, first, flag, addr);
      <u>integer</u> n, m, first, flag;
        <u>integer</u> <u>array</u> vector, list, addr;
        <u>comment</u> outlist removes the first. entry (information pair with 
        lowest order key) from list(i,j) and puts it in vector(k);
   <u>begin</u>
      <u>integer</u> i;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> m+1 <u>do</u>
           vector[i] ≔ list[first, i];
      <u>comment</u> Find the place to record release of the first entry;
      <u>for</u> i ≔ n, i - 1 <u>while</u> i &gt; 1 ∧ addr[i] = 0 <u>do</u>
        ;
      <u>if</u> addr[i] = 0 <u>then</u>
        addr[i] ≔ first
      <u>else</u>
        addr[i+1] ≔ first;

      <u>if</u> list[first, m+3]=flag <u>then</u> 
        <u>begin</u>
         <u>comment</u> The list is now empty;
         list[1, m+2] ≔ flag
      <u>end</u>
      <u>else</u> 
        <u>begin</u>
         first ≔ list[first, m+3];
         list[first, m+2] ≔ flag 
        <u>end</u>
   <u>end</u> outlist ;

   <u>procedure</u> fill list (tvalues,info,m,list,n,first,flag,addr,listfull); 
        <u>comment</u> Fill list with sample data using Algorithm 100: inlist;
      <u>integer</u> n, m, first, flag;
        <u>integer</u> <u>array</u> addr, info, list, tvalues; 
        <u>label</u> listfull;

   <u>begin</u>
      <u>integer</u> i, j, t;

      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
         <u>begin</u>
            <u>comment</u> set the key;
            t ≔ tvalues[i];
            <u>comment</u> set arbitrary values to be associated with the key;
            <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> m <u>do</u>
               <u>begin</u>
                  info[j] ≔ 2×i + j
               <u>end</u>;
            outstrintnl(“Adding key ”, t);
            inlist(t, info, m, list, n, first, flag, addr, listfull);
            showlist(list, first, m, n, flag);
            checklist(list, first, m, n, flag, i);
            check free(addr, n, n - i)
         <u>end</u>
   <u>end</u> fill list;

   <u>procedure</u> showlist(list, first, m, n, flag);
      <u>comment</u> show the contents of the list, the end is
        assumed to be indicated by list[i, m+3] = flag;

      <u>integer</u> <u>array</u> list;
        <u>integer</u> first, m, n, flag;
   <u>begin</u>
      <u>integer</u> i, prev;
      outstring(1, “List contents: ”);
      i ≔ first;
      prev ≔ flag;
      next:
      <u>if</u> i = flag <u>then</u> <u>go</u> <u>to</u> endofproc;
      prev ≔ i;
      outinteger(1, list[i, 1]);
      i ≔ list[i, m+3];
      <u>go</u> <u>to</u> next;
      endofproc:
      outstring(1, “\n”)
   <u>end</u> showlist;

   <u>procedure</u> checklist(list, first, m, n, flag, list length);
      <u>comment</u> check that list is sorted from 1 ... list length
        and of the indicated length;
      <u>integer</u> <u>array</u> list;
        <u>integer</u> first, m, n, flag, list length;
   <u>begin</u>
      <u>boolean</u> error;

      error ≔ <u>false</u>;
      <u>if</u> first &lt; 1 ∨ first &gt; n <u>then</u>
      <u>begin</u>
         outstrintnl(“Invalid v̲a̲l̲u̲e̲ f̲o̲r̲ first: ”, first);
         error ≔ <u>true</u>
      <u>end</u>
      <u>else</u>
      <u>begin</u>
         <u>integer</u> checked, i, prev, t;

         <u>comment</u> dummy key for comparison at the start of the list;
         prev ≔ -1;
         i ≔ first;
         <u>comment</u> how many have been checked;
         checked ≔ 0;
         <u>for</u> checked ≔ checked + 1 <u>while</u> (checked &LessSlantEqual; list length ∧ ¬error ∧ i ≠ flag) <u>do</u>
            <u>begin</u>
               t ≔ list[i, 1];
               <u>if</u> t &lt; prev <u>then</u>
               <u>begin</u>
                  error ≔ <u>true</u>;
                  outstrint(“key ”, t);
                  outstrintnl(“out of order at position ”, i)
               <u>end</u>;
               prev ≔ t;
               <u>comment</u> follow the n̲e̲x̲t̲ link.;
               i ≔ list[i, m + 3]
            <u>end</u>;
         <u>if</u> checked ≠ list length + 1 <u>then</u>
         <u>begin</u>
            outstrint(“checking prematurely terminated after ”, checked - 1);
            outstrintnl(“rather than ”, list length)
         <u>end</u>
      <u>end</u>
   <u>end</u> checklist;

   <u>procedure</u> check free(addr, n, num free);
      <u>comment</u> check that addr has the correct number of free locations
        which should be equal to num free;
      <u>integer</u> <u>array</u> addr;
        <u>integer</u> n, num free;
   <u>begin</u>
      <u>integer</u> i, count;
      count ≔ 0;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
           <u>if</u> addr[i] ≠ 0 <u>then</u> <u>begin</u>
            count ≔ count + 1
         <u>end</u>;
      <u>if</u> count ≠ num free <u>then</u>
      <u>begin</u>
         outstrint(“Incorrect number of free locations: ”, count);
         outstrintnl(“ which should be ”, num free)
      <u>end</u>
   <u>end</u> check free;

   <u>procedure</u> clear list(m, list, n, first, addr, list length);
      <u>comment</u> Clear the list sorted from 1 ... list length using
        Algorithm 101: outlist;
      <u>integer</u> <u>array</u> addr, list;
        <u>integer</u> first, m, n, list length;
   <u>begin</u>
      <u>comment</u> Variables for the outlist procedure;
      <u>integer</u> <u>array</u> vector[1:m + 1];

      <u>integer</u> i;

      outstring(1, “Clearing the list.\n”);
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> list length <u>do</u>
         <u>begin</u>
            outlist(vector, m, list, n, first, flag, addr);
            check free(addr, n, i)
         <u>end</u>;
      <u>if</u> list[1, m+2] ≠ flag <u>then</u>
      <u>begin</u>
         outstrintnl(“list[1, m+2] has incorrect v̲a̲l̲u̲e̲ f̲o̲r̲ an empty list: ”, list[1, m+2])
      <u>end</u>
   <u>end</u> clear list;

   <u>procedure</u> outstrint(str, i);
      <u>comment</u> output str following by i;
      <u>string</u> str;
        <u>integer</u> i;
   <u>begin</u>
      outstring(1, str); outinteger(1, i)
   <u>end</u> outstrint;

   <u>procedure</u> outstrintnl(str, i);
      <u>comment</u> output str following by i and terminate with a newline;
      <u>string</u> str;
        <u>integer</u> i;
   <u>begin</u>
      outstrint(str, i);
      outstring(1, “\n”)
   <u>end</u> outstrintnl;

   <u>comment</u> Beginning of the test program;

   <u>comment</u> Variables for the inlist procedure;
   <u>integer</u> n, m, first, flag;
   <u>comment</u> info[1:m], list[1:n, 1:m+3], addr[1:n];
   <u>comment</u> 100 must be &lt;= n, and 4 should be &gt;= m;
   <u>integer</u> <u>array</u> info[1:4], list[1:100, 1:4+3], addr[1:100];

   <u>comment</u> Variables for the test procedure;
   <u>integer</u> i, j;
   <u>comment</u> 100 must be &lt;= n;
   <u>integer</u> <u>array</u> tvalues[1:100];

   <u>comment</u> capacity of the list;
   n ≔ 4;
   <u>comment</u> amount of data associated with each key;
   m ≔ 4;
   <u>comment</u> index of the head item in list.
     first := -1;
   <u>comment</u> end-of-list marker;
   flag ≔ -1;

   <u>comment</u> set up the list of free locations;
   <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
      <u>begin</u>
         addr[i] ≔ i
      <u>end</u>;

   <u>comment</u> Set necessary initial terminating marker;
   list[1, m + 2] ≔ flag;

   <u>comment</u> Set up the tvalues to be used;
   tvalues[1] ≔ 7;
   tvalues[2] ≔ 13;
   tvalues[3] ≔ 6;
   tvalues[4] ≔ 11;

   <u>if</u> list[1, 2] ≠ 1 <u>then</u>
   <u>begin</u>
      <u>comment</u> The following is needed to avoid subscript error if list[1,2] != 0;
      list[1, m + 3] ≔ flag
   <u>end</u>;

   fill list(tvalues, info, m, list, n, first, flag, addr, listfull);

   <u>comment</u> try to fill the list beyond its capacity.
     This should fail with a jump to listfull;
   inlist(n + 1, info, m, list, n, first, flag, addr, listfull);
   outstring(1, “Failed t̲o̲ correctly detect attempt t̲o̲ add too many entries.\n”);
   <u>go</u> <u>to</u> testend;
   listfull:
   <u>if</u> i = (n+1) <u>then</u>
   <u>begin</u>
      outstrintnl(“List full correctly detected when attempting t̲o̲ add entry ”, i)
   <u>end</u>;
   testend:
   clear list(m, list, n, first, addr, n)
<u>end</u>
</pre></body></html>
