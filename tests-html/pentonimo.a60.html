<html><head><meta charset="utf-8"></head><body><pre><u>begin</u> <u>comment</u> pentomino, 130968;
   <u>integer</u> score, nummer, lengte, breedte, aantal stenen, aantal standen,
     i, j, k, teller, lb;
   lengte ≔ read; breedte ≔ read; aantal stenen ≔ read; aantal standen ≔ read;
   lb ≔ lengte × (breedte - 1);
   <u>begin</u> <u>integer</u> <u>array</u> bord [-39 : 100],
        standen, wijzer [1 : aantal stenen],
        informatie [1 : 8 × aantal standen];
      <u>boolean</u> <u>array</u> ongebruikt [1 : aantal stenen];
      <u>procedure</u> output;
      <u>begin</u> <u>integer</u> i, j;
         score ≔ score + 1;
         SPACE (1);
         <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> 2 × lengte <u>do</u>
            <u>begin</u> PRINTTEXT (“-”); SPACE (1) <u>end</u> ;
         <u>comment</u> ABSFIXT (6, 2, time); NLCR;
         <u>for</u> i ≔ 0 <u>step</u> lengte <u>until</u> lb <u>do</u>
            <u>begin</u> PRINTTEXT(“I”);
               <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> lengte - 1 <u>do</u>
                  <u>begin</u> SPACE (3);
                     <u>if</u> bord [i + j] ≠ bord [i + j + 1]
                       <u>then</u> PRINTTEXT(“I”) <u>else</u> SPACE (1)
                  <u>end</u> ;
               SPACE (3); PRINTTEXT(“I”); NLCR; SPACE (1);
               <u>if</u> i &lt; lb
                 <u>then</u> <u>begin</u> <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> lengte <u>do</u>
                     <u>begin</u> <u>if</u> bord [i + j] ≠ bord [i + j + lengte]
                          <u>then</u> <u>begin</u> PRINTTEXT (“-”); SPACE (1);
                           PRINTTEXT (“-”); SPACE (1)
                        <u>end</u>
                        <u>else</u> SPACE (4)
                     <u>end</u>
               <u>end</u>
               <u>else</u> <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> 2 × lengte <u>do</u>
                  <u>begin</u> PRINTTEXT (“-”); SPACE (1) <u>end</u> ;
               NLCR
            <u>end</u> ;
         NLCR; NLCR;
         <u>if</u> score = 7 <u>then</u> <u>goto</u> ex
      <u>end</u> output;
      <u>procedure</u> up (veld, kolom); <u>value</u> veld, kolom;
         <u>integer</u> veld, kolom;
      <u>begin</u> <u>integer</u> i, j, k, r, w, steen, aantal;
         nummer ≔ nummer + 1;
         <u>for</u> steen ≔ 1 <u>step</u> 1 <u>until</u> aantal stenen <u>do</u>
              <u>if</u> ongebruikt [steen] <u>then</u>
            <u>begin</u> ongebruikt [steen] ≔ <u>false</u> ;
               bord [veld] ≔ steen;
               aantal ≔ standen [steen] - 1;
               <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> aantal <u>do</u>
                  <u>begin</u> w ≔ wijzer [steen] + 4 × i;
                     <u>if</u> bord [informatie [w] + veld] = 0 <u>then</u>
                     <u>begin</u> <u>if</u> bord [informatie [w + 1] + veld] = 0 <u>then</u>
                        <u>begin</u> <u>if</u> bord [informatie [w + 2] + veld] = 0 <u>then</u>
                           <u>begin</u> <u>if</u> bord [informatie [w + 3] + veld] = 0 <u>then</u>
                              <u>begin</u> <u>for</u> j ≔ 0, 1, 2, 3 <u>do</u>
                                      bord [informatie [w + j] + veld] ≔ steen;
                                 <u>if</u> nummer = aantal stenen
                                   <u>then</u> output
                                 <u>else</u>
                                 <u>begin</u>
                                    <u>for</u> k ≔ kolom <u>step</u> 1 <u>until</u> lengte <u>do</u>
                                         <u>for</u> r ≔ 0 <u>step</u> lengte <u>until</u> lb <u>do</u>
                                            <u>if</u> bord [r + k] = 0 <u>then</u> <u>goto</u> beet;
                                    beet: up (r + k, k)
                                 <u>end</u> ;
                                 <u>for</u> j ≔ 0, 1, 2, 3 <u>do</u>
                                      bord [informatie [w + j] + veld] ≔ 0
                              <u>end</u>
                           <u>end</u>
                        <u>end</u>
                     <u>end</u>
                  <u>end</u> ;
               ongebruikt [steen] ≔ <u>true</u>
            <u>end</u> ;
         bord [veld] ≔ 0;
         down: nummer ≔ nummer - 1
      <u>end</u> up;
      teller ≔ 1;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> aantal stenen <u>do</u>
         <u>begin</u> j ≔ read; <u>comment</u> steennummer, wordt niet gebruikt;
            wijzer [i] ≔ teller; standen [i] ≔ read;
            <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> standen [i] <u>do</u>
               <u>begin</u> <u>for</u> k ≔ 0 <u>step</u> 1 <u>until</u> 3 <u>do</u>
                       informatie [teller + k] ≔ read;
                  teller ≔ teller + 4
               <u>end</u>
         <u>end</u> ;
      <u>for</u> i ≔ - 39 <u>step</u> 1 <u>until</u> 0,
        61 <u>step</u> 1 <u>until</u> 100 <u>do</u> bord [i] ≔ - 1;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> 60 <u>do</u> bord [i] ≔ 0;
      <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> aantal stenen <u>do</u>
           ongebruikt [i] ≔ <u>true</u> ;
      score ≔ nummer ≔ 0;
      NLCR; PRINTTEXT (“The first 7 solutions:”); NLCR; NLCR; NLCR;
      up (1, 1);
      ex:
   <u>end</u>
<u>end</u>
</pre></body></html>
