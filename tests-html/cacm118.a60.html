<html><head><meta charset="utf-8"></head><body><pre><u>procedure</u> magicodd (n, x); <u>value</u> n; <u>integer</u> n; <u>integer</u>
     <u>array</u> x;
     <u>comment</u> for given side n the procedure generates a magic
     square of the integers 1 - n^2. For the method of De la
     Loubere, see M. Kraitchik, "Mathematical Recreations," p.
     149. n must be odd and n &gt;= 3;
<u>begin</u> <u>integer</u> i, j, k;
   <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
        <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u> x[i,j] ≔ 0;
   i ≔ (n + 1) / 2; j ≔ n;
   <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> n × n <u>do</u> <u>begin</u>
         <u>if</u> x[i,j] ≠ 0 <u>then</u> <u>begin</u> i ≔ i - 1; j ≔ j - 2;
            <u>if</u> i &lt; 1 <u>then</u> i ≔ i + n; <u>if</u> j &lt; 1 <u>then</u> j ≔ j + n <u>end</u>;
         x[i, j] ≔ k;
         i ≔ i + 1; <u>if</u> i &gt; n <u>then</u> i ≔ i - n;
         j ≔ j + 1; <u>if</u> j &gt; n <u>then</u> j ≔ j - n
      <u>end</u>
<u>end</u> magicodd;
</pre></body></html>
