<html><head><meta charset="utf-8"></head><body><pre><u>begin</u>
   <u>integer</u> year, month, day;

   <u>integer</u> <u>procedure</u> easter sunday(y);
      <u>value</u> y; <u>integer</u> y;
   <u>begin</u> <u>integer</u> a, b, c, h;
      <u>integer</u> <u>procedure</u> rem(a, b);
         <u>value</u> a, b; <u>integer</u> a, b;
      <u>begin</u> <u>integer</u> c;
         c ≔ a / b; rem ≔ a - b × c
      <u>end</u> rem;

      a ≔ rem(y, 19);
      b ≔ y / 100; c ≔ rem(y, 100);
      h ≔ rem((19 × a + b - b / 4 + 15 - ((8 × b + 13) / 25)), 30);
      c ≔ rem((2 × (rem(b, 4) + c / 4) - h - rem(c, 4) + 32), 7);
      h ≔ h + c - 7 × ((a + 11 × h + 19 × c) / 433);
      a ≔ (h + 90) / 25;
      easter sunday ≔ rem(100 × a + (33 × a + h + 19), 32)
   <u>end</u> easter sunday;

   <u>for</u> year ≔ 2010 <u>step</u> 1 <u>until</u> 2029 <u>do</u>
      <u>begin</u>
         day ≔ easter sunday(year);
         month ≔ day / 100; day ≔ day - 100 × month;

         write text(30, “c”);
         write(30, format(“ndddd”), year);
         <u>if</u> month = 3 <u>then</u> write text(30, “ March”) <u>else</u> write text(30, “ April”);
         write(30, format(“ndd”), day)
      <u>end</u>
<u>end</u>



</pre></body></html>
