<html><head><meta charset="utf-8"></head><body><pre><u>real</u> <u>procedure</u> INNERPRODUCT(u, v, k, s, f);
   <u>value</u> s, f; <u>integer</u> k, s, f; <u>real</u> u, v;
     <u>comment</u>;
<u>begin</u>
   <u>real</u> h;
   h ≔ 0; <u>for</u> k ≔ s <u>step</u> 1 <u>until</u> f <u>do</u> h ≔ h + u × v;
   INNERPRODUCT ≔ h
<u>end</u> INNERPRODUCT;
<u>procedure</u> CROUT(A, b, n, y, pivot, INNERPRODUCT);
   <u>value</u> n; <u>array</u> A, b, y; <u>integer</u> n; <u>integer</u> <u>array</u> pivot;
     <u>real</u> <u>procedure</u> INNERPRODUCT;
     <u>comment</u>;
<u>begin</u>
   <u>integer</u> k, i, j, imax, p; <u>real</u> TEMP, quot;
   <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
      <u>begin</u>
         TEMP ≔ 0;
         <u>for</u> i ≔ k <u>step</u> 1 <u>until</u> n <u>do</u>
            <u>begin</u>
               A[i, k] ≔ A[i, k] - INNERPRODUCT(A[i,p], A[p,k],
                 p, 1, k - 1);
               <u>if</u> abs(A[i,k]) &gt; TEMP <u>then</u>
               <u>begin</u>
                  TEMP ≔ abs(A[i, k]); imax ≔ i
               <u>end</u>
            <u>end</u>;
         pivot[k] ≔ imax;
         <u>comment</u>;
         <u>if</u> imax ≠ k <u>then</u>
         <u>begin</u> <u>for</u> j ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
               <u>begin</u>
                  TEMP ≔ A[k,j]; A[k,j] ≔ A[imax,j];
                  A[imax,j] ≔ TEMP
               <u>end</u>;
            TEMP ≔ b[k]; b[k] ≔ b[imax]; b[imax] ≔ TEMP
         <u>end</u>;
         <u>comment</u>;
         <u>if</u> A[k,k] = 0 <u>then</u> <u>go</u> <u>to</u> singular;
         <u>for</u> i ≔ k+1 <u>step</u> 1 <u>until</u> n <u>do</u>
            <u>begin</u> quot ≔ 1·0/A[k,k]; A[i,k] ≔ quot × A[i,k]
            <u>end</u>;
         <u>for</u> j ≔ k + 1 <u>step</u> 1 <u>until</u> n <u>do</u>
              A[k, j] ≔ A[k, j] - INNERPRODUCT(A[k,p],
              A[p,j], p, 1, k - 1);
         b[k] ≔ b[k] - INNERPRODUCT(A[k, p], b[p], p,
           1, k - 1)
      <u>end</u>;
   <u>comment</u>;
   <u>for</u> k ≔ n <u>step</u> -1 <u>until</u> 1 <u>do</u>
        y[k] ≔ (b[k] - INNERPRODUCT(A[k,p], y[p], p, k + 1, n))/A[k, k];
   singular:
<u>end</u> CROUT;

<u>procedure</u> SOLVE(B, c, n, z, pivot, INNERPRODUCT);
   <u>value</u> n; <u>array</u> B, c, z; <u>integer</u> n; <u>integer</u> <u>array</u> pivot;
     <u>real</u> <u>procedure</u> INNERPRODUCT;
     <u>comment</u>;
<u>begin</u>
   <u>integer</u> k, p; <u>real</u> TEMP;
   <u>for</u> k ≔ 1 <u>step</u> 1 <u>until</u> n <u>do</u>
      <u>begin</u>
         TEMP ≔ c[pivot[k]]; c[pivot[k]] ≔ c[k]; c[k] ≔            TEMP; c[k] ≔ c[k] - INNERPRODUCT(B[k, p],
           c[p], p, 1, k - 1)
      <u>end</u>;
   <u>for</u> k ≔ n <u>step</u> -1 <u>until</u> 1 <u>do</u>
        z[k] ≔ (c[k] - INNERPRODUCT(B[k,p], z[p], p,
        k+1, n))/B[k,k]
<u>end</u>;
</pre></body></html>
