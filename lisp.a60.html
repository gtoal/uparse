<html><head><meta charset="utf-8"></head><body><pre><u>begin</u> <u>comment</u> ALGOL 60 version of program lisp(input,output).
     *** version 1, March 28, l988 ***
     *** author: F.E.J. Kruseman Aretz ***
     *** Philips Research Laboratory Eindhoven ***;
   <u>integer</u> maxidf,maxnbr,maxstruct; maxidf ≔ 200; maxnbr ≔ 200; maxstruct ≔ 2000;
   <u>begin</u>
      <u>integer</u> sym,shift,lastidf,lastnbr,d24,d25,free,indentation,linewidth,dummy,
        f,
        args,
        p,
        id,
        olp,
        t,
        nilv,
        quote,
        cond,
        lambda,
        define,
        car,
        cdr,
        cons,
        equal,
        atom,
        numberp,
        lessp,
        greaterp,
        add1,
        sub1,
        add,
        minus,
        timesv,
        divf;
      <u>integer</u> <u>array</u> idf[0:maxidf,0:9],alist[0:maxidf];
      <u>real</u> <u>array</u> nbr[0:maxnbr];
      <u>integer</u> <u>array</u> a,d[1:maxstruct];
      <u>boolean</u> <u>array</u> m[1:maxstruct];
      <u>comment</u> *** error handling ***;
      <u>procedure</u> errorhandler(errorstring); <u>string</u> errorstring;
      <u>begin</u> NLCR; NLCR; PRINTTEXT(“+++ error: ”); PRINTTEXT(errorstring);
         <u>goto</u> ex;
      <u>end</u> errorhandler;
      <u>comment</u> *** representation dependent functions ***;
      <u>procedure</u> collect garbage;
      <u>begin</u> <u>integer</u> i,j;
         free ≔ 0;
         <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> maxstruct <u>do</u> m[i] ≔ <u>true</u>;
         NLCR; PRINTTEXT(“garbage collector: ”);
         mark(olp);
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> lastidf - 1 <u>do</u> mark(alist[i]);
         <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> maxstruct <u>do</u>
              <u>if</u> m[i] <u>then</u> <u>begin</u> a[i] ≔ free; free ≔ i <u>end</u>;
         <u>if</u> free = 0 <u>then</u> errorhandler(“free list exhausted”);
         i ≔ 1; j ≔ free;
         <u>for</u> j ≔ carf(j) <u>while</u> j ≠ 0 <u>do</u> i ≔ i + 1;
         ABSFIXT(4,0,i); NLCR
      <u>end</u> collect garbage;
      <u>procedure</u> mark(ref); <u>value</u> ref; <u>integer</u> ref;
      <u>begin</u>
         work: <u>if</u> ref &lt; d24
           <u>then</u> <u>begin</u> <u>if</u> m[ref]
              <u>then</u> <u>begin</u> m[ref] ≔ <u>false</u>;
               mark(a[ref]); ref ≔ d[ref]; <u>goto</u> work
            <u>end</u>
         <u>end</u>
      <u>end</u> mark;
      <u>integer</u> <u>procedure</u> createidf;
      <u>begin</u> <u>integer</u> i,j;
         i ≔ 0;
         <u>for</u> dummy ≔ 0 <u>while</u> i &lt; lastidf <u>do</u>
            <u>begin</u> <u>for</u> j ≔ 0 <u>step</u> 1 <u>until</u> 9 <u>do</u>
                    <u>if</u> idf[lastidf,j] ≠ idf[i,j] <u>then</u> <u>goto</u> diff;
               <u>goto</u> old;
               diff: i ≔ i + 1
            <u>end</u>;
         new: i ≔ lastidf; alist[i] ≔ nilv; lastidf ≔ lastidf + 1;
         <u>if</u> lastidf = maxidf <u>then</u>
         <u>begin</u> <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 99 <u>do</u>
               <u>begin</u> NLCR; write(d25+i) <u>end</u>;
            errorhandler(“too much identifiers”)
         <u>end</u>;
         old: createidf ≔ d25 + i
      <u>end</u> createidf;
      <u>integer</u> <u>procedure</u> createnum(x); <u>real</u> x;
      <u>begin</u> <u>integer</u> i;
         nbr[last nbr] ≔ x; i ≔ 0;
         <u>for</u> dummy ≔ 0 <u>while</u> i &lt; last nbr <u>do</u>
            <u>begin</u> <u>if</u> nbr[last nbr] = nbr[i] <u>then</u> <u>goto</u> old;
               i ≔ i + 1
            <u>end</u>;
         new: i ≔ last nbr; last nbr ≔ last nbr + 1;
         <u>if</u> last nbr = maxnbr <u>then</u> errorhandler(“too much numbers”);
         old: createnum ≔ d24 + i
      <u>end</u> createnum;
      <u>boolean</u> <u>procedure</u> atomf(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> atomf ≔ x &gt; d24 <u>end</u> atomf;
      <u>boolean</u> <u>procedure</u> numberpf(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> numberpf ≔ x &gt; d24 ∧ x &lt; d25 <u>end</u> numberpf;
      <u>procedure</u> getidfval(x,idf); <u>value</u> x; <u>integer</u> x,idf;
      <u>begin</u> idf ≔ x - d25 <u>end</u> getidfval;

      <u>real</u> <u>procedure</u> numval(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> numval ≔ br[ x - d24] <u>end</u> numval;
      <u>integer</u> <u>procedure</u> carf(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>if</u> x &gt; d24
           <u>then</u> errorhandler(“car undefined for atomic lisp value”);
         carf ≔ a[x]
      <u>end</u> carf;
      <u>integer</u> <u>procedure</u> cdrf(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>if</u> x &gt; d24
           <u>then</u> errorhandler(“cdr undefined for atomic lisp value”);
         cdrf ≔ d[x]
      <u>end</u> cdrf;
      <u>integer</u> <u>procedure</u> consf(x,y);
         <u>value</u> x,y; <u>integer</u> x,y;
      <u>begin</u> <u>integer</u> n;
         <u>if</u> free = 0 <u>then</u> collect garbage;
         n ≔ free; free ≔ a[free];
         a[n] ≔ x; d[n] ≔ y; consf ≔ n
      <u>end</u> consf;
      <u>procedure</u> returncell(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> a[x] ≔ free; free ≔ x <u>end</u>;
      <u>procedure</u> returnlist(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>for</u> dummy ≔ 0 <u>while</u> x ≠ nilv <u>do</u>
            <u>begin</u> returncell(x); x ≔ d[x] <u>end</u>
      <u>end</u> returnlist;
      <u>procedure</u> recycle(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>for</u> dummy ≔ 0 <u>while</u> ¬ atomf(x) <u>do</u>
            <u>begin</u> recycle(a[x]); returncell(x); x ≔ d[x] <u>end</u>
      <u>end</u> recycle;
      <u>boolean</u> <u>procedure</u> equalf(x,y);
         <u>value</u> x,y; <u>integer</u> x,y;
      <u>begin</u> <u>switch</u> s ≔ str,num,id;
         work:
         <u>if</u> x ÷ d24 = y ÷ d24
           <u>then</u> <u>begin</u> <u>goto</u> s[x ÷ d24 + 1];
            id: num: equalf ≔ x = y; <u>goto</u> ex;
            str: <u>if</u> equalf(a[x],a[y])
              <u>then</u> <u>begin</u> x ≔ d[x]; y ≔ d[y];
               <u>goto</u> work
            <u>end</u>
            <u>else</u> equalf ≔ <u>false</u>
         <u>end</u>
         <u>else</u> equalf ≔ <u>false</u>;
         ex:
      <u>end</u> equalf;
      <u>comment</u> *** input procedures ***;
      <u>integer</u> <u>procedure</u> RESYM;
      <u>begin</u> <u>integer</u> s;
         s ≔ read;
         <u>if</u> s = 122 ∨ s = 124 <u>then</u> <u>begin</u> shift ≔ s;
            RESYM ≔ RESYM
         <u>end</u> <u>else</u>
         <u>if</u> s = 16 <u>then</u> RESYM ≔ 93 <u>else</u>
         <u>if</u> s = 26 <u>then</u> RESYM ≔ 119 <u>else</u>
         <u>if</u> s = 8 ∧ shift = 124 <u>then</u> RESYM ≔ 98 <u>else</u>
         <u>if</u> s = 25 ∧ shift = 124 <u>then</u> RESYM ≔ 99 <u>else</u>
         <u>if</u> s = 107 <u>then</u> RESYM ≔ 88 <u>else</u>
         <u>if</u> s = 32 <u>then</u> RESYM ≔ 0 <u>else</u>
         <u>begin</u> s ≔ s ÷ 32×32 + s - s ÷ 16×16;
            <u>if</u> s = 0 <u>then</u> errorhandler(“eof”);
            RESYM ≔ <u>if</u> s &lt; 10 <u>then</u> s <u>else</u>
              <u>if</u> s &lt; 64 <u>then</u> s - 6 <u>else</u>
              <u>if</u> s &lt; 96 <u>then</u> s - 46 <u>else</u> s - 87
         <u>end</u>
      <u>end</u> RESYM;
      <u>procedure</u> nextsym;
      <u>begin</u> sym ≔ RESYM; PRSYM(sym) <u>end</u> nextsym;
      <u>procedure</u> skipspaces;
      <u>begin</u> <u>for</u> dummy ≔ 0
           <u>while</u> sym = 93 ∨ sym = 118 ∨ sym = 119 <u>do</u> nextsym
      <u>end</u> skipspaces;
      <u>integer</u> <u>procedure</u> number;
      <u>begin</u> <u>real</u> x; <u>boolean</u> signed;
         x ≔ 0; signed ≔ (sym = 65);
         <u>if</u> signed
           <u>then</u> <u>begin</u> nextsym;
            <u>if</u> sym &gt; 9 <u>then</u> errorhandler(“digit expected in input”)
         <u>end</u>;
         <u>for</u> dummy ≔ 0 <u>while</u> sym &lt; 10 <u>do</u>
            <u>begin</u> x ≔ 10 × x + sym; nextsym <u>end</u>;
         number ≔ createnum(<u>if</u> signed <u>then</u> -x <u>else</u> x)
      <u>end</u> number;
      <u>integer</u> <u>procedure</u> identifier;
      <u>begin</u> <u>integer</u> i;
         idf[lastidf,0] ≔ sym; nextsym;
         <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> 9 <u>do</u> idf[lastidf,i] ≔ 93;
         i ≔ 0;
         <u>for</u> dummy ≔ 0 <u>while</u> sym &lt; 64 ∧ i &lt; 9 <u>do</u>
            <u>begin</u> i ≔ i + 1; idf[lastidf,i] ≔ sym; nextsym <u>end</u>;
         <u>for</u> dummy ≔ 0 <u>while</u> sym &lt; 64 <u>do</u> nextsym;
         identifier ≔ createidf
      <u>end</u> identifier;
      <u>integer</u> <u>procedure</u> nextitem;
      <u>begin</u> <u>integer</u> lv,op;
         skipspaces;
         <u>if</u> sym &lt; 10 ∨ sym = 65 <u>then</u> nextitem ≔ number
           <u>else</u>
         <u>if</u> sym &lt; 64 <u>then</u> nextitem ≔ identifier
           <u>else</u>
         <u>if</u> sym = 98
           <u>then</u> <u>begin</u> nextsym; skipspaces;
            <u>if</u> sym = 99
              <u>then</u> <u>begin</u> nextitem ≔ nilv; nextsym <u>end</u>
            <u>else</u> <u>begin</u> op ≔ olp; olp ≔ consf(nilv,op);
               lv ≔ a[olp] ≔ consf(nilv,nilv); nextitem ≔ lv;
               a[lv] ≔ nextitem; skipspaces;
               <u>if</u> sym = 88
                 <u>then</u> <u>begin</u> nextsym; d[lv] ≔ nextitem;
                  skipspaces;
                  <u>if</u> sym ≠ 99
                    <u>then</u> errorhandler
                    (“close missing for dotted pair in input”)
               <u>end</u>
               <u>else</u> <u>for</u> dummy ≔ 0 <u>while</u> sym ≠ 99 <u>do</u>
                  <u>begin</u> lv ≔ d[lv] ≔ consf(nilv,nilv);
                     a[lv] ≔ nextitem; skipspaces
                  <u>end</u>;
               nextsym;
               olp ≔ op
            <u>end</u>;
         <u>end</u>
           <u>else</u>
         <u>if</u> sym = 120
           <u>then</u> <u>begin</u> nextsym;
            op ≔ olp; olp ≔ consf(nilv,olp);
            lv ≔ a[olp] ≔ consf(nilv,nilv); nextitem ≔ lv;
            a[lv] ≔ quote; lv ≔ d[lv] ≔ consf(nilv,nilv); a[lv] ≔ nextitem;
            olp ≔ op
         <u>end</u>
         <u>else</u> errorhandler(“illegal symbol in input”)
      <u>end</u> nextitem;
      <u>comment</u> *** output procedures ***;
      <u>procedure</u> PRSYM(sym); <u>value</u> sym; <u>integer</u> sym;
      <u>begin</u> <u>switch</u> sw ≔ a0,a1,a2,a3,a4,a5,a6,a7,a8,a9,
           a,b,c,d,e,f,g,h,i,j,k,l,m,
           n,o,p,q,r,s,t,u,v,w,x,y,z;
         <u>if</u> sym = 93 <u>then</u> SPACE(1) <u>else</u>
         <u>if</u> sym = 88 <u>then</u> PRINTTEXT(“.”) <u>else</u>
         <u>if</u> sym = 98 <u>then</u> PRINTTEXT(“(”) <u>else</u>
         <u>if</u> sym = 99 <u>then</u> PRINTTEXT(“)”) <u>else</u>
         <u>if</u> sym = 119 <u>then</u> NLCR <u>else</u>
         <u>begin</u> <u>if</u> sym &gt; 35
              <u>then</u> errorhandler(“illegal output symbol”);
            <u>goto</u> sw[sym+1];
            a0: PRINTTEXT(“0”); <u>goto</u> ex;
            a1: PRINTTEXT(“1”); <u>goto</u> ex;
            a2: PRINTTEXT(“2”); <u>goto</u> ex;
            a3: PRINTTEXT(“3”); <u>goto</u> ex;
            a4: PRINTTEXT(“4”); <u>goto</u> ex;
            a5: PRINTTEXT(“5”); <u>goto</u> ex;
            a6: PRINTTEXT(“6”); <u>goto</u> ex;
            a7: PRINTTEXT(“7”); <u>goto</u> ex;
            a8: PRINTTEXT(“8”); <u>goto</u> ex;
            a9: PRINTTEXT(“9”); <u>goto</u> ex;
            a: PRINTTEXT(“a”); <u>goto</u> ex;
            b: PRINTTEXT(“b”); <u>goto</u> ex;
            c: PRINTTEXT(“c”); <u>goto</u> ex;
            d: PRINTTEXT(“d”); <u>goto</u> ex;
            e: PRINTTEXT(“e”); <u>goto</u> ex;
            f: PRINTTEXT(“f”); <u>goto</u> ex;
            g: PRINTTEXT(“g”); <u>goto</u> ex;
            h: PRINTTEXT(“h”); <u>goto</u> ex;
            i: PRINTTEXT(“i”); <u>goto</u> ex;
            j: PRINTTEXT(“j”); <u>goto</u> ex;
            k: PRINTTEXT(“k”); <u>goto</u> ex;
            l: PRINTTEXT(“l”); <u>goto</u> ex;
            m: PRINTTEXT(“m”); <u>goto</u> ex;
            n: PRINTTEXT(“n”); <u>goto</u> ex;
            o: PRINTTEXT(“o”); <u>goto</u> ex;
            p: PRINTTEXT(“p”); <u>goto</u> ex;
            q: PRINTTEXT(“q”); <u>goto</u> ex;
            r: PRINTTEXT(“r”); <u>goto</u> ex;
            s: PRINTTEXT(“s”); <u>goto</u> ex;
            t: PRINTTEXT(“t”); <u>goto</u> ex;
            u: PRINTTEXT(“u”); <u>goto</u> ex;
            v: PRINTTEXT(“v”); <u>goto</u> ex;
            w: PRINTTEXT(“w”); <u>goto</u> ex;
            x: PRINTTEXT(“x”); <u>goto</u> ex;
            y: PRINTTEXT(“y”); <u>goto</u> ex;
            z: PRINTTEXT(“z”)
         <u>end</u>;
         ex:
      <u>end</u> PRSYM;
      <u>procedure</u> analyse(x,r); <u>value</u> x; <u>integer</u> x,r;
      <u>begin</u> <u>integer</u> n,l; <u>boolean</u> simple;
         <u>if</u> numberpf(x)
           <u>then</u> <u>begin</u> <u>real</u> dg,v,absv;
            v ≔ numval(x);
            <u>if</u> v &gt; 0
              <u>then</u> <u>begin</u> absv ≔ v; l ≔ 1 <u>end</u>
            <u>else</u> <u>begin</u> absv ≔ - v;l ≔ 2 <u>end</u>;
            dg ≔ 10;
            <u>for</u> dummy ≔ 0 <u>while</u> dg &lt; absv <u>do</u>
               <u>begin</u> l ≔ l + 1; dg ≔ 10 × dg <u>end</u>;
            r ≔ createnum(l)
         <u>end</u>
           <u>else</u>
         <u>if</u> atomf(x)
           <u>then</u> <u>begin</u> getidfval(x,id); n ≔ 10;
            <u>for</u> dummy ≔ 0 <u>while</u> idf[id,n-1] = 93 <u>do</u> n ≔ n - 1;
            r ≔ createnum(n)
         <u>end</u>
           <u>else</u>
         <u>if</u> islist(x)
           <u>then</u> <u>begin</u> indentation ≔ indentation + 1;
            analyselist(x,r,l,simple);
            indentation ≔ indentation - 1;
            <u>if</u> simple ∧ indentation + l &lt; linewidth
              <u>then</u> <u>begin</u> recycle(r); r ≔ createnum(l) <u>end</u>
         <u>end</u>
         <u>else</u> <u>begin</u> indentation ≔ indentation + 1;
            olp ≔ consf(nilv,olp);
            r ≔ a[olp] ≔ consf(nilv,nilv);
            analyse(carf(x),a[r]); analyse(cdrf(x),d[r]);
            indentation ≔ indentation - 1;
            <u>if</u> atomf(a[r]) ∧ atomf(d[r])
              <u>then</u> <u>begin</u> l ≔ numval(carf(r)) + numval(cdrf(r)) + 5;
               <u>if</u> indentation + l &lt; linewidth
                 <u>then</u> <u>begin</u> recycle(r); r ≔ createnum(l) <u>end</u>
            <u>end</u>;
            returncell(olp); olp ≔ d[olp]
         <u>end</u>
      <u>end</u> analyse;
      <u>procedure</u> analyselist(x,r,l,simple);
         <u>value</u> x; <u>integer</u> x,r,l; <u>boolean</u> simple;
      <u>begin</u> <u>if</u> x = nilv
           <u>then</u> <u>begin</u> r ≔ nilv; l ≔ 1; simple ≔ <u>true</u> <u>end</u>
         <u>else</u> <u>begin</u> olp ≔ consf(nilv, olp);
            r ≔ a[olp] ≔ consf(nilv,nilv);
            analyse(carf(x),a[r]); analyselist(cdrf(x),d[r],l,simple);
            <u>if</u> simple ∧ atomf(a[r])
              <u>then</u> l ≔ numval(a[r]) + l + 1
            <u>else</u> simple ≔ <u>false</u>;
            returncell(olp); olp ≔ d[olp]
         <u>end</u>
      <u>end</u> analyselist;
      <u>boolean</u> <u>procedure</u> islist(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u>
         work: <u>if</u> atomf(x)
           <u>then</u> islist ≔ equalf(x,nilv)
         <u>else</u> <u>begin</u> x ≔ cdrf(x); <u>goto</u> work <u>end</u>
      <u>end</u> islist;
      <u>procedure</u> writenumber(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>integer</u> n,d,v;
         v ≔ numval(x);
         <u>if</u> v &lt; 0 <u>then</u> v ≔ - v;
         d ≔ 10;
         <u>for</u> dummy ≔ 0 <u>while</u> d &lt; v <u>do</u> d ≔ d × 10;
         <u>for</u> d ≔ d ÷ 10 <u>while</u> d &gt; 0·5 <u>do</u>
            <u>begin</u> n ≔ v ÷ d; PRSYM(n); v ≔ v - d × n <u>end</u>
      <u>end</u> writenumber;
      <u>procedure</u> writeidentifier(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>integer</u> i;
         getidfval(x,id);
         <u>for</u> i ≔ 0 <u>step</u> 1 <u>until</u> 9 <u>do</u>
              <u>if</u> idf[id,i] ≠ 93 <u>then</u> PRSYM(idf[id,i])
      <u>end</u> writeidentifier;
      <u>procedure</u> writelist(x,r); <u>value</u> x,r; <u>integer</u> x,r;
      <u>begin</u> <u>integer</u> a,ind; <u>boolean</u> simple,nl;
         PRSYM(98);
         <u>if</u> atomf(r)
           <u>then</u> <u>begin</u> <u>for</u> dummy ≔ 0 <u>while</u> x ≠ nilv <u>do</u>
               <u>begin</u> writevalue(carf(x),r); x ≔ cdrf(x);
                  <u>if</u> x ≠ nilv <u>then</u> PRSYM(93)
               <u>end</u>
         <u>end</u>
         <u>else</u> <u>begin</u> indentation ≔ indentation + 1; ind ≔ indentation;
            <u>for</u> dummy ≔ 0 <u>while</u> x ≠ nilv <u>do</u>
               <u>begin</u> a ≔ carf(r);
                  simple ≔ atomf(a);
                  <u>if</u> simple
                    <u>then</u> nl ≔ numval(a) + indentation &gt; linewidth
                  <u>else</u> nl ≔ indentation &gt; ind;
                  <u>if</u> nl
                    <u>then</u> <u>begin</u> indentation ≔ ind;
                     NLCR; SPACE(ind)
                  <u>end</u>
                    <u>else</u> <u>if</u> indentation &gt; ind <u>then</u> PRSYM(93);
                  writevalue(carf(x),a);
                  <u>if</u> simple
                    <u>then</u> indentation ≔ indentation + numval(a) + 1
                  <u>else</u> indentation ≔ linewidth + 1;
                  x ≔ cdrf(x); r ≔ cdrf(r)
               <u>end</u>;
            indentation ≔ ind - 1; NLCR; SPACE(indentation)
         <u>end</u>;
         PRSYM(99)
      <u>end</u> writelist;
      <u>procedure</u> writepair(x,r); <u>value</u> x,r; <u>integer</u> x,r;
      <u>begin</u> PRSYM(98);
         <u>if</u> atomf(r)
           <u>then</u> <u>begin</u> writevalue(carf(x),r); PRINTTEXT(“ . ”);
            writevalue(cdrf(x),r)
         <u>end</u>
         <u>else</u> <u>begin</u> indentation ≔ indentation + 1;
            writevalue(carf(x),carf(r));
            NLCR; SPACE(indentation-1); PRINTTEXT(“ . ”);
            NLCR; SPACE(indentation); writevalue(cdrf(x),cdrf(r));
            NLCR; SPACE(indentation)
         <u>end</u>;
         PRSYM(99)
      <u>end</u> writepair;
      <u>procedure</u> writevalue(x,r); <u>value</u> x,r; <u>integer</u> x,r;
      <u>begin</u> <u>if</u> numberpf(x) <u>then</u> writenumber(x)
           <u>else</u>
         <u>if</u> atomf(x) <u>then</u> writeidentifier(x)
           <u>else</u>
         <u>if</u> islist(x) <u>then</u> writelist(x,r)
         <u>else</u> writepair(x,r)
      <u>end</u> writevalue;
      <u>procedure</u> write(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>integer</u> r;
         indentation ≔ 0;
         analyse(x,r); writevalue(x,r); recycle(r)
      <u>end</u> write;
      <u>comment</u> *** interpreter proper ***;
      <u>integer</u> <u>procedure</u> assoc(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>integer</u> ax;
         getidfval(x,id); ax ≔ alist[id];
         <u>if</u> ax = nilv <u>then</u> errorhandler(“identifier has no value”);
         assoc ≔ carf(ax)
      <u>end</u> assoc;
      <u>procedure</u> pairlis(x,y); <u>value</u> x,y; <u>integer</u> x,y;
      <u>begin</u> <u>for</u> dummy ≔ 0 <u>while</u> ¬ equalf(x,nilv) <u>do</u>
            <u>begin</u> getidfval(carf(x),id); alist[id] ≔ consf(carf(y),alist[id]);
               x ≔ cdrf(x); y ≔ cdrf(y)
            <u>end</u>
      <u>end</u> pairlis;
      <u>procedure</u> depairlis(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>for</u> dummy ≔ 0 <u>while</u> ¬ equalf(x,nilv) <u>do</u>
            <u>begin</u> getidfval(carf(x),id); alist[id] ≔ cdrf(alist[id]);
               x ≔ cdrf(x)
            <u>end</u>
      <u>end</u> depairlis;
      <u>integer</u> <u>procedure</u> eval(e); <u>value</u> e; <u>integer</u> e;
      <u>begin</u> <u>integer</u> care;
         work: <u>if</u> atomf(e)
           <u>then</u> <u>begin</u> <u>if</u> equalf(e,nilv) ∨ equalf(e,t) ∨ numberpf(e)
              <u>then</u> eval ≔ e <u>else</u> eval ≔ assoc(e)
         <u>end</u>
         <u>else</u> <u>begin</u> care ≔ carf(e);
            <u>if</u> equalf(care,cond)
              <u>then</u> <u>begin</u> e ≔ evcon(cdrf(e)); <u>goto</u> work <u>end</u>
              <u>else</u> <u>if</u> equalf(care,quote)
              <u>then</u> eval ≔ carf(cdrf(e))
            <u>else</u> <u>begin</u> olp ≔ consf(nilv,olp);
               a[olp] ≔ evlist(cdrf(e)); eval ≔ apply(care,a[olp]);
               returnlist(a[olp]); returncell(olp); olp ≔ cdrf(olp)
            <u>end</u>
         <u>end</u>
      <u>end</u> eval;
      <u>integer</u> <u>procedure</u> apply(f,x);
         <u>value</u> f,x; <u>integer</u> f,x;
      <u>begin</u>
         work: <u>if</u> atomf(f)
           <u>then</u> <u>begin</u>
            <u>if</u> equalf(f,car) <u>then</u> apply ≔ carf(carf(x))
              <u>else</u>
            <u>if</u> equalf(f,cdr) <u>then</u> apply ≔ cdrf(carf(x))
              <u>else</u>
            <u>if</u> equalf(f,cons) <u>then</u> apply ≔ consf(carf(x),carf(cdrf(x)))
              <u>else</u>
            <u>if</u> equalf(f,equal)
              <u>then</u> <u>begin</u> <u>if</u> equalf(carf(x),carf(cdrf(x)))
                 <u>then</u> apply ≔ t
               <u>else</u> apply ≔ nilv
            <u>end</u>
              <u>else</u>
            <u>if</u> equalf(f,atom) <u>then</u> <u>begin</u> <u>if</u> atomf(carf(x))
                 <u>then</u> apply ≔ t
               <u>else</u> apply ≔ nilv
            <u>end</u>
              <u>else</u>
            <u>if</u> equalf(f,numberp) <u>then</u> <u>begin</u> <u>if</u> numberpf(carf(x))
                 <u>then</u> apply ≔ t
               <u>else</u> apply ≔ nilv
            <u>end</u>
              <u>else</u>
            <u>if</u> equalf(f,lessp)
              <u>then</u> <u>begin</u> <u>if</u> numval(carf(x)) &lt; numval(carf(cdrf(x)))
                 <u>then</u> apply ≔ t
               <u>else</u> apply ≔ nilv
            <u>end</u>
              <u>else</u>
            <u>if</u> equalf(f,greaterp)
              <u>then</u> <u>begin</u> <u>if</u> numval(carf(x)) &gt; numval(carf(cdrf(x)))
                 <u>then</u> apply ≔ t
               <u>else</u> apply ≔ nilv
            <u>end</u>
              <u>else</u>
            <u>if</u> equalf(f,add)
              <u>then</u> apply ≔ createnum(numval(carf(x)) + 1)
              <u>else</u>
            <u>if</u> equalf(f,sub1)
              <u>then</u> apply ≔ createnum(numval(carf(x)) - 1)
              <u>else</u>
            <u>if</u> equalf(f,add)
              <u>then</u> apply ≔ createnum(numval(carf(x)) + numval(carf(cdrf(x))))
              <u>else</u>
            <u>if</u> equalf(f,minus)
              <u>then</u> apply ≔ createnum(numval(carf(x)) - numval(carf(cdrf(x))))
              <u>else</u>
            <u>if</u> equalf(f,timesv)
              <u>then</u> apply ≔ createnum(numval(carf(x)) × numval(carf(cdrf(x))))
              <u>else</u>
            <u>if</u> equalf(f,divf)
              <u>then</u> apply ≔ createnum(numval(carf(x)) ÷ numval(carf(cdrf(x))))
            <u>else</u> <u>begin</u> f ≔ assoc(f); <u>goto</u> work <u>end</u>
         <u>end</u>
         <u>else</u> <u>begin</u> pairlis(carf(cdrf(f)),x);
            apply ≔ eval(carf(cdrf(cdrf(f))));
            depairlis(carf(cdrf(f)))
         <u>end</u>
      <u>end</u> apply;
      <u>integer</u> <u>procedure</u> evcon(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>integer</u> r;
         work: r ≔ carf(x);
         <u>if</u> equalf(eval(carf(r)),nilv)
           <u>then</u> <u>begin</u> x ≔ cdrf(x); <u>goto</u> work <u>end</u>
         <u>else</u> evcon ≔ carf(cdrf(r))
      <u>end</u> evcon;
      <u>integer</u> <u>procedure</u> evlist(x); <u>value</u> x; <u>integer</u> x;
      <u>begin</u> <u>integer</u> res;
         <u>if</u> equalf(x,nilv)
           <u>then</u> evlist ≔ nilv
         <u>else</u> <u>begin</u> olp ≔ consf(nilv,olp); a[olp] ≔ res ≔ consf(nilv,nilv);
            a[res] ≔ eval(carf(x)); d[res] ≔ evlist(cdrf(x));
            evlist ≔ res;
            returncell(olp); olp ≔ cdrf(olp)
         <u>end</u>
      <u>end</u> evlist;
      <u>comment</u> *** initialization ***;
      <u>procedure</u> create(lv); <u>integer</u> lv;
      <u>begin</u> skipspaces;
         lv ≔ identifier;
      <u>end</u> create;
      <u>procedure</u> init;
      <u>begin</u> <u>integer</u> i,j;
         d24 ≔ 16777216; d25 ≔ 33554432;
         last idf ≔ 0; sym ≔ 93; nilv ≔ d25 + 1;
         create(t); create(nilv);
         create(quote); create(cond);
         create(lambda); create(define);
         create(car); create(cdr);
         create(cons); create(equal);
         create(atom); create(numberp);
         create(lessp); create(greaterp);
         create(add1); create(sub1);
         create(add); create(minus);
         create(timesv); create(divf);
         olp ≔ nilv;
         free ≔ 1; last nbr ≔ 0; linewidth ≔ 40;
         <u>for</u> i ≔ 1 <u>step</u> 1 <u>until</u> maxstruct - 1 <u>do</u> a[i] ≔ i + 1;
         a[maxstruct] ≔ 0
      <u>end</u> init;
      <u>comment</u> *** main program ***;
      <u>procedure</u> function definitions(x,a,r); <u>value</u> x; <u>integer</u> x,a,r;
      <u>begin</u> <u>integer</u> carx,lr;
         <u>if</u> equalf(x,nilv)
           <u>then</u> r ≔ nilv
         <u>else</u> <u>begin</u> carx ≔ carf(x);
            a ≔ consf(consf(carf(carx),carf(cdrf(carx))),a);
            function definitions(cdrf(x),a,lr);
            r ≔ consf(carf(carx),lr)
         <u>end</u>
      <u>end</u> function definitions;
      PRINTTEXT(“Lisp interpreter version 1, Oktober 2004”);
      NLCR; NLCR;
      init;
      <u>for</u> dummy ≔ 0 <u>while</u> <u>true</u> <u>do</u>
         <u>begin</u> olp ≔ consf(nilv,olp); a[olp] ≔ p ≔ consf(nilv,nilv);
            a[p] ≔ f ≔ nextitem; d[p] ≔ args ≔ nextitem;
            NLCR;
            <u>if</u> equalf(f,define)
              <u>then</u> <u>begin</u> args ≔ carf(args); PRSYM(98);
               <u>for</u> dummy ≔ 0 <u>while</u> ¬ equalf(args,nilv) <u>do</u>
                  <u>begin</u> p ≔ carf(args); write(carf(p));
                     getidfval(carf(p),id);
                     alist[id] ≔ consf(carf(cdrf(p)),nilv);
                     args ≔ cdrf(args);
                     <u>if</u> ¬ equalf(args,nilv) <u>then</u> SPACE(1)
                  <u>end</u>;
               PRSYM(99)
            <u>end</u>
            <u>else</u> <u>begin</u> p ≔ apply(f,args);
               NLCR; write(p)
            <u>end</u>;
            olp ≔ cdrf(olp)
         <u>end</u>;
      ex:
   <u>end</u>
<u>end</u>
</pre></body></html>
