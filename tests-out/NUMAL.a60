c̲o̲m̲m̲e̲n̲t̲ ================== 34010 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); v̲a̲l̲u̲e̲ L, U, SHIFT;
   i̲n̲t̲e̲g̲e̲r̲ L, U, SHIFT; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K; r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ K := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := A[K] × B[SHIFT + K] + S;
   VECVEC := S
e̲n̲d̲ VECVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34011 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); v̲a̲l̲u̲e̲ L, U, I;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K; r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ K := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := A[I, K] × B[K] + S;
   MATVEC := S
e̲n̲d̲ MATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34012 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); v̲a̲l̲u̲e̲ L, U, I;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K; r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ K := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := A[K, I] × B[K] + S;
   TAMVEC := S
e̲n̲d̲ TAMVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34013 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); v̲a̲l̲u̲e̲ L, U, I, J;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K; r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ K := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := A[I, K] × B[K, J] + S;
   MATMAT := S
e̲n̲d̲ MATMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34014 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); v̲a̲l̲u̲e̲ L, U, I, J;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K; r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ K := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := A[K, I] × B[K, J] + S;
   TAMMAT := S
e̲n̲d̲ TAMMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34015 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); v̲a̲l̲u̲e̲ L, U, I, J;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K; r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ K := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := A[I, K] × B[J, K] + S;
   MATTAM := S
e̲n̲d̲ MATTAM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34016 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SEQVEC(L, U, IL, SHIFT, A, B);
   v̲a̲l̲u̲e̲ L, U, IL, SHIFT; i̲n̲t̲e̲g̲e̲r̲ L, U, IL, SHIFT; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S;
   S := 0;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ S := A[IL] × B[L + SHIFT] + S; IL := IL + L e̲n̲d̲;
   SEQVEC := S
e̲n̲d̲ SEQVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34017 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SCAPRD1(LA, SA, LB, SB, N, A, B);
   v̲a̲l̲u̲e̲ LA, SA, LB, SB, N; i̲n̲t̲e̲g̲e̲r̲ LA, SA, LB, SB, N; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S; i̲n̲t̲e̲g̲e̲r̲ K;
   S := 0;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ S := A[LA] × B[LB] + S; LA := LA + SA; LB := LB + SB e̲n̲d̲;
   SCAPRD1 := S
e̲n̲d̲ SCAPRD1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34018 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SYMMATVEC(L, U, I, A, B); v̲a̲l̲u̲e̲ L, U, I;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K, M;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SEQVEC(L, U, IL, SHIFT, A, B); c̲o̲d̲e̲ 34016;
   M := i̲f̲ L > I t̲h̲e̲n̲ L e̲l̲s̲e̲ I; K := M × (M - 1) ÷ 2;
   SYMMATVEC := VECVEC(L, i̲f̲ I ≤ U t̲h̲e̲n̲ I-1 e̲l̲s̲e̲ U, K, B, A)
     + SEQVEC(M, U, K + I, 0, A, B)
e̲n̲d̲ SYMMATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31500 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FULMATVEC(LR, UR, LC, UC, A, B, C);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, B; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        C[LR] := MATVEC(LC, UC, LR, A, B);
e̲n̲d̲ FULMATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31501 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FULTAMVEC(LR, UR, LC, UC, A, B, C);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, B; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   f̲o̲r̲ LC := LC s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UC d̲o̲
        C[LC] := TAMVEC(LR, UR, LC, A, B);
e̲n̲d̲ FULTAMVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31502 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FULSYMMATVEC(LR, UR, LC, UC, A, B, C);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, B; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SYMMATVEC(L, U, I, A, B);
      c̲o̲d̲e̲ 34018;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        C[LR] := SYMMATVEC(LC, UC, LR, A, B)
e̲n̲d̲ FULSYMMATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31503 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RESVEC(LR, UR, LC, UC, A, B, C, X);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        C[LR] := MATVEC(LC, UC, LR, A, B) + C[LR] × X
e̲n̲d̲ RESVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31504 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SYMRESVEC(LR, UR, LC, UC, A, B, C, X);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SYMMATVEC(L, U, I, A, B);
      c̲o̲d̲e̲ 34018;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        C[LR] := SYMMATVEC(LC, UC, LR, A, B) + C[LR] × X
e̲n̲d̲ SYMRESVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34214 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ RNK1MIN(N, X, G, H, FUNCT, IN, OUT);
   v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ X, G, H, IN, OUT;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, IT, N2, CNTL, CNTE, EVL, EVLMAX;
   B̲o̲o̲l̲e̲a̲n̲ OK;
   r̲e̲a̲l̲ F, F0, FMIN, MU, DG, DG0, GHG, GS, NRMDELTA, ALFA,
     MACHEPS, RELTOL, ABSTOL, EPS, TOLG, ORTH, AID;
   a̲r̲r̲a̲y̲ V, DELTA, GAMMA, S, P[1:N];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SYMMATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34018;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X); c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INISYMD(LR, UR, SHIFT, A, X); c̲o̲d̲e̲ 31013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 31020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 31030;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGSYM1(A, N, NUMVAL, VAL, VEC, EM); c̲o̲d̲e̲ 34156;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LINEMIN(N, X, D, ND, A, G, F, F0, F1, DFO, DF1,
     E, S, IN); c̲o̲d̲e̲ 34210;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ RNK1UPD(H, N, V, C); c̲o̲d̲e̲ 34211;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DAVUPD(H, N, V, W, C1, C2); c̲o̲d̲e̲ 34212;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FLEUPD(H, N, V, W, C1, C2); c̲o̲d̲e̲ 34213;

   MACHEPS := IN[0]; RELTOL := IN[1]; ABSTOL := IN[2];
   MU := IN[3]; TOLG := IN[4]; FMIN := IN[5]; IT := 0;
   ALFA := IN[6]; EVLMAX := IN[7]; ORTH := IN[8];
   N2 := N × (N + 1) ÷ 2; CNTL := CNTE := 0; i̲f̲ ALFA > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ INIVEC(1, N2, H, 0); INISYMD(1, N, 0, H, ALFA) e̲n̲d̲;
   F := FUNCT(N, X, G); EVL := 1; DG := SQRT(VECVEC(1, N, 0, G, G));
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        DELTA[I] := - SYMMATVEC(1, N, I, H, G);
   NRMDELTA := SQRT(VECVEC(1, N, 0, DELTA, DELTA));
   DG0 := VECVEC(1, N, 0, DELTA, G); OK := DG0 < 0;
   EPS := SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL;
   f̲o̲r̲ IT := IT + 1  w̲h̲i̲l̲e̲
     (NRMDELTA > EPS ∨ DG > TOLG ∨ ¬OK) ∧ EVL < EVLMAX
     d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ ¬OK t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ VEC[1:N, 1:N], TH[1:N2], EM[0:9];
            EM[0] := MACHEPS; EM[2] := AID := SQRT(MACHEPS × RELTOL);
            EM[4] := ORTH; EM[6] := AID × N; EM[8] := 5;
            CNTE := CNTE + 1; DUPVEC(1, N2, 0, TH, H);
            EIGSYM1(TH, N, N, V, VEC, EM);
            f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ AID := - TAMVEC(1, N, I, VEC, G);
                  S[I] := AID × ABS(V[I]); V[I] := AID × SIGN(V[I])
               e̲n̲d̲;

            f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ DELTA[I] := MATVEC(1, N, I, VEC, S);
                  P[I] := MATVEC(1, N, I, VEC, V)
               e̲n̲d̲;
            DG0 := VECVEC(1, N, 0, DELTA, G);
            NRMDELTA := SQRT(VECVEC(1, N, 0, DELTA, DELTA))
         e̲n̲d̲ CALCULATING GREENSTADTS DIRECTION;
         DUPVEC(1, N, 0, S, X); DUPVEC(1, N, 0, V, G);
         i̲f̲ IT > N t̲h̲e̲n̲ ALFA := 1 e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i̲f̲ IT ≠ 1 t̲h̲e̲n̲ ALFA := ALFA / NRMDELTA e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ ALFA := 2 × (FMIN - F) / DG0;
               i̲f̲ ALFA > 1 t̲h̲e̲n̲ ALFA := 1
            e̲n̲d̲
         e̲n̲d̲;
         ELMVEC(1, N, 0, X, DELTA, ALFA);
         F0 := F; F := FUNCT(N, X, G); EVL := EVL + 1 ;
         DG := VECVEC(1, N, 0, DELTA, G);
         i̲f̲ IT = 1 ∨ F0 - F < -MU × DG0 × ALFA t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ I := EVLMAX - EVL; CNTL := CNTL + 1 ;
            LINEMIN(N, S, DELTA, NRMDELTA, ALFA, G, FUNCT, F0, F,
              DG0, DG, I, f̲a̲l̲s̲e̲, IN); EVL := EVL + I;
            DUPVEC(1, N, 0, X, S);
         e̲n̲d̲ LINEMINIMIZATION;
         DUPVEC(1, N, 0, GAMMA, G); ELMVEC(1, N, 0, GAMMA, V, -1);
         i̲f̲ ¬OK t̲h̲e̲n̲ MULVEC(1, N, 0, V, P, -1);
         DG := DG - DG0; i̲f̲ ALFA ≠ 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MULVEC(1, N, 0, DELTA, DELTA, ALFA);
            MULVEC(1, N, 0, V, V, ALFA);
            NRMDELTA := NRMDELTA × ALFA; DG := DG × ALFA
         e̲n̲d̲;
         DUPVEC(1, N, 0, P, GAMMA); ELMVEC(1, N, 0, P, V, 1);
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              V[I] := SYMMATVEC(1, N, I, H, GAMMA);
         DUPVEC(1, N, 0, S, DELTA); ELMVEC(1, N, 0, S, V, -1);
         GS := VECVEC(1, N, 0, GAMMA, S);
         GHG := VECVEC(1, N, 0, V, GAMMA);
         AID := DG / GS;
         i̲f̲ VECVEC(1, N, 0, DELTA, P) ⭡ 2 > VECVEC(1, N, 0, P, P)× (ORTH × NRMDELTA) ⭡ 2 t̲h̲e̲n̲ RNK1UPD(H, N, S, 1 / GS)
           e̲l̲s̲e̲ i̲f̲ AID ≥ 0 t̲h̲e̲n̲
           FLEUPD(H, N, DELTA, V, 1 / DG, (1 + GHG / DG) / DG) e̲l̲s̲e̲
           DAVUPD(H, N, DELTA, V, 1 / DG, 1 / GHG);
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              DELTA[I] := -SYMMATVEC(1, N, I, H, G);
         ALFA := NRMDELTA;
         NRMDELTA := SQRT(VECVEC(1, N, 0, DELTA, DELTA));
         EPS := SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL;
         DG := SQRT(VECVEC(1, N, 0, G, G));
         DG0 := VECVEC(1, N, 0, DELTA, G); OK := DG0 ≤ 0
      e̲n̲d̲ ITERATION;
   OUT[0] := NRMDELTA; OUT[1] := DG; OUT[2] := EVL;
   OUT[3] := CNTL; OUT[4] := CNTE; RNK1MIN := F
e̲n̲d̲ RNK1MIN;
c̲o̲m̲m̲e̲n̲t̲ ================== 34215 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FLEMIN(N, X, G, H, FUNCT, IN, OUT);
   v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ X, G, H, IN, OUT;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, IT, CNTL, EVL, EVLMAX;
   r̲e̲a̲l̲ F, F0, FMIN, MU, DG, DG0, NRMDELTA, ALFA, RELTOL, ABSTOL,
     EPS, TOLG, AID;
   a̲r̲r̲a̲y̲ V, DELTA, S[1:N];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SYMMATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34018;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X); c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INISYMD(LR, UR, SHIFT, A, X); c̲o̲d̲e̲ 31013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULVEC(L, U, SHIFT, A, B, XB); c̲o̲d̲e̲ 31020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 31030;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LINEMIN(N, X, D, ND, A, G, F, F0, F1, DF0, DF1,
     E, S, IN); c̲o̲d̲e̲ 34210;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DAVUPD(H, N, V, W, C1, C2); c̲o̲d̲e̲ 34212;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FLEUPD(H, N, V, W, C1, C2); c̲o̲d̲e̲ 34213;

   RELTOL := IN[1]; ABSTOL := IN[2]; MU := IN[3];
   TOLG := IN[4]; FMIN := IN[5]; ALFA := IN[6];
   EVLMAX := IN[7]; OUT[4] := 0; IT := 0;
   F := FUNCT(N, X, G); EVL := 1; CNTL := 0; i̲f̲ ALFA > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ INIVEC(1, N × (N + 1) ÷ 2, H, 0);
      INISYMD(1, N, 0, H, ALFA)
   e̲n̲d̲;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        DELTA[I] := - SYMMATVEC(1, N, I, H, G);
   DG := SQRT(VECVEC(1, N, 0, G, G));
   NRMDELTA := SQRT(VECVEC(1, N, 0, DELTA, DELTA));
   EPS := SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL;
   DG0 := VECVEC(1, N, 0, DELTA, G);
   f̲o̲r̲ IT := IT + 1  w̲h̲i̲l̲e̲
     (NRMDELTA > EPS ∨ DG > TOLG ) ∧ EVL < EVLMAX d̲o̲
      b̲e̲g̲i̲n̲ DUPVEC(1, N, 0, S, X); DUPVEC(1, N, 0, V, G);
         i̲f̲ IT ≥ N t̲h̲e̲n̲ ALFA := 1 e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i̲f̲ IT ≠ 1 t̲h̲e̲n̲ ALFA := ALFA / NRMDELTA e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ ALFA := 2 × (FMIN - F) / DG0;
               i̲f̲ ALFA > 1 t̲h̲e̲n̲ ALFA := 1
            e̲n̲d̲
         e̲n̲d̲;
         ELMVEC(1, N, 0, X, DELTA, ALFA);
         F0 := F; F := FUNCT(N, X, G); EVL := EVL + 1 ;
         DG := VECVEC(1, N, 0, DELTA, G);
         i̲f̲ IT = 1 ∨ F0 - F < - MU × DG0 × ALFA t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ I := EVLMAX - EVL; CNTL := CNTL + 1 ;
            LINEMIN(N, S, DELTA, NRMDELTA, ALFA, G, FUNCT, F0, F,
              DG0, DG, I, f̲a̲l̲s̲e̲, IN); EVL := EVL + I;
            DUPVEC(1, N, 0, X, S);
         e̲n̲d̲ LINEMINIMIZATION;
         i̲f̲ ALFA ≠ 1 t̲h̲e̲n̲ MULVEC(1, N, 0, DELTA, DELTA, ALFA);
         MULVEC(1, N, 0, V, V, -1); ELMVEC(1, N, 0, V, G, 1);
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              S[I] := SYMMATVEC(1, N, I, H, V);
         AID := VECVEC(1, N, 0, V, S); DG := (DG - DG0) × ALFA;
         i̲f̲ DG > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ DG ≥ AID t̲h̲e̲n̲
              FLEUPD(H, N, DELTA, S, 1 / DG, (1 + AID / DG) / DG)
            e̲l̲s̲e̲ DAVUPD(H, N, DELTA, S, 1 / DG, 1 / AID)
         e̲n̲d̲ UPDATING;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              DELTA[I] := -SYMMATVEC(1, N, I, H, G);
         ALFA := NRMDELTA × ALFA;
         NRMDELTA := SQRT(VECVEC(1, N, 0, DELTA, DELTA));
         EPS := SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL;
         DG := SQRT(VECVEC(1, N, 0, G, G));
         DG0 := VECVEC(1, N, 0, DELTA, G); i̲f̲ DG0 > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ OUT[4] := -1 ; g̲o̲t̲o̲ EXIT e̲n̲d̲
      e̲n̲d̲ ITERATION;
   EXIT: OUT[0] := NRMDELTA; OUT[1] := DG; OUT[2] := EVL;
   OUT[3] := CNTL; FLEMIN := F
e̲n̲d̲ FLEMIN;
c̲o̲m̲m̲e̲n̲t̲ ================== 34352 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMCOLCST(L, U, J, AR, AI, XR, XI);
   v̲a̲l̲u̲e̲ L, U, J, XR, XI; i̲n̲t̲e̲g̲e̲r̲ L, U, J; r̲e̲a̲l̲ XR, XI;
     a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMUL(AR, AI, BR, BI, RR, RI); c̲o̲d̲e̲ 34341;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
        COMMUL(AR[L, J], AI[L, J], XR, XI, AR[L, J], AI[L, J]);
e̲n̲d̲ COMCOLCST;
c̲o̲m̲m̲e̲n̲t̲ ================== 34353 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMROWCST(L, U, I, AR, AI, XR, XI);
   v̲a̲l̲u̲e̲ L, U, I, XR, XI; i̲n̲t̲e̲g̲e̲r̲ L, U, I; r̲e̲a̲l̲ XR, XI;
     a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMUL(AR, AI, BR, BI, RR, RI); c̲o̲d̲e̲ 34341;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ COMMUL(AR[I, L], AI[I, L], XR,
        XI, AR[I, L], AI[I, L]);
e̲n̲d̲ COMROWCST;
c̲o̲m̲m̲e̲n̲t̲ ================== 34354 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMATVEC(L, U, I, AR, AI, BR, BI, RR, RI);
   v̲a̲l̲u̲e̲ L, U, I; i̲n̲t̲e̲g̲e̲r̲ L, U, I; r̲e̲a̲l̲ RR, RI;
     a̲r̲r̲a̲y̲ AR, AI, BR, BI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ MV;
   MV := MATVEC(L, U, I, AR, BR) - MATVEC(L, U, I, AI, BI);
   RI := MATVEC(L, U, I, AI, BR) + MATVEC(L, U, I, AR, BI);
   RR := MV
e̲n̲d̲ COMMATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34355 ================= ;
B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMCOL(L, U, J, AR, AI, TOL, K, C, S, T);
   v̲a̲l̲u̲e̲ L, U, J, TOL; i̲n̲t̲e̲g̲e̲r̲ L, U, J; r̲e̲a̲l̲ TOL, K, C, S, T;
     a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ VR, DEL, MOD, H, ARLJ, AILJ;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CARPOL(AR, AI, R, C, S); c̲o̲d̲e̲ 34344;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   VR := TAMMAT(L + 1, U, J, J, AR, AR) + TAMMAT(L + 1, U,
     J, J, AI, AI); ARLJ := AR[L, J]; AILJ := AI[L, J];
   CARPOL(ARLJ, AILJ, MOD, C, S); i̲f̲ VR > TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ VR := VR + ARLJ ⭡ 2 + AILJ ⭡ 2; H := K := SQRT(VR);
      T := VR + MOD × H;
      i̲f̲ ARLJ = 0 ∧ AILJ = 0 t̲h̲e̲n̲ AR[L, J] := H e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ AR[L, J] := ARLJ + C × K; AI[L, J] := AILJ + S × K;
         S := - S
      e̲n̲d̲;
      C := - C; HSHCOMCOL := t̲r̲u̲e̲
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ HSHCOMCOL := f̲a̲l̲s̲e̲; K := MOD; T := - 1 e̲n̲d̲
e̲n̲d̲ HSHCOMCOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34356 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMPRD(I, II, L, U, J, AR, AI, BR, BI, T);
   v̲a̲l̲u̲e̲ I, II, L, U, J, T; i̲n̲t̲e̲g̲e̲r̲ I, II, L, U, J; r̲e̲a̲l̲ T;
     a̲r̲r̲a̲y̲ AR, AI, BR, BI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOMCOL(L, U, I, J, AR, AI, BR, BI, XR, XI); c̲o̲d̲e̲ 34377;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ ELMCOMCOL(I, II, L, J, AR, AI,
        BR, BI, ( - TAMMAT(I, II, J, L, BR, AR) - TAMMAT(I, II, J,
        L, BI, AI)) / T, (TAMMAT(I, II, J, L, BI, AR) - TAMMAT(I,
        II, J, L, BR, AI)) / T);
e̲n̲d̲ HSHCOMPRD;
c̲o̲m̲m̲e̲n̲t̲ ================== 34376 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOMVECCOL(L, U, J, AR, AI, BR, BI, XR, XI);
   v̲a̲l̲u̲e̲ L, U, J, XR, XI;
   i̲n̲t̲e̲g̲e̲r̲ L, U, J; r̲e̲a̲l̲ XR, XI; a̲r̲r̲a̲y̲ AR, AI, BR, BI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X); c̲o̲d̲e̲ 34021;
   ELMVECCOL(L, U, J, AR, BR, XR);
   ELMVECCOL(L, U, J, AR, BI, -XI);
   ELMVECCOL(L, U, J, AI, BR, XI);
   ELMVECCOL(L, U, J, AI, BI, XR)
e̲n̲d̲ ELMCOMVECCOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34377 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOMCOL(L, U, I, J, AR, AI, BR, BI, XR, XI);
   v̲a̲l̲u̲e̲ L, U, I, J, XR, XI;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; r̲e̲a̲l̲ XR, XI; a̲r̲r̲a̲y̲ AR, AI, BR, BI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   ELMCOL(L, U, I, J, AR, BR, XR);
   ELMCOL(L, U, I, J, AR, BI, -XI);
   ELMCOL(L, U, I, J, AI, BR, XI);
   ELMCOL(L, U, I, J, AI, BI, XR)
e̲n̲d̲ ELMCOMCOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34378 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOMROWVEC(L, U, I, AR, AI, BR, BI, XR, XI);
   v̲a̲l̲u̲e̲ L, U, I, XR, XI;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I; r̲e̲a̲l̲ XR, XI; a̲r̲r̲a̲y̲ AR, AI, BR, BI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34027;
   ELMROWVEC(L, U, I, AR, BR, XR);
   ELMROWVEC(L, U, I, AR, BI, -XI);
   ELMROWVEC(L, U, I, AI, BR, XI);
   ELMROWVEC(L, U, I, AI, BI, XR)
e̲n̲d̲ ELMCOMROWVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34360 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SCLCOM(AR, AI, N, N1, N2); v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K;
   r̲e̲a̲l̲ S, R;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMCOLCST(L, U, J, AR, AI, XR, XI); c̲o̲d̲e̲ 34352;
   f̲o̲r̲ J := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ S := 0;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ R := AR[I, J] ⭡ 2 + AI[I, J] ⭡ 2; i̲f̲ R > S t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ S := R; K := I e̲n̲d̲
            e̲n̲d̲;
         i̲f̲ S ≠ 0 t̲h̲e̲n̲ COMCOLCST(1, N, J, AR, AI, AR[K, J] /
           S, - AI[K, J] / S)
      e̲n̲d̲
e̲n̲d̲ SCLCOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34359 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMEUCNRM(AR, AI, LW, N); v̲a̲l̲u̲e̲ N, LW;
   i̲n̲t̲e̲g̲e̲r̲ N, LW; a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, L;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); c̲o̲d̲e̲ 34015;
   r̲e̲a̲l̲ R;
   R := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ L := i̲f̲ I > LW t̲h̲e̲n̲ I - LW e̲l̲s̲e̲ 1;
         R := MATTAM(L, N, I, I, AR, AR) + MATTAM(L, N, I,
           I, AI, AI) + R;
      e̲n̲d̲;
   COMEUCNRM := SQRT(R)
e̲n̲d̲ COMEUCNRM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34340 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMABS(XR, XI); v̲a̲l̲u̲e̲ XR, XI; r̲e̲a̲l̲ XR, XI;
b̲e̲g̲i̲n̲ XR := ABS(XR); XI := ABS(XI);
   COMABS := i̲f̲ XI > XR t̲h̲e̲n̲ SQRT((XR/XI)⭡2 + 1) × XI
   e̲l̲s̲e̲ i̲f̲ XI = 0 t̲h̲e̲n̲ XR e̲l̲s̲e̲ SQRT((XI/XR)⭡2 + 1) × XR
e̲n̲d̲ COMABS;
c̲o̲m̲m̲e̲n̲t̲ ================== 34343 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMSQRT(AR, AI, PR, PI);
   v̲a̲l̲u̲e̲ AR, AI; r̲e̲a̲l̲ AR, AI, PR, PI;
   i̲f̲ AR = 0 ∧ AI = 0 t̲h̲e̲n̲ PR := PI := 0 e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ BR, BI, H;
      BR := ABS(AR); BI := ABS(AI);
      H := i̲f̲ BI < BR t̲h̲e̲n̲
        (i̲f̲ BR < 1 t̲h̲e̲n̲ SQRT((SQRT((BI/BR)⭡2 + 1) × ·5 + ·5) × BR)
      e̲l̲s̲e̲ SQRT((SQRT((BI/BR)⭡2 + 1) × ·125 + ·125) × BR) × 2)
      e̲l̲s̲e̲ i̲f̲ BI < 1 t̲h̲e̲n̲ SQRT((SQRT((BR/BI)⭡2 + 1) × BI + BR) × 2) × ·5
      e̲l̲s̲e̲ i̲f̲ BR + 1 = 1 t̲h̲e̲n̲ SQRT(BI × ·5)
      e̲l̲s̲e̲ SQRT(SQRT((BR/BI)⭡2 + 1) × BI × ·125 + BR × ·125) × 2;
      i̲f̲ AR ≥ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ PR := H; PI := AI/H × ·5 e̲n̲d̲
      e̲l̲s̲e̲ b̲e̲g̲i̲n̲ PI := i̲f̲ AI ≥ 0 t̲h̲e̲n̲ H e̲l̲s̲e̲ -H;
         PR := BI/H × ·5
      e̲n̲d̲
   e̲n̲d̲ COMSQRT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34342 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMDIV(XR, XI, YR, YI, ZR, ZI);
   v̲a̲l̲u̲e̲ XR, XI, YR, YI; r̲e̲a̲l̲ XR, XI, YR, YI, ZR, ZI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ H, D;
   i̲f̲ ABS(YI) < ABS(YR) t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ YI = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ ZR := XR/YR; ZI := XI/YR e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ H := YI/YR; D := H × YI + YR;
         ZR := (XR + H × XI)/D; ZI := (XI-H × XR)/D
      e̲n̲d̲
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ H := YR/YI; D := H × YR + YI;
      ZR := (XR × H + XI)/D; ZI := (XI × H - XR)/D
   e̲n̲d̲
e̲n̲d̲ COMDIV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34301 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSOL(A, N, AUX, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;
   DEC(A, N, AUX, P);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲ SOL(A, N, P, B)
e̲n̲d̲ DECSOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34061 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLELM(A, N, RI, CI, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, B;
     i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ R, CIR;
   r̲e̲a̲l̲ W;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;
   SOL(A, N, RI, B);
   f̲o̲r̲ R := N s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ CIR := CI[R]; i̲f̲ CIR ≠ R t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := B[R]; B[R] := B[CIR]; B[CIR] := W e̲n̲d̲
      e̲n̲d̲
e̲n̲d̲ SOLELM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34243 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSSOLERB(A, N, AUX, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLELM(A, N, RI, CI, B); c̲o̲d̲e̲ 34061;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSERB(A, N, AUX, RI, CI); c̲o̲d̲e̲ 34242;
   GSSERB(A, N, AUX, RI, CI);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲ SOLELM(A, N, RI, CI, B)
e̲n̲d̲ GSSSOLERB;
c̲o̲m̲m̲e̲n̲t̲ ================== 34302 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECINV(A, N, AUX); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INV(A, N, P); c̲o̲d̲e̲ 34053;
   DEC(A, N, AUX, P); i̲f̲ AUX[3] = N t̲h̲e̲n̲ INV(A, N, P)
e̲n̲d̲ DECINV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34236 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSINV(A, N, AUX); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSELM(A, N, AUX, RI, CI); c̲o̲d̲e̲ 34231;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INV1(A, N, RI, CI, WITHNORM); c̲o̲d̲e̲ 34235;
   GSSELM(A, N, AUX, RI, CI);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲ AUX[9] := INV1(A, N, RI, CI, t̲r̲u̲e̲)
e̲n̲d̲ GSSINV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34244 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSINVERB(A, N, AUX); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSELM(A, N, AUX, RI, CI); c̲o̲d̲e̲ 34231;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INV1(A, N, RI, CI, WITHNORM); c̲o̲d̲e̲ 34235;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ERBELM(N, AUX, NRMINV); c̲o̲d̲e̲ 34241;
   GSSELM(A, N, AUX, RI, CI);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲
     ERBELM(N, AUX, INV1(A, N, RI, CI, t̲r̲u̲e̲))
e̲n̲d̲ GSSINVERB;
c̲o̲m̲m̲e̲n̲t̲ ================== 34251 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSITISOL(A, N, AUX, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J;
   a̲r̲r̲a̲y̲ AA[1:N, 1:N];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSELM(A, N, AUX, RI, CI); c̲o̲d̲e̲ 34231;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ITISOL(A, LU, N, AUX, RI, CI, B); c̲o̲d̲e̲ 34250;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 31035;
   DUPMAT(1, N, 1, N, AA, A);
   GSSELM(A, N, AUX, RI, CI);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲ ITISOL(AA, A, N, AUX, RI, CI, B)
e̲n̲d̲ GSSITISOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34254 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSITISOLERB(A, N, AUX, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, AUX, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J;
   a̲r̲r̲a̲y̲ AA[1:N, 1:N];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSNRI(A, N, AUX, RI, CI); c̲o̲d̲e̲ 34252;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ITISOLERB(A, LU, N, AUX, RI, CI, B); c̲o̲d̲e̲ 34253;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 31035;
   DUPMAT(1, N, 1, N, AA, A);
   GSSNRI(A, N, AUX, RI, CI);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲ ITISOLERB(AA, A, N, AUX, RI, CI, B)
e̲n̲d̲ GSSITISOLERB;
c̲o̲m̲m̲e̲n̲t̲ ================== 34131 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LSQSOL(A, N, M, AID, CI, B); v̲a̲l̲u̲e̲ N, M;
   i̲n̲t̲e̲g̲e̲r̲ N, M; a̲r̲r̲a̲y̲ A, AID, B; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ CI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K, CIK;
   r̲e̲a̲l̲ W;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X); c̲o̲d̲e̲ 34021;

   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ ELMVECCOL(K, N, K, B, A,
        TAMVEC(K, N, K, A, B) / (AID[K] × A[K, K]));
   f̲o̲r̲ K := M s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲ B[K] := (B[K] - MATVEC
        (K + 1, M, K, A, B)) / AID[K];
   f̲o̲r̲ K := M s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ CIK := CI[K]; i̲f̲ CIK ≠ K t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := B[K]; B[K] := B[CIK]; B[CIK] := W e̲n̲d̲
      e̲n̲d̲
e̲n̲d̲ LSQSOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34135 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LSQORTDECSOL(A, N, M, AUX, DIAG, B); v̲a̲l̲u̲e̲ N, M;
   i̲n̲t̲e̲g̲e̲r̲ N, M; a̲r̲r̲a̲y̲ A, AUX, DIAG, B;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ AID[1:M];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ CI[1:M];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LSQORTDEC(A, N, M, AUX, AID, CI); c̲o̲d̲e̲ 34134;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LSQDGLINV(A, M, AID, CI, DIAG); c̲o̲d̲e̲ 34132;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LSQSOL(A, N, M, AID, CI, B); c̲o̲d̲e̲ 34131;

   LSQORTDEC(A, N, M, AUX, AID, CI);
   i̲f̲ AUX[3] = M t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LSQDGLINV(A, M, AID, CI, DIAG);
      LSQSOL(A, N, M, AID, CI, B)
   e̲n̲d̲
e̲n̲d̲ LSQORTDECSOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34280 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLSVDOVR(U, VAL, V, M, N, X, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V, X, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ MIN;
   a̲r̲r̲a̲y̲ X1[1:N];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B);
      v̲a̲l̲u̲e̲ L, U, I; i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B);
      v̲a̲l̲u̲e̲ L, U, I; i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34012;
   MIN := EM[6];
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        X1[I] := i̲f̲ VAL[I] ≤ MIN t̲h̲e̲n̲ 0 e̲l̲s̲e̲ TAMVEC(1, M, I, U, X) /
        VAL[I];
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        X[I] := MATVEC(1, N, I, V, X1)
e̲n̲d̲ SOLSVDOVR;
c̲o̲m̲m̲e̲n̲t̲ ================== 34281 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLOVR(A, M, N, X, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, X, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   a̲r̲r̲a̲y̲ VAL[1:N], V[1:N, 1:N];
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, VAL, V, EM;
      c̲o̲d̲e̲ 34273;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLSVDOVR(U, VAL, V, M, N, X, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V, X, EM;
      c̲o̲d̲e̲ 34280;

   SOLOVR := I := QRISNGVALDEC(A, M, N, VAL, V, EM);
   i̲f̲ I = 0 t̲h̲e̲n̲ SOLSVDOVR(A, VAL, V, M, N, X, EM)
e̲n̲d̲ SOLOVR;
c̲o̲m̲m̲e̲n̲t̲ ================== 34282 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLSVDUND(U, VAL, V, M, N, X, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V, X, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ MIN;
   a̲r̲r̲a̲y̲ X1[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B);
      v̲a̲l̲u̲e̲ L, U, I; i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34011;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B);
      v̲a̲l̲u̲e̲ L, U, I; i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34012;

   MIN := EM[6];
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        X1[I] := i̲f̲ VAL[I] ≤ MIN t̲h̲e̲n̲ 0 e̲l̲s̲e̲ TAMVEC(1, N, I, V, X) /
        VAL[I];
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
        X[I] := MATVEC(1, N, I, U, X1)
e̲n̲d̲ SOLSVDUND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34283 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLUND(A, M, N, X, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, X, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   a̲r̲r̲a̲y̲ VAL[1:N], V[1:N, 1:N];

   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, VAL, V, EM;
      c̲o̲d̲e̲ 34273;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLSVDUND(U, VAL, V, M, N, X, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V, X, EM;
      c̲o̲d̲e̲ 34282;

   SOLUND := I := QRISNGVALDEC(A, M, N, VAL, V, EM);
   i̲f̲ I = 0 t̲h̲e̲n̲ SOLSVDUND(A, VAL, V, M, N, X, EM)
e̲n̲d̲ SOLUND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34285 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ HOMSOL(A, M, N, V, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, V, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   a̲r̲r̲a̲y̲ VAL[1:N];

   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, VAL, V, EM;
      c̲o̲d̲e̲ 34273;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HOMSOLSVD(U, VAL, V, M, N);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V;
      c̲o̲d̲e̲ 34284;

   HOMSOL := I := QRISNGVALDEC(A, M, N, VAL, V, EM);
   i̲f̲ I = 0 t̲h̲e̲n̲ HOMSOLSVD(A, VAL, V, M, N)
e̲n̲d̲ HOMSOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34286 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ PSDINVSVD(U, VAL, V, M, N, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J;
   r̲e̲a̲l̲ MIN, VALI;
   a̲r̲r̲a̲y̲ X[1:N];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B);
      v̲a̲l̲u̲e̲ L, U, I; i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34011;
   MIN := EM[6];
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        i̲f̲ VAL[I] > MIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ VALI := 1 / VAL[I];
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ U[J, I] := U[J, I] × VALI
      e̲n̲d̲
      e̲l̲s̲e̲ f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ U[J, I] := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[J] := U[I, J];
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              U[I, J] := MATVEC(1, N, J, V, X)
      e̲n̲d̲
e̲n̲d̲ PSDINVSVD;
c̲o̲m̲m̲e̲n̲t̲ ================== 34287 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ PSDINV(A, M, N, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   a̲r̲r̲a̲y̲ VAL[1:N], V[1:N, 1:N];
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, VAL, V, EM;
      c̲o̲d̲e̲ 34273;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ PSDINVSVD(U, VAL, V, M, N, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ U, VAL, V, EM;
      c̲o̲d̲e̲ 34286;

   PSDINV := I := QRISNGVALDEC(A, M, N, VAL, V, EM);
   i̲f̲ I = 0 t̲h̲e̲n̲ PSDINVSVD(A, VAL, V, M, N, EM)
e̲n̲d̲ PSDINV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34320 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECBND(A, N, LW, RW, AUX, M, P); v̲a̲l̲u̲e̲ N, LW, RW;
   i̲n̲t̲e̲g̲e̲r̲ N, LW, RW; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P; a̲r̲r̲a̲y̲ A, M, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K, KK, KK1, PK, MK, IK, LW1, F, Q, W, W1,
     W2, NRW, IW, SDET;
   r̲e̲a̲l̲ R, S, EPS, MIN;
   a̲r̲r̲a̲y̲ V[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(A, B, C, D, E); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(A, B, C, D, E, F); c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHVEC(A, B, C, D); c̲o̲d̲e̲ 34030;
   F := LW; W1 := LW + RW; W := W1 + 1; W2 := W - 2; IW := 0; SDET := 1;
   NRW := N - RW; LW1 := LW + 1; Q := LW - 1;
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ LW d̲o̲
      b̲e̲g̲i̲n̲ Q := Q - 1; IW := IW + W1;
         f̲o̲r̲ J := IW - Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ IW d̲o̲ A[J] := 0
      e̲n̲d̲;
   IW := - W2; Q := - LW;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ IW := IW + W; i̲f̲ I ≤ LW1 t̲h̲e̲n̲ IW := IW - 1;
         Q := Q + W; i̲f̲ I > NRW t̲h̲e̲n̲ Q := Q - 1;
         V[I] := SQRT(VECVEC(IW, Q, 0, A, A))
      e̲n̲d̲;
   EPS := AUX[2]; MIN := 1; KK := - W1; MK := - LW;
   i̲f̲ F > NRW t̲h̲e̲n̲ W2 := W2 + NRW - F;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ F < N t̲h̲e̲n̲ F := F + 1; IK := KK := KK + W;
         MK := MK + LW; S := ABS(A[KK]) / V[K]; PK := K; KK1 := KK + 1;
         f̲o̲r̲ I := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ F d̲o̲
            b̲e̲g̲i̲n̲ IK := IK + W1; M[MK + I - K] := R := A[IK]; A[IK] := 0;
               R := ABS(R) / V[I]; i̲f̲ R > S t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ S := R; PK := I e̲n̲d̲
            e̲n̲d̲;
         i̲f̲ S < MIN t̲h̲e̲n̲ MIN := S; i̲f̲ S < EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ AUX[3] := K - 1; AUX[5] := S; g̲o̲ t̲o̲ END e̲n̲d̲;
         i̲f̲ K + W2 ≥ N t̲h̲e̲n̲ W2 := W2 - 1;
         P[K] := PK; i̲f̲ PK ≠ K t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ V[PK] := V[K];
            PK := PK - K; ICHVEC(KK1, KK1 + W2, PK × W1, A);
            SDET := - SDET; R := M[MK + PK]; M[MK + PK] := A[KK];
            A[KK] := R
         e̲n̲d̲ e̲l̲s̲e̲ R := A[KK]; i̲f̲ R < 0 t̲h̲e̲n̲ SDET := - SDET;
         IW := KK1; LW1 := F - K + MK;
         f̲o̲r̲ I := MK + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ LW1 d̲o̲
            b̲e̲g̲i̲n̲ M[I] := S := M[I] / R; IW := IW + W1;
               ELMVEC(IW, IW + W2, KK1 - IW, A, A, - S)
            e̲n̲d̲
      e̲n̲d̲;
   AUX[3] := N; AUX[5] := MIN;
   END: AUX[1] := SDET
e̲n̲d̲ DECBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34321 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DETERMBND(A, N, LW, RW, SGNDET);
   v̲a̲l̲u̲e̲ N, LW, RW, SGNDET; i̲n̲t̲e̲g̲e̲r̲ N, LW, RW, SGNDET; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, L; r̲e̲a̲l̲ P;
   L := 1; P := 1; LW := LW + RW + 1;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ P := A[L] × P; L := L + LW e̲n̲d̲;
   DETERMBND := ABS(P) × SGNDET
e̲n̲d̲ DETERMBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34071 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLBND(A, N, LW, RW, M, P, B); v̲a̲l̲u̲e̲ N, LW, RW;
   i̲n̲t̲e̲g̲e̲r̲ N, LW, RW; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P; a̲r̲r̲a̲y̲ A, B, M;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ F, I, K, KK, W, W1, W2, SHIFT;
   r̲e̲a̲l̲ S;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(A, B, C, D, E); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(A, B, C, D, E, F); c̲o̲d̲e̲ 34020;

   F := LW; SHIFT := - LW; W1 := LW - 1;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ F < N t̲h̲e̲n̲ F := F + 1; SHIFT := SHIFT + W1;
         I := P[K]; S := B[I]; i̲f̲ I ≠ K t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B[I] := B[K]; B[K] := S e̲n̲d̲;
         ELMVEC(K + 1, F, SHIFT, B, M, - S)
      e̲n̲d̲;
   W1 := LW + RW; W := W1 + 1; KK := (N + 1) × W - W1; W2 := - 1;
   SHIFT := N × W1;
   f̲o̲r̲ K := N s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ KK := KK - W; SHIFT := SHIFT - W1;
         i̲f̲ W2 < W1 t̲h̲e̲n̲ W2 := W2 + 1;
         B[K] := (B[K] - VECVEC(K + 1, K + W2, SHIFT, B, A)) / A[KK]
      e̲n̲d̲
e̲n̲d̲ SOLBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34322 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSOLBND(A, N, LW, RW, AUX, B); v̲a̲l̲u̲e̲ N, LW, RW;
   i̲n̲t̲e̲g̲e̲r̲ N, LW, RW; a̲r̲r̲a̲y̲ A, B, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K, KK, KK1, PK, IK, LW1, F, Q, W, W1, W2, IW,
     NRW, SHIFT, SDET;
   r̲e̲a̲l̲ R, S, EPS, MIN; a̲r̲r̲a̲y̲ M[0:LW], V[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(A, B, C, D, E); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(A, B, C, D, E, F); c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHVEC(A, B, C, D); c̲o̲d̲e̲ 34030;

   F := LW; SDET := 1; W1 := LW + RW; W := W1 + 1; W2 := W - 2; IW := 0;
   NRW := N - RW; LW1 := LW + 1; Q := LW - 1;
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ LW d̲o̲
      b̲e̲g̲i̲n̲ Q := Q - 1; IW := IW + W1;
         f̲o̲r̲ J := IW - Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ IW d̲o̲ A[J] := 0
      e̲n̲d̲;
   IW := - W2; Q := - LW;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ IW := IW + W; i̲f̲ I ≤ LW1 t̲h̲e̲n̲ IW := IW - 1;
         Q := Q + W; i̲f̲ I > NRW t̲h̲e̲n̲ Q := Q - 1;
         V[I] := SQRT(VECVEC(IW, Q, 0, A, A))
      e̲n̲d̲;
   EPS := AUX[2]; MIN := 1; KK := - W1;
   i̲f̲ F > NRW t̲h̲e̲n̲ W2 := W2 + NRW - F;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ F < N t̲h̲e̲n̲ F := F + 1; IK := KK := KK + W;
         S := ABS(A[KK]) / V[K]; PK := K; KK1 := KK + 1;
         f̲o̲r̲ I := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ F d̲o̲
            b̲e̲g̲i̲n̲ IK := IK + W1; M[I - K] := R := A[IK]; A[IK] := 0;
               R := ABS(R) / V[I]; i̲f̲ R > S t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ S := R; PK := I e̲n̲d̲
            e̲n̲d̲;
         i̲f̲ S < MIN t̲h̲e̲n̲ MIN := S; i̲f̲ S < EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ AUX[3] := K - 1; AUX[5] := S; g̲o̲ t̲o̲ END e̲n̲d̲;
         i̲f̲ K + W2 ≥ N t̲h̲e̲n̲ W2 := W2 - 1; i̲f̲ PK ≠ K t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ V[PK] := V[K];
            PK := PK - K; ICHVEC(KK1, KK1 + W2, PK × W1, A);
            SDET := - SDET; R := B[K]; B[K] := B[PK + K];
            B[PK + K] := R; R := M[PK]; M[PK] := A[KK]; A[KK] := R
         e̲n̲d̲
         e̲l̲s̲e̲ R := A[KK]; IW := KK1; LW1 := F - K;
         i̲f̲ R < 0 t̲h̲e̲n̲ SDET := - SDET;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ LW1 d̲o̲
            b̲e̲g̲i̲n̲ M[I] := S := M[I] / R; IW := IW + W1;
               ELMVEC(IW, IW + W2, KK1 - IW, A, A, - S);
               B[K + I] := B[K + I] - B[K] × S
            e̲n̲d̲
      e̲n̲d̲;
   AUX[3] := N; AUX[5] := MIN;
   KK := (N + 1) × W - W1; W2 := - 1; SHIFT := N × W1;
   f̲o̲r̲ K := N s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ KK := KK - W; SHIFT := SHIFT - W1;
         i̲f̲ W2 < W1 t̲h̲e̲n̲ W2 := W2 + 1;
         B[K] := (B[K] - VECVEC(K + 1, K + W2, SHIFT, B, A)) / A[KK]
      e̲n̲d̲;
   END: AUX[1] := SDET
e̲n̲d̲ DECSOLBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34423 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECTRI(SUB, DIAG, SUPER, N, AUX);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ SUB, DIAG, SUPER, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, N1;
   r̲e̲a̲l̲ D, R, S, U, NORM, NORM1, TOL;
   TOL := AUX[2]; D := DIAG[1]; R := SUPER[1];
   NORM := NORM1 := ABS(D) + ABS(R);
   i̲f̲ ABS(D) ≤ NORM1 × TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AUX[3] := 0; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
   U := SUPER[1] := R / D; S := SUB[1]; N1 := N - 1;
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 d̲o̲
      b̲e̲g̲i̲n̲ D := DIAG[I]; R := SUPER[I];
         NORM1 := ABS(S) + ABS(D) + ABS(R);
         D := DIAG[I] := D - U × S;
         i̲f̲ ABS(D) ≤ NORM1 × TOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ AUX[3] := I - 1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
         U := SUPER[I] := R / D; S := SUB[I];
         i̲f̲ NORM1 > NORM t̲h̲e̲n̲ NORM := NORM1
      e̲n̲d̲;
   D := DIAG[N]; NORM1 := ABS(D) + ABS(S);
   D := DIAG[N] := D - U × S;
   i̲f̲ ABS(D) ≤ NORM1 × TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AUX[3] := N1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
   i̲f̲ NORM1 > NORM t̲h̲e̲n̲ NORM := NORM1;
   AUX[3] := N; AUX[5] := NORM;
   EXIT:
e̲n̲d̲ DECTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34426 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECTRIPIV(SUB, DIAG, SUPER, N, AID, AUX, PIV);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ SUB, DIAG, SUPER, AID, AUX;
     B̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ PIV;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, I1, N1, N2;
   r̲e̲a̲l̲ D, R, S, U, T, Q, V, W, NORM, NORM1, NORM2, TOL;
   TOL := AUX[2]; D := DIAG[1]; R := SUPER[1];
   NORM := NORM2 := ABS(D) + ABS(R); N2 := N - 2;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1; S := SUB[I]; T := DIAG[I1]; Q := SUPER[I1];
         NORM1 := NORM2; NORM2 := ABS(S) + ABS(T) + ABS(Q);
         i̲f̲ NORM2 > NORM t̲h̲e̲n̲ NORM := NORM2;
         i̲f̲ ABS(D) × NORM2 < ABS(S) × NORM1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ ABS(S) ≤ TOL × NORM2 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ AUX[3] := I - 1; AUX[5] := S; g̲o̲t̲o̲ EXIT e̲n̲d̲;
            DIAG[I] := S; U := SUPER[I] := T / S;
            V := AID[I] := Q / S; SUB[I] := D;
            W := SUPER[I1] := -V × D; D := DIAG[I1] := R - U × D;
            R := W; NORM2 := NORM1; PIV[I] := t̲r̲u̲e̲
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i̲f̲ ABS(D) ≤ TOL × NORM1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ AUX[3] := I - 1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
            U := SUPER[I] := R / D; D := DIAG[I1] := T - U × S;
            AID[I] := 0; PIV[I] := f̲a̲l̲s̲e̲; R := Q
         e̲n̲d̲
      e̲n̲d̲;
   N1 := N - 1; S := SUB[N1]; T := DIAG[N]; NORM1 := NORM2;
   NORM2 := ABS(S) + ABS(T); i̲f̲ NORM2 > NORM t̲h̲e̲n̲ NORM := NORM2;
   i̲f̲ ABS(D) × NORM2 < ABS(S) × NORM1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(S) ≤ TOL × NORM2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ AUX[3] := N2; AUX[5] := S; g̲o̲t̲o̲ EXIT e̲n̲d̲;
      DIAG[N1] := S; U := SUPER[N1] := T / S; SUB[N1] := D;
      D := DIAG[N] := R - U × D; NORM2 := NORM1; PIV[N1] := t̲r̲u̲e̲
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(D) ≤ TOL × NORM1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ AUX[3] := N2; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
      U := SUPER[N1] := R / D; D := DIAG[N] := T - U × S;
      PIV[N1] := f̲a̲l̲s̲e̲
   e̲n̲d̲;
   i̲f̲ ABS(D) ≤ TOL × NORM2 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AUX[3] := N1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
   AUX[3] := N; AUX[5] := NORM;
   EXIT:
e̲n̲d̲ DECTRIPIV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34424 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLTRI(SUB, DIAG, SUPER, N, B);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ SUB, DIAG, SUPER, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ R;
   R := B[1] := B[1] / DIAG[1];
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        R := B[I] := (B[I] - SUB[I - 1] × R) / DIAG[I];
   f̲o̲r̲ I := N - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
        R := B[I] := B[I] - SUPER[I] × R
e̲n̲d̲ SOLTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34425 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSOLTRI(SUB, DIAG, SUPER, N, AUX, B);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ SUB, DIAG, SUPER, AUX, B;
b̲e̲g̲i̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECTRI(SUB, DIAG, SUPER, N, AUX); c̲o̲d̲e̲ 34423;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLTRI( SUB, DIAG, SUPER, N, B);  c̲o̲d̲e̲ 34424;
   DECTRI(SUB, DIAG, SUPER, N, AUX); i̲f̲ AUX[3] = N t̲h̲e̲n̲
     SOLTRI(SUB, DIAG, SUPER, N, B)
e̲n̲d̲ DECSOLTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34427 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLTRIPIV(SUB, DIAG, SUPER, N, AID, PIV, B);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ SUB, DIAG, SUPER, AID, B;
     B̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ PIV;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, N1;
   r̲e̲a̲l̲ BI, BI1, R, S, T;
   N1 := N - 1;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ PIV[I] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ BI := B[I + 1]; BI1 := B[I] e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ BI := B[I]; BI1 := B[I + 1] e̲n̲d̲;
         R := B[I] := BI / DIAG[I];
         B[I + 1] := BI1 - SUB[I] × R
      e̲n̲d̲;
   R := B[N] := B[N] / DIAG[N];
   T := B[N1] := B[N1] - SUPER[N1] × R;
   f̲o̲r̲ I := N - 2 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ S := R; R := T; T := B[I] := B[I] - SUPER[I] × R -
           (i̲f̲ PIV[I] t̲h̲e̲n̲ AID[I] × S e̲l̲s̲e̲ 0)
      e̲n̲d̲
e̲n̲d̲ SOLTRIPIV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34428 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSOLTRIPIV(SUB, DIAG, SUPER, N, AUX, B);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ SUB, DIAG, SUPER, AUX, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, I1, N1, N2;
   r̲e̲a̲l̲ D, R, S, U, T, Q, V, W, NORM, NORM1, NORM2, TOL,
     BI, BI1, BI2;
   B̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ PIV[1:N];
   TOL := AUX[2]; D := DIAG[1]; R := SUPER[1]; BI := B[1];
   NORM := NORM2 := ABS(D) + ABS(R); N2 := N - 2;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1; S := SUB[I]; T := DIAG[I1]; Q := SUPER[I1];
         BI1 := B[I1];
         NORM1 := NORM2; NORM2 := ABS(S) + ABS(T) + ABS(Q);
         i̲f̲ NORM2 > NORM t̲h̲e̲n̲ NORM := NORM2;
         i̲f̲ ABS(D) × NORM2 < ABS(S) × NORM1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ ABS(S) ≤ TOL × NORM2 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ AUX[3] := I - 1; AUX[5] := S; g̲o̲t̲o̲ EXIT e̲n̲d̲;
            U := SUPER[I] := T / S; BI1 := B[I] := BI1 / S;
            BI := BI - BI1 × D; V := SUB[I] := Q / S;
            W := SUPER[I1] := -V × D; D := DIAG[I1] := R - U × D;
            R := W; NORM2 := NORM1; PIV[I] := t̲r̲u̲e̲
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ i̲f̲ ABS(D) ≤ TOL × NORM1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ AUX[3] := I - 1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
            U := SUPER[I] := R / D; BI := B[I] := BI / D;
            BI := BI1 - BI × S; D := DIAG[I1] := T - U × S;
            PIV[I] := f̲a̲l̲s̲e̲; R := Q
         e̲n̲d̲
      e̲n̲d̲;
   N1 := N - 1; S := SUB[N1]; T := DIAG[N]; NORM1 := NORM2; BI1 := B[N];
   NORM2 := ABS(S) + ABS(T); i̲f̲ NORM2 > NORM t̲h̲e̲n̲ NORM := NORM2;
   i̲f̲ ABS(D) × NORM2 < ABS(S) × NORM1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(S) ≤ TOL × NORM2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ AUX[3] := N2; AUX[5] := S; g̲o̲t̲o̲ EXIT e̲n̲d̲;
      U := SUPER[N1] := T / S; BI1 := B[N1] := BI1 / S;
      BI := BI - BI1 × D; D := R - U × D; NORM2 := NORM1
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(D) ≤ TOL × NORM1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ AUX[3] := N2; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
      U := SUPER[N1] := R / D; BI := B[N1] := BI / D;
      BI := BI1 - BI × S; D := T - U × S
   e̲n̲d̲;
   i̲f̲ ABS(D) ≤ TOL × NORM2 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AUX[3] := N1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
   AUX[3] := N; AUX[5] := NORM;
   BI1 := B[N] := BI / D; BI := B[N1] := B[N1] - SUPER[N1] × BI1;
   f̲o̲r̲ I := N - 2 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ BI2 := BI1; BI1 := BI;
         BI := B[I] := B[I] - SUPER[I] × BI1 -
           (i̲f̲ PIV[I] t̲h̲e̲n̲ SUB[I] × BI2 e̲l̲s̲e̲ 0)
      e̲n̲d̲;
   EXIT:
e̲n̲d̲ DECSOLTRIPIV;
c̲o̲m̲m̲e̲n̲t̲ ================== 34330 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLDECBND(A, N, W, AUX); v̲a̲l̲u̲e̲ N, W; i̲n̲t̲e̲g̲e̲r̲ N, W;
     a̲r̲r̲a̲y̲ A, AUX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, K, JMAX, KK, KJ, W1, START;
   r̲e̲a̲l̲ R, EPS, MAX;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, S, A, B); c̲o̲d̲e̲ 34010;
   MAX := 0; KK := - W; W1 := W + 1;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ KK := KK + W1; i̲f̲ A[KK] > MAX t̲h̲e̲n̲ MAX := A[KK] e̲n̲d̲;
   JMAX := W; W1 := W + 1; KK := - W; EPS := AUX[2] × MAX;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ K + W > N t̲h̲e̲n̲ JMAX := JMAX - 1; KK := KK + W1;
         START := KK - K + 1;
         R := A[KK] - VECVEC(i̲f̲ K ≤ W1 t̲h̲e̲n̲ START e̲l̲s̲e̲ KK - W,
           KK - 1, 0, A, A); i̲f̲ R ≤ EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ AUX[3] := K - 1; g̲o̲ t̲o̲ END e̲n̲d̲;
         A[KK] := R := SQRT(R); KJ := KK;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ JMAX d̲o̲
            b̲e̲g̲i̲n̲ KJ := KJ + W;
               A[KJ] := (A[KJ] - VECVEC(i̲f̲ K + J ≤ W1 t̲h̲e̲n̲ START
               e̲l̲s̲e̲ KK - W + J, KK - 1, KJ - KK, A, A)) / R
            e̲n̲d̲
      e̲n̲d̲;
   AUX[3] := N;
   END:
e̲n̲d̲ CHLDECBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34331 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLDETERMBND(A, N, W); v̲a̲l̲u̲e̲ N, W; i̲n̲t̲e̲g̲e̲r̲ N, W;
     a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, KK, W1; r̲e̲a̲l̲ P;
   W1 := W + 1; KK := - W; P := 1;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ KK := KK + W1; P := A[KK] × P e̲n̲d̲;
   CHLDETERMBND := P × P
e̲n̲d̲ CHLDETERMBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34332 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLSOLBND(A, N, W, B); v̲a̲l̲u̲e̲ N, W; i̲n̲t̲e̲g̲e̲r̲ N, W;
     a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, K, IMAX, KK, W1;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, S, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SCAPRD1(LA, SA, LB, SB, N, A, B);
      c̲o̲d̲e̲ 34017;
   KK := - W; W1 := W + 1;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ KK := KK + W1;
         B[K] := (B[K] - VECVEC(i̲f̲ K ≤ W1 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ K - W,
           K - 1, KK - K, B, A)) / A[KK]
      e̲n̲d̲;
   IMAX := - 1;
   f̲o̲r̲ K := N s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ IMAX < W t̲h̲e̲n̲ IMAX := IMAX + 1;
         B[K] := (B[K] - SCAPRD1(KK + W, W, K + 1, 1, IMAX, A, B)) /
           A[KK]; KK := KK - W1
      e̲n̲d̲
e̲n̲d̲ CHLSOLBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34333 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLDECSOLBND(A, N, W, AUX, B); v̲a̲l̲u̲e̲ N, W;
   i̲n̲t̲e̲g̲e̲r̲ N, W; a̲r̲r̲a̲y̲ A, AUX, B;
b̲e̲g̲i̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLDECBND(A, N, W, AUX); c̲o̲d̲e̲ 34330;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLSOLBND(A, N, W, B); c̲o̲d̲e̲ 34332;
   CHLDECBND(A, N, W, AUX);
   i̲f̲ AUX[3] = N t̲h̲e̲n̲ CHLSOLBND(A, N, W, B)
e̲n̲d̲ CHLDECSOLBND;
c̲o̲m̲m̲e̲n̲t̲ ================== 34420 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSYMTRI(DIAG, CO, N, AUX); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ DIAG, CO, AUX;
b̲e̲g̲i̲n̲  i̲n̲t̲e̲g̲e̲r̲ I, N1;
   r̲e̲a̲l̲ D, R, S, U, TOL, NORM, NORMR;
   TOL := AUX[2]; D := DIAG[1]; R := CO[1];
   NORM := NORMR := ABS(D) + ABS(R);
   i̲f̲ ABS(D) ≤ NORMR × TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AUX[3] := 0; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
   U := CO[1] := R / D; N1 := N - 1;
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 d̲o̲
      b̲e̲g̲i̲n̲ S := R; R := CO[I]; D := DIAG[I];
         NORMR := ABS(S) + ABS(D) + ABS(R);
         D := DIAG[I] := D - U × S;
         i̲f̲ ABS(D) ≤ NORMR × TOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ AUX[3] := I - 1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
         U := CO[I] := R / D; i̲f̲ NORMR > NORM t̲h̲e̲n̲ NORM := NORMR
      e̲n̲d̲;
   D := DIAG[N]; NORMR := ABS(D) + ABS(R);
   D := DIAG[N] := D - U × R;
   i̲f̲ ABS(D) ≤ NORMR × TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AUX[3] := N1; AUX[5] := D; g̲o̲t̲o̲ EXIT e̲n̲d̲;
   i̲f̲ NORMR > NORM t̲h̲e̲n̲ NORM := NORMR;
   AUX[3] := N; AUX[5] := NORM;
   EXIT:
e̲n̲d̲ DECSYMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34421 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLSYMTRI(DIAG, CO, N, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ DIAG, CO, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ R, S;
   R := B[1]; B[1] := R / DIAG[1];
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ R := B[I] - CO[I-1] × R; B[I] := R / DIAG[I] e̲n̲d̲;
   S := B[N];
   f̲o̲r̲ I := N - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
        S := B[I] := B[I] - CO[I] × S
e̲n̲d̲ SOLSYMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34422 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSOLSYMTRI(DIAG, CO, N, AUX, B); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ DIAG, CO, AUX, B;
b̲e̲g̲i̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSYMTRI(DIAG, CO, N, AUX); c̲o̲d̲e̲ 34420;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLSYMTRI(DIAG, CO, N, B);   c̲o̲d̲e̲ 34421;
   DECSYMTRI(DIAG, CO, N, AUX); i̲f̲ AUX[3] = N t̲h̲e̲n̲
     SOLSYMTRI(DIAG, CO, N, B)
e̲n̲d̲ DECSOLSYMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34220 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ CONJ GRAD( MATVEC, X, R, L, N, GO ON, ITERATE, NORM2);
   v̲a̲l̲u̲e̲ L, N; p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC; a̲r̲r̲a̲y̲ X, R; B̲o̲o̲l̲e̲a̲n̲ GO ON;
     i̲n̲t̲e̲g̲e̲r̲ L, N, ITERATE; r̲e̲a̲l̲ NORM2;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ P, AP[ L: N];
   i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲  A, B, PRR, RRP;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC( A, B, C, D, E); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC( A, B, C, D, E, F); c̲o̲d̲e̲ 34020;
   f̲o̲r̲ ITERATE := 0, ITERATE + 1 w̲h̲i̲l̲e̲ GO ON d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ ITERATE = 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MATVEC( X, P);
            f̲o̲r̲ I := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 P[ I] := R[ I] := R[ I] - P[ I];
            PRR := VECVEC( L, N, 0, R, R)
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ B := RRP / PRR; PRR := RRP;
            f̲o̲r̲ I := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 P[ I] := R[ I] + B × P[ I]
         e̲n̲d̲;
         MATVEC( P, AP);
         A := PRR / VECVEC( L, N, 0, P, AP);
         ELMVEC( L, N, 0, X, P, A);
         ELMVEC( L, N, 0, R, AP, -A);
         NORM2 := RRP := VECVEC( L, N, 0, R, R)
      e̲n̲d̲
e̲n̲d̲ CONJ GRAD;
c̲o̲m̲m̲e̲n̲t̲ ================== 34173 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2405;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBR(A, N, EM, D, INT); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, EM, D; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, IM, I1, P, Q, J, T, COUNT, EXPONENT, NI;
   r̲e̲a̲l̲ C, R, EPS, OMEGA, FACTOR;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MOVE(K); v̲a̲l̲u̲e̲ K; i̲n̲t̲e̲g̲e̲r̲ K;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ DI;
      NI := Q - P; T := T + 1; i̲f̲ K ≠ I t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ ICHCOL(1, N, K, I, A); ICHROW(1, N, K, I, A);
         DI := D[I]; D[I] := D[K]; D[K] := DI
      e̲n̲d̲
   e̲n̲d̲ MOVE;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); c̲o̲d̲e̲ 34015;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHCOL(L, U, I, J, A); c̲o̲d̲e̲ 34031;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHROW(L, U, I, J, A); c̲o̲d̲e̲ 34032;

   FACTOR := 1 / (2 × LN(2)); c̲o̲m̲m̲e̲n̲t̲ MORE GENERALLY: LN(BASE);
   EPS := EM[0]; OMEGA := 1 / EPS; T := P := 1; Q := NI := I := N;
   COUNT := (N + 1) × N ÷ 2;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ D[J] := 1; INT[J] := 0 e̲n̲d̲;
   f̲o̲r̲ I := i̲f̲ I < Q t̲h̲e̲n̲ I + 1 e̲l̲s̲e̲ P
     w̲h̲i̲l̲e̲ COUNT > 0 ∧ NI > 0 d̲o̲
      b̲e̲g̲i̲n̲ COUNT := COUNT - 1; IM := I - 1; I1 := I + 1;
         C := SQRT(TAMMAT(P, IM, I, I, A, A) +
           TAMMAT(I1, Q, I, I, A, A));
         R := SQRT(MATTAM(P, IM, I, I, A, A) +
           MATTAM(I1, Q, I, I, A, A));
         i̲f̲ C × OMEGA ≤ R × EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ INT[T] := I; MOVE(P); P := P + 1 e̲n̲d̲
           e̲l̲s̲e̲ i̲f̲ R × OMEGA ≤ C × EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ INT[T] := -I; MOVE(Q); Q := Q - 1 e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ EXPONENT := LN(R / C) × FACTOR;
            i̲f̲ ABS(EXPONENT) > 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ NI := Q - P; C := 2 ⭡ EXPONENT; R := 1 / C;
               D[I] := D[I] × C;
               f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ IM,
                 I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                  b̲e̲g̲i̲n̲ A[J, I] := A[J, I] × C;
                     A[I, J] := A[I, J] × R
                  e̲n̲d̲
            e̲n̲d̲ e̲l̲s̲e̲ NI := NI - 1
         e̲n̲d̲
      e̲n̲d̲
e̲n̲d̲ EQILBR;
c̲o̲m̲m̲e̲n̲t̲ ================== 34174 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2406;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBR(N, N1, N2, D, INT, VEC); v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ D, VEC; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K, P, Q; r̲e̲a̲l̲ DI;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHROW(L, U, I, J, A); c̲o̲d̲e̲ 34032;

   P := 1; Q := N;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ DI := D[I]; i̲f̲ DI ≠ 1 t̲h̲e̲n̲
           f̲o̲r̲ J := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲ VEC[I, J] :=
              VEC[I, J] × DI; K := INT[I];
         i̲f̲ K > 0 t̲h̲e̲n̲ P := P + 1 e̲l̲s̲e̲
         i̲f̲ K < 0 t̲h̲e̲n̲ Q := Q - 1
      e̲n̲d̲;
   f̲o̲r̲ I := P - 1 + N - Q s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ K := INT[I]; i̲f̲ K > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ P := P - 1; i̲f̲ K ≠ P t̲h̲e̲n̲
              ICHROW(N1, N2, K, P, VEC)
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ Q := Q + 1; i̲f̲ -K ≠ Q t̲h̲e̲n̲
              ICHROW(N1, N2, -K, Q, VEC)
         e̲n̲d̲
      e̲n̲d̲
e̲n̲d̲ BAKLBR;
c̲o̲m̲m̲e̲n̲t̲ ================== 34361 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBRCOM(A1, A2, N, EM, D, INT); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A1, A2, EM, D; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, P, Q, J, T, COUNT, EXPONENT, NI, IM, I1;
   r̲e̲a̲l̲ C, R, EPS;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHCOL(L, U, I, J, A); c̲o̲d̲e̲ 34031;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHROW(L, U, I, J, A); c̲o̲d̲e̲ 34032;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); c̲o̲d̲e̲ 34015;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MOVE(K); v̲a̲l̲u̲e̲ K; i̲n̲t̲e̲g̲e̲r̲ K;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ DI;
      NI := Q - P; T := T + 1; i̲f̲ K ≠ I t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ ICHCOL(1, N, K, I, A1); ICHROW(1, N, K, I, A1);
         ICHCOL(1, N, K, I, A2); ICHROW(1, N, K, I, A2);
         DI := D[I]; D[I] := D[K]; D[K] := DI
      e̲n̲d̲
   e̲n̲d̲ MOVE;
   EPS := EM[0] ⭡ 4; T := P := 1; Q := NI := I := N;
   COUNT := EM[6];
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ D[J] := 1; INT[J] := 0 e̲n̲d̲;
   f̲o̲r̲ I := i̲f̲ I < Q t̲h̲e̲n̲ I + 1 e̲l̲s̲e̲ P w̲h̲i̲l̲e̲ COUNT > 0∧ NI > 0 d̲o̲
      b̲e̲g̲i̲n̲ COUNT := COUNT - 1; IM := I - 1; I1 := I + 1;
         C := TAMMAT(P, IM, I, I, A1, A1) + TAMMAT(I1, Q, I,
           I, A1, A1) + TAMMAT(P, IM, I, I, A2, A2) +
           TAMMAT(I1, Q, I, I, A2, A2);
         R := MATTAM(P, IM, I, I, A1, A1) + MATTAM(I1, Q, I,
           I, A1, A1) + MATTAM(P, IM, I, I, A2, A2) +
           MATTAM(I1, Q, I, I, A2, A2); i̲f̲ C / EPS ≤ R t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ INT[T] := I; MOVE(P); P := P + 1 e̲n̲d̲
           e̲l̲s̲e̲ i̲f̲ R / EPS ≤ C t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ INT[T] := - I; MOVE(Q); Q := Q - 1 e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ EXPONENT := LN(R / C) × 0·36067;
            i̲f̲ ABS(EXPONENT) > 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ NI := Q - P; C := 2 ⭡ EXPONENT;
               D[I] := D[I] × C;
               f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ IM, I1 s̲t̲e̲p̲ 1
                 u̲n̲t̲i̲l̲ N d̲o̲
                  b̲e̲g̲i̲n̲ A1[J, I] := A1[J, I] × C;
                     A1[I, J] := A1[I, J] / C;
                     A2[J, I] := A2[J, I] × C;
                     A2[I, J] := A2[I, J] / C
                  e̲n̲d̲
            e̲n̲d̲
            e̲l̲s̲e̲ NI := NI - 1
         e̲n̲d̲
      e̲n̲d̲;
   EM[7] := EM[6] - COUNT
e̲n̲d̲ EQILBRCOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34362 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBRCOM(N, N1, N2, D, INT, VR, VI);
   v̲a̲l̲u̲e̲ N, N1, N2; i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ D, VR, VI;
     i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBR(N, N1, N2, D, INT, VEC); c̲o̲d̲e̲ 34174;
   BAKLBR(N, N1, N2, D, INT, VR);
   BAKLBR(N, N1, N2, D, INT, VI)
e̲n̲d̲ BAKLBRCOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34140 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2300;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI2(A, N, D, B, BB, EM); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, B, BB, D, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, R, R1;
   r̲e̲a̲l̲ W, X, A1, B0, BB0, D0, MACHTOL, NORM;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X); c̲o̲d̲e̲ 34021;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34022;

   NORM := 0;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ W := 0;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J d̲o̲ W := ABS(A[I, J]) + W;
         f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ W := ABS(A[J, I]) +
              W; i̲f̲ W > NORM t̲h̲e̲n̲ NORM := W
      e̲n̲d̲;
   MACHTOL := EM[0] × NORM; EM[1] := NORM; R := N;
   f̲o̲r̲ R1 := N - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ D[R] := A[R, R]; X := TAMMAT(1, R - 2, R, R, A, A);
         A1 := A[R1, R]; i̲f̲ SQRT(X) ≤ MACHTOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B0 := B[R1] := A1; BB[R1] := B0 × B0; A[R, R] := 1 e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ BB0 := BB[R1] := A1 × A1 + X;
            B0 := i̲f̲ A1 > 0 t̲h̲e̲n̲ -SQRT(BB0) e̲l̲s̲e̲ SQRT(BB0);
            A1 := A[R1, R] := A1 - B0; W := A[R, R] := 1 / (A1 × B0);
            f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R1 d̲o̲ B[J] := (TAMMAT(1,
                 J, J, R, A, A) + MATMAT(J + 1, R1, J, R, A, A)) × W;
            ELMVECCOL(1, R1, R, B, A, TAMVEC(1, R1, R, A, B) ×
              W × ·5); f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R1 d̲o̲
               b̲e̲g̲i̲n̲ ELMCOL(1, J, J, R, A, A, B[J]);
                  ELMCOLVEC(1, J, J, A, B, A[J, R])
               e̲n̲d̲; B[R1] := B0
         e̲n̲d̲; R := R1
      e̲n̲d̲;
   D[1] := A[1, 1]; A[1, 1] := 1; B[N] := BB[N] := 0
e̲n̲d̲ TFMSYMTRI2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34141 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2301;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKSYMTRI2(A, N, N1, N2, VEC); v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ A, VEC;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K; r̲e̲a̲l̲ W;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;

   f̲o̲r̲ J := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ W := A[J, J]; i̲f̲ W < 0 t̲h̲e̲n̲
           f̲o̲r̲ K := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
              ELMCOL(1, J - 1, K, J, VEC, A,
              TAMMAT(1, J - 1, J, K, A, VEC) × W)
      e̲n̲d̲
e̲n̲d̲ BAKSYMTRI2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34142 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2302;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMPREVEC(A, N); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, J1, K; r̲e̲a̲l̲ AB;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;

   J1 := 1;
   f̲o̲r̲ J := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J1 - 1 ,
           J s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ A[I, J1] := 0;
         A[J1, J1] := 1; AB := A[J, J];
         i̲f̲ AB < 0 t̲h̲e̲n̲
           f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J1 d̲o̲
              ELMCOL(1, J1, K, J, A, A,
              TAMMAT(1, J1, J, K, A, A) × AB); J1 := J
      e̲n̲d̲;
   f̲o̲r̲ I := N - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
        A[I, N] := 0; A[N, N] := 1
e̲n̲d̲ TFMPREVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34143 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2305;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI1(A, N, D, B, BB, EM); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, B, BB, D, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, R, R1, P, Q, TI, TJ;
   r̲e̲a̲l̲ S, W, X, A1, B0, BB0, D0, NORM, MACHTOL;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SEQVEC(L, U, IL, SHIFT, A, B); c̲o̲d̲e̲ 34016;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;

   NORM := 0; TJ := 0;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ W := 0;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J d̲o̲ W := ABS(A[I + TJ]) + W;
         TJ := TJ + J; TI := TJ + J;
         f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ W := ABS(A[TI]) + W; TI := TI + I e̲n̲d̲;
         i̲f̲ W > NORM t̲h̲e̲n̲ NORM := W
      e̲n̲d̲;
   MACHTOL := EM[0] × NORM; EM[1] := NORM; Q := (N + 1) × N ÷ 2;
   R := N; f̲o̲r̲ R1 := N - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ P := Q - R; D[R] := A[Q];
         X := VECVEC(P + 1, Q - 2, 0, A, A);
         A1 := A[Q - 1]; i̲f̲ SQRT(X) ≤ MACHTOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B0 := B[R1] := A1; BB[R1] := B0 × B0; A[Q] := 1 e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ BB0 := BB[R1] := A1 × A1 + X;
            B0 := i̲f̲ A1 > 0 t̲h̲e̲n̲ -SQRT(BB0) e̲l̲s̲e̲ SQRT(BB0);
            A1 := A[Q - 1] := A1 - B0; W := A[Q] := 1 / (A1 × B0);
            TJ := 0; f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R1 d̲o̲
               b̲e̲g̲i̲n̲ TI := TJ + J; S := VECVEC(TJ + 1, TI, P - TJ,
                    A, A); TJ := TI + J;
                  B[J] := (SEQVEC(J + 1, R1, TJ, P, A, A) + S) × W;
                  TJ := TI
               e̲n̲d̲;
            ELMVEC(1, R1, P, B, A, VECVEC(1, R1, P, B, A) × W × ·5);
            TJ := 0; f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R1 d̲o̲
               b̲e̲g̲i̲n̲ TI := TJ + J; ELMVEC(TJ + 1, TI, P - TJ, A, A,
                    B[J]); ELMVEC(TJ + 1, TI, -TJ, A, B, A[J + P]);
                  TJ := TI
               e̲n̲d̲; B[R1] := B0
         e̲n̲d̲;
         Q := P; R := R1
      e̲n̲d̲;
   D[1] := A[1]; A[1] := 1; B[N] := BB[N] := 0
e̲n̲d̲ TFMSYMTRI1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34144 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2306;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKSYMTRI1(A, N, N1, N2, VEC); v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ A, VEC;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, J1, K, TI, TJ;
   r̲e̲a̲l̲ W; a̲r̲r̲a̲y̲ AUXVEC[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;

   f̲o̲r̲ K := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              AUXVEC[J] := VEC[J, K]; TJ := J1 := 1;
         f̲o̲r̲ J := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ TI := TJ + J; W := A[TI];
               i̲f̲ W < 0 t̲h̲e̲n̲ ELMVEC(1, J1, TJ, AUXVEC, A, VECVEC(1,
                 J1, TJ, AUXVEC, A) × W); J1 := J; TJ := TI
            e̲n̲d̲;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VEC[J, K] := AUXVEC[J]
      e̲n̲d̲
e̲n̲d̲ BAKSYMTRI1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34170 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2400;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMREAHES(A, N, EM, INT); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, EM; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, J1, K, L;
   r̲e̲a̲l̲ S, T, MACHTOL, MACHEPS, NORM;
   a̲r̲r̲a̲y̲ B[0:N - 1];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHCOL(L, U, I, J, A); c̲o̲d̲e̲ 34031;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHROW(L, U, I, J, A); c̲o̲d̲e̲ 34032;

   MACHEPS := EM[0]; NORM := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ S := 0;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ S := S + ABS(A[I, J]);
         i̲f̲ S > NORM t̲h̲e̲n̲ NORM := S
      e̲n̲d̲;
   EM[1] := NORM; MACHTOL := NORM × MACHEPS; INT[1] := 0;
   f̲o̲r̲ J := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ J1 := J - 1; L := 0; S := MACHTOL;
         f̲o̲r̲ K := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ T := ABS(A[K, J1]); i̲f̲ T > S t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ L := K; S := T e̲n̲d̲
            e̲n̲d̲;
         i̲f̲ L ≠ 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ ABS(A[J, J1]) < S t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ ICHROW(1, N, J, L, A);
               ICHCOL(1, N, J, L, A)
            e̲n̲d̲
            e̲l̲s̲e̲ L := J; T := A[J, J1];
            f̲o̲r̲ K := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 A[K, J1] := A[K, J1] / T
         e̲n̲d̲
         e̲l̲s̲e̲
           f̲o̲r̲ K := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ A[K, J1] := 0;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              B[I - 1] := A[I, J] := A[I, J] +
              (i̲f̲ L = 0 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ MATMAT(J + 1, N, I, J1, A, A))-
              MATVEC(1, i̲f̲ J1 < I - 2 t̲h̲e̲n̲ J1 e̲l̲s̲e̲ I - 2, I, A, B);
         INT[J] := L
      e̲n̲d̲
e̲n̲d̲ TFMREAHES;
c̲o̲m̲m̲e̲n̲t̲ ================== 34171 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2401;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKREAHES1(A, N, INT, V); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, V; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, L;
   r̲e̲a̲l̲ W; a̲r̲r̲a̲y̲ X[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;

   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I - 1] := V[I];
   f̲o̲r̲ I := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 2 d̲o̲
      b̲e̲g̲i̲n̲ V[I] := V[I] + MATVEC(1, I - 2, I, A, X);
         L := INT[I]; i̲f̲ L > I t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := V[I]; V[I] := V[L]; V[L] := W e̲n̲d̲
      e̲n̲d̲
e̲n̲d̲ BAKREAHES1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34172 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2402;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKREAHES2(A, N, N1, N2, INT, VEC); v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ A, VEC; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, L, K; a̲r̲r̲a̲y̲ U[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHROW(L, U, I, J, A); c̲o̲d̲e̲ 34032;

   f̲o̲r̲ I := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 2 d̲o̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ K := I - 2 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
              U[K + 1] := A[I, K];
         f̲o̲r̲ K := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
              VEC[I, K] := VEC[I, K] + TAMVEC(2 , I - 1, K, VEC, U);
         L := INT[I]; i̲f̲ L > I t̲h̲e̲n̲ ICHROW(N1, N2, I, L, VEC)
      e̲n̲d̲
e̲n̲d̲ BAKREAHES2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34363 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHHRMTRI(A, N, D, B, BB, EM, TR, TI); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, D, B, BB, EM, TR, TI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, J1, JM1, R, RM1;
   r̲e̲a̲l̲ NRM, W, TOL2, X, AR, AI, MOD, C, S, H, K, T, Q,
     AJR, ARJ, BJ, BBJ;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); c̲o̲d̲e̲ 34015;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X); c̲o̲d̲e̲ 34021;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34022;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34024;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECROW(L, U, I, A, B, X); c̲o̲d̲e̲ 34026;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34027;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34028;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34029;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CARPOL(AR, AI, R, C, S); c̲o̲d̲e̲ 34344;
   NRM := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ W := ABS(A[I, I]);
         f̲o̲r̲ J := I - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1, I + 1 s̲t̲e̲p̲ 1
           u̲n̲t̲i̲l̲ N d̲o̲ W := W + ABS(A[I, J]) + ABS(A[J, I]);
         i̲f̲ W > NRM t̲h̲e̲n̲ NRM := W
      e̲n̲d̲ I;
   TOL2 := (EM[0] × NRM) ⭡ 2; EM[1] := NRM; R := N;
   f̲o̲r̲ RM1 := N - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ X := TAMMAT(1, R - 2, R, R, A, A) + MATTAM(1, R -
           2, R, R, A, A); AR := A[RM1, R]; AI := - A[R, RM1];
         D[R] := A[R, R]; CARPOL(AR, AI, MOD, C, S);
         i̲f̲ X < TOL2 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ A[R, R] := - 1; B[RM1] := MOD;
            BB[RM1] := MOD × MOD
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ H := MOD × MOD + X; K := SQRT(H);
            T := A[R, R] := H + MOD × K;
            i̲f̲ AR = 0 ∧ AI = 0 t̲h̲e̲n̲ A[RM1, R] := K e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ A[RM1, R] := AR + C × K;
               A[R, RM1] := - AI - S × K; S := - S
            e̲n̲d̲;
            C := - C; J := 1; JM1 := 0;
            f̲o̲r̲ J1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲
               b̲e̲g̲i̲n̲ B[J] := (TAMMAT(1, J, J, R, A, A) +
                    MATMAT(J1, RM1, J, R, A, A) + MATTAM(1,
                    JM1, J, R, A, A) - MATMAT(J1, RM1, R, J,
                    A, A)) / T;
                  BB[J] := (MATMAT(1, JM1, J, R, A, A) -
                    TAMMAT(J1, RM1, J, R, A, A) - MATMAT(1, J,
                    R, J, A, A) - MATTAM(J1, RM1, J, R, A, A))
                    / T; JM1 := J; J := J1
               e̲n̲d̲ J1;
            Q := (TAMVEC(1, RM1, R, A, B) - MATVEC(1, RM1,
              R, A, BB)) / T / 2;
            ELMVECCOL(1, RM1, R, B, A, - Q);
            ELMVECROW(1, RM1, R, BB, A, Q); J := 1;
            f̲o̲r̲ J1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲
               b̲e̲g̲i̲n̲ AJR := A[J, R]; ARJ := A[R, J]; BJ := B[J];
                  BBJ := BB[J];
                  ELMROWVEC(J, RM1, J, A, B, - AJR);
                  ELMROWVEC(J, RM1, J, A, BB, ARJ);
                  ELMROWCOL(J, RM1, J, R, A, A, - BJ);
                  ELMROW(J, RM1, J, R, A, A, BBJ);
                  ELMCOLVEC(J1, RM1, J, A, B, - ARJ);
                  ELMCOLVEC(J1, RM1, J, A, BB, - AJR);
                  ELMCOL(J1, RM1, J, R, A, A, BBJ);
                  ELMCOLROW(J1, RM1, J, R, A, A, BJ); J := J1;
               e̲n̲d̲ J1;
            BB[RM1] := H; B[RM1] := K;
         e̲n̲d̲;
         TR[RM1] := C; TI[RM1] := S; R := RM1;
      e̲n̲d̲ RM1;
   D[1] := A[1, 1];
e̲n̲d̲ HSHHRMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34365 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKHRMTRI(A, N, N1, N2, VECR, VECI, TR, TI);
   v̲a̲l̲u̲e̲ N, N1, N2; i̲n̲t̲e̲g̲e̲r̲ N, N1, N2;
     a̲r̲r̲a̲y̲ A, VECR, VECI, TR, TI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, R, RM1;
   r̲e̲a̲l̲ C, S, T, QR, QI;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34029;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMUL(AR, AI, BR, BI, RR, RI); c̲o̲d̲e̲ 34341;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMROWCST(L, U, I, AR, AI, XR, XI); c̲o̲d̲e̲ 34353;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        f̲o̲r̲ J := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲ VECI[I, J] := 0; C := 1;
   S := 0;
   f̲o̲r̲ J := N - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ COMMUL(C, S, TR[J], TI[J], C, S);
         COMROWCST(N1, N2, J, VECR, VECI, C, S)
      e̲n̲d̲ J;
   RM1 := 2;
   f̲o̲r̲ R := 3 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ T := A[R, R]; i̲f̲ T > 0 t̲h̲e̲n̲
           f̲o̲r̲ J := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
            b̲e̲g̲i̲n̲ QR := (TAMMAT(1, RM1, R, J, A, VECR) -
                 MATMAT(1, RM1, R, J, A, VECI)) / T;
               QI := (TAMMAT(1, RM1, R, J, A, VECI) +
                 MATMAT(1, RM1, R, J, A, VECR)) / T;
               ELMCOL(1, RM1, J, R, VECR, A, - QR);
               ELMCOLROW(1, RM1, J, R, VECR, A, - QI);
               ELMCOLROW(1, RM1, J, R, VECI, A, QR);
               ELMCOL(1, RM1, J, R, VECI, A, - QI)
            e̲n̲d̲;
         RM1 := R;
      e̲n̲d̲ R;
e̲n̲d̲ BAKHRMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34364 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHHRMTRIVAL(A, N, D, BB, EM); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, D, BB, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, J1, JM1, R, RM1;
   r̲e̲a̲l̲ NRM, W, TOL2, X, AR, AI, H, T, Q, AJR, ARJ, DJ,
     BBJ, MOD2;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); c̲o̲d̲e̲ 34015;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X); c̲o̲d̲e̲ 34021;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34022;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34024;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECROW(L, U, I, A, B, X); c̲o̲d̲e̲ 34026;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34027;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34028;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34029;
   NRM := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ W := ABS(A[I, I]);
         f̲o̲r̲ J := I - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1, I + 1 s̲t̲e̲p̲ 1
           u̲n̲t̲i̲l̲ N d̲o̲ W := W + ABS(A[I, J]) + ABS(A[J, I]);
         i̲f̲ W > NRM t̲h̲e̲n̲ NRM := W
      e̲n̲d̲ I;
   TOL2 := (EM[0] × NRM) ⭡ 2; EM[1] := NRM; R := N;
   f̲o̲r̲ RM1 := N - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ X := TAMMAT(1, R - 2, R, R, A, A) + MATTAM(1, R -
           2, R, R, A, A); AR := A[RM1, R]; AI := - A[R, RM1];
         D[R] := A[R, R];
         i̲f̲ X < TOL2 t̲h̲e̲n̲ BB[RM1] := AR × AR + AI × AI e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ MOD2 := AR × AR + AI × AI; i̲f̲ MOD2 = 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ A[RM1, R] := SQRT(X); T := X e̲n̲d̲
            e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ X := X + MOD2; H := SQRT(MOD2 × X);
               T := X + H; H := 1 + X / H;
               A[R, RM1] := - AI × H; A[RM1, R] := AR × H;
            e̲n̲d̲;
            J := 1; JM1 := 0;
            f̲o̲r̲ J1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲
               b̲e̲g̲i̲n̲ D[J] := (TAMMAT(1, J, J, R, A, A) +
                    MATMAT(J1, RM1, J, R, A, A) + MATTAM(1,
                    JM1, J, R, A, A) - MATMAT(J1, RM1, R, J,
                    A, A)) / T;
                  BB[J] := (MATMAT(1, JM1, J, R, A, A) -
                    TAMMAT(J1, RM1, J, R, A, A) - MATMAT(1, J,
                    R, J, A, A) - MATTAM(J1, RM1, J, R, A, A))
                    / T; JM1 := J; J := J1
               e̲n̲d̲ J1;
            Q := (TAMVEC(1, RM1, R, A, D) - MATVEC(1, RM1,
              R, A, BB)) / T / 2;
            ELMVECCOL(1, RM1, R, D, A, - Q);
            ELMVECROW(1, RM1, R, BB, A, Q); J := 1;
            f̲o̲r̲ J1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲

               b̲e̲g̲i̲n̲ AJR := A[J, R]; ARJ := A[R, J]; DJ := D[J];
                  BBJ := BB[J];
                  ELMROWVEC(J, RM1, J, A, D, - AJR);
                  ELMROWVEC(J, RM1, J, A, BB, ARJ);
                  ELMROWCOL(J, RM1, J, R, A, A, - DJ);
                  ELMROW(J, RM1, J, R, A, A, BBJ);
                  ELMCOLVEC(J1, RM1, J, A, D, - ARJ);
                  ELMCOLVEC(J1, RM1, J, A, BB, - AJR);
                  ELMCOL(J1, RM1, J, R, A, A, BBJ);
                  ELMCOLROW(J1, RM1, J, R, A, A, DJ); J := J1;
               e̲n̲d̲ J1;
            BB[RM1] := X;
         e̲n̲d̲;
         R := RM1;
      e̲n̲d̲ RM1;
   D[1] := A[1, 1];
e̲n̲d̲ HSHHRMTRIVAL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34366 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMHES(AR, AI, N, EM, B, TR, TI, DEL); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ AR, AI, EM, B, TR, TI, DEL;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ R, RM1, I, J, NM1;
   r̲e̲a̲l̲ TOL, T, XR, XI;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34028;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMPRD(I, II, L, U, J, AR, AI, BR, BI, T); c̲o̲d̲e̲ 34356;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMCOLCST(L, U, J, AR, AI, XR, XI); c̲o̲d̲e̲ 34352;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMROWCST(L, U, I, AR, AI, XR, XI); c̲o̲d̲e̲ 34353;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CARPOL(AR, AI, R, C, S); c̲o̲d̲e̲ 34344;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMUL(AR, AI, BR, BI, RR, RI); c̲o̲d̲e̲ 34341;
   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMCOL(L, U, J, AR, AI, TOL, K, C, S, T);
      c̲o̲d̲e̲ 34355;
   NM1 := N - 1; TOL := (EM[0] × EM[1]) ⭡ 2; RM1 := 1;
   f̲o̲r̲ R := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NM1 d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ HSHCOMCOL(R, N, RM1, AR, AI, TOL, B[RM1],
           TR[R], TI[R], T) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ XR := (MATMAT(R, N, I, RM1, AI, AI) -
                    MATMAT(R, N, I, RM1, AR, AR)) / T;
                  XI := ( - MATMAT(R, N, I, RM1, AR, AI) -
                    MATMAT(R, N, I, RM1, AI, AR)) / T;
                  ELMROWCOL(R, N, I, RM1, AR, AR, XR);
                  ELMROWCOL(R, N, I, RM1, AR, AI, XI);
                  ELMROWCOL(R, N, I, RM1, AI, AR, XI);
                  ELMROWCOL(R, N, I, RM1, AI, AI, - XR)
               e̲n̲d̲;
            HSHCOMPRD(R, N, R, N, RM1, AR, AI, AR, AI, T);
         e̲n̲d̲;
         DEL[RM1] := T; RM1 := R
      e̲n̲d̲ FORR;
   i̲f̲ N > 1 t̲h̲e̲n̲ CARPOL(AR[N, NM1], AI[N, NM1], B[NM1],
     TR[N], TI[N]); RM1 := 1; TR[1] := 1; TI[1] := 0;
   f̲o̲r̲ R := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ COMMUL(TR[RM1], TI[RM1], TR[R], TI[R], TR[R],
           TI[R]); COMCOLCST(1, RM1, R, AR, AI, TR[R], TI[R]);
         COMROWCST(R + 1, N, R, AR, AI, TR[R], - TI[R]);
         RM1 := R
      e̲n̲d̲;
e̲n̲d̲ HSHCOMHES;
c̲o̲m̲m̲e̲n̲t̲ ================== 34367 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKCOMHES(AR, AI, TR, TI, DEL, VR, VI, N, N1, N2);
   v̲a̲l̲u̲e̲ N, N1, N2; i̲n̲t̲e̲g̲e̲r̲ N, N1, N2;
     a̲r̲r̲a̲y̲ AR, AI, TR, TI, DEL, VR, VI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, R, RM1;
   r̲e̲a̲l̲ H;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMPRD(I, II, L, U, J, AR, AI, BR, BI, T); c̲o̲d̲e̲ 34356;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMROWCST(L, U, I, AR, AI, XR, XI); c̲o̲d̲e̲ 34353;
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ COMROWCST(N1, N2, I, VR,
        VI, TR[I], TI[I]); R := N - 1;
   f̲o̲r̲ RM1 := N - 2 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ H := DEL[RM1];
         i̲f̲ H > 0 t̲h̲e̲n̲ HSHCOMPRD(R, N, N1, N2, RM1, VR, VI,
           AR, AI, H); R := RM1
      e̲n̲d̲
e̲n̲d̲ BAKCOMHES;
c̲o̲m̲m̲e̲n̲t̲ ================== 34260 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHREABID(A, M, N, D, B, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, D, B, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, I1;
   r̲e̲a̲l̲ NORM, MACHTOL, W, S, F, G, H;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B);
      v̲a̲l̲u̲e̲ L, U, I, J; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B);
      v̲a̲l̲u̲e̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34015;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X);
      v̲a̲l̲u̲e̲ L, U, I, J, X; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; r̲e̲a̲l̲ X;
        a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34023;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROW(L, U, I, J, A, B, X);
      v̲a̲l̲u̲e̲ L, U, I, J, X; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; r̲e̲a̲l̲ X;
        a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34024;

   NORM := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
      b̲e̲g̲i̲n̲ W := 0;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ W := ABS(A[I, J]) + W;
         i̲f̲ W > NORM t̲h̲e̲n̲ NORM := W
      e̲n̲d̲;
   MACHTOL := EM[0] × NORM; EM[1] := NORM;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1; S := TAMMAT(I1, M, I, I, A, A);
         i̲f̲ S < MACHTOL t̲h̲e̲n̲ D[I] := A[I, I] e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ F := A[I, I]; S := F × F + S;
            D[I] := G := i̲f̲ F < 0 t̲h̲e̲n̲ SQRT(S) e̲l̲s̲e̲ - SQRT(S);
            H := F × G - S; A[I, I] := F - G;
            f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 ELMCOL(I, M, J, I, A, A, TAMMAT(I, M, I, J, A, A) / H)
         e̲n̲d̲;
         i̲f̲ I < N t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ S := MATTAM(I1 + 1, N, I, I, A, A);
            i̲f̲ S < MACHTOL t̲h̲e̲n̲ B[I] := A[I, I1] e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ F := A[I, I1]; S := F × F + S;
               B[I] := G := i̲f̲ F < 0 t̲h̲e̲n̲ SQRT(S) e̲l̲s̲e̲ - SQRT(S);
               H := F × G - S; A[I, I1] := F - G;
               f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
                    ELMROW(I1, N, J, I, A, A, MATTAM(I1, N, I, J, A, A) /
                    H)
            e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲
e̲n̲d̲ HSHREABID;
c̲o̲m̲m̲e̲n̲t̲ ================== 34261 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ PSTTFMMAT(A, N, V, B);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, V, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, I1, J;
   r̲e̲a̲l̲ H;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B);
      v̲a̲l̲u̲e̲ L, U, I, J; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X);
      v̲a̲l̲u̲e̲ L, U, I, J, X; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; r̲e̲a̲l̲ X;
        a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34023;

   I1 := N; V[N, N] := 1;
   f̲o̲r̲ I := N - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ H := B[I] × A[I, I1]; i̲f̲ H < 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ V[J, I] := A[I, J] /
                 H;
            f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 ELMCOL(I1, N, J, I, V, V, MATMAT(I1, N, I, J, A, V))
         e̲n̲d̲;
         f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ V[I, J] := V[J, I] := 0;
         V[I, I] := 1; I1 := I
      e̲n̲d̲
e̲n̲d̲ PSTTFMMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34262 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ PRETFMMAT(A, M, N, D);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, D;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, I1, J;
   r̲e̲a̲l̲ G, H;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B);
      v̲a̲l̲u̲e̲ L, U, I, J; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34014;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X);
      v̲a̲l̲u̲e̲ L, U, I, J, X; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J; r̲e̲a̲l̲ X;
        a̲r̲r̲a̲y̲ A, B;
      c̲o̲d̲e̲ 34023;

   f̲o̲r̲ I := N s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1; G := D[I]; H := G × A[I, I];
         f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ A[I, J] := 0;
         i̲f̲ H < 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 ELMCOL(I, M, J, I, A, A, TAMMAT(I1, M, I, J, A, A) / H);
            f̲o̲r̲ J := I s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ A[J, I] := A[J, I] / G
         e̲n̲d̲
         e̲l̲s̲e̲
           f̲o̲r̲ J := I s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ A[J, I] := 0;
         A[I, I] := A[I, I] + 1
      e̲n̲d̲
e̲n̲d̲ PRETFMMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34151 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2311;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM);
   v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ D, BB, VAL, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K, COUNT;
   r̲e̲a̲l̲ MAX, X, Y, MACHEPS, NORM, RE, MACHTOL, UB, LB, LAMBDA;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ STURM;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ P, I; r̲e̲a̲l̲ F;
      COUNT := COUNT + 1;
      P := K; F := D[1] - X;
      f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ F ≤ 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ P := P + 1;
               i̲f̲ P > N t̲h̲e̲n̲ g̲o̲t̲o̲ OUT
            e̲n̲d̲
              e̲l̲s̲e̲ i̲f̲ P < I - 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ LB := X; g̲o̲t̲o̲ OUT e̲n̲d̲;
            i̲f̲ ABS(F) < MACHTOL t̲h̲e̲n̲
              F := i̲f̲ F ≤ 0 t̲h̲e̲n̲ - MACHTOL e̲l̲s̲e̲ MACHTOL;
            F := D[I] - X - BB[I - 1] / F
         e̲n̲d̲;
      i̲f̲ P = N ∨ F ≤ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ X < UB t̲h̲e̲n̲ UB := X e̲n̲d̲ e̲l̲s̲e̲ LB := X;
      OUT: STURM := i̲f̲ P = N t̲h̲e̲n̲ F e̲l̲s̲e̲ (N - P) × MAX
   e̲n̲d̲ STURM;

   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ZEROIN(X, Y, FX, TOLX); c̲o̲d̲e̲ 34150;

   MACHEPS := EM[0]; NORM := EM[1]; RE := EM[2];
   MACHTOL := NORM × MACHEPS; MAX := NORM / MACHEPS; COUNT := 0;
   UB := 1·1 × NORM; LB := - UB; LAMBDA := UB;
   f̲o̲r̲ K := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ X := LB; Y := UB; LB := -1·1  × NORM;
         ZEROIN(X, Y, STURM, ABS(X) × RE + MACHTOL);
         VAL[K] := LAMBDA := i̲f̲ X > LAMBDA t̲h̲e̲n̲ LAMBDA e̲l̲s̲e̲ X;
         i̲f̲ UB > X t̲h̲e̲n̲ UB := i̲f̲ X > Y t̲h̲e̲n̲ X e̲l̲s̲e̲ Y
      e̲n̲d̲;
   EM[3] := COUNT
e̲n̲d̲ VALSYMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34152 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2312;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECSYMTRI(D, B, N, N1, N2, VAL, VEC, EM);
   v̲a̲l̲u̲e̲ N, N1, N2;
   i̲n̲t̲e̲g̲e̲r̲ N, N1, N2; a̲r̲r̲a̲y̲ D, B, VAL, VEC, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K, COUNT, MAXCOUNT, COUNTLIM, ORTH, IND;
   r̲e̲a̲l̲ BI, BI1, U, W, Y, MI1, LAMBDA, OLDLAMBDA, ORTHEPS,
     VALSPREAD, SPR, RES, MAXRES, OLDRES, NORM, NEWNORM, OLDNORM,
     MACHTOL, VECTOL;
   a̲r̲r̲a̲y̲ M, P, Q, R, X[1:N];
   B̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ INT[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X); c̲o̲d̲e̲ 34021;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;

   NORM := EM[1]; MACHTOL := EM[0] × NORM; VALSPREAD := EM[4] × NORM;
   VECTOL := EM[6] × NORM; COUNTLIM := EM[8]; ORTHEPS := SQRT(EM[0]);
   MAXCOUNT := IND := 0; MAXRES := 0;
   i̲f̲ N1 > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ ORTH := EM[5]; OLDLAMBDA := VAL[N1 - ORTH];
      f̲o̲r̲ K := N1 - ORTH + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 - 1  d̲o̲
         b̲e̲g̲i̲n̲ LAMBDA := VAL[K]; SPR := OLDLAMBDA - LAMBDA;
            i̲f̲ SPR < MACHTOL t̲h̲e̲n̲ LAMBDA := OLDLAMBDA - MACHTOL;
            OLDLAMBDA := LAMBDA
         e̲n̲d̲
   e̲n̲d̲ e̲l̲s̲e̲ ORTH := 1;
   f̲o̲r̲ K := N1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲ LAMBDA := VAL[K]; i̲f̲ K > 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ SPR := OLDLAMBDA - LAMBDA;
            i̲f̲ SPR < VALSPREAD t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ i̲f̲ SPR < MACHTOL t̲h̲e̲n̲
                 LAMBDA := OLDLAMBDA - MACHTOL;
               ORTH := ORTH + 1
            e̲n̲d̲ e̲l̲s̲e̲ ORTH := 1
         e̲n̲d̲;
         COUNT := 0; U := D[1] - LAMBDA; BI := W := B[1];
         i̲f̲ ABS(BI) < MACHTOL t̲h̲e̲n̲ BI := MACHTOL;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1  d̲o̲
            b̲e̲g̲i̲n̲ BI1 := B[I + 1];
               i̲f̲ ABS(BI1) < MACHTOL t̲h̲e̲n̲ BI1 := MACHTOL;
               i̲f̲ ABS(BI) ≥ ABS(U) t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ MI1 := M[I + 1] := U / BI; P[I] := BI;
                  Y := Q[I] := D[I + 1] - LAMBDA; R[I] := BI1;
                  U := W - MI1 × Y; W := - MI1 × BI1; INT[I] := t̲r̲u̲e̲
               e̲n̲d̲
               e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ MI1 := M[I + 1] := BI / U; P[I] := U; Q[I] := W;
                  R[I] := 0; U := D[I + 1] - LAMBDA - MI1 × W; W := BI1;
                  INT[I] := f̲a̲l̲s̲e̲
               e̲n̲d̲;
               X[I] := 1; BI := BI1
            e̲n̲d̲ TRANSFORM;

         P[N] := i̲f̲ ABS(U) < MACHTOL t̲h̲e̲n̲ MACHTOL e̲l̲s̲e̲ U;
         Q[N] := R[N] := 0; X[N] := 1; g̲o̲t̲o̲ ENTRY;
         ITERATE: W := X[1];
         f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ INT[I - 1] t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ U := W; W := X[I - 1] := X[I] e̲n̲d̲
               e̲l̲s̲e̲ U := X[I]; W := X[I] := U - M[I] × W
            e̲n̲d̲ ALTERNATE;
         ENTRY: U := W := 0;
         f̲o̲r̲ I := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
            b̲e̲g̲i̲n̲ Y := U; U := X[I] := (X[I] - Q[I] × U - R[I] × W) /
                 P[I]; W := Y
            e̲n̲d̲ NEXT ITERATION;
         NEWNORM := SQRT(VECVEC(1, N, 0, X, X)); i̲f̲ ORTH > 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ OLDNORM := NEWNORM;
            f̲o̲r̲ J := K - ORTH + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K - 1 d̲o̲
                 ELMVECCOL(1, N, J, X, VEC, -TAMVEC(1, N, J, VEC, X));
            NEWNORM := SQRT(VECVEC(1, N, 0, X, X));
            i̲f̲ NEWNORM < ORTHEPS × OLDNORM t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ IND := IND + 1; COUNT := 1;
               f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ IND - 1,
                 IND + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := 0;
               X[IND] := 1; i̲f̲ IND = N t̲h̲e̲n̲ IND := 0;
               g̲o̲t̲o̲ ITERATE
            e̲n̲d̲ NEW START
         e̲n̲d̲ ORTHOGONALISATION;
         RES := 1 / NEWNORM; i̲f̲ RES > VECTOL ∨ COUNT = 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ COUNT := COUNT + 1; i̲f̲ COUNT ≤ COUNTLIM t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                    X[I] := X[I] × RES; g̲o̲t̲o̲ ITERATE
            e̲n̲d̲
         e̲n̲d̲;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VEC[I, K] := X[I] × RES;
         i̲f̲ COUNT > MAXCOUNT t̲h̲e̲n̲ MAXCOUNT := COUNT;
         i̲f̲ RES > MAXRES t̲h̲e̲n̲ MAXRES := RES; OLDLAMBDA := LAMBDA
      e̲n̲d̲;
   EM[5] := ORTH; EM[7] := MAXRES; EM[9] := MAXCOUNT
e̲n̲d̲ VECSYMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34161 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2321;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISYMTRI(A, N, D, B, BB, EM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, D, B, BB, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, J1, K, M, M1, COUNT, MAX;
   r̲e̲a̲l̲ BBMAX, R, S, SIN, T, C, COS, OLDCOS, G, P, W, TOL, TOL2,
     LAMBDA, DK1, A0, A1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOL(L, U, I, J, A, C, S); c̲o̲d̲e̲ 34040;

   TOL := EM[2] × EM[1]; TOL2 := TOL × TOL; COUNT := 0; BBMAX := 0;
   MAX := EM[4]; M := N;
   IN: K := M; M1 := M - 1;
   NEXT: K := K - 1; i̲f̲ K > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ BB[K] ≥ TOL2 t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT;
      i̲f̲ BB[K] > BBMAX t̲h̲e̲n̲ BBMAX := BB[K]
   e̲n̲d̲;
   i̲f̲ K = M1 t̲h̲e̲n̲ M := M1 e̲l̲s̲e̲
   b̲e̲g̲i̲n̲
      T := D[M] - D[M1]; R := BB[M1];
      i̲f̲ ABS(T) < TOL t̲h̲e̲n̲ S := SQRT(R) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ W := 2 / T; S := W × R / (SQRT(W × W × R + 1) + 1)
      e̲n̲d̲; i̲f̲ K = M - 2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[M] := D[M] + S; D[M1] := D[M1] - S;
         T := - S / B[M1]; R := SQRT(T × T + 1); COS := 1 / R;
         SIN := T / R; ROTCOL(1, N, M1, M, A, COS, SIN); M := M - 2
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ COUNT := COUNT + 1;
         i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ END;
         LAMBDA := D[M] + S; i̲f̲ ABS(T) < TOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := D[M1] - S;
            i̲f̲ ABS(W) < ABS(LAMBDA) t̲h̲e̲n̲ LAMBDA := W
         e̲n̲d̲;
         K := K + 1; T := D[K] - LAMBDA; COS := 1; W := B[K];
         P := SQRT(T × T + W × W); J1 := K;
         f̲o̲r̲ J := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
            b̲e̲g̲i̲n̲ OLDCOS := COS; COS := T / P; SIN := W / P;
               DK1 := D[J] - LAMBDA; T := OLDCOS × T;
               D[J1] := (T + DK1) × SIN × SIN + LAMBDA + T;
               T := COS × DK1 - SIN × W × OLDCOS; W := B[J];
               P := SQRT(T × T + W × W); G := B[J1] := SIN × P;
               BB[J1] := G × G; ROTCOL(1, N, J1, J, A, COS, SIN);
               J1 := J
            e̲n̲d̲;
         D[M] := COS × T + LAMBDA; i̲f̲ T < 0 t̲h̲e̲n̲ B[M1] := - G
      e̲n̲d̲ QRSTEP
   e̲n̲d̲;
   i̲f̲ M > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   END: EM[3] := SQRT(BBMAX); EM[5] := COUNT; QRISYMTRI := M
e̲n̲d̲ QRISYMTRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34153 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2313;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGVALSYM2(A, N, NUMVAL, VAL, EM); v̲a̲l̲u̲e̲ N, NUMVAL;
   i̲n̲t̲e̲g̲e̲r̲ N, NUMVAL; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB, D[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI2(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34140;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM); c̲o̲d̲e̲ 34151;

   TFMSYMTRI2(A, N, D, B, BB, EM);
   VALSYMTRI(D, BB, N, 1, NUMVAL, VAL, EM)
e̲n̲d̲ EIGVALSYM2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34154 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2314;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGSYM2(A, N, NUMVAL, VAL, VEC, EM); v̲a̲l̲u̲e̲ N, NUMVAL;
   i̲n̲t̲e̲g̲e̲r̲ N, NUMVAL; a̲r̲r̲a̲y̲ A, VAL, VEC, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB, D[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI2(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34140;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM); c̲o̲d̲e̲ 34151;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECSYMTRI(D, B, N, N1, N2, VAL, VEC, EM);
      c̲o̲d̲e̲ 34152;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKSYMTRI2(A, N, N1, N2, VEC); c̲o̲d̲e̲ 34141;

   TFMSYMTRI2(A, N, D, B, BB, EM);
   VALSYMTRI(D, BB, N, 1, NUMVAL, VAL, EM);
   VECSYMTRI(D, B, N, 1, NUMVAL, VAL, VEC, EM);
   BAKSYMTRI2(A, N, 1, NUMVAL, VEC)
e̲n̲d̲ EIGSYM2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34155 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2318;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGVALSYM1(A, N, NUMVAL, VAL, EM); v̲a̲l̲u̲e̲ N, NUMVAL;
   i̲n̲t̲e̲g̲e̲r̲ N, NUMVAL; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB, D[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI1(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34143;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM); c̲o̲d̲e̲ 34151;

   TFMSYMTRI1(A, N, D, B, BB, EM);
   VALSYMTRI(D, BB, N, 1, NUMVAL, VAL, EM)
e̲n̲d̲ EIGVALSYM1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34156 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2319;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGSYM1(A, N, NUMVAL, VAL, VEC, EM); v̲a̲l̲u̲e̲ N, NUMVAL;
   i̲n̲t̲e̲g̲e̲r̲ N, NUMVAL; a̲r̲r̲a̲y̲ A, VAL, VEC, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB, D[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI1(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34143;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM); c̲o̲d̲e̲ 34151;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECSYMTRI(D, B, N, N1, N2, VAL, VEC, EM);
      c̲o̲d̲e̲ 34152;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKSYMTRI1(A, N, N1, N2, VEC); c̲o̲d̲e̲ 34144;

   TFMSYMTRI1(A, N, D, B, BB, EM);
   VALSYMTRI(D, BB, N, 1, NUMVAL, VAL, EM);
   VECSYMTRI(D, B, N, 1, NUMVAL, VAL, VEC, EM);
   BAKSYMTRI1(A, N, 1, NUMVAL, VEC)
e̲n̲d̲ EIGSYM1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34162 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2322;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIVALSYM2(A, N, VAL, EM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI2(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34140;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIVALSYMTRI(D, BB, N, EM);
      c̲o̲d̲e̲ 34160;

   TFMSYMTRI2(A, N, VAL, B, BB, EM);
   QRIVALSYM2 := QRIVALSYMTRI(VAL, BB, N, EM)
e̲n̲d̲ QRIVALSYM2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34163 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2323;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISYM(A, N, VAL, EM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI2(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34140;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMPREVEC(A, N); c̲o̲d̲e̲ 34142;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISYMTRI(A, N, D, B, BB, EM);
      c̲o̲d̲e̲ 34161;

   TFMSYMTRI2(A, N, VAL, B, BB, EM); TFMPREVEC(A, N);
   QRISYM := QRISYMTRI(A, N, VAL, B, BB, EM)
e̲n̲d̲ QRISYM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34164 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2327;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIVALSYM1(A, N, VAL, EM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB[1 : N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMSYMTRI1(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34143;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIVALSYMTRI(D, BB, N, EM);
      c̲o̲d̲e̲ 34160;

   TFMSYMTRI1(A, N, VAL, B, BB, EM);
   QRIVALSYM1 := QRIVALSYMTRI(VAL, BB, N, EM)
e̲n̲d̲ QRIVALSYM1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34180 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2410;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAVALQRI(A, N, EM, VAL); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, VAL;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N1, I, I1, J, Q, MAX, COUNT;
   r̲e̲a̲l̲ DET, W, SHIFT, KAPPA, NU, MU, R, TOL, DELTA, MACHTOL, S;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOL(L, U, I, J, A, C, S); c̲o̲d̲e̲ 34040;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTROW(L, U, I, J, A, C, S); c̲o̲d̲e̲ 34041;
   MACHTOL := EM[0] × EM[1]; TOL := EM[1] × EM[2]; MAX := EM[4];
   COUNT := 0; R := 0;
   IN: N1 := N - 1;
   f̲o̲r̲ I := N, I - 1 w̲h̲i̲l̲e̲ (i̲f̲ I ≥ 1 t̲h̲e̲n̲
     ABS(A[I + 1, I]) > TOL e̲l̲s̲e̲ f̲a̲l̲s̲e̲) d̲o̲ Q := I;
   i̲f̲ Q > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(A[Q, Q - 1]) > R t̲h̲e̲n̲
        R := ABS(A[Q, Q - 1])
   e̲n̲d̲;
   i̲f̲ Q = N t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ VAL[N] := A[N, N]; N := N1 e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ DELTA := A[N, N] - A[N1, N1]; DET := A[N, N1] × A[N1, N];
      i̲f̲ ABS(DELTA) < MACHTOL t̲h̲e̲n̲ S := SQRT(DET) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ W := 2 / DELTA; S := W × W × DET + 1;
         S := i̲f̲ S ≤ 0 t̲h̲e̲n̲ -DELTA × ·5 e̲l̲s̲e̲
           W × DET / (SQRT(S) + 1)
      e̲n̲d̲;
      i̲f̲ Q = N1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ VAL[N] := A[N, N] + S;
         VAL[N1] := A[N1, N1] - S; N := N - 2
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ COUNT := COUNT + 1;
         i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ OUT;
         SHIFT := A[N, N] + S; i̲f̲ ABS(DELTA) < TOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := A[N1, N1] - S;
            i̲f̲ ABS(W) < ABS(SHIFT) t̲h̲e̲n̲ SHIFT := W
         e̲n̲d̲;
         A[Q, Q] := A[Q, Q] - SHIFT;
         f̲o̲r̲ I := Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
            b̲e̲g̲i̲n̲ I1 := I + 1; A[I1, I1] := A[I1, I1] - SHIFT;
               KAPPA := SQRT(A[I, I] ⭡ 2 + A[I1, I] ⭡ 2);
               i̲f̲ I > Q t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ A[I, I - 1] := KAPPA × NU;
                  W := KAPPA × MU
               e̲n̲d̲
               e̲l̲s̲e̲ W := KAPPA; MU := A[I, I] / KAPPA;
               NU := A[I1, I] / KAPPA; A[I, I] := W;
               ROTROW(I1, N, I, I1, A, MU, NU);
               ROTCOL(Q, I, I, I1, A, MU, NU);
               A[I, I] := A[I, I] + SHIFT
            e̲n̲d̲;
         A[N, N - 1] := A[N, N] × NU; A[N, N] := A[N, N] × MU + SHIFT
      e̲n̲d̲
   e̲n̲d̲;
   i̲f̲ N > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   OUT: EM[3] := R; EM[5] := COUNT; REAVALQRI := N
e̲n̲d̲ REAVALQRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34181 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2411;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAVECHES(A, N, LAMBDA, EM, V); v̲a̲l̲u̲e̲ N, LAMBDA;
   i̲n̲t̲e̲g̲e̲r̲ N; r̲e̲a̲l̲ LAMBDA; a̲r̲r̲a̲y̲ A, EM, V;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, I1, J, COUNT, MAX;
   r̲e̲a̲l̲ M, R, NORM, MACHTOL, TOL;
   B̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ P[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;

   NORM := EM[1]; MACHTOL := EM[0] × NORM; TOL := EM[6] × NORM;
   MAX := EM[8]; A[1, 1] := A[1, 1] - LAMBDA;
   GAUSS: f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1; R := A[I, I]; M := A[I1, I];
         i̲f̲ ABS(M) < MACHTOL t̲h̲e̲n̲ M := MACHTOL;
         P[I] := ABS(M) ≤ ABS(R);
         i̲f̲ P[I] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ A[I1, I] := M := M / R;
            f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 A[I1, J] := (i̲f̲ J > I1 t̲h̲e̲n̲ A[I1, J]
               e̲l̲s̲e̲ A[I1, J] - LAMBDA) - M × A[I, J]
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ A[I, I] := M; A[I1, I] := M := R / M;
            f̲o̲r̲ J := I1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ R := (i̲f̲ J > I1 t̲h̲e̲n̲ A[I1, J] e̲l̲s̲e̲
                    A[I1, J] - LAMBDA);
                  A[I1, J] := A[I, J] - M × R; A[I, J] := R
               e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲ GAUSS;
   i̲f̲ ABS(A[N, N]) < MACHTOL t̲h̲e̲n̲ A[N, N] := MACHTOL;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ V[J] := 1; COUNT := 0;
   FORWARD: COUNT := COUNT + 1; i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ OUT;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1;
         i̲f̲ P[I] t̲h̲e̲n̲ V[I1] := V[I1] - A[I1, I] × V[I] e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ R := V[I1]; V[I1] := V[I] - A[I1, I] × R;
            V[I] := R
         e̲n̲d̲
      e̲n̲d̲ FORWARD;
   BACKWARD: f̲o̲r̲ I := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
        V[I] := (V[I] - MATVEC(I + 1, N, I, A, V)) / A[I, I];
   R := 1 / SQRT(VECVEC(1, N, 0, V, V));
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ V[J] := V[J] × R;
   i̲f̲ R > TOL t̲h̲e̲n̲ g̲o̲t̲o̲ FORWARD;
   OUT: EM[7] := R; EM[9] := COUNT
e̲n̲d̲ REAVECHES;
c̲o̲m̲m̲e̲n̲t̲ ================== 34186 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2416;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAQRI(A, N, EM, VAL, VEC); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, VAL, VEC;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M1, I, I1, M, J, Q, MAX, COUNT;
   r̲e̲a̲l̲ W, SHIFT, KAPPA, NU, MU, R, TOL, S, MACHTOL,
     ELMAX, T, DELTA, DET;
   a̲r̲r̲a̲y̲ TF[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOL(L, U, I, J, A, C, S); c̲o̲d̲e̲ 34040;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTROW(L, U, I, J, A, C, S); c̲o̲d̲e̲ 34041;

   MACHTOL := EM[0] × EM[1]; TOL := EM[1] × EM[2]; MAX := EM[4];
   COUNT := 0; ELMAX := 0; M := N;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ VEC[I, I] := 1;
         f̲o̲r̲ J := I + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              VEC[I, J] := VEC[J, I] := 0
      e̲n̲d̲;
   IN: M1 := M - 1;
   f̲o̲r̲ I := M, I - 1 w̲h̲i̲l̲e̲ (i̲f̲ I ≥ 1 t̲h̲e̲n̲
     ABS(A[I + 1, I]) > TOL e̲l̲s̲e̲ f̲a̲l̲s̲e̲) d̲o̲ Q := I;
   i̲f̲ Q > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(A[Q, Q - 1]) > ELMAX t̲h̲e̲n̲
        ELMAX := ABS(A[Q, Q - 1])
   e̲n̲d̲;
   i̲f̲ Q = M t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ VAL[M] := A[M, M]; M := M1 e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ DELTA := A[M, M] - A[M1, M1]; DET := A[M, M1] × A[M1, M];
      i̲f̲ ABS(DELTA) < MACHTOL t̲h̲e̲n̲ S := SQRT(DET) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ W := 2 / DELTA; S := W × W × DET + 1;
         S := i̲f̲ S ≤ 0 t̲h̲e̲n̲ -DELTA × ·5 e̲l̲s̲e̲
           W × DET / (SQRT(S) + 1)
      e̲n̲d̲;
      i̲f̲ Q = M1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ A[M, M] := VAL[M] := A[M, M] + S;
         A[Q, Q] := VAL[Q] := A[Q, Q] - S;
         T := i̲f̲ ABS(S) < MACHTOL t̲h̲e̲n̲
           (S + DELTA) / A[M, Q] e̲l̲s̲e̲ A[Q, M] / S;
         R := SQRT(T × T + 1); NU := 1 / R;
         MU := -T × NU; A[Q, M] := A[Q, M] - A[M, Q];
         ROTROW(Q + 2, N, Q, M, A, MU, NU);
         ROTCOL(1, Q - 1, Q, M, A, MU, NU);
         ROTCOL(1, N, Q, M, VEC, MU, NU); M := M - 2
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ COUNT := COUNT + 1;
         i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ END;
         SHIFT := A[M, M] + S; i̲f̲ ABS(DELTA) < TOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := A[M1, M1] - S;
            i̲f̲ ABS(W) < ABS(SHIFT) t̲h̲e̲n̲ SHIFT := W
         e̲n̲d̲;
         A[Q, Q] := A[Q, Q] - SHIFT;
         f̲o̲r̲ I := Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M1 d̲o̲
            b̲e̲g̲i̲n̲ I1 := I + 1; A[I1, I1] := A[I1, I1] - SHIFT;
               KAPPA := SQRT(A[I, I] ⭡ 2 + A[I1, I] ⭡ 2);
               i̲f̲ I > Q t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ A[I, I - 1] := KAPPA × NU;
                  W := KAPPA × MU
               e̲n̲d̲
               e̲l̲s̲e̲ W := KAPPA; MU := A[I, I] / KAPPA;
               NU := A[I1, I] / KAPPA; A[I, I] := W;
               ROTROW(I1, N, I, I1, A, MU, NU);
               ROTCOL(1, I, I, I1, A, MU, NU);
               A[I, I] := A[I, I] + SHIFT;
               ROTCOL(1, N, I, I1, VEC, MU, NU)
            e̲n̲d̲;
         A[M, M1] := A[M, M] × NU; A[M, M] := A[M, M] × MU + SHIFT
      e̲n̲d̲
   e̲n̲d̲;
   i̲f̲ M > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   f̲o̲r̲ J := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 2 d̲o̲
      b̲e̲g̲i̲n̲ TF[J] := 1; T := A[J, J];
         f̲o̲r̲ I := J - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
            b̲e̲g̲i̲n̲ DELTA := T - A[I, I];
               TF[I] := MATVEC(I + 1, J, I, A, TF) /
                 (i̲f̲ ABS(DELTA) < MACHTOL t̲h̲e̲n̲ MACHTOL e̲l̲s̲e̲ DELTA)
            e̲n̲d̲;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              VEC[I, J] := MATVEC(1, J, I, VEC, TF)
      e̲n̲d̲;
   END: EM[3] := ELMAX; EM[5] := COUNT; REAQRI := M
e̲n̲d̲ REAQRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34190 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2420;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMVALQRI(A, N, EM, RE, IM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, RE, IM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, P, Q, MAX, COUNT, N1, P1, P2, IMIN1,
     I1, I2, I3;
   r̲e̲a̲l̲ DISC, SIGMA, RHO, G1, G2, G3, PSI1, PSI2, AA, E, K,
     S, NORM, MACHTOL2, TOL, W;
   B̲o̲o̲l̲e̲a̲n̲ B;

   NORM := EM[1]; MACHTOL2 := (EM[0] × NORM) ⭡ 2;
   TOL := EM[2] × NORM; MAX := EM[4]; COUNT := 0; W := 0;
   IN: f̲o̲r̲ I := N, I - 1 w̲h̲i̲l̲e̲
     (i̲f̲ I ≥ 1 t̲h̲e̲n̲ ABS(A[I + 1, I]) > TOL e̲l̲s̲e̲ f̲a̲l̲s̲e̲)
     d̲o̲ Q := I; i̲f̲ Q > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(A[Q, Q - 1]) > W t̲h̲e̲n̲ W := ABS(A[Q, Q - 1])
   e̲n̲d̲;
   i̲f̲ Q ≥ N - 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ N1 := N - 1; i̲f̲ Q = N t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ RE[N] := A[N, N]; IM[N] := 0; N := N1 e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ SIGMA := A[N, N] - A[N1, N1];
         RHO := -A[N, N1] × A[N1, N];
         DISC := SIGMA ⭡ 2 - 4 × RHO; i̲f̲ DISC > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ DISC := SQRT(DISC);
            S := -2 × RHO / (SIGMA + (i̲f̲ SIGMA ≥ 0
              t̲h̲e̲n̲ DISC e̲l̲s̲e̲ -DISC));
            RE[N] := A[N, N] + S;
            RE[N1] := A[N1, N1] - S; IM[N] := IM[N1] := 0
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ RE[N] := RE[N1] := (A[N1, N1] + A[N, N]) / 2;
            IM[N1] := SQRT( -DISC) / 2; IM[N] := -IM[N1]
         e̲n̲d̲;
         N := N - 2
      e̲n̲d̲
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ COUNT := COUNT + 1; i̲f̲ COUNT > MAX t̲h̲e̲n̲
        g̲o̲t̲o̲ OUT; N1 := N - 1;
      SIGMA := A[N, N] + A[N1, N1] + SQRT(ABS(A[N1, N - 2] × A[N, N1])× EM[0]); RHO := A[N, N] × A[N1, N1] - A[N, N1] × A[N1, N];
      f̲o̲r̲ I := N - 1, I - 1 w̲h̲i̲l̲e̲
        (i̲f̲ I - 1 ≥ Q t̲h̲e̲n̲ ABS(A[I, I - 1] ×
        A[I1, I] × (ABS(A[I, I] + A[I1, I1] - SIGMA) +
        ABS(A[I + 2, I1]))) > ABS(A[I, I] × ((A[I, I] - SIGMA) +
        A[I, I1] × A[I1, I] + RHO)) × TOL
      e̲l̲s̲e̲ f̲a̲l̲s̲e̲) d̲o̲ P1 := I1 := I; P := P1 - 1;
      P2 := P + 2;
      f̲o̲r̲ I := P s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
         b̲e̲g̲i̲n̲ IMIN1 := I - 1; I1 := I + 1; I2 := I + 2;
            i̲f̲ I = P t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ G1 := A[P, P] × (A[P, P] - SIGMA) + A[P, P1] ×
                 A[P1, P] + RHO;
               G2 := A[P1, P] × (A[P, P] + A[P1, P1] - SIGMA);
               i̲f̲ P1 ≤ N1 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ G3 := A[P1, P] × A[P2, P1]; A[P2, P] := 0 e̲n̲d̲
               e̲l̲s̲e̲ G3 := 0
            e̲n̲d̲
            e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ G1 := A[I, IMIN1]; G2 := A[I1, IMIN1];
               G3 := i̲f̲ I2 ≤ N t̲h̲e̲n̲ A[I2, IMIN1] e̲l̲s̲e̲ 0
            e̲n̲d̲;
            K := i̲f̲ G1 ≥ 0 t̲h̲e̲n̲
              SQRT(G1 ⭡ 2 + G2 ⭡ 2 + G3 ⭡ 2) e̲l̲s̲e̲
              -SQRT(G1 ⭡ 2 + G2 ⭡ 2 + G3 ⭡ 2);
            B := ABS(K) > MACHTOL2;
            AA := i̲f̲ B t̲h̲e̲n̲ G1 / K + 1 e̲l̲s̲e̲ 2;
            PSI1 := i̲f̲ B t̲h̲e̲n̲ G2 / (G1 + K) e̲l̲s̲e̲ 0;
            PSI2 := i̲f̲ B t̲h̲e̲n̲ G3 / (G1 + K) e̲l̲s̲e̲ 0;
            i̲f̲ I ≠ Q t̲h̲e̲n̲ A[I, IMIN1] := i̲f̲ I = P t̲h̲e̲n̲
              -A[I, IMIN1] e̲l̲s̲e̲ -K;
            f̲o̲r̲ J := I s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ E := AA × (A[I, J] + PSI1 × A[I1, J] +
                    (i̲f̲ I2 ≤ N t̲h̲e̲n̲ PSI2 × A[I2, J] e̲l̲s̲e̲ 0));
                  A[I, J] := A[I, J] - E; A[I1, J] := A[I1, J] - PSI1 × E;
                  i̲f̲ I2 ≤ N t̲h̲e̲n̲ A[I2, J] := A[I2, J] - PSI2 × E
               e̲n̲d̲;
            f̲o̲r̲ J := Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲
              (i̲f̲ I2 ≤ N t̲h̲e̲n̲ I2 e̲l̲s̲e̲ N) d̲o̲
               b̲e̲g̲i̲n̲ E := AA × (A[J, I] + PSI1 × A[J, I1] +
                    (i̲f̲ I2 ≤ N t̲h̲e̲n̲ PSI2 × A[J, I2] e̲l̲s̲e̲ 0));
                  A[J, I] := A[J, I] - E; A[J, I1] := A[J, I1] - PSI1 × E;
                  i̲f̲ I2 ≤ N t̲h̲e̲n̲ A[J, I2] := A[J, I2] - PSI2 × E
               e̲n̲d̲;
            i̲f̲ I2 ≤ N1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ I3 := I + 3; E := AA × PSI2 × A[I3, I2];
               A[I3, I] := -E;
               A[I3, I1] := -PSI1 × E;
               A[I3, I2] := A[I3, I2] - PSI2 × E
            e̲n̲d̲
         e̲n̲d̲
   e̲n̲d̲;
   i̲f̲ N > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   OUT: EM[3] := W; EM[5] := COUNT; COMVALQRI := N
e̲n̲d̲ COMVALQRI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34191 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2421;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMVECHES(A, N, LAMBDA, MU, EM, U, V);
   v̲a̲l̲u̲e̲ N, LAMBDA, MU;
   i̲n̲t̲e̲g̲e̲r̲ N; r̲e̲a̲l̲ LAMBDA, MU; a̲r̲r̲a̲y̲ A, EM, U, V;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, I1, J, COUNT, MAX;
   r̲e̲a̲l̲ AA, BB, D, M, R, S, W, X, Y, NORM, MACHTOL, TOL;
   a̲r̲r̲a̲y̲ G, F[1:N];
   B̲o̲o̲l̲e̲a̲n̲ a̲r̲r̲a̲y̲ P[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;

   NORM := EM[1]; MACHTOL := EM[0] × NORM; TOL := EM[6] × NORM;
   MAX := EM[8];
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ F[I - 1] := A[I, I - 1]; A[I, 1] := 0 e̲n̲d̲;
   AA := A[1, 1] - LAMBDA; BB := -MU;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1; M := F[I];
         i̲f̲ ABS(M) < MACHTOL t̲h̲e̲n̲ M := MACHTOL;
         A[I, I] := M; D := AA ⭡ 2 + BB ⭡ 2; P[I] := ABS(M) < SQRT(D);
         i̲f̲ P[I] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ A[I, J] × FACTOR AND A[I1, J] - A[I, J];
            F[I] := R := M × AA / D; G[I] := S := -M × BB / D;
            W := A[I1, I]; X := A[I, I1]; A[I1, I] := Y := X × S + W × R;
            A[I, I1] := X := X × R - W × S;
            AA := A[I1, I1] - LAMBDA - X; BB := -(MU + Y);
            f̲o̲r̲ J := I + 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ W := A[J, I]; X := A[I, J];
                  A[J, I] := Y := X × S + W × R;
                  A[I, J] := X := X × R - W × S; A[J, I1] := -Y;
                  A[I1, J] := A[I1, J] - X
               e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ INTERCHANGE A[I1, J] AND
              A[I, J] - A[I1, J] × FACTOR;
            F[I] := R := AA / M; G[I] := S := BB / M;
            W := A[I1, I1] - LAMBDA; AA := A[I, I1] - R × W - S × MU;
            A[I, I1] := W; BB := A[I1, I] - S × W + R × MU;
            A[I1, I] := -MU;
            f̲o̲r̲ J := I + 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ W := A[I1, J]; A[I1, J] := A[I, J] - R × W;
                  A[I, J] := W;
                  A[J, I1] := A[J, I] - S × W; A[J, I] := 0
               e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲
        P[N] := t̲r̲u̲e̲; D := AA ⭡ 2 + BB ⭡ 2; i̲f̲ D < MACHTOL ⭡ 2
     t̲h̲e̲n̲ b̲e̲g̲i̲n̲ AA := MACHTOL; BB := 0; D := MACHTOL ⭡ 2 e̲n̲d̲;
   A[N, N] := D; F[N] := AA; G[N] := -BB;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ U[I] := 1; V[I] := 0 e̲n̲d̲;
   COUNT := 0;
   FORWARD: i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ OUTM;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ P[I] t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ W := V[I]; V[I] := G[I] × U[I] + F[I] × W;
            U[I] := F[I] × U[I] - G[I] × W; i̲f̲ I < N t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ V[I + 1] := V[I + 1] - V[I];
               U[I + 1] := U[I + 1] - U[I]
            e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ AA := U[I + 1]; BB := V[I + 1];
            U[I + 1] := U[I] - (F[I] × AA - G[I] × BB); U[I] := AA;
            V[I + 1] := V[I] - (G[I] × AA + F[I] × BB); V[I] := BB
         e̲n̲d̲
      e̲n̲d̲ FORWARD;
   BACKWARD: f̲o̲r̲ I := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ I1 := I + 1;
         U[I] := (U[I] - MATVEC(I1, N, I, A, U) + (i̲f̲ P[I] t̲h̲e̲n̲
           TAMVEC(I1, N, I, A, V) e̲l̲s̲e̲ A[I1, I] × V[I1])) / A[I, I];
         V[I] := (V[I] - MATVEC(I1, N, I, A, V) - (i̲f̲ P[I] t̲h̲e̲n̲
           TAMVEC(I1, N, I, A, U) e̲l̲s̲e̲ A[I1, I] × U[I1])) / A[I, I]
      e̲n̲d̲ BACKWARD;
   NORMALISE: W := 1 / SQRT(VECVEC(1, N, 0, U, U) +
     VECVEC(1, N, 0, V, V));
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ U[J] := U[J] × W; V[J] := V[J] × W e̲n̲d̲;
   COUNT := COUNT + 1; i̲f̲ W > TOL t̲h̲e̲n̲ g̲o̲t̲o̲ FORWARD;
   OUTM: EM[7] := W; EM[9] := COUNT
e̲n̲d̲ COMVECHES;
c̲o̲m̲m̲e̲n̲t̲ ================== 34182 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2412;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAEIGVAL(A, N, EM, VAL); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, VAL;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J; r̲e̲a̲l̲ R;
   a̲r̲r̲a̲y̲ D[1:N]; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT, INT0[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMREAHES(A, N, EM, INT); c̲o̲d̲e̲ 34170;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBR(A, N, EM, D, INT); c̲o̲d̲e̲ 34173;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAVALQRI(A, N, EM, VAL); c̲o̲d̲e̲ 34180;

   EQILBR(A, N, EM, D, INT0); TFMREAHES(A, N, EM, INT);
   J := REAEIGVAL := REAVALQRI(A, N, EM, VAL);
   f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        f̲o̲r̲ J := I + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ VAL[J] > VAL[I] t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ R := VAL[I]; VAL[I] := VAL[J]; VAL[J] := R e̲n̲d̲
         e̲n̲d̲
e̲n̲d̲ REAEIGVAL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34184 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2414;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAEIG1(A, N, EM, VAL, VEC); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, VAL, VEC;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, K, MAX, J, L;
   r̲e̲a̲l̲ RESIDU, R, MACHTOL;
   a̲r̲r̲a̲y̲ D, V[1:N], B[1:N, 1:N];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT, INT0[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMREAHES(A, N, EM, INT); c̲o̲d̲e̲ 34170;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKREAHES2(A, N, N1, N2, INT, VEC); c̲o̲d̲e̲ 34172;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBR(A, N, EM, D, INT); c̲o̲d̲e̲ 34173;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBR(N, N1, N2, D, INT, VEC); c̲o̲d̲e̲ 34174;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAVALQRI(A, N, EM, VAL); c̲o̲d̲e̲ 34180;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAVECHES(A, N, LAMBDA, EM, V); c̲o̲d̲e̲ 34181;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ REASCL(A, N, N1, N2); c̲o̲d̲e̲ 34183;

   RESIDU := 0; MAX := 0; EQILBR(A, N, EM, D, INT0);
   TFMREAHES(A, N, EM, INT);
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        f̲o̲r̲ J := (i̲f̲ I = 1 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ I - 1)
        s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ B[I, J] := A[I, J];
   K := REAEIG1 := REAVALQRI(B, N, EM, VAL);
   f̲o̲r̲ I := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        f̲o̲r̲ J := I + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ VAL[J] > VAL[I] t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ R := VAL[I]; VAL[I] := VAL[J]; VAL[J] := R e̲n̲d̲
         e̲n̲d̲;
   MACHTOL := EM[0] × EM[1];
   f̲o̲r̲ L := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ L > 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ VAL[L - 1] - VAL[L] < MACHTOL t̲h̲e̲n̲
              VAL[L] := VAL[L - 1] - MACHTOL
         e̲n̲d̲;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              f̲o̲r̲ J := (i̲f̲ I = 1 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ I - 1)
              s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ B[I, J] := A[I, J];
         REAVECHES(B, N, VAL[L], EM, V);
         i̲f̲ EM[7] > RESIDU t̲h̲e̲n̲ RESIDU := EM[7];
         i̲f̲ EM[9] > MAX t̲h̲e̲n̲ MAX := EM[9];
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VEC[J, L] := V[J]
      e̲n̲d̲;
   EM[7] := RESIDU; EM[9] := MAX;
   BAKREAHES2(A, N, K + 1, N, INT, VEC);
   BAKLBR(N, K + 1, N, D, INT0, VEC);
   REASCL(VEC, N, K + 1, N)
e̲n̲d̲ REAEIG1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34187 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2417;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAEIG3(A, N, EM, VAL, VEC); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, VAL, VEC;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I; r̲e̲a̲l̲ S;
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT, INT0[1:N]; a̲r̲r̲a̲y̲ D[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMREAHES(A, N, EM, INT); c̲o̲d̲e̲ 34170;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKREAHES2(A, N, N1, N2, INT, VEC); c̲o̲d̲e̲ 34172;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBR(A, N, EM, D, INT); c̲o̲d̲e̲ 34173;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBR(N, N1, N2, D, INT, VEC); c̲o̲d̲e̲ 34174;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ REASCL(A, N, N1, N2); c̲o̲d̲e̲ 34183;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAQRI(A, N, EM, VAL, VEC); c̲o̲d̲e̲ 34186;

   EQILBR(A, N, EM, D, INT0); TFMREAHES(A, N, EM, INT);
   I := REAEIG3 := REAQRI(A, N, EM, VAL, VEC);
   i̲f̲ I = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ BAKREAHES2(A, N, 1, N, INT, VEC);
      BAKLBR(N, 1, N, D, INT0, VEC); REASCL(VEC, N, 1, N)
   e̲n̲d̲
e̲n̲d̲ REAEIG3;
c̲o̲m̲m̲e̲n̲t̲ ================== 34192 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2422;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMEIGVAL(A, N, EM, RE, IM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, EM, RE, IM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT, INT0[1:N];
   a̲r̲r̲a̲y̲ D[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBR(A, N, EM, D, INT); c̲o̲d̲e̲ 34173;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMREAHES(A, N, EM, INT); c̲o̲d̲e̲ 34170;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMVALQRI(A, N, EM, RE, IM);
      c̲o̲d̲e̲ 34190;

   EQILBR(A, N, EM, D, INT0); TFMREAHES(A, N, EM, INT);
   COMEIGVAL := COMVALQRI(A, N, EM, RE, IM)
e̲n̲d̲ COMEIGVAL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34194 ================= ;
c̲o̲m̲m̲e̲n̲t̲ MCA 2424;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMEIG1(A, N, EM, RE, IM, VEC);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A, EM, RE, IM, VEC;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K, PJ, ITT;
   r̲e̲a̲l̲ X, Y, MAX, NEPS;
   a̲r̲r̲a̲y̲ AB[1:N, 1:N], D, U, V[1:N];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT, INT0[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TRANSFER;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           f̲o̲r̲ J := (i̲f̲ I = 1 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ I - 1) s̲t̲e̲p̲ 1
           u̲n̲t̲i̲l̲ N d̲o̲ AB[I, J] := A[I, J]
   e̲n̲d̲ TRANSFER;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBR(A, N, EM, D, INT); c̲o̲d̲e̲ 34173;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ TFMREAHES(A, N, EM, INT); c̲o̲d̲e̲ 34170;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKREAHES2(A, N, N1, N2, INT, VEC); c̲o̲d̲e̲ 34172;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBR(N, N1, N2, D, INT, VEC); c̲o̲d̲e̲ 34174;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ REAVECHES(A, N, LAMBDA, EM, V); c̲o̲d̲e̲ 34181;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMSCL(A, N, N1, N2, IM); c̲o̲d̲e̲ 34193;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMVALQRI(A, N, EM, RE, IM);
      c̲o̲d̲e̲ 34190;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMVECHES(A, N, LAMBDA, MU, EM, U, V);
      c̲o̲d̲e̲ 34191;

   EQILBR(A, N, EM, D, INT0); TFMREAHES(A, N, EM, INT); TRANSFER;
   K := COMEIG1 := COMVALQRI(AB, N, EM, RE, IM);
   NEPS := EM[0] × EM[1]; MAX := 0; ITT := 0;
   f̲o̲r̲ I := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ X := RE[I]; Y := IM[I]; PJ := 0;
         AGAIN: f̲o̲r̲ J := K + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ I - 1 d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ ((X - RE[J]) ⭡ 2 +
                 (Y - IM[J]) ⭡ 2 ≤ NEPS ⭡ 2) t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ PJ = J t̲h̲e̲n̲ NEPS := EM[2] × EM[1]
                  e̲l̲s̲e̲ PJ := J; X := X + 2 × NEPS; g̲o̲t̲o̲ AGAIN
               e̲n̲d̲
            e̲n̲d̲;
         RE[I] := X; TRANSFER; i̲f̲ Y ≠ 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ COMVECHES(AB, N, RE[I], IM[I], EM, U, V);
            f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VEC[J, I] := U[J];
            I := I + 1; RE[I] := X
         e̲n̲d̲
         e̲l̲s̲e̲ REAVECHES(AB, N, X, EM, V);
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VEC[J, I] := V[J];
         i̲f̲ EM[7] > MAX t̲h̲e̲n̲ MAX := EM[7];
         ITT := i̲f̲ ITT > EM[9] t̲h̲e̲n̲ ITT e̲l̲s̲e̲ EM[9]
      e̲n̲d̲;
   EM[7] := MAX; EM[9] := ITT; BAKREAHES2(A, N, K + 1, N, INT, VEC);
   BAKLBR(N, K + 1, N, D, INT0, VEC); COMSCL(VEC, N, K + 1, N, IM)
e̲n̲d̲ COMEIG1;
c̲o̲m̲m̲e̲n̲t̲ ================== 34368 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGVALHRM(A, N, NUMVAL, VAL, EM); v̲a̲l̲u̲e̲ N, NUMVAL;
   i̲n̲t̲e̲g̲e̲r̲ N, NUMVAL; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ D[1:N], BB[1:N - 1];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHHRMTRIVAL(A, N, D, BB, EM); c̲o̲d̲e̲ 34364;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM); c̲o̲d̲e̲ 34151;
   HSHHRMTRIVAL(A, N, D, BB, EM);
   VALSYMTRI(D, BB, N, 1, NUMVAL, VAL, EM)
e̲n̲d̲ EIGVALHRM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34369 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGHRM(A, N, NUMVAL, VAL, VECR, VECI, EM);
   v̲a̲l̲u̲e̲ N, NUMVAL; i̲n̲t̲e̲g̲e̲r̲ N, NUMVAL;
     a̲r̲r̲a̲y̲ A, VAL, VECR, VECI, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ BB, TR, TI[1:N - 1], D, B[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHHRMTRI(A, N, D, B, BB, EM, TR, TI); c̲o̲d̲e̲ 34363;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALSYMTRI(D, BB, N, N1, N2, VAL, EM); c̲o̲d̲e̲ 34151;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECSYMTRI(D, B, N, N1, N2, VAL, VEC, EM); c̲o̲d̲e̲ 34152;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKHRMTRI(A, N, N1, N2, VECR, VECI, TR, TI); c̲o̲d̲e̲ 34365;
   HSHHRMTRI(A, N, D, B, BB, EM, TR, TI);
   VALSYMTRI(D, BB, N, 1, NUMVAL, VAL, EM); B[N] := 0;
   VECSYMTRI(D, B, N, 1, NUMVAL, VAL, VECR, EM);
   BAKHRMTRI(A, N, 1, NUMVAL, VECR, VECI, TR, TI)
e̲n̲d̲ EIGHRM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34370 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIVALHRM(A, N, VAL, EM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B, BB[1:N];
   i̲n̲t̲e̲g̲e̲r̲ I;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHHRMTRIVAL(A, N, D, BB, EM); c̲o̲d̲e̲ 34364;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIVALSYMTRI(D, BB, N, EM); c̲o̲d̲e̲ 34160;
   HSHHRMTRIVAL(A, N, VAL, BB, EM); B[N] := BB[N] := 0;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N-1 d̲o̲ B[I] := SQRT(BB[I]);
   QRIVALHRM := QRIVALSYMTRI(VAL, BB, N, EM)
e̲n̲d̲ QRIVALHRM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34371 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRIHRM(A, N, VAL, VR, VI, EM); v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, VAL, VR, VI, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J;
   a̲r̲r̲a̲y̲ B, BB[1:N], TR, TI[1:N - 1];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHHRMTRI(A, N, D, B, BB, EM, TR, TI); c̲o̲d̲e̲ 34363;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISYMTRI(A, N, D, B, BB, EM); c̲o̲d̲e̲ 34161;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKHRMTRI(A, N, N1, N2, VECR, VECI, TR, TI); c̲o̲d̲e̲ 34365;
   HSHHRMTRI(A, N, VAL, B, BB, EM, TR, TI);
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ VR[I, I] := 1;
         f̲o̲r̲ J := I + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VR[I, J] := VR[J, I] :=
              0
      e̲n̲d̲;
   B[N] := BB[N] := 0;
   I := QRIHRM := QRISYMTRI(VR, N, VAL, B, BB, EM);
   BAKHRMTRI(A, N, I + 1, N, VR, VI, TR, TI);
e̲n̲d̲ QRIHRM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34372 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALQRICOM(A1, A2, B, N, EM, VAL1, VAL2);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A1, A2, B, EM, VAL1, VAL2;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, NM1, I, I1, Q, Q1, MAX, COUNT;
   r̲e̲a̲l̲ R, Z1, Z2, DD1, DD2, CC, G1, G2, K1, K2, HC, A1NN,
     A2NN, AIJ1, AIJ2, AI1I, KAPPA, NUI, MUI1, MUI2,
     MUIM11, MUIM12, NUIM1, TOL;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMCOLCST(L, U, J, AR, AI, XR, XI); c̲o̲d̲e̲ 34352;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOMCOL(L, U, I, J, AR, AI, CR, CI, S); c̲o̲d̲e̲ 34357;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOMROW(L, U, I, J, AR, AI, CR, CI, S); c̲o̲d̲e̲ 34358;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMKWD(PR, PI, QR, QI, GR, GI, KR, KI); c̲o̲d̲e̲ 34345;
   TOL := EM[1] × EM[2]; MAX := EM[4]; COUNT := 0; R := 0;
   M := N; i̲f̲ N > 1 t̲h̲e̲n̲ HC := B[N - 1];
   IN: NM1 := N - 1;
   f̲o̲r̲ I := N, I - 1 w̲h̲i̲l̲e̲ (i̲f̲ I ≥ 1 t̲h̲e̲n̲ ABS(B[I]) > TOL
   e̲l̲s̲e̲ f̲a̲l̲s̲e̲) d̲o̲ Q := I; i̲f̲ Q > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(B[Q - 1]) > R t̲h̲e̲n̲ R := ABS(B[Q - 1]) e̲n̲d̲;
   i̲f̲ Q = N t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ VAL1[N] := A1[N, N]; VAL2[N] := A2[N, N]; N := NM1;
      i̲f̲ N > 1 t̲h̲e̲n̲ HC := B[N - 1];
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ DD1 := A1[N, N]; DD2 := A2[N, N]; CC := B[NM1];
      COMKWD((A1[NM1, NM1] - DD1) / 2, (A2[NM1, NM1] - DD2)
        / 2, CC × A1[NM1, N], CC × A2[NM1, N], G1, G2, K1,
        K2); i̲f̲ Q = NM1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ VAL1[NM1] := G1 + DD1; VAL2[NM1] := G2 + DD2;
         VAL1[N] := K1 + DD1; VAL2[N] := K2 + DD2;
         N := N - 2; i̲f̲ N > 1 t̲h̲e̲n̲ HC := B[N - 1];
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ COUNT := COUNT + 1;
         i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ OUT; Z1 := K1 + DD1;
         Z2 := K2 + DD2;
         i̲f̲ ABS(CC) > ABS(HC) t̲h̲e̲n̲ Z1 := Z1 + ABS(CC);
         HC := CC / 2; I := Q1 := Q + 1;
         AIJ1 := A1[Q, Q] - Z1; AIJ2 := A2[Q, Q] - Z2;
         AI1I := B[Q];
         KAPPA := SQRT(AIJ1 ⭡ 2 + AIJ2 ⭡ 2 + AI1I ⭡ 2);
         MUI1 := AIJ1 / KAPPA; MUI2 := AIJ2 / KAPPA;
         NUI := AI1I / KAPPA; A1[Q, Q] := KAPPA;
         A2[Q, Q] := 0; A1[Q1, Q1] := A1[Q1, Q1] - Z1;
         A2[Q1, Q1] := A2[Q1, Q1] - Z2;
         ROTCOMROW(Q1, N, Q, Q1, A1, A2, MUI1, MUI2,
           NUI);
         ROTCOMCOL(Q, Q, Q, Q1, A1, A2, MUI1, - MUI2, -
           NUI); A1[Q, Q] := A1[Q, Q] + Z1;
         A2[Q, Q] := A2[Q, Q] + Z2;
         f̲o̲r̲ I1 := Q1 + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ AIJ1 := A1[I, I]; AIJ2 := A2[I, I];
               AI1I := B[I];
               KAPPA := SQRT(AIJ1 ⭡ 2 + AIJ2 ⭡ 2 + AI1I ⭡
                 2); MUIM11 := MUI1; MUIM12 := MUI2;
               NUIM1 := NUI; MUI1 := AIJ1 / KAPPA;
               MUI2 := AIJ2 / KAPPA; NUI := AI1I / KAPPA;
               A1[I1, I1] := A1[I1, I1] - Z1;
               A2[I1, I1] := A2[I1, I1] - Z2;
               ROTCOMROW(I1, N, I, I1, A1, A2, MUI1,
                 MUI2, NUI); A1[I, I] := MUIM11 × KAPPA;
               A2[I, I] := - MUIM12 × KAPPA;
               B[I - 1] := NUIM1 × KAPPA;

               ROTCOMCOL(Q, I, I, I1, A1, A2, MUI1, -
                 MUI2, - NUI); A1[I, I] := A1[I, I] + Z1;
               A2[I, I] := A2[I, I] + Z2; I := I1;
            e̲n̲d̲;
         AIJ1 := A1[N, N]; AIJ2 := A2[N, N];
         KAPPA := SQRT(AIJ1 ⭡ 2 + AIJ2 ⭡ 2);
         i̲f̲ (i̲f̲ KAPPA < TOL t̲h̲e̲n̲ t̲r̲u̲e̲ e̲l̲s̲e̲ AIJ2 ⭡ 2≤ EM[0] × AIJ1 ⭡ 2) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B[NM1] := NUI × AIJ1;
            A1[N, N] := AIJ1 × MUI1 + Z1;
            A2[N, N] := - AIJ1 × MUI2 + Z2
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ B[NM1] := NUI × KAPPA; A1NN := MUI1 × KAPPA;
            A2NN := - MUI2 × KAPPA; MUI1 := AIJ1 / KAPPA;
            MUI2 := AIJ2 / KAPPA;
            COMCOLCST(Q, NM1, N, A1, A2, MUI1, MUI2);
            A1[N, N] := MUI1 × A1NN - MUI2 × A2NN + Z1;
            A2[N, N] := MUI1 × A2NN + MUI2 × A1NN + Z2;
         e̲n̲d̲;
      e̲n̲d̲
   e̲n̲d̲;
   i̲f̲ N > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   OUT: EM[3] := R; EM[5] := COUNT; VALQRICOM := N;
e̲n̲d̲ VALQRICOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34373 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRICOM(A1, A2, B, N, EM, VAL1, VAL2, VEC1,
  VEC2); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ A1, A2, B, EM, VAL1, VAL2, VEC1, VEC2;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, NM1, I, I1, J, Q, Q1, MAX, COUNT;
   r̲e̲a̲l̲ R, Z1, Z2, DD1, DD2, CC, P1, P2, T1, T2, DELTA1,
     DELTA2, MV1, MV2, H, H1, H2, G1, G2, K1, K2, HC,
     AIJ12, AIJ22, A1NN, A2NN, AIJ1, AIJ2, AI1I, KAPPA,
     NUI, MUI1, MUI2, MUIM11, MUIM12, NUIM1, TOL, MACHTOL;
   a̲r̲r̲a̲y̲ TF1, TF2[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMKWD(PR, PI, QR, QI, GR, GI, KR, KI); c̲o̲d̲e̲ 34345;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOMROW(L, U, I, J, AR, AI, CR, CI, S); c̲o̲d̲e̲ 34358;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOMCOL(L, U, I, J, AR, AI, CR, CI, S); c̲o̲d̲e̲ 34357;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMCOLCST(L, U, J, AR, AI, XR, XI); c̲o̲d̲e̲ 34352;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMROWCST(L, U, I, AR, AI, XR, XI); c̲o̲d̲e̲ 34353;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMATVEC(L, U, I, AR, AI, BR, BI, RR, RI); c̲o̲d̲e̲ 34354;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMDIV(XR, XI, YR, YI, ZR, ZI); c̲o̲d̲e̲ 34342;
   TOL := EM[1] × EM[2]; MACHTOL := EM[0] × EM[1];
   MAX := EM[4]; COUNT := 0; R := 0; M := N;
   i̲f̲ N > 1 t̲h̲e̲n̲ HC := B[N - 1];
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ VEC1[I, I] := 1; VEC2[I, I] := 0;
         f̲o̲r̲ J := I + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ VEC1[I, J] :=
              VEC1[J, I] := VEC2[I, J] := VEC2[J, I] := 0
      e̲n̲d̲;
   IN: NM1 := N - 1;
   f̲o̲r̲ I := N, I - 1 w̲h̲i̲l̲e̲ (i̲f̲ I ≥ 1 t̲h̲e̲n̲ ABS(B[I]) > TOL
   e̲l̲s̲e̲ f̲a̲l̲s̲e̲) d̲o̲ Q := I; i̲f̲ Q > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(B[Q - 1]) > R t̲h̲e̲n̲ R := ABS(B[Q - 1]) e̲n̲d̲;
   i̲f̲ Q = N t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ VAL1[N] := A1[N, N]; VAL2[N] := A2[N, N]; N := NM1;
      i̲f̲ N > 1 t̲h̲e̲n̲ HC := B[N - 1];
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ DD1 := A1[N, N]; DD2 := A2[N, N]; CC := B[NM1];
      P1 := (A1[NM1, NM1] - DD1) × ·5;
      P2 := (A2[NM1, NM1] - DD2) × ·5;
      COMKWD(P1, P2, CC × A1[NM1, N], CC × A2[NM1, N], G1,
        G2, K1, K2); i̲f̲ Q = NM1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ A1[N, N] := VAL1[N] := G1 + DD1;
         A2[N, N] := VAL2[N] := G2 + DD2;
         A1[Q, Q] := VAL1[Q] := K1 + DD1;
         A2[Q, Q] := VAL2[Q] := K2 + DD2;
         KAPPA := SQRT(K1 ⭡ 2 + K2 ⭡ 2 + CC ⭡ 2);
         NUI := CC / KAPPA; MUI1 := K1 / KAPPA;
         MUI2 := K2 / KAPPA; AIJ1 := A1[Q, N];
         AIJ2 := A2[Q, N]; H1 := MUI1 ⭡ 2 - MUI2 ⭡ 2;
         H2 := 2 × MUI1 × MUI2; H := - NUI × 2;
         A1[Q, N] := H × (P1 × MUI1 + P2 × MUI2) - NUI ×
           NUI × CC + AIJ1 × H1 + AIJ2 × H2;
         A2[Q, N] := H × (P2 × MUI1 - P1 × MUI2) + AIJ2 ×
           H1 - AIJ1 × H2;
         ROTCOMROW(Q + 2, M, Q, N, A1, A2, MUI1, MUI2,
           NUI);
         ROTCOMCOL(1, Q - 1, Q, N, A1, A2, MUI1, -
           MUI2, - NUI);
         ROTCOMCOL(1, M, Q, N, VEC1, VEC2, MUI1, -
           MUI2, - NUI); N := N - 2;
         i̲f̲ N > 1 t̲h̲e̲n̲ HC := B[N - 1]; B[Q] := 0
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ COUNT := COUNT + 1;
         i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ OUT; Z1 := K1 + DD1;
         Z2 := K2 + DD2;
         i̲f̲ ABS(CC) > ABS(HC) t̲h̲e̲n̲ Z1 := Z1 + ABS(CC);
         HC := CC / 2; Q1 := Q + 1; AIJ1 := A1[Q, Q] - Z1;
         AIJ2 := A2[Q, Q] - Z2; AI1I := B[Q];
         KAPPA := SQRT(AIJ1 ⭡ 2 + AIJ2 ⭡ 2 + AI1I ⭡ 2);
         MUI1 := AIJ1 / KAPPA; MUI2 := AIJ2 / KAPPA;
         NUI := AI1I / KAPPA; A1[Q, Q] := KAPPA;
         A2[Q, Q] := 0; A1[Q1, Q1] := A1[Q1, Q1] - Z1;
         A2[Q1, Q1] := A2[Q1, Q1] - Z2;
         ROTCOMROW(Q1, M, Q, Q1, A1, A2, MUI1, MUI2,
           NUI);
         ROTCOMCOL(1, Q, Q, Q1, A1, A2, MUI1, - MUI2, -
           NUI); A1[Q, Q] := A1[Q, Q] + Z1;
         A2[Q, Q] := A2[Q, Q] + Z2;
         ROTCOMCOL(1, M, Q, Q1, VEC1, VEC2, MUI1, -
           MUI2, - NUI);
         f̲o̲r̲ I := Q1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NM1 d̲o̲
            b̲e̲g̲i̲n̲ I1 := I + 1; AIJ1 := A1[I, I]; AIJ2 := A2[I, I];
               AI1I := B[I];
               KAPPA := SQRT(AIJ1 ⭡ 2 + AIJ2 ⭡ 2 + AI1I ⭡
                 2); MUIM11 := MUI1; MUIM12 := MUI2;
               NUIM1 := NUI; MUI1 := AIJ1 / KAPPA;
               MUI2 := AIJ2 / KAPPA; NUI := AI1I / KAPPA;
               A1[I1, I1] := A1[I1, I1] - Z1;
               A2[I1, I1] := A2[I1, I1] - Z2;
               ROTCOMROW(I1, M, I, I1, A1, A2, MUI1,
                 MUI2, NUI); A1[I, I] := MUIM11 × KAPPA;
               A2[I, I] := - MUIM12 × KAPPA;
               B[I - 1] := NUIM1 × KAPPA;
               ROTCOMCOL(1, I, I, I1, A1, A2, MUI1, -
                 MUI2, - NUI); A1[I, I] := A1[I, I] + Z1;
               A2[I, I] := A2[I, I] + Z2;
               ROTCOMCOL(1, M, I, I1, VEC1, VEC2, MUI1, -
                 MUI2, - NUI);
            e̲n̲d̲;

         AIJ1 := A1[N, N]; AIJ2 := A2[N, N]; AIJ12 := AIJ1 ⭡ 2;
         AIJ22 := AIJ2 ⭡ 2; KAPPA := SQRT(AIJ12 + AIJ22);
         i̲f̲ (i̲f̲ KAPPA < TOL t̲h̲e̲n̲ t̲r̲u̲e̲ e̲l̲s̲e̲ AIJ22 ≤
           EM[0] × AIJ12) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B[NM1] := NUI × AIJ1;
            A1[N, N] := AIJ1 × MUI1 + Z1;
            A2[N, N] := - AIJ1 × MUI2 + Z2
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ B[NM1] := NUI × KAPPA; A1NN := MUI1 × KAPPA;
            A2NN := - MUI2 × KAPPA; MUI1 := AIJ1 / KAPPA;
            MUI2 := AIJ2 / KAPPA;
            COMCOLCST(1, NM1, N, A1, A2, MUI1, MUI2);
            COMCOLCST(1, NM1, N, VEC1, VEC2, MUI1,
              MUI2);
            COMROWCST(N + 1, M, N, A1, A2, MUI1, -
              MUI2);
            COMCOLCST(N, M, N, VEC1, VEC2, MUI1, MUI2);
            A1[N, N] := MUI1 × A1NN - MUI2 × A2NN + Z1;
            A2[N, N] := MUI1 × A2NN + MUI2 × A1NN + Z2;
         e̲n̲d̲;
      e̲n̲d̲;
   e̲n̲d̲;
   i̲f̲ N > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   f̲o̲r̲ J := M s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 2 d̲o̲
      b̲e̲g̲i̲n̲ TF1[J] := 1; TF2[J] := 0; T1 := A1[J, J]; T2 := A2[J, J];
         f̲o̲r̲ I := J - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
            b̲e̲g̲i̲n̲ DELTA1 := T1 - A1[I, I]; DELTA2 := T2 - A2[I, I];
               COMMATVEC(I + 1, J, I, A1, A2, TF1, TF2, MV1,
                 MV2);
               i̲f̲ ABS(DELTA1) < MACHTOL ∧ ABS(DELTA2) <
                 MACHTOL t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ TF1[I] := MV1 / MACHTOL;
                  TF2[I] := MV2 / MACHTOL
               e̲n̲d̲
               e̲l̲s̲e̲ COMDIV(MV1, MV2, DELTA1, DELTA2, TF1[I],
                 TF2[I]);
            e̲n̲d̲;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ COMMATVEC(1, J, I,
              VEC1, VEC2, TF1, TF2, VEC1[I, J], VEC2[I, J]);
      e̲n̲d̲;
   OUT: EM[3] := R; EM[5] := COUNT; QRICOM := N;
e̲n̲d̲ QRICOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34374 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGVALCOM(AR, AI, N, EM, VALR, VALI);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ AR, AI, EM, VALR, VALI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT[1:N];
   a̲r̲r̲a̲y̲ D, B, DEL, TR, TI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMHES(AR, AI, N, EM, B, TR, TI, DEL); c̲o̲d̲e̲ 34366;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMEUCNRM(AR, AI, LW, N); c̲o̲d̲e̲ 34359;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBRCOM(A1, A2, N, EM, D, INT); c̲o̲d̲e̲ 34361;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VALQRICOM(A1, A2, B, N, EM, VAL1, VAL2);
      c̲o̲d̲e̲ 34372;
   EQILBRCOM(AR, AI, N, EM, D, INT);
   EM[1] := COMEUCNRM(AR, AI, N - 1, N);
   HSHCOMHES(AR, AI, N, EM, B, TR, TI, DEL);
   EIGVALCOM := VALQRICOM(AR, AI, B, N, EM, VALR, VALI)
e̲n̲d̲ EIGVALCOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34375 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EIGCOM(AR, AI, N, EM, VALR, VALI, VR, VI);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ AR, AI, EM, VALR, VALI, VR, VI;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ INT[1:N];
   a̲r̲r̲a̲y̲ D, B, DEL, TR, TI[1:N];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EQILBRCOM(A1, A2, N, EM, D, INT); c̲o̲d̲e̲ 34361;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMEUCNRM(AR, AI, LW, N); c̲o̲d̲e̲ 34359;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOMHES(AR, AI, N, EM, B, TR, TI, DEL); c̲o̲d̲e̲ 34366;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRICOM(A1, A2, B, N, EM, VAL1, VAL2, VEC1, VEC2);
      c̲o̲d̲e̲ 34373;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKCOMHES(AR, AI, TR, TI, DEL, VR, VI, N, N1, N2);
      c̲o̲d̲e̲ 34367;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BAKLBRCOM(N, N1, N2, D, INT, VR, VI); c̲o̲d̲e̲ 34362;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SCLCOM(AR, AI, N, N1, N2); c̲o̲d̲e̲ 34360;
   EQILBRCOM(AR, AI, N, EM, D, INT);
   EM[1] := COMEUCNRM(AR, AI, N - 1, N);
   HSHCOMHES(AR, AI, N, EM, B, TR, TI, DEL);
   I := EIGCOM := QRICOM(AR, AI, B, N, EM, VALR, VALI, VR,
     VI); i̲f̲ I = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ BAKCOMHES(AR, AI, TR, TI, DEL, VR, VI, N, 1, N);
      BAKLBRCOM(N, 1, N, D, INT, VR, VI);
      SCLCOM(VR, VI, N, 1, N)
   e̲n̲d̲
e̲n̲d̲ EIGCOM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34270 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALBID(D, B, N, EM);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ D, B, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N1, K, K1, I, I1, COUNT, MAX, RNK;
   r̲e̲a̲l̲ TOL, BMAX, Z, X, Y, G, H, F, C, S, MIN;
   TOL := EM[2] × EM[1]; COUNT := 0; BMAX := 0; MAX := EM[4]; MIN := EM[6];
   RNK := N;
   IN: K := N; N1 := N - 1;
   NEXT: K := K - 1; i̲f̲ K > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(B[K]) ≥ TOL t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ ABS(D[K]) ≥ TOL t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT;
         C := 0; S := 1;
         f̲o̲r̲ I := K s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 d̲o̲
            b̲e̲g̲i̲n̲ F := S × B[I]; B[I] := C × B[I]; I1 := I + 1;
               i̲f̲ ABS(F) < TOL t̲h̲e̲n̲ g̲o̲t̲o̲ NEGLECT;
               G := D[I1]; D[I1] := H := SQRT(F × F + G × G);
               C := G / H; S := - F / H
            e̲n̲d̲;
         NEGLECT:
      e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ ABS(B[K]) > BMAX t̲h̲e̲n̲ BMAX := ABS(B[K])
   e̲n̲d̲;
   i̲f̲ K = N1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ D[N] < 0 t̲h̲e̲n̲ D[N] := - D[N];
      i̲f̲ D[N] ≤ MIN t̲h̲e̲n̲ RNK := RNK - 1; N := N1
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ COUNT := COUNT + 1; i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ END;
      K1 := K + 1; Z := D[N]; X := D[K1]; Y := D[N1];
      G := i̲f̲ N1 = 1 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ B[N1 - 1]; H := B[N1];
      F := ((Y - Z) × (Y + Z) + (G - H) × (G + H)) / (2 × H × Y);
      G := SQRT(F × F + 1);
      F := ((X - Z) × (X + Z) + H × (Y / (i̲f̲ F < 0 t̲h̲e̲n̲ F - G
      e̲l̲s̲e̲ F + G) - H)) / X; C := S := 1;
      f̲o̲r̲ I := K1 + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ I1 := I - 1; G := B[I1]; Y := D[I]; H := S × G; G := C × G;
            Z := SQRT(F × F + H × H); C := F / Z; S := H / Z;
            i̲f̲ I1 ≠ K1 t̲h̲e̲n̲ B[I1 - 1] := Z; F := X × C + G × S;
            G := G × C - X × S; H := Y × S; Y := Y × C;
            D[I1] := Z := SQRT(F × F + H × H); C := F / Z; S := H / Z;
            F := C × G + S × Y; X := C × Y - S × G
         e̲n̲d̲;
      B[N1] := F; D[N] := X
   e̲n̲d̲;
   i̲f̲ N > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   END: EM[3] := BMAX; EM[5] := COUNT; EM[7] := RNK; QRISNGVALBID := N
e̲n̲d̲ QRISNGVALBID;
c̲o̲m̲m̲e̲n̲t̲ ================== 34271 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDECBID(D, B, M, N, U, V, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ D, B, U, V, EM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N0, N1, K, K1, I, I1, COUNT, MAX, RNK;
   r̲e̲a̲l̲ TOL, BMAX, Z, X, Y, G, H, F, C, S, MIN;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOL(L, U, I, J, A, C, S);
      v̲a̲l̲u̲e̲ L, U, I, J, C, S; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J;
        r̲e̲a̲l̲ C, S; a̲r̲r̲a̲y̲ A;
      c̲o̲d̲e̲ 34040;
   TOL := EM[2] × EM[1]; COUNT := 0; BMAX := 0; MAX := EM[4]; MIN := EM[6];
   RNK := N0 := N;
   IN: K := N; N1 := N - 1;
   NEXT: K := K - 1; i̲f̲ K > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABS(B[K]) ≥ TOL t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ ABS(D[K]) ≥ TOL t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT;
         C := 0; S := 1;
         f̲o̲r̲ I := K s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 d̲o̲
            b̲e̲g̲i̲n̲ F := S × B[I]; B[I] := C × B[I]; I1 := I + 1;
               i̲f̲ ABS(F) < TOL t̲h̲e̲n̲ g̲o̲t̲o̲ NEGLECT;
               G := D[I1]; D[I1] := H := SQRT(F × F + G × G);
               C := G / H; S := - F / H;
               ROTCOL(1, M, K, I1, U, C, S)
            e̲n̲d̲;
         NEGLECT:
      e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ ABS(B[K]) > BMAX t̲h̲e̲n̲ BMAX := ABS(B[K])
   e̲n̲d̲;
   i̲f̲ K = N1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ D[N] < 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[N] := - D[N];
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N0 d̲o̲ V[I, N] := - V[I, N]
      e̲n̲d̲;
      i̲f̲ D[N] ≤ MIN t̲h̲e̲n̲ RNK := RNK - 1; N := N1
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ COUNT := COUNT + 1; i̲f̲ COUNT > MAX t̲h̲e̲n̲ g̲o̲t̲o̲ END;
      K1 := K + 1; Z := D[N]; X := D[K1]; Y := D[N1];
      G := i̲f̲ N1 = 1 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ B[N1 - 1]; H := B[N1];
      F := ((Y - Z) × (Y + Z) + (G - H) × (G + H)) / (2 × H × Y);
      G := SQRT(F × F + 1);
      F := ((X - Z) × (X + Z) + H × (Y / (i̲f̲ F < 0 t̲h̲e̲n̲ F - G
      e̲l̲s̲e̲ F + G) - H)) / X; C := S := 1;
      f̲o̲r̲ I := K1 + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ I1 := I - 1; G := B[I1]; Y := D[I]; H := S × G; G := C × G;
            Z := SQRT(F × F + H × H); C := F / Z; S := H / Z;
            i̲f̲ I1 ≠ K1 t̲h̲e̲n̲ B[I1 - 1] := Z; F := X × C + G × S;
            G := G × C - X × S; H := Y × S; Y := Y × C;
            ROTCOL(1, N0, I1, I, V, C, S);
            D[I1] := Z := SQRT(F × F + H × H); C := F / Z; S := H / Z;
            F := C × G + S × Y; X := C × Y - S × G;
            ROTCOL(1, M, I1, I, U, C, S)
         e̲n̲d̲;
      B[N1] := F; D[N] := X
   e̲n̲d̲;
   i̲f̲ N > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ IN;
   END: EM[3] := BMAX; EM[5] := COUNT; EM[7] := RNK; QRISNGVALDECBID := N
e̲n̲d̲ QRISNGVALDECBID;
c̲o̲m̲m̲e̲n̲t̲ ================== 34272 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVAL(A, M, N, VAL, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, VAL, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHREABID(A, M, N, D, B, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ D, B, EM;
      c̲o̲d̲e̲ 34260;

   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALBID(D, B, N, EM);
      v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ D, B, EM;
      c̲o̲d̲e̲ 34270;

   HSHREABID(A, M, N, VAL, B, EM);
   QRISNGVAL := QRISNGVALBID(VAL, B, N, EM)
e̲n̲d̲ QRISNGVAL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34273 ================= ;
i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, VAL, V, EM;
b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ B[1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHREABID(A, M, N, D, B, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, D, B, EM;
      c̲o̲d̲e̲ 34260;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ PSTTFMMAT(A, N, V, B);
      v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, V, B;
      c̲o̲d̲e̲ 34261;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ PRETFMMAT(A, M, N, D);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ A, D;
      c̲o̲d̲e̲ 34262;

   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDECBID(D, B, M, N, U, V, EM);
      v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ D, B, U, V, EM;
      c̲o̲d̲e̲ 34271;

   HSHREABID(A, M, N, VAL, B, EM);
   PSTTFMMAT(A, N, V, B);  PRETFMMAT(A, M, N, VAL);
   QRISNGVALDEC := QRISNGVALDECBID(VAL, B, M, N, A, V, EM)
e̲n̲d̲ QRISNGVALDEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34345 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMKWD(PR, PI, QR, QI, GR, GI, KR, KI);
   v̲a̲l̲u̲e̲ PR, PI, QR, QI; r̲e̲a̲l̲ PR, PI, QR, QI, GR, GI, KR, KI;
b̲e̲g̲i̲n̲
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMMUL (AR, AI, BR, BI, RR, RI);
      c̲o̲d̲e̲ 34341;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMDIV(XR, XI, YR, YI, ZR, ZI);
      c̲o̲d̲e̲ 34342;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMSQRT(AR, AI, PR, PI);
      c̲o̲d̲e̲ 34343;
   i̲f̲ QR = 0 ∧ QI = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ KR := KI := 0 ; GR := PR × 2; GI := PI × 2 e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ PR = 0 ∧ PI = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ COMSQRT(QR, QI, GR, GI); KR := -GR; KI := -GI e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ HR, HI;
      i̲f̲ ABS(PR) > 1 ∨ ABS(PI) > 1 t̲h̲e̲n̲ b̲e̲g̲i̲n̲
         COMDIV(QR, QI, PR, PI, HR, HI);
         COMDIV(HR, HI, PR, PI, HR, HI);
         COMSQRT(1 + HR, HI, HR, HI);
         COMMUL(PR, PI, HR + 1, HI, GR, GI);
      e̲n̲d̲ e̲l̲s̲e̲ b̲e̲g̲i̲n̲ COMSQRT(QR + (PR + PI) × (PR-PI), QI + PR × PI × 2, HR, HI);
         i̲f̲ PR × HR + PI × HI > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ GR := PR + HR; GI := PI + HI e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ GR := PR - HR; GI := PI - HI e̲n̲d̲;
      e̲n̲d̲;
      COMDIV(-QR, -QI, GR, GI, KR, KI);
   e̲n̲d̲
e̲n̲d̲ COMKWD;
c̲o̲m̲m̲e̲n̲t̲ ================== 32010 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EULER(AI, I, EPS, TIM);
   v̲a̲l̲u̲e̲ EPS, TIM; i̲n̲t̲e̲g̲e̲r̲ I, TIM; r̲e̲a̲l̲ AI, EPS;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K, N, T; r̲e̲a̲l̲ MN, MP, DS, SUM; a̲r̲r̲a̲y̲ M[0:15];
   N := T := 0; I := 0; M[0] := AI; SUM := M[0] / 2;
   NEXT TERM: I := I + 1; MN := AI;
   f̲o̲r̲ K := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ MP := (MN + M[K]) / 2; M[K] := MN; MN := MP e̲n̲d̲;
   i̲f̲ ABS(MN) < ABS(M[N]) ∧ N < 15 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ DS := MN / 2; N := N + 1; M[N] := MN e̲n̲d̲ e̲l̲s̲e̲ DS := MN;
   SUM := SUM + DS; T := i̲f̲ ABS(DS) < EPS t̲h̲e̲n̲ T + 1 e̲l̲s̲e̲ 0;
   i̲f̲ T < TIM t̲h̲e̲n̲ g̲o̲ t̲o̲ NEXT TERM;
   EULER := SUM
e̲n̲d̲ EULER;
c̲o̲m̲m̲e̲n̲t̲ ================== 32020 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SUMPOSSERIES(AI, I, MAXADDUP, MAXZERO, MAXRECURS,
  MACHEXP, TIM);
   v̲a̲l̲u̲e̲ MAXADDUP, MAXZERO, MAXRECURS, MACHEXP, TIM;
   r̲e̲a̲l̲ AI, I, MAXZERO; i̲n̲t̲e̲g̲e̲r̲ MAXADDUP, MAXRECURS, MACHEXP, TIM;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ RECURS, VL, VL2, VL4;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EULER(AI, I, EPS, TIM); c̲o̲d̲e̲ 32010;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SUMUP(AI, I); r̲e̲a̲l̲ AI, I;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J; r̲e̲a̲l̲ SUM, NEXTTERM;
      I := MAXADDUP + 1; J := 1;
      CHECK ADD: i̲f̲ AI ≤ MAXZERO t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ J < TIM t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ J := J + 1; I := I + 1; g̲o̲ t̲o̲ CHECK ADD e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲
      i̲f̲ RECURS ≠ MAXRECURS t̲h̲e̲n̲ g̲o̲ t̲o̲ TRANSFORMSERIES;
      SUM := 0; I := 0; J := 0;
      ADD LOOP: I := I + 1; NEXTTERM := AI;
      J := i̲f̲ NEXTTERM ≤ MAXZERO t̲h̲e̲n̲ J + 1 e̲l̲s̲e̲ 0;
      SUM := SUM + NEXTTERM;
      i̲f̲ J < TIM t̲h̲e̲n̲ g̲o̲ t̲o̲ ADD LOOP;
      SUMUP := SUM; g̲o̲ t̲o̲ GOTSUM;
      TRANSFORMSERIES:
      b̲e̲g̲i̲n̲ B̲o̲o̲l̲e̲a̲n̲ JODD; i̲n̲t̲e̲g̲e̲r̲ J2; a̲r̲r̲a̲y̲ V[1:VL];

         r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ BJK(J, K); v̲a̲l̲u̲e̲ J, K; r̲e̲a̲l̲ K;
              i̲n̲t̲e̲g̲e̲r̲ J;
         b̲e̲g̲i̲n̲ r̲e̲a̲l̲ COEFF;
            i̲f̲ K > MACHEXP t̲h̲e̲n̲ BJK := 0 e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ COEFF := 2 ⭡ (K - 1); I := J × COEFF;
               BJK := COEFF × AI
            e̲n̲d̲
         e̲n̲d̲ BJK;
         r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VJ(J); v̲a̲l̲u̲e̲ J; i̲n̲t̲e̲g̲e̲r̲ J;
         b̲e̲g̲i̲n̲ r̲e̲a̲l̲ TEMP, K;
            i̲f̲ JODD t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ JODD := f̲a̲l̲s̲e̲; RECURS := RECURS + 1;
               TEMP := VJ := SUMUP(BJK(J, K), K);
               RECURS := RECURS - 1;
               i̲f̲ J ≤ VL t̲h̲e̲n̲ V[J] := TEMP e̲l̲s̲e̲
               i̲f̲ J ≤ VL2 t̲h̲e̲n̲ V[J - VL] := TEMP
            e̲n̲d̲ e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ JODD := t̲r̲u̲e̲; i̲f̲ J > VL4 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ RECURS := RECURS + 1;
                  VJ := - SUMUP(BJK(J, K), K); RECURS := RECURS - 1
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ J2 := J2 + 1; I := J2;
                  i̲f̲ J > VL2 t̲h̲e̲n̲ VJ := - (V[J2 - VL] - AI) / 2
                  e̲l̲s̲e̲
                  b̲e̲g̲i̲n̲ TEMP := V[ i̲f̲ J ≤ VL t̲h̲e̲n̲ J e̲l̲s̲e̲
                       J - VL] := (V[J2] - AI) / 2; VJ := - TEMP
                  e̲n̲d̲
               e̲n̲d̲
            e̲n̲d̲
         e̲n̲d̲ VJ;

         J2 := 0;
         JODD := t̲r̲u̲e̲; SUMUP := EULER(VJ(J + 1), J, MAXZERO, TIM)
      e̲n̲d̲ TRANSFORMSERIES;
      GOTSUM:
   e̲n̲d̲ SUMUP;

   RECURS := 0; VL := 1000; VL2 := 2 × VL; VL4 := 2 × VL2;
   SUMPOSSERIES := SUMUP(AI, I)
e̲n̲d̲ SUMPOSSERIES;
c̲o̲m̲m̲e̲n̲t̲ ================== 32070 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QADRAT(X, A, B, FX, E);
   v̲a̲l̲u̲e̲ A, B; r̲e̲a̲l̲ X, A, B, FX; a̲r̲r̲a̲y̲ E;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ F0, F2, F3, F5, F6, F7, F9,
     F14, V, W, HMIN, HMAX, RE, AE;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ LINT(X0, XN, F0, F2, F3, F5, F6, F7, F9, F14);
      r̲e̲a̲l̲ X0, XN, F0, F2, F3, F5, F6, F7, F9, F14;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ H, XM, F1, F4, F8, F10, F11, F12, F13;
      XM := (X0 + XN) / 2; H := (XN - X0) / 32; X := XM + 4 × H;
      F8 := FX; X := XN - 4 × H; F11 := FX; X := XN - 2 × H; F12 := FX;
      V := 0·330580178199226 × F7 + 0·173485115707338 × (F6 + F8) +
        0·321105426559972 × (F5 + F9) + 0·135007708341042 × (F3 + F11)
        + 0·165714514228223 × (F2 + F12) + 0·393971460638127@-1 × (F0
        + F14); X := X0 + H; F1 := FX; X := XN - H; F13 := FX;
      W := 0·260652434656970 × F7 + 0·239063286684765 × (F6 + F8) +
        0·263062635477467 × (F5 + F9) + 0·218681931383057 × (F3 + F11)
        + 0·275789764664284@-1 × (F2 + F12) + 0·105575010053846 × (F1
        + F13) + 0·157119426059518@-1 × (F0 + F14);
      i̲f̲ ABS(H) < HMIN t̲h̲e̲n̲ E[3] := E[3] + 1;
      i̲f̲ ABS(V - W) < ABS(W) × RE + AE ∨ ABS(H) < HMIN
        t̲h̲e̲n̲ LINT := H × W e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ X := X0 + 6 × H; F4 := FX; X := XN - 6 × H; F10 := FX;
         V := 0·245673430093324 × F7 + 0·255786258286921 × (F6 + F8) +
           0·228526063690406 × (F5 + F9) + 0·500557131525460@-1 × (F4 +
           F10) + 0·177946487736780 × (F3 + F11) + 0·584014599347449@-1× (F2 + F12) + 0·874830942871331@-1 × (F1 + F13) +
           0·189642078648079@-1 × (F0 + F14);
         LINT := i̲f̲ ABS(V - W) < ABS(V) × RE + AE t̲h̲e̲n̲ H × V
         e̲l̲s̲e̲
           LINT(X0, XM, F0, F1, F2, F3, F4, F5, F6, F7) - LINT(XN,
           XM, F14, F13, F12, F11, F10, F9, F8, F7)
      e̲n̲d̲
   e̲n̲d̲ LINT;

   HMAX := (B - A) / 16; i̲f̲ HMAX = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ QADRAT := 0; g̲o̲t̲o̲ RETURN e̲n̲d̲;
   RE := E[1]; AE := 2 × E[2] / ABS(B - A); E[3] := 0;
   HMIN := ABS(B - A) × RE; X := A; F0 := FX;
   X := A + HMAX; F2 := FX; X := A + 2 × HMAX; F3 := FX;
   X := A + 4 × HMAX; F5 := FX; X := A + 6 × HMAX; F6 := FX;
   X := A + 8 × HMAX; F7 := FX; X := B - 4 × HMAX; F9 := FX; X := B;
   F14 := FX;
   QADRAT := LINT(A, B, F0, F2, F3, F5, F6, F7, F9, F14) × 16;
   RETURN:
e̲n̲d̲ QADRAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 32051 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INTEGRAL(X, A, B, FX, E, UA, UB);
   v̲a̲l̲u̲e̲ A, B; r̲e̲a̲l̲ X, A, B, FX; a̲r̲r̲a̲y̲ E; B̲o̲o̲l̲e̲a̲n̲ UA, UB;
b̲e̲g̲i̲n̲
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TRANSF;
   b̲e̲g̲i̲n̲ Z := 1 / X; X := Z + B1; TRANSF := FX × Z × Z e̲n̲d̲;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QAD(FX); r̲e̲a̲l̲ FX;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ T, V, SUM, HMIN;
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ INT;
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ X3, X4, F3, F4, H;
         X4 := X2; X2 := X1; F4 := F2; F2 := F1;
         ANEW: X := X1 := (X0 + X2) × ·5; F1 := FX;
         X := X3 := (X2 + X4) × ·5; F3 := FX; H := X4 - X0;
         V := (4 × (F1 + F3) + 2  × F2 + F0 + F4) × 15;
         T := 6 × F2 -4  × (F1 + F3) + F0 + F4;
         i̲f̲ ABS(T) < ABS(V) × RE + AE t̲h̲e̲n̲
           SUM := SUM + (V - T) × H e̲l̲s̲e̲
         i̲f̲ ABS(H) < HMIN t̲h̲e̲n̲ E[3] := E[3] + 1
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ INT; X2 := X3; F2 := F3; g̲o̲t̲o̲ ANEW e̲n̲d̲;
         X0 := X4; F0 := F4
      e̲n̲d̲ INT;

      HMIN := ABS(X0 - X2) × RE; X := X1 := (X0 + X2) × ·5;
      F1 := FX; SUM := 0; INT; QAD := SUM / 180
   e̲n̲d̲ QAD;
   r̲e̲a̲l̲ X0, X1, X2, F0, F1, F2, RE, AE, B1, Z;
   RE := E[1]; i̲f̲ UB t̲h̲e̲n̲ AE := E[2] × 180 / ABS(B - A)
   e̲l̲s̲e̲ AE := E[2] × 90 / ABS(B - A); i̲f̲ UA t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ E[3] := E[4] := 0; X := X0 := A; F0 := FX e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ X := X0 := A := E[5]; F0 := E[6] e̲n̲d̲;
   E[5] := X := X2 := B; E[6] := F2 := FX; E[4] := E[4] + QAD(FX);
   i̲f̲  ¬UB t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ A < B t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ B1 := B -1 ; X0 := 1 e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ B1 := B + 1 ; X0 := -1  e̲n̲d̲;
      F0 := E[6]; E[5] := X2 := 0; E[6] := F2 := 0;
      AE := E[2] × 90;
      E[4] := E[4] - QAD(TRANSF)
   e̲n̲d̲;
   INTEGRAL := E[4]
e̲n̲d̲ INTEGRAL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34210 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LINEMIN(N, X, D, ND, ALFA, G, FUNCT, F0, F1, DF0, DF1,
  EVLMAX, STRONGSEARCH, IN); v̲a̲l̲u̲e̲ N, ND, F0, DF0, STRONGSEARCH;
   i̲n̲t̲e̲g̲e̲r̲ N, EVLMAX; B̲o̲o̲l̲e̲a̲n̲ STRONGSEARCH;
     r̲e̲a̲l̲ ND, ALFA, F0, F1, DF0, DF1;
     a̲r̲r̲a̲y̲ X, D, G, IN;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, EVL;
   B̲o̲o̲l̲e̲a̲n̲ NOTININT;
   r̲e̲a̲l̲ F, OLDF, DF, OLDDF, MU, ALFA0, Q, W, Y, Z, RELTOL, ABSTOL
     , EPS, AID;
   a̲r̲r̲a̲y̲ X0[1:N];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 31030;

   RELTOL := IN[1]; ABSTOL := IN[2]; MU := IN[3]; EVL := 0;
   ALFA0 := 0; OLDF := F0; OLDDF := DF0; Y := ALFA; NOTININT := t̲r̲u̲e̲;
   DUPVEC(1, N, 0, X0, X);
   EPS := (SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL) / ND;
   Q := (F1 - F0) / (ALFA × DF0);
   INT: i̲f̲ NOTININT t̲h̲e̲n̲ NOTININT := DF1 < 0 ∧ Q > MU;
   AID := ALFA; i̲f̲ DF1 ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ Z := 3 × (OLDF - F1) / ALFA + OLDDF + DF1;
      W := SQRT(Z ⭡ 2 - OLDDF × DF1);
      ALFA := ALFA × (1 - (DF1 + W - Z) / (DF1 - OLDDF + W × 2));
      i̲f̲ ALFA < EPS t̲h̲e̲n̲ ALFA := EPS e̲l̲s̲e̲
      i̲f̲ AID - ALFA < EPS t̲h̲e̲n̲ ALFA := AID - EPS
   e̲n̲d̲ CUBIC INTERPOLATION
     e̲l̲s̲e̲ i̲f̲ NOTININT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ ALFA0 := ALFA := Y; OLDDF := DF1; OLDF := F1 e̲n̲d̲
   e̲l̲s̲e̲ ALFA := 0·5 × ALFA; Y := ALFA + ALFA0;
   DUPVEC(1, N, 0, X, X0); ELMVEC(1, N, 0, X, D, Y);
   EPS := (SQRT(VECVEC(1, N, 0, X, X)) × RELTOL + ABSTOL) / ND;
   F := FUNCT(N, X, G); EVL := EVL + 1 ; DF := VECVEC(1, N, 0, D, G);
   Q := (F - F0) / (Y × DF0);
   i̲f̲ (i̲f̲ NOTININT ∨ STRONGSEARCH t̲h̲e̲n̲ t̲r̲u̲e̲ e̲l̲s̲e̲
     Q < MU ∨ Q > 1 - MU) ∧ EVL < EVLMAX t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ NOTININT ∨ DF > 0 ∨ Q < MU t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DF1 := DF; F1 := F e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ ALFA0 := Y; ALFA := AID - ALFA; OLDDF := DF; OLDF := F
      e̲n̲d̲;
      i̲f̲ ALFA > EPS × 2 t̲h̲e̲n̲ g̲o̲t̲o̲ INT
   e̲n̲d̲;
   ALFA := Y; EVLMAX := EVL; DF1 := DF; F1 := F
e̲n̲d̲ LINEMIN;
c̲o̲m̲m̲e̲n̲t̲ ================== 34211 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RNK1UPD(H, N, V, C); v̲a̲l̲u̲e̲ N, C; i̲n̲t̲e̲g̲e̲r̲ N;
     r̲e̲a̲l̲ C; a̲r̲r̲a̲y̲ H, V;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, K;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;
   K := 0;
   f̲o̲r̲ J := 1, J + K w̲h̲i̲l̲e̲ K < N d̲o̲
      b̲e̲g̲i̲n̲ K := K + 1 ;
         ELMVEC(J, J + K - 1, 1 - J, H, V, V[K] × C)
      e̲n̲d̲
e̲n̲d̲ RNK1UPD;
c̲o̲m̲m̲e̲n̲t̲ ================== 34212 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DAVUPD(H, N, V, W, C1, C2); v̲a̲l̲u̲e̲ N, C1, C2;
   i̲n̲t̲e̲g̲e̲r̲ N; r̲e̲a̲l̲ C1, C2; a̲r̲r̲a̲y̲ H, V, W;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K;
   r̲e̲a̲l̲ VK, WK;
   K := 0;
   f̲o̲r̲ J := 1, J + K w̲h̲i̲l̲e̲ K < N d̲o̲
      b̲e̲g̲i̲n̲ K := K + 1 ; VK := V[K] × C1; WK := W[K] × C2;
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K -1  d̲o̲
              H[I + J] := H[I + J] + V[I + 1] × VK - W[I + 1] × WK
      e̲n̲d̲
e̲n̲d̲ DAVUPD;
c̲o̲m̲m̲e̲n̲t̲ ================== 34213 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FLEUPD(H, N, V, W, C1, C2); v̲a̲l̲u̲e̲ N, C1, C2;
   i̲n̲t̲e̲g̲e̲r̲ N; r̲e̲a̲l̲ C1, C2; a̲r̲r̲a̲y̲ H, V, W;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K;
   r̲e̲a̲l̲ VK, WK;
   K := 0; f̲o̲r̲ J := 1, J + K w̲h̲i̲l̲e̲ K < N d̲o̲
      b̲e̲g̲i̲n̲ K := K + 1; VK := - W[K] × C1 + V[K] × C2; WK := V[K] × C1;
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K - 1  d̲o̲
              H[I + J] := H[I + J] + V[I + 1] × VK -W[I + 1] × WK
      e̲n̲d̲
e̲n̲d̲ FLEUPD;
c̲o̲m̲m̲e̲n̲t̲ ================== 33010 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK1(X, A, B, Y, YA, FXY, E, D, FI);
   v̲a̲l̲u̲e̲ B, FI; r̲e̲a̲l̲ X, A, B, Y, YA, FXY; B̲o̲o̲l̲e̲a̲n̲ FI;
     a̲r̲r̲a̲y̲ E, D;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E1, E2, XL, YL, H, INT, HMIN, ABSH, K0, K1,
     K2, K3, K4, K5, DISCR, TOL, MU, MU1, FH, HL;
   B̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3] := A; D[4] := YA e̲n̲d̲;
   D[1] := 0; XL := D[3]; YL := D[4];
   i̲f̲ FI t̲h̲e̲n̲ D[2] := B - D[3]; ABSH := H := ABS(D[2]);
   i̲f̲ B - XL < 0 t̲h̲e̲n̲ H := - H; INT := ABS(B - XL);
   HMIN := INT × E[1] + E[2]; E1 := E[1] / INT;
   E2 := E[2] / INT; FIRST := t̲r̲u̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN; ABSH := HMIN
   e̲n̲d̲;
   i̲f̲ H ≥ B - XL ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[2] := H; LAST := t̲r̲u̲e̲; H := B - XL;
      ABSH := ABS(H)
   e̲n̲d̲
   e̲l̲s̲e̲ LAST := f̲a̲l̲s̲e̲;
   STEP: X := XL; Y := YL; K0 := FXY × H;
   X := XL + H / 4·5; Y := YL + K0 / 4·5;
   K1 := FXY × H; X := XL + H / 3;
   Y := YL + (K0 + K1 × 3) / 12; K2 := FXY × H;
   X := XL + H × ·5; Y := YL + (K0 + K2 × 3) / 8;
   K3 := FXY × H; X := XL + H × ·8;
   Y := YL + (K0 × 53 - K1 × 135 + K2 × 126 + K3 × 56)
     / 125; K4 := FXY × H; X := i̲f̲ LAST t̲h̲e̲n̲ B e̲l̲s̲e̲ XL + H;
   Y := YL + (K0 × 133 - K1 × 378 + K2 × 276 + K3 × 112
     + K4 × 25) / 168; K5 := FXY × H;
   DISCR := ABS(K0 × 21 - K2 × 162 + K3 × 224 - K4 × 125
     + K5 × 42) / 14; TOL := ABS(K0) × E1 + ABSH × E2;
   REJECT := DISCR > TOL; MU := TOL / (TOL + DISCR) + ·45;
   i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[1] := D[1] + 1; Y := YL; FIRST := t̲r̲u̲e̲;
         g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; HL := H; H := MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FH := MU × H / HL + MU - MU1; HL := H; H := FH × H;
   ACC: MU1 := MU;
   Y := YL + ( - K0 × 63 + K1 × 189 - K2 × 36 - K3 × 112
     + K4 × 50) / 28; K5 := FXY × HL;
   Y := YL + (K0 × 35 + K2 × 162 + K4 × 125 + K5 × 14)
     / 336;

   NEXT: i̲f̲ B ≠ X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XL := X; YL := Y; g̲o̲t̲o̲ TEST e̲n̲d̲;
   i̲f̲ ¬LAST t̲h̲e̲n̲ D[2] := H; D[3] := X; D[4] := Y
e̲n̲d̲ RK1;
c̲o̲m̲m̲e̲n̲t̲ ================== 33033 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RKE (X, XE, N, Y, DER, DATA, FI, OUT);
   v̲a̲l̲u̲e̲  FI, N; i̲n̲t̲e̲g̲e̲r̲ N; r̲e̲a̲l̲ X, XE;
     B̲o̲o̲l̲e̲a̲n̲ FI; a̲r̲r̲a̲y̲ Y, DATA;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DER, OUT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J;
   r̲e̲a̲l̲ XT, H, HMIN, INT, HL, HT, ABSH, FHM, DISCR, TOL, MU,
     MU1, FH, E1, E2;
   B̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   a̲r̲r̲a̲y̲ K0, K1, K2, K3, K4[1:N];
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ DATA[3] := XE - X; DATA[4] := DATA[5] := DATA[6] := 0 e̲n̲d̲;
   ABSH := H := ABS(DATA[3]);
   i̲f̲ XE < X t̲h̲e̲n̲ H := - H; INT := ABS(XE - X);
   HMIN := INT × DATA[1] + DATA[2];
   E1 := 12 × DATA[1] / INT; E2 := 12 × DATA[2] / INT;
   FIRST := t̲r̲u̲e̲; REJECT := f̲a̲l̲s̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := SIGN (XE - X) × HMIN; ABSH := HMIN e̲n̲d̲;
   i̲f̲ H ≥ XE - X ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; H := XE - X; ABSH := ABS(H) e̲n̲d̲
   e̲l̲s̲e̲ LAST := f̲a̲l̲s̲e̲;
   STEP: i̲f̲ ¬REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K0[J] := Y[J];
      DER(X, K0)
   e̲n̲d̲;
   HT := ·184262134833347 × H; XT := X + HT;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K1[J] := K0[J] × HT + Y[J];
   DER(XT, K1);
   HT := ·690983005625053@-1 × H; XT := 4 × HT + X;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K2[J] :=
        (3 × K1[J] + K0[J]) × HT + Y[J];
   DER(XT, K2);
   XT := ·5 × H + X; HT := ·1875 × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K3[J] := ((1·74535599249993× K2[J] - K1[J]) × 2·23606797749979 + K0[J]) × HT + Y[J];
   DER(XT, K3);
   XT := ·723606797749979 × H + X; HT := ·4 × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K4[J] := (((·517595468166681× K0[J] - K1[J]) × ·927050983124840 + K2[J]) × 1·46352549156242
        + K3[J]) × HT + Y[J];
   DER(XT, K4);
   XT := i̲f̲ LAST t̲h̲e̲n̲ XE e̲l̲s̲e̲ X + H; HT := 2 × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K1[J] := ((((2 × K4[J] +
        K2[J]) × ·412022659166595 + K1[J]) × 2·23606797749979 -
        K0[J]) × ·375 - K3[J]) × HT + Y[J];
   DER(XT, K1);
   REJECT := f̲a̲l̲s̲e̲; FHM := 0;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ DISCR := ABS((1·6 × K3[J] - K2[J] - K4[J]) × 5 +
           K0[J] + K1[J]);
         TOL := ABS(K0[J]) × E1 + E2;
         REJECT := DISCR > TOL ∨ REJECT;
         FH := DISCR / TOL; i̲f̲ FH > FHM t̲h̲e̲n̲ FHM := FH
      e̲n̲d̲;
   MU := 1 / (1 + FHM) + ·45; i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ DATA[5] := DATA[5] + 1; i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DATA[6] := DATA[6] + 1; HL := H; REJECT := f̲a̲l̲s̲e̲;
         FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; HL := H; H := MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FH := MU × H / HL + MU - MU1; HL := H; H := FH × H;
   ACC: MU1 := MU; HT := HL / 12;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[J] :=
        ((K2[J] + K4[J]) × 5 + K0[J] + K1[J]) × HT + Y[J];
   NEXT: DATA[3] := HL; DATA[4] := DATA[4] + 1; X := XT; OUT;
   i̲f̲ X ≠ XE t̲h̲e̲n̲ g̲o̲t̲o̲ TEST
e̲n̲d̲ RKE;
c̲o̲m̲m̲e̲n̲t̲ ================== 33016 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK4A(X, XA, B, Y, YA, FXY, E, D, FI, XDIR,
  POS); v̲a̲l̲u̲e̲ FI, XDIR, POS; B̲o̲o̲l̲e̲a̲n̲ FI, XDIR, POS;
     r̲e̲a̲l̲ X, XA, B, Y, YA, FXY; a̲r̲r̲a̲y̲ E, D;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   B̲o̲o̲l̲e̲a̲n̲ IV, FIRST, FIR, REJ;
   r̲e̲a̲l̲ K0, K1, K2, K3, K4, K5, FHM, ABSH, DISCR, S, XL,
     COND0, S1, COND1, YL, HMIN, H, ZL, TOL, HL, MU, MU1;
   a̲r̲r̲a̲y̲ E1[1:2];

   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ZEROIN(X, Y, FX, EPS) ; r̲e̲a̲l̲ X, Y, FX, EPS ;
      c̲o̲d̲e̲            34150 ;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ RKSTEP(X, XL, H, Y, YL, ZL, FXY, D);
      v̲a̲l̲u̲e̲ XL, YL, ZL, H; r̲e̲a̲l̲ X, XL, H, Y, YL, ZL, FXY;
        i̲n̲t̲e̲g̲e̲r̲ D;
   b̲e̲g̲i̲n̲ i̲f̲ D = 2 t̲h̲e̲n̲ g̲o̲t̲o̲ INTEGRATE; i̲f̲ D = 3 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ X := XL; Y := YL; K0 := FXY × H e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ D = 1 t̲h̲e̲n̲ K0 := ZL × H e̲l̲s̲e̲ K0 := K0 × MU;
      X := XL + H / 4·5; Y := YL + K0 / 4·5; K1 := FXY × H;
      X := XL + H / 3; Y := YL + (K0 + K1 × 3) / 12;
      K2 := FXY × H; X := XL + H × ·5;
      Y := YL + (K0 + K2 × 3) / 8; K3 := H × FXY;
      X := XL + H × ·8;
      Y := YL + (K0 × 53 - K1 × 135 + K2 × 126 + K3 ×
        56) / 125; K4 := FXY × H; i̲f̲ D ≤ 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ X := XL + H;
         Y := YL + (K0 × 133 - K1 × 378 + K2 × 276 + K3× 112 + K4 × 25) / 168; K5 := FXY × H;
         DISCR := ABS(K0 × 21 - K2 × 162 + K3 × 224 - K4× 125 + K5 × 42) / 14; g̲o̲t̲o̲ END
      e̲n̲d̲;
      INTEGRATE: X := XL + H;
      Y := YL + ( - K0 × 63 + K1 × 189 - K2 × 36 - K3 ×
        112 + K4 × 50) / 28; K5 := FXY × H;
      Y := YL + (K0 × 35 + K2 × 162 + K4 × 125 + K5 ×
        14) / 336;
      END:
   e̲n̲d̲ RKSTEP;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FZERO;
   b̲e̲g̲i̲n̲ i̲f̲ IV t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ S = XL t̲h̲e̲n̲ FZERO := COND0 e̲l̲s̲e̲ i̲f̲ S = S1
           t̲h̲e̲n̲ FZERO := COND1 e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ RKSTEP(X, XL, S - XL, Y, YL, ZL, FXY, 3);
            FZERO := B
         e̲n̲d̲
      e̲n̲d̲
      e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ i̲f̲ S = YL t̲h̲e̲n̲ FZERO := COND0 e̲l̲s̲e̲ i̲f̲ S = S1
           t̲h̲e̲n̲ FZERO := COND1 e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ RKSTEP(Y, YL, S - YL, X, XL, ZL, 1 /
              FXY, 3); FZERO := B
         e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ FZERO;

   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3] := XA; D[4] := YA; D[0] := 1 e̲n̲d̲;
   D[1] := 0; X := XL := D[3]; Y := YL := D[4]; IV := D[0] > 0;
   FIRST := FIR := t̲r̲u̲e̲; HMIN := E[0] + E[1];
   H := E[2] + E[3]; i̲f̲ H < HMIN t̲h̲e̲n̲ HMIN := H;
   CHANGE: ZL := FXY; i̲f̲ ABS(ZL) ≤ 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ¬IV t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[2] := H := H / ZL; D[0] := 1;
         IV := FIRST := t̲r̲u̲e̲
      e̲n̲d̲;
      i̲f̲ FIR t̲h̲e̲n̲ g̲o̲t̲o̲ A; I := 1; g̲o̲t̲o̲ AGAIN
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ IV t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ ¬FIR t̲h̲e̲n̲ D[2] := H := H × ZL; D[0] := - 1;
         IV := f̲a̲l̲s̲e̲; FIRST := t̲r̲u̲e̲
      e̲n̲d̲;
      i̲f̲ FIR t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ H := E[0] + E[1];
         A: i̲f̲ (i̲f̲ FI t̲h̲e̲n̲ (i̲f̲ IV ≡ XDIR t̲h̲e̲n̲ H e̲l̲s̲e̲
           H × ZL) < 0 ≡ POS e̲l̲s̲e̲ H × D[2] < 0) t̲h̲e̲n̲ H := - H
      e̲n̲d̲;
      I := 1
   e̲n̲d̲;
   AGAIN: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := SIGN(H) × HMIN; ABSH := HMIN e̲n̲d̲;
   i̲f̲ IV t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ RKSTEP(X, XL, H, Y, YL, ZL, FXY, I);
      TOL := E[2] × ABS(K0) + E[3] × ABSH
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ RKSTEP(Y, YL, H, X, XL, 1 / ZL, 1 / FXY, I);
      TOL := E[0] × ABS(K0) + E[1] × ABSH
   e̲n̲d̲;
   REJ := DISCR > TOL; MU := TOL / (TOL + DISCR) + ·45;
   i̲f̲ REJ t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ IV t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ X := XL + H; Y := YL + K0 e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ X := XL + K0; Y := YL + H e̲n̲d̲;
         D[1] := D[1] + 1; FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := H × MU; I := 0; g̲o̲t̲o̲ AGAIN
   e̲n̲d̲ REJ;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := FIR; HL := H; H := MU × H; g̲o̲t̲o̲ ACCEPT
   e̲n̲d̲;
   FHM := MU × H / HL + MU - MU1; HL := H; H := FHM × H;
   ACCEPT: i̲f̲ IV t̲h̲e̲n̲ RKSTEP(X, XL, HL, Y, YL, ZL, FXY,
     2) e̲l̲s̲e̲ RKSTEP(Y, YL, HL, X, XL, ZL, 1 / FXY, 2);
   MU1 := MU;
   NEXT: i̲f̲ FIR t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIR := f̲a̲l̲s̲e̲; COND0 := B;
      i̲f̲ ¬(FI ∨ REJ) t̲h̲e̲n̲ H := D[2]
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ D[2] := H; COND1 := B;
      i̲f̲ COND0 × COND1 ≤ 0 t̲h̲e̲n̲ g̲o̲t̲o̲ ZERO;
      COND0 := COND1
   e̲n̲d̲;
   D[3] := XL := X; D[4] := YL := Y; g̲o̲t̲o̲ CHANGE;
   ZERO: E1[1] := E[4]; E1[2] := E[5];
   S1 := i̲f̲ IV t̲h̲e̲n̲ X e̲l̲s̲e̲ Y;
   S := i̲f̲ IV t̲h̲e̲n̲ XL e̲l̲s̲e̲ YL ;
   ZEROIN(S, S1, FZERO, ABS(E1[1] × S) + ABS(E1[2])) ;
   S1 := i̲f̲ IV t̲h̲e̲n̲ X e̲l̲s̲e̲ Y ;
   i̲f̲ IV t̲h̲e̲n̲ RKSTEP(X, XL, S - XL, Y, YL, ZL, FXY, 3)
   e̲l̲s̲e̲ RKSTEP(Y, YL, S - YL, X, XL, ZL, 1 / FXY,
     3); D[3] := X; D[4] := Y
e̲n̲d̲ RK4A;
c̲o̲m̲m̲e̲n̲t̲ ================== 33017 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK4NA(X, XA, B, FXJ, J, E, D, FI, N, L, POS);
   v̲a̲l̲u̲e̲ FI, N, L, POS; i̲n̲t̲e̲g̲e̲r̲ J, N, L; B̲o̲o̲l̲e̲a̲n̲ FI, POS;
     r̲e̲a̲l̲ B, FXJ; a̲r̲r̲a̲y̲ X, XA, E, D;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, IV, IV0;
   B̲o̲o̲l̲e̲a̲n̲ FIR, FIRST, REJ;
   r̲e̲a̲l̲ H, COND0, COND1, FHM, ABSH, TOL, FH, MAX, X0,
     X1, S, HMIN, HL, MU, MU1;
   a̲r̲r̲a̲y̲ XL, DISCR, Y[0:N], K[0:5, 0:N], E1[1:2];

   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ZEROIN(X, Y, FX, EPS) ; r̲e̲a̲l̲ X, Y, FX, EPS ;
      c̲o̲d̲e̲            34150 ;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ RKSTEP(H, D); v̲a̲l̲u̲e̲ H, D; i̲n̲t̲e̲g̲e̲r̲ D; r̲e̲a̲l̲ H;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ F(T); v̲a̲l̲u̲e̲ T; i̲n̲t̲e̲g̲e̲r̲ T;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
         r̲e̲a̲l̲ P;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[J] := FXJ;
         P := H / Y[IV];
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲ K[T, I] := Y[I] × P e̲n̲d̲
      e̲n̲d̲ F;

      i̲f̲ D = 2 t̲h̲e̲n̲ g̲o̲t̲o̲ INTEGRATE; i̲f̲ D = 3 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I];
         F(0)
      e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ D = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ P;
         P := H / Y[IV];
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲ K[0, I] := P × Y[I] e̲n̲d̲
      e̲n̲d̲
      e̲l̲s̲e̲
        f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲ K[0, I] := K[0, I] × MU e̲n̲d̲;
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I] + (i̲f̲ I= IV t̲h̲e̲n̲ H e̲l̲s̲e̲ K[0, I]) / 4·5; F(1);
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I] + (i̲f̲ I= IV t̲h̲e̲n̲ H × 4 e̲l̲s̲e̲ (K[0, I] + K[1, I] × 3)) / 12;
      F(2);
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I] + (i̲f̲ I= IV t̲h̲e̲n̲ H × ·5 e̲l̲s̲e̲ (K[0, I] + K[2, I] × 3) / 8);
      F(3);
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I] + (i̲f̲ I= IV t̲h̲e̲n̲ H × ·8 e̲l̲s̲e̲ (K[0, I] × 53 - K[1, I] × 135
           + K[2, I] × 126 + K[3, I] × 56) / 125); F(4);
      i̲f̲ D ≤ 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I] +
              (i̲f̲ I = IV t̲h̲e̲n̲ H e̲l̲s̲e̲ (K[0, I] × 133 -
              K[1, I] × 378 + K[2, I] × 276 + K[3, I] × 112 +
              K[4, I] × 25) / 168); F(5);
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲ DISCR[I] := ABS(K[0, I] × 21
                 - K[2, I] × 162 + K[3, I] × 224 - K[4, I] ×
                 125 + K[5, I] × 42) / 14
            e̲n̲d̲;
         g̲o̲t̲o̲ END
      e̲n̲d̲;
      INTEGRATE: f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I]
           + (i̲f̲ I = IV t̲h̲e̲n̲ H e̲l̲s̲e̲ ( - K[0, I] × 63 + K[1, I]× 189 - K[2, I] × 36 - K[3, I] × 112 + K[4, I] × 50)
           / 28); F(5);
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲ X[I] := XL[I] + (K[0, I] × 35
              + K[2, I] × 162 + K[4, I] × 125 + K[5, I] × 14) / 336
         e̲n̲d̲ ;
      END:
   e̲n̲d̲ RKSTEP ;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FZERO;
   b̲e̲g̲i̲n̲ i̲f̲ S = X0 t̲h̲e̲n̲ FZERO := COND0 e̲l̲s̲e̲ i̲f̲ S = X1
        t̲h̲e̲n̲ FZERO := COND1 e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ RKSTEP(S - XL[IV], 3); FZERO := B e̲n̲d̲
   e̲n̲d̲ FZERO;

   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ D[I + 3] := XA[I];
      D[0] := D[2] := 0
   e̲n̲d̲;
   D[1] := 0;
   f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ X[I] := XL[I] := D[I + 3];
   IV := D[0]; H := D[2]; FIRST := FIR := t̲r̲u̲e̲; Y[0] := 1;
   g̲o̲t̲o̲ CHANGE;
   AGAIN: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN;
      ABSH := ABS(H)
   e̲n̲d̲;
   RKSTEP(H, I); REJ := f̲a̲l̲s̲e̲; FHM := 0;
   f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ TOL := E[2 × I] × ABS(K[0, I]) + E[2 × I + 1]× ABSH; REJ := TOL < DISCR[I] ∨ REJ;
            FH := DISCR[I] / TOL; i̲f̲ FH > FHM t̲h̲e̲n̲ FHM := FH
         e̲n̲d̲
      e̲n̲d̲;
   MU := 1 / (1 + FHM) + ·45; i̲f̲ REJ t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ I ≠ IV t̲h̲e̲n̲ X[I] := XL[I] + K[0, I]
               e̲l̲s̲e̲ X[I] := XL[I] + H
            e̲n̲d̲;
         D[1] := D[1] + 1; FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := H × MU; I := 0; g̲o̲t̲o̲ AGAIN
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := FIR; HL := H; H := MU × H; g̲o̲t̲o̲ ACCEPT
   e̲n̲d̲;
   FH := MU × H / HL + MU - MU1; HL := H; H := FH × H;
   ACCEPT: RKSTEP(HL, 2); MU1 := MU;
   NEXT: i̲f̲ FIR t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIR := f̲a̲l̲s̲e̲; COND0 := B;
      i̲f̲ ¬(FI ∨ REJ) t̲h̲e̲n̲ H := D[2]
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ D[2] := H; COND1 := B;
      i̲f̲ COND0 × COND1 ≤ 0 t̲h̲e̲n̲ g̲o̲t̲o̲ ZERO;
      COND0 := COND1
   e̲n̲d̲;
   f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ D[I + 3] := XL[I] := X[I];
   CHANGE: IV0 := IV;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[J] := FXJ;
   MAX := ABS(Y[IV]);
   f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ ABS(Y[I]) > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := ABS(Y[I]); IV := I e̲n̲d̲
      e̲n̲d̲;
   i̲f̲ IV0 ≠ IV t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := t̲r̲u̲e̲; D[0] := IV;
      D[2] := H := Y[IV] / Y[IV0] × H
   e̲n̲d̲;
   X0 := XL[IV]; i̲f̲ FIR t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ HMIN := E[0] + E[1];
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ H := E[2 × I] + E[2 × I + 1];
            i̲f̲ H < HMIN t̲h̲e̲n̲ HMIN := H
         e̲n̲d̲;
      H := E[2 × IV] + E[2 × IV + 1];
      i̲f̲ (FI ∧ (Y[L]/Y[IV] × H < 0 ≡ POS)) ∨
        ( ¬FI ∧ D[2] × H < 0) t̲h̲e̲n̲ H := -H
   e̲n̲d̲;
   I := 1; g̲o̲t̲o̲ AGAIN;
   ZERO: E1[1] := E[2 × N + 2]; E1[2] := E[2 × N + 3];
   X1 := X[IV] ; S := X0 ;
   ZEROIN(S, X1, FZERO, ABS(E1[1] × S) + ABS(E1[2])) ; X0 := S ; X1 := X[IV];
   RKSTEP(X0 - XL[IV], 3);
   f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ D[I + 3] := X[I]
e̲n̲d̲ RK4NA;
c̲o̲m̲m̲e̲n̲t̲ ================== 33080 ================= ;
B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULTISTEP(X, XEND, Y, HMIN, HMAX, YMAX, EPS,
  FIRST, SAVE, DERIV, AVAILABLE, JACOBIAN, STIFF, N, OUT);
   v̲a̲l̲u̲e̲ HMIN, HMAX, EPS, XEND, N, STIFF;
   B̲o̲o̲l̲e̲a̲n̲ FIRST, AVAILABLE, STIFF;
     i̲n̲t̲e̲g̲e̲r̲ N;
     r̲e̲a̲l̲ X, XEND, HMIN, HMAX, EPS;
     a̲r̲r̲a̲y̲ Y, YMAX, SAVE, JACOBIAN;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIV, OUT;
b̲e̲g̲i̲n̲ o̲w̲n̲ B̲o̲o̲l̲e̲a̲n̲ ADAMS, WITH JACOBIAN;
   o̲w̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, SAME, KOLD;
   o̲w̲n̲ r̲e̲a̲l̲ XOLD, HOLD, A0, TOLUP, TOL, TOLDWN, TOLCONV;
   B̲o̲o̲l̲e̲a̲n̲ EVALUATE, EVALUATED, DECOMPOSE, DECOMPOSED, CONV;
   i̲n̲t̲e̲g̲e̲r̲ I, J, L, K, KNEW, FAILS;
   r̲e̲a̲l̲ H, CH, CHNEW, ERROR, DFI, C;
   a̲r̲r̲a̲y̲ A[0:5], DELTA, LAST DELTA, DF[1:N], JAC[1:N, 1:N], AUX[1:3];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P[1:N];

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ NORM2(AI); r̲e̲a̲l̲ AI;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S, A; S := 1·0@-100;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ A := AI/YMAX[I]; S := S + A × A e̲n̲d̲;
      NORM2 := S
   e̲n̲d̲ NORM2;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ RESET;
   b̲e̲g̲i̲n̲ i̲f̲ CH < HMIN/HOLD t̲h̲e̲n̲ CH := HMIN/HOLD e̲l̲s̲e̲
      i̲f̲ CH > HMAX/HOLD t̲h̲e̲n̲ CH := HMAX/HOLD;
      X := XOLD; H := HOLD × CH; C := 1;
      f̲o̲r̲ J := 0 s̲t̲e̲p̲ M u̲n̲t̲i̲l̲ K × M d̲o̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 Y[J + I] := SAVE[J + I] × C;
            C := C × CH
         e̲n̲d̲;
      DECOMPOSED := f̲a̲l̲s̲e̲
   e̲n̲d̲ RESET;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ METHOD;
   b̲e̲g̲i̲n̲ I := -39;
      i̲f̲ ADAMS t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ C := 1, 1, 144, 4, 0, ·5, 1, ·5, 576, 144, 1, 5/12, 1,·75, 1/6, 1436, 576, 4, ·375, 1, 11/12, 1/3, 1/24,
           2844, 1436, 1, 251/720, 1, 25/24, 35/72,
           5/48, 1/120, 0, 2844, 0·1
           d̲o̲ b̲e̲g̲i̲n̲ I := I + 1; SAVE[I] := C e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲

      b̲e̲g̲i̲n̲ f̲o̲r̲ C := 1, 1, 9, 4, 0, 2/3, 1, 1/3, 36, 20·25, 1, 6/11,
           1, 6/11, 1/11, 84·028, 53·778, 0·25, ·48, 1, ·7, ·2, ·02,
           156·25, 108·51, ·027778, 120/274, 1, 225/274,
           85/274, 15/274, 1/274, 0, 187·69, ·0047361
           d̲o̲ b̲e̲g̲i̲n̲ I := I + 1; SAVE[I] := C e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ METHOD;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ORDER;
   b̲e̲g̲i̲n̲ C := EPS × EPS; J := (K-1) × (K + 8)/2 - 38;
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲ A[I] := SAVE[I + J];
      TOLUP  := C × SAVE[J + K + 1];
      TOL    := C × SAVE[J + K + 2];
      TOLDWN := C × SAVE[J + K + 3];
      TOLCONV := EPS/(2 × N × (K + 2));
      A0 := A[0];  DECOMPOSE := t̲r̲u̲e̲;
   e̲n̲d̲ ORDER;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ EVALUATE JACOBIAN;
   b̲e̲g̲i̲n̲ EVALUATE := f̲a̲l̲s̲e̲;
      DECOMPOSE := EVALUATED := t̲r̲u̲e̲;
      i̲f̲ AVAILABLE t̲h̲e̲n̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ D; a̲r̲r̲a̲y̲ FIXY, FIXDY, DY[1:N];
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              FIXY[I] := Y[I];
         DERIV(FIXDY);
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ D := i̲f̲ EPS > ABS(FIXY[J])
                 t̲h̲e̲n̲ EPS × EPS
               e̲l̲s̲e̲ EPS × ABS(FIXY[J]);
               Y[J] := Y[J] + D; DERIV(DY);
               f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                    JACOBIAN[I, J] := (DY[I]-FIXDY[I])/D;
               Y[J] := FIXY[J]
            e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ EVALUATE JACOBIAN;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECOMPOSE JACOBIAN;
   b̲e̲g̲i̲n̲ DECOMPOSE := f̲a̲l̲s̲e̲;
      DECOMPOSED := t̲r̲u̲e̲; C := -A0 × H;
      f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 JAC[I, J] := JACOBIAN[I, J] × C;
            JAC[J, J] := JAC[J, J] + 1
         e̲n̲d̲;
      AUX[2] := 1·0@-12;
      DEC(JAC, N, AUX, P)
   e̲n̲d̲ DECOMPOSE JACOBIAN;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CALCULATE STEP AND ORDER;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ A1, A2, A3;
      A1 := i̲f̲ K ≤ 1 t̲h̲e̲n̲ 0 e̲l̲s̲e̲
        0·75 × (TOLDWN/NORM2(Y[K × M + I])) ⭡ (0·5/K);
      A2 := 0·80 × (TOL/ERROR) ⭡ (0·5/(K + 1));
      A3 := i̲f̲ K ≥ 5 ∨ FAILS ≠ 0
        t̲h̲e̲n̲ 0 e̲l̲s̲e̲
        0·70 × (TOLUP/NORM2(DELTA[I] - LAST DELTA[I])) ⭡
        (0·5/(K + 2));

      i̲f̲ A1 > A2 ∧ A1 > A3 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ KNEW := K-1; CHNEW := A1 e̲n̲d̲ e̲l̲s̲e̲
      i̲f̲ A2 > A3 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ KNEW := K  ; CHNEW := A2 e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ KNEW := K + 1; CHNEW := A3 e̲n̲d̲
   e̲n̲d̲ CALCULATE STEP AND ORDER;

   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; M := N;
      f̲o̲r̲ I := -1, -2, -3 d̲o̲ SAVE[I] := 0;
      OUT(0, 0);
      ADAMS := ¬STIFF; WITH JACOBIAN := ¬ADAMS;
      i̲f̲ WITH JACOBIAN t̲h̲e̲n̲ EVALUATE JACOBIAN;
      METHOD;
      NEW START: K := 1; SAME := 2; ORDER; DERIV(DF);
      H := i̲f̲ ¬WITH JACOBIAN t̲h̲e̲n̲ HMIN e̲l̲s̲e̲
        SQRT(2 × EPS/SQRT(NORM2 (MATVEC(1, N, I, JACOBIAN, DF))));
      i̲f̲ H > HMAX t̲h̲e̲n̲ H := HMAX e̲l̲s̲e̲
      i̲f̲ H < HMIN t̲h̲e̲n̲ H := HMIN;
      XOLD := X; HOLD := H; KOLD := K; CH := 1;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ SAVE[I] := Y[I]; SAVE[M + I] := Y[M + I] := DF[I] × H
         e̲n̲d̲;
      OUT(0, 0)
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ WITH JACOBIAN := ¬ADAMS; CH := 1;
      K := KOLD; RESET; ORDER;
      DECOMPOSE := WITH JACOBIAN
   e̲n̲d̲;
   FAILS := 0;
   f̲o̲r̲ L := 0 w̲h̲i̲l̲e̲ X < XEND d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ X + H ≤ XEND t̲h̲e̲n̲ X := X + H e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ H := XEND-X; X := XEND; CH := H/HOLD; C := 1;
            f̲o̲r̲ J := M s̲t̲e̲p̲ M u̲n̲t̲i̲l̲ K × M d̲o̲
               b̲e̲g̲i̲n̲ C := C × CH;
                  f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J + N d̲o̲
                       Y[I] := Y[I] × C
               e̲n̲d̲;
            SAME := i̲f̲ SAME < 3 t̲h̲e̲n̲ 3 e̲l̲s̲e̲ SAME + 1;
         e̲n̲d̲;

         c̲o̲m̲m̲e̲n̲t̲ PREDICTION;
         f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ I := L s̲t̲e̲p̲ M u̲n̲t̲i̲l̲ (K-1) × M + L d̲o̲
                    f̲o̲r̲ J := (K-1) × M + L s̲t̲e̲p̲ -M u̲n̲t̲i̲l̲ I d̲o̲
                       Y[J] := Y[J] + Y[J + M];
               DELTA[L] := 0
            e̲n̲d̲;  EVALUATED := f̲a̲l̲s̲e̲;

         c̲o̲m̲m̲e̲n̲t̲ CORRECTION AND ESTIMATION LOCAL ERROR;
         f̲o̲r̲ L := 1, 2, 3 d̲o̲
            b̲e̲g̲i̲n̲ DERIV(DF);
               f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                    DF[I] := DF[I] × H - Y[M + I];
               i̲f̲ WITH JACOBIAN t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ EVALUATE t̲h̲e̲n̲ EVALUATE JACOBIAN;
                  i̲f̲ DECOMPOSE t̲h̲e̲n̲ DECOMPOSE JACOBIAN;
                  SOL(JAC, N, P, DF)
               e̲n̲d̲;

               CONV := t̲r̲u̲e̲;
               f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                  b̲e̲g̲i̲n̲ DFI := DF[I];
                     Y[  I] := Y[  I] + A0 × DFI;
                     Y[M + I] := Y[M + I] +      DFI;
                     DELTA[I] := DELTA[I] +  DFI;
                     CONV := CONV ∧ ABS(DFI) < TOLCONV × YMAX[I]
                  e̲n̲d̲;
               i̲f̲ CONV t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ ERROR := NORM2(DELTA[I]);
                  g̲o̲t̲o̲ CONVERGENCE
               e̲n̲d̲
            e̲n̲d̲;

         c̲o̲m̲m̲e̲n̲t̲ ACCEPTANCE OR REJECTION;
         i̲f̲ ¬CONV t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ ¬WITH JACOBIAN t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ EVALUATE := WITH JACOBIAN := SAME ≥ K∨ H < 1·1 × HMIN;
               i̲f̲ ¬WITH JACOBIAN t̲h̲e̲n̲ CH := CH/4;
            e̲n̲d̲ e̲l̲s̲e̲
            i̲f̲ ¬DECOMPOSED t̲h̲e̲n̲ DECOMPOSE := t̲r̲u̲e̲ e̲l̲s̲e̲
            i̲f̲ ¬EVALUATED  t̲h̲e̲n̲ EVALUATE := t̲r̲u̲e̲ e̲l̲s̲e̲
            i̲f̲ H > 1·1 × HMIN   t̲h̲e̲n̲ CH := CH/4 e̲l̲s̲e̲
            i̲f̲ ADAMS            t̲h̲e̲n̲ g̲o̲t̲o̲ TRY CURTISS e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ SAVE[-1] := 1; g̲o̲t̲o̲ RETURN e̲n̲d̲;

            RESET
         e̲n̲d̲ e̲l̲s̲e̲ CONVERGENCE:

         i̲f̲ ERROR > TOL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ FAILS := FAILS + 1;
            i̲f̲ H > 1·1 × HMIN t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ i̲f̲ FAILS > 2 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ ADAMS t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ ADAMS := f̲a̲l̲s̲e̲; METHOD e̲n̲d̲;
                  KOLD := 0; RESET; g̲o̲t̲o̲ NEW START
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ CALCULATE STEP AND ORDER;
                  i̲f̲ KNEW ≠ K t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ K := KNEW; ORDER e̲n̲d̲;
                  CH := CH × CHNEW; RESET
               e̲n̲d̲
            e̲n̲d̲ e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ i̲f̲ ADAMS t̲h̲e̲n̲ TRY CURTISS:
               b̲e̲g̲i̲n̲ ADAMS := f̲a̲l̲s̲e̲; METHOD
               e̲n̲d̲ e̲l̲s̲e̲
               i̲f̲ K = 1 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ VIOLATE EPS CRITERION;
                  C := EPS × SQRT(ERROR/TOL);
                  i̲f̲ C > SAVE[-3] t̲h̲e̲n̲ SAVE[-3] := C;
                  SAVE[-2] := SAVE[-2] + 1;
                  SAME := 4; g̲o̲t̲o̲ ERROR TEST OK
               e̲n̲d̲;
               K := KOLD := 1; RESET; ORDER; SAME := 2
            e̲n̲d̲
         e̲n̲d̲ e̲l̲s̲e̲ ERROR TEST OK:
         b̲e̲g̲i̲n̲
            FAILS := 0;
            f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ C := DELTA[I];
                  f̲o̲r̲ L := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
                       Y[L × M + I] := Y[L × M + I] + A[L] × C;
                  i̲f̲ ABS(Y[I]) > YMAX[I] t̲h̲e̲n̲
                    YMAX[I] :=  ABS(Y[I])
               e̲n̲d̲;

            SAME := SAME-1;
            i̲f̲ SAME = 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                    LAST DELTA[I] := DELTA[I]
            e̲n̲d̲ e̲l̲s̲e̲
            i̲f̲ SAME = 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ CALCULATE STEP AND ORDER;
               i̲f̲ CHNEW > 1·1 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ DECOMPOSED := f̲a̲l̲s̲e̲;
                  i̲f̲ K ≠ KNEW t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ i̲f̲ KNEW > K t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1
                          u̲n̲t̲i̲l̲ N d̲o̲ Y[KNEW × M + I]
                             := DELTA[I] × A[K]/KNEW
                     e̲n̲d̲;
                     K := KNEW; ORDER
                  e̲n̲d̲;
                  SAME := K + 1;
                  i̲f̲ CHNEW × H > HMAX
                    t̲h̲e̲n̲ CHNEW := HMAX/H;
                  H := H × CHNEW; C := 1;
                  f̲o̲r̲ J := M s̲t̲e̲p̲ M u̲n̲t̲i̲l̲ K × M d̲o̲
                     b̲e̲g̲i̲n̲ C := C × CHNEW;
                        f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲
                          J + N d̲o̲ Y[I] := Y[I] × C
                     e̲n̲d̲
               e̲n̲d̲
               e̲l̲s̲e̲ SAME := 10
            e̲n̲d̲;
            i̲f̲ X ≠ XEND t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ XOLD := X; HOLD := H; KOLD := K; CH := 1;
               f̲o̲r̲ I := K × M + N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
                    SAVE[I] := Y[I];
               OUT(H, K)
            e̲n̲d̲
         e̲n̲d̲ CORRECTION AND ESTIMATION LOCAL ERROR;
      e̲n̲d̲ STEP;

   RETURN: SAVE[0] := i̲f̲ ADAMS t̲h̲e̲n̲ 0 e̲l̲s̲e̲ 1;
   MULTISTEP := SAVE[-1] = 0 ∧ SAVE[-2] = 0
e̲n̲d̲ MULTISTEP;
c̲o̲m̲m̲e̲n̲t̲ ================== 33180 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFSYS(X, XE, N, Y, DERIVATIVE, AETA, RETA, S, H0, OUTPUT);
   v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N;
     r̲e̲a̲l̲ X, XE, AETA, RETA, H0;
     a̲r̲r̲a̲y̲ Y, S;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, OUTPUT;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ A, B, B1, C, G, H, U, V, TA, FC; i̲n̲t̲e̲g̲e̲r̲ I, J, K, KK, JJ, L, M, R, SR;
   a̲r̲r̲a̲y̲ YA, YL, YM, DY, DZ[1:N], DT[1:N, 0:6], D[0:6], YG, YH[0:7, 1:N];
   B̲o̲o̲l̲e̲a̲n̲ KONV, B0, BH, LAST;
   LAST := f̲a̲l̲s̲e̲; H := H0;
   NEXT: i̲f̲ H × 1·1 ≥ XE-X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; H0 := H; H := XE-X + @-13 e̲n̲d̲;
   DERIVATIVE(X, Y, DZ); BH := f̲a̲l̲s̲e̲;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ YA[I] := Y[I];
   ANF: A := H + X; FC := 1·5; B0 := f̲a̲l̲s̲e̲; M := 1; R := 2; SR := 3; JJ := -1;
   f̲o̲r̲ J := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 9 d̲o̲
      b̲e̲g̲i̲n̲ i̲f̲ B0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ D[1] := 16/9; D[3] := 64/9; D[5] := 256/9 e̲n̲d̲
         e̲l̲s̲e̲ b̲e̲g̲i̲n̲ D[1] := 9/4; D[3] := 9; D[5] := 36 e̲n̲d̲;
         KONV := t̲r̲u̲e̲;
         i̲f̲ J > 6 t̲h̲e̲n̲ b̲e̲g̲i̲n̲ L := 6; D[6] := 64; FC := ·6 × FC e̲n̲d̲
         e̲l̲s̲e̲ b̲e̲g̲i̲n̲ L := J; D[L] := M × M e̲n̲d̲;
         M := M × 2; G := H/M; B := G × 2;
         i̲f̲ BH ∧ J < 8 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ YM[I] := YH[J, I]; YL[I] := YG[J, I] e̲n̲d̲
         e̲n̲d̲
         e̲l̲s̲e̲
         b̲e̲g̲i̲n̲
            KK := (M-2)/2; M := M-1;
            f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ YL[I] := YA[I]; YM[I] := YA[I] + G × DZ[I] e̲n̲d̲;
            f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
               b̲e̲g̲i̲n̲ DERIVATIVE(X + K × G, YM, DY);
                  f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                     b̲e̲g̲i̲n̲ U := YL[I] + B × DY[I]; YL[I] := YM[I]; YM[I] := U;
                        U := ABS(U); i̲f̲ U > S[I] t̲h̲e̲n̲ S[I] := U
                     e̲n̲d̲;
                  i̲f̲ K = KK ∧ K ≠ 2 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ JJ := JJ + 1; f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                        b̲e̲g̲i̲n̲ YH[JJ, I] := YM[I]; YG[JJ, I] := YL[I] e̲n̲d̲
                  e̲n̲d̲
               e̲n̲d̲
         e̲n̲d̲;
         DERIVATIVE(A, YM, DY);
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ V := DT[I, 0]; TA := C := DT[I, 0] := (YM[I] + YL[I] + G × DY[I])/2;
               f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L d̲o̲
                  b̲e̲g̲i̲n̲ B1 := D[K] × V; B := B1-C; U := V;
                     i̲f̲ B ≠ 0 t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ B := (C-V)/B; U := C × B; C := B1 × B e̲n̲d̲;
                     V := DT[I, K]; DT[I, K] := U; TA := U + TA
                  e̲n̲d̲;
               i̲f̲ ABS(Y[I]-TA) > RETA × S[I] + AETA t̲h̲e̲n̲ KONV := f̲a̲l̲s̲e̲;
               Y[I] := TA
            e̲n̲d̲;
         i̲f̲ KONV t̲h̲e̲n̲ g̲o̲t̲o̲ END;
         D[2] := 4; D[4] := 16; B0 := ¬B0; M := R; R := SR; SR := M × 2
      e̲n̲d̲;
   BH := ¬BH; LAST := f̲a̲l̲s̲e̲; H := H/2; g̲o̲t̲o̲ ANF;
   END: H := FC × H; X := A; OUTPUT; i̲f̲ ¬LAST t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT;
e̲n̲d̲ DIFFSYS;
c̲o̲m̲m̲e̲n̲t̲ ================== 33061 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ARK (T, TE, M0, M, U, DERIVATIVE, DATA, OUT);
   i̲n̲t̲e̲g̲e̲r̲ M0, M;
     r̲e̲a̲l̲ T, TE;
     a̲r̲r̲a̲y̲ U, DATA;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, OUT;

b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ P, N, Q;
   o̲w̲n̲ r̲e̲a̲l̲ EC0, EC1, EC2, TAU0, TAU1, TAU2, TAUS, T2;
   r̲e̲a̲l̲ THETANM1, TAU, BETAN, QINV, ETA;
   a̲r̲r̲a̲y̲ MU, LAMBDA[1:DATA[1]], THETHA[0:DATA[1]], RO, R[M0:M];
   B̲o̲o̲l̲e̲a̲n̲ START, STEP1, LAST;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X);               c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULVEC(L, U, SHIFT, A, B, X);     c̲o̲d̲e̲ 31020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, SHIFT, A, B);        c̲o̲d̲e̲ 31030;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X);     c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECSOL(A, N, AUX, B);             c̲o̲d̲e̲ 34301;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INITIALIZE;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K, L, N1; r̲e̲a̲l̲ S, THETA0;
      a̲r̲r̲a̲y̲ ALFA[1:8, 1:DATA[1] + 1], TH[1:8], AUX[1:3];

      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ LABDA(I, J); v̲a̲l̲u̲e̲ I, J; i̲n̲t̲e̲g̲e̲r̲ I, J;
         LABDA := i̲f̲ P < 3 t̲h̲e̲n̲ (i̲f̲ J = I-1 t̲h̲e̲n̲ MUI(I) e̲l̲s̲e̲ 0)
         e̲l̲s̲e̲ i̲f̲ P = 3 t̲h̲e̲n̲ (i̲f̲ I = N t̲h̲e̲n̲ (i̲f̲ J = 0
           t̲h̲e̲n̲ ·25 e̲l̲s̲e̲ i̲f̲ J = N - 1 t̲h̲e̲n̲ ·75
         e̲l̲s̲e̲ 0) e̲l̲s̲e̲ i̲f̲ J = 0 t̲h̲e̲n̲ (i̲f̲ I = 1
           t̲h̲e̲n̲ MUI(1) e̲l̲s̲e̲ ·25) e̲l̲s̲e̲ i̲f̲ J = I - 1
           t̲h̲e̲n̲ LAMBDA[I] e̲l̲s̲e̲ 0) e̲l̲s̲e̲ 0;

      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MUI(I); v̲a̲l̲u̲e̲ I; i̲n̲t̲e̲g̲e̲r̲ I;
         MUI := i̲f̲ I = N t̲h̲e̲n̲ 1 e̲l̲s̲e̲
           i̲f̲ I < 1 ∨ I > N t̲h̲e̲n̲ 0 e̲l̲s̲e̲
           i̲f̲ P < 3 t̲h̲e̲n̲ LAMBDA[I] e̲l̲s̲e̲
           i̲f̲ P = 3 t̲h̲e̲n̲ LAMBDA[I] + ·25 e̲l̲s̲e̲ 0;

      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SUM(I, A, B, X);
         v̲a̲l̲u̲e̲ B; i̲n̲t̲e̲g̲e̲r̲ I, A, B; r̲e̲a̲l̲ X;
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S; S := 0;
         f̲o̲r̲ I := A s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ B d̲o̲ S := S + X;
         SUM := S
      e̲n̲d̲ SUM;
      N := DATA[1]; P := DATA[2]; EC1 := EC2 := 0;
      BETAN := DATA[3];
      THETANM1 := i̲f̲ P = 3 t̲h̲e̲n̲ ·75 e̲l̲s̲e̲ 1;
      THETA0 := 1 - THETANM1; S := 1;
      f̲o̲r̲ J := N - 1 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
         b̲e̲g̲i̲n̲ S := - S × THETA0 + DATA[N + 10 - J];
            MU[J] := DATA[N + 11 - J] / S;
            LAMBDA[J] := MU[J] - THETA0
         e̲n̲d̲;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 8 d̲o̲
           f̲o̲r̲ J := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              ALFA[I, J + 1] := i̲f̲ I = 1 t̲h̲e̲n̲ 1 e̲l̲s̲e̲
              i̲f̲ J = 0 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ i̲f̲ I = 2 ∨ I = 4 ∨ I = 8 t̲h̲e̲n̲
              MUI(J) ⭡ ENTIER((I + 2) / 3) e̲l̲s̲e̲
              i̲f̲ (I = 3 ∨ I = 6) ∧ J > 1 t̲h̲e̲n̲ SUM(L, 1, J-1,
              LABDA(J, L) × MUI(L) ⭡ ENTIER(I / 3)) e̲l̲s̲e̲
              i̲f̲ I = 5 ∧ J > 2 t̲h̲e̲n̲ SUM(L, 2, J - 1, LABDA(J, L) ×
              SUM(K, 1, L - 1, LABDA(L, K) × MUI(K))) e̲l̲s̲e̲
              i̲f̲ I = 7 ∧ J > 1 t̲h̲e̲n̲ SUM(L, 1, J - 1, LABDA(J, L) ×
              MUI(L)) × MUI(J) e̲l̲s̲e̲ 0;
      N1 := i̲f̲ N < 4 t̲h̲e̲n̲ N + 1 e̲l̲s̲e̲ i̲f̲ N < 7 t̲h̲e̲n̲ 4
      e̲l̲s̲e̲ 8;
      I := 1;
      f̲o̲r̲ S := 1, ·5, 1 / 6, 1 / 3, 1 / 24, 1 / 12, ·125, ·25 d̲o̲
         b̲e̲g̲i̲n̲ TH[I] := S; I := I + 1 e̲n̲d̲;
      i̲f̲ P = 3 ∧ N < 7 t̲h̲e̲n̲ TH[1] := TH[2] := 0;
      AUX[2] := @-14; DECSOL(ALFA, N1, AUX, TH);
      INIVEC(0, N, THETHA, 0);
      DUPVEC(0, N1 - 1, 1, THETHA, TH);
      i̲f̲ ¬(P = 3 ∧ N < 7) t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ THETHA[0] := THETHA[0] - THETA0;
         THETHA[N - 1] := THETHA[N - 1] - THETANM1; Q := P + 1
      e̲n̲d̲ e̲l̲s̲e̲ Q := 3;
      QINV := 1 / Q;
      START := DATA[8] = 0; DATA[10] := 0; LAST := f̲a̲l̲s̲e̲;
      DUPVEC(M0, M, 0, R, U); DERIVATIVE(T, R)
   e̲n̲d̲ INITIALIZE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCAL ERROR CONSTRUCTION(I); v̲a̲l̲u̲e̲ I; i̲n̲t̲e̲g̲e̲r̲ I;
   b̲e̲g̲i̲n̲ i̲f̲ THETHA[I] ≠ 0 t̲h̲e̲n̲
        ELMVEC(M0, M, 0, RO, R, THETHA[I]);
      i̲f̲ I = N t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DATA[9] := SQRT(VECVEC(M0, M, 0, RO, RO)) × TAU;
         EC0 := EC1; EC1 := EC2; EC2 := DATA[9] / TAU ⭡ Q
      e̲n̲d̲
   e̲n̲d̲ LEC;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ TAUACC, TAUSTAB, AA, BB, CC, EC;
      ETA := SQRT(VECVEC(M0, M, 0, U, U)) × DATA[7] + DATA[6];
      i̲f̲ ETA > 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ START t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ DATA[8] = 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ TAUACC := DATA[5];
               STEP1 := t̲r̲u̲e̲
            e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ STEP1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ TAUACC := (ETA / EC2) ⭡ QINV;
               i̲f̲ TAUACC > 10 × TAU2 t̲h̲e̲n̲
                 TAUACC := 10 × TAU2 e̲l̲s̲e̲ STEP1 := f̲a̲l̲s̲e̲
            e̲n̲d̲ e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ BB := (EC2 - EC1) / TAU1; CC := - BB × T2 + EC2;
               EC := BB × T + CC;
               TAUACC := i̲f̲ EC < 0 t̲h̲e̲n̲ TAU2 e̲l̲s̲e̲
                 (ETA / EC) ⭡ QINV;
               START := f̲a̲l̲s̲e̲
            e̲n̲d̲
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ AA := ((EC0 - EC1) / TAU0 + (EC2 - EC1) / TAU1)
              / (TAU1 + TAU0);
            BB := (EC2 - EC1) / TAU1 - (2 × T2 - TAU1) × AA;
            CC := - (AA × T2 + BB) × T2 + EC2;
            EC := (AA × T + BB) × T + CC;
            TAUACC := i̲f̲ EC < 0 t̲h̲e̲n̲
              TAUS e̲l̲s̲e̲ (ETA / EC) ⭡ QINV;
            i̲f̲ TAUACC > 2 × TAUS t̲h̲e̲n̲ TAUACC := 2 × TAUS;
            i̲f̲ TAUACC < TAUS / 2 t̲h̲e̲n̲ TAUACC := TAUS / 2
         e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲ TAUACC := DATA[5];
      i̲f̲ TAUACC < DATA[5] t̲h̲e̲n̲ TAUACC := DATA[5];
      TAUSTAB := BETAN / DATA[4]; i̲f̲ TAUSTAB < DATA[5] t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DATA[10] := 1; g̲o̲t̲o̲ ENDARK e̲n̲d̲;
      TAU := i̲f̲ TAUACC > TAUSTAB t̲h̲e̲n̲ TAUSTAB e̲l̲s̲e̲ TAUACC;
      TAUS := TAU; i̲f̲ TAU ≥ TE - T t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU := TE - T; LAST := t̲r̲u̲e̲ e̲n̲d̲;
      TAU0 := TAU1; TAU1 := TAU2; TAU2 := TAU
   e̲n̲d̲ STEPSIZE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFERENCE SCHEME;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J;
      r̲e̲a̲l̲ MT, LT;
      MULVEC(M0, M, 0, RO, R, THETHA[0]);
      i̲f̲ P = 3 t̲h̲e̲n̲ ELMVEC(M0, M, 0, U, R, ·25 × TAU);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
         b̲e̲g̲i̲n̲ MT := MU[I] × TAU; LT := LAMBDA[I] × TAU;
            f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
                 R[J] := LT × R[J] + U[J];
            DERIVATIVE(T + MT, R); LOCAL ERROR CONSTRUCTION(I)
         e̲n̲d̲;
      ELMVEC(M0, M, 0, U, R, THETANM1 × TAU);
      DUPVEC(M0, M, 0, R, U); DERIVATIVE(T + TAU, R);
      LOCAL ERROR CONSTRUCTION(N); T2 := T;
      i̲f̲ LAST t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ LAST := f̲a̲l̲s̲e̲; T := TE e̲n̲d̲ e̲l̲s̲e̲ T := T + TAU;
      DATA[8] := DATA[8] + 1
   e̲n̲d̲ DIFSCH;

   INITIALIZE;

   NEXT STEP:
   STEPSIZE; DIFFERENCE SCHEME; OUT;
   i̲f̲ T ≠ TE t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT STEP;

   ENDARK:
e̲n̲d̲ ARK;
c̲o̲m̲m̲e̲n̲t̲ ================== 33070 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EFRK(T, TE, M0, M, U, SIGMA, PHI, DIAMETER, DERIVATIVE, K, STEP, R, L,
  BETA, THIRDORDER, TOL, OUTPUT);
   v̲a̲l̲u̲e̲ R, L;
   i̲n̲t̲e̲g̲e̲r̲ M0, M, K, R, L;
     r̲e̲a̲l̲ T, TE, SIGMA, PHI, DIAMETER, STEP, TOL;
     a̲r̲r̲a̲y̲ U, BETA;
     B̲o̲o̲l̲e̲a̲n̲ THIRDORDER;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, OUTPUT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N;
   r̲e̲a̲l̲ THETA0, THETANM1, H, B, B0, PHI0, PHIL, PI, COSPHI, SINPHI, EPS, BETAR;
   B̲o̲o̲l̲e̲a̲n̲ FIRST, LAST, COMPLEX, CHANGE;
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P[1:L];
   r̲e̲a̲l̲ a̲r̲r̲a̲y̲ MU, LABDA[0:R + L-1], PT[0:R], FAC, BETAC[0:L-1], RL[M0:M],
     A[1:L, 1:L], AUX[0:3];
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORM CONSTANTS;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
      FIRST := f̲a̲l̲s̲e̲;
      FAC[0] := 1;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L-1 d̲o̲ FAC[I] := I × FAC[I-1];
      PT[R] := L × FAC[L-1];
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲
           PT[R-I] := PT[R-I + 1] × (L + I)/I
   e̲n̲d̲ FORM CONSTANTS;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORM BETA;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J; r̲e̲a̲l̲ BB, C, D;
      i̲f̲ FIRST t̲h̲e̲n̲ FORM CONSTANTS;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ C := 1-EXP(-B);
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲ C := BETA[J]-C/B;
         BETA[R + 1] := C/B
      e̲n̲d̲ e̲l̲s̲e̲
      i̲f̲ B > 40 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := R + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R + L d̲o̲
            b̲e̲g̲i̲n̲ C := 0;
               f̲o̲r̲ J := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲
                    C := BETA[J] × PT[J]/(I-J)-C/B;
               BETA[I] := C/B/FAC[L + R-I]/FAC[I-R-1]
            e̲n̲d̲;
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ D := C := EXP(-B); BETAC[L-1] := D/FAC[L-1];
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L-1 d̲o̲
            b̲e̲g̲i̲n̲ C := B × C/I; D := D + C; BETAC[L-1-I] := D/FAC[L-1-I] e̲n̲d̲;
         BB := 1;
         f̲o̲r̲ I := R + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R + L d̲o̲
            b̲e̲g̲i̲n̲ C := 0;
               f̲o̲r̲ J := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ R d̲o̲
                    C := (BETA[J]-(i̲f̲ J < L t̲h̲e̲n̲ BETAC[J] e̲l̲s̲e̲ 0)) ×
                    PT[J]/(I-J)-C/B;
               BETA[I] := C/B/FAC[L + R-I]/FAC[I-R-1] +
                 (i̲f̲ I < L t̲h̲e̲n̲ BB × BETAC[I] e̲l̲s̲e̲ 0);
               BB := BB × B
            e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ FORM BETA;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLUTION OF COMPLEX EQUATIONS;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, C1, C3;
      r̲e̲a̲l̲ C2, E, B1, ZI, COSIPHI, SINIPHI, COSPHIL;
      r̲e̲a̲l̲ a̲r̲r̲a̲y̲ D[1:L];

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENTS OF MATRIX;
      b̲e̲g̲i̲n̲ PHIL := PHI0;
         COSPHI := COS(PHIL); SINPHI := SIN(PHIL);
         COSIPHI := 1; SINIPHI := 0;
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L-1 d̲o̲
            b̲e̲g̲i̲n̲ C1 := R + 1 + I; C2 := 1;
               f̲o̲r̲ J := L-1 s̲t̲e̲p̲ -2 u̲n̲t̲i̲l̲ 1 d̲o̲
                  b̲e̲g̲i̲n̲ A[J, L-I] := C2 × COSIPHI;
                     A[J + 1, L-I] := C2 × SINIPHI;
                     C2 := C1 × C2; C1 := C1-1
                  e̲n̲d̲;
               COSPHIL := COSIPHI × COSPHI-SINIPHI × SINPHI;
               SINIPHI := COSIPHI × SINPHI + SINIPHI × COSPHI;
               COSIPHI := COSPHIL
            e̲n̲d̲;
         AUX[2] := 0; DEC(A, L, AUX, P)
      e̲n̲d̲ EL OF MAT;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ RIGHTHANDSIDE;
      b̲e̲g̲i̲n̲ E := EXP(B × COSPHI);
         B1 := B × SINPHI-(R + 1) × PHIL;
         COSIPHI := E × COS(B1); SINIPHI := E × SIN(B1);
         B1 := 1/B; ZI := B1⭡R;
         f̲o̲r̲ J := L s̲t̲e̲p̲ -2 u̲n̲t̲i̲l̲ 2 d̲o̲
            b̲e̲g̲i̲n̲ D[J] := ZI × SINIPHI;
               D[J-1] := ZI × COSIPHI;
               COSPHIL := COSIPHI × COSPHI-SINIPHI × SINPHI;
               SINIPHI := COSIPHI × SINPHI + SINIPHI × COSPHI;
               COSIPHI := COSPHIL;
               ZI := ZI × B
            e̲n̲d̲;
         COSIPHI := ZI := 1; SINIPHI := 0;
         f̲o̲r̲ I := R s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 0 d̲o̲
            b̲e̲g̲i̲n̲ C1 := I; C2 := BETA[I];
               C3 := i̲f̲ 2 × I > L-2 t̲h̲e̲n̲ 2 e̲l̲s̲e̲ L-2 × I;
               COSPHIL := COSIPHI × COSPHI-SINIPHI × SINPHI;
               SINIPHI := COSIPHI × SINPHI + SINIPHI × COSPHI;
               COSIPHI := COSPHIL;
               f̲o̲r̲ J := L s̲t̲e̲p̲ -2 u̲n̲t̲i̲l̲ C3 d̲o̲
                  b̲e̲g̲i̲n̲ D[J] := D[J] + ZI × C2 × SINIPHI;
                     D[J-1] := D[J-1]-ZI × C2 × COSIPHI;
                     C2 := C2 × C1; C1 := C1-1
                  e̲n̲d̲;
               ZI := ZI × B1
            e̲n̲d̲
      e̲n̲d̲ RIGHT HAND SIDE;

      i̲f̲ PHI0 ≠ PHIL t̲h̲e̲n̲ ELEMENTS OF MATRIX;
      RIGHTHANDSIDE;
      SOL(A, L, P, D);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L d̲o̲ BETA[R + I] := D[L + 1-I] × B1
   e̲n̲d̲ SOLOFCOMEQ;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COEFFICIENT;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, K; r̲e̲a̲l̲ C;
      B0 := B; PHI0 := PHI;
      i̲f̲ B ≥ 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ COMPLEX t̲h̲e̲n̲ SOLUTION OF COMPLEX EQUATIONS
         e̲l̲s̲e̲ FORM BETA
      e̲n̲d̲;
      LABDA[0] := MU[0] := 0;
      i̲f̲ THIRDORDER t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ THETA0 := ·25; THETANM1 := ·75;
         i̲f̲ B < 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ C := MU[N-1] := 2/3; LABDA[N-1] := 5/12;
            f̲o̲r̲ J := N-2 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
               b̲e̲g̲i̲n̲ C := MU[J] := C/(C-·25)/(N-J + 1);
                  LABDA[J] := C-·25
               e̲n̲d̲
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ C := MU[N-1] := BETA[2] × 4/3; LABDA[N-1] := C-·25;
            f̲o̲r̲ J := N-2 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
               b̲e̲g̲i̲n̲ C := MU[J] := C/(C-·25) × BETA[N-J + 1]/BETA[N-J]/
                    (i̲f̲ J < L t̲h̲e̲n̲ B e̲l̲s̲e̲ 1);
                  LABDA[J] := C-·25
               e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ THETA0 := 0; THETANM1 := 1;
         i̲f̲ B < 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ J := N-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
                 MU[J] := LABDA[J] := 1/(N-J + 1)
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ LABDA[N-1] := MU[N-1] := BETA[2];
            f̲o̲r̲ J := N-2 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
                 MU[J] := LABDA[J] := BETA[N-J + 1]/BETA[N-J]/
                 (i̲f̲ J < L t̲h̲e̲n̲ B e̲l̲s̲e̲ 1)
         e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ COEFFICIENT;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ D, HSTAB, HSTABINT;
      H := STEP;
      D := ABS(SIGMA × SIN(PHI));
      COMPLEX := L÷2 × 2 = L ∧ 2 × D > DIAMETER;
      i̲f̲ DIAMETER > 0 t̲h̲e̲n̲
        HSTAB := (SIGMA⭡2/(DIAMETER × (DIAMETER × ·25 + D)))⭡(L × ·5/R)/
        BETAR/SIGMA
      e̲l̲s̲e̲ HSTAB := H;
      D := i̲f̲ THIRDORDER t̲h̲e̲n̲ (2 × TOL/EPS/BETA[R])⭡(1/(N-1)) ×
        4⭡((L-1)/(N-1)) e̲l̲s̲e̲ (TOL/EPS)⭡(1/R)/BETAR;
      HSTABINT := ABS(D/SIGMA);
      i̲f̲ H > HSTAB t̲h̲e̲n̲ H := HSTAB;
      i̲f̲ H > HSTABINT t̲h̲e̲n̲ H := HSTABINT;
      i̲f̲ T + H > TE × (1-K × EPS) t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; H := TE-T e̲n̲d̲;
      B := H × SIGMA; D := DIAMETER × ·1 × H; D := D × D;
      i̲f̲ H < T × EPS  t̲h̲e̲n̲ g̲o̲t̲o̲ ENDOFEFRK;
      CHANGE := B0 = -1 ∨ ((B-B0) × (B-B0) + B × B0 × (PHI-PHI0) × (PHI-PHI0) > D)
   e̲n̲d̲ STEPSIZE;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFERENCESCHEME ;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J; r̲e̲a̲l̲ MT, LT, THT;
      I := -1;
      NEXTTERM:
      I := I + 1; MT := MU[I] × H; LT := LABDA[I] × H;
      f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ RL[J] := U[J] + LT × RL[J];
      DERIVATIVE(T + MT, RL);
      i̲f̲ I = 0 ∨ I = N-1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ THT := i̲f̲ I = 0 t̲h̲e̲n̲ THETA0 × H e̲l̲s̲e̲ THETANM1 × H;
         ELMVEC(M0, M, 0, U, RL, THT)
      e̲n̲d̲;
      i̲f̲ I < N-1 t̲h̲e̲n̲ g̲o̲t̲o̲ NEXTTERM;
      T := T + H
   e̲n̲d̲ DIFFERENCE SCHEME;

   N := R + L; FIRST := t̲r̲u̲e̲; B0 := -1; BETAR := BETA[R]⭡(1/R);
   LAST := f̲a̲l̲s̲e̲; EPS := 2⭡(-48); PI := PHI0 := PHIL := 4 × ARCTAN(1);
   NEXTLEVEL:
   STEPSIZE;
   i̲f̲ CHANGE t̲h̲e̲n̲ COEFFICIENT;
   K := K + 1;
   DIFFERENCE SCHEME;
   OUTPUT;
   i̲f̲ ¬LAST t̲h̲e̲n̲ g̲o̲t̲o̲ NEXTLEVEL;
   ENDOFEFRK:
e̲n̲d̲ EXPONENTIALLY FITTED RUNGE KUTTA;
c̲o̲m̲m̲e̲n̲t̲ ================== 33160 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EFSIRK(X, XE, M, Y, DELTA, DERIVATIVE, JACOBIAN, J,
  N, AETA, RETA, HMIN, HMAX, LINEAR, OUTPUT);
   v̲a̲l̲u̲e̲ M; i̲n̲t̲e̲g̲e̲r̲ M, N;
     r̲e̲a̲l̲ X, XE, DELTA, AETA, RETA, HMIN, HMAX;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, JACOBIAN, OUTPUT;
     B̲o̲o̲l̲e̲a̲n̲ LINEAR;
     a̲r̲r̲a̲y̲ Y, J;

b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K, L;
   r̲e̲a̲l̲ STEP, H, MU0, MU1, MU2, THETA0, THETA1, NU1, NU2,
     NU3, YK, FK, C1, C2, D;
   a̲r̲r̲a̲y̲ F, K0, LABDA[1 : M], J1[1 : M, 1 : M], AUX[1 : 7];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ RI, CI[1 : M];
   B̲o̲o̲l̲e̲a̲n̲ LIN;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ GSSELM(A, N, AUX, RI, CI); c̲o̲d̲e̲ 34231;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLELM(A, N, RI, CI, B); c̲o̲d̲e̲ 34061;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ DISCR, ETA, S;
      i̲f̲ LINEAR t̲h̲e̲n̲ S := H := HMAX e̲l̲s̲e̲
      i̲f̲ N = 1 ∨ HMIN = HMAX t̲h̲e̲n̲ S := H := HMIN e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ ETA := AETA + RETA × SQRT(VECVEC(1, M, 0, Y, Y));
         C1 := NU3 × STEP; f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
              LABDA[K] := LABDA[K] + C1 × F[K] - Y[K];
         DISCR := SQRT(VECVEC(1, M, 0, LABDA, LABDA));
         S := H := (ETA / (0·75 × (ETA + DISCR)) + 0·33) × H;
         i̲f̲ H < HMIN t̲h̲e̲n̲ S := H := HMIN e̲l̲s̲e̲
         i̲f̲ H > HMAX t̲h̲e̲n̲ S := H := HMAX
      e̲n̲d̲;
      i̲f̲ X + S > XE t̲h̲e̲n̲ S := XE - X;
      LIN := STEP = S ∧ LINEAR; STEPSIZE := S
   e̲n̲d̲ STEPSIZE;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COEFFICIENT;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Z1, E, ALPHA1, A, B;
      o̲w̲n̲ r̲e̲a̲l̲ Z2;
      Z1 := STEP × DELTA; i̲f̲ N = 1 t̲h̲e̲n̲ Z2 := Z1 + Z1;
      i̲f̲ ABS(Z2 - Z1) > @-6 × ABS(Z1) ∨ Z2 > - 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ A := Z1 × Z1 + 12; B := 6 × Z1;
         i̲f̲ ABS(Z1) < 0·1 t̲h̲e̲n̲
           ALPHA1 := (Z1 × Z1 / 140 - 1) × Z1 / 30 e̲l̲s̲e̲
         i̲f̲ Z1 < - @14 t̲h̲e̲n̲ ALPHA1 := 1 / 3 e̲l̲s̲e̲
         i̲f̲ Z1 < - 33 t̲h̲e̲n̲
           ALPHA1 := (A + B) / (3 × Z1 × (2 + Z1)) e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ E := i̲f̲ Z1 < 230 t̲h̲e̲n̲ EXP(Z1) e̲l̲s̲e̲ @100;
            ALPHA1 := ((A - B) × E - A - B) /
              (((2 - Z1) × E - 2 - Z1) × 3 × Z1)
         e̲n̲d̲;
         MU2 := (1 / 3 + ALPHA1) × 0·25;
         MU1 := - (1 + ALPHA1) × 0·5;
         MU0 := (6 × MU1 + 2) / 9; THETA0 := 0·25;
         THETA1 := 0·75; A := 3 × ALPHA1;
         NU3 := (1 + A) / (5 - A) × 0·5; A := NU3 + NU3;
         NU1 := 0·5 - A; NU2 := (1 + A) × 0·75;
         Z2 := Z1
      e̲n̲d̲
   e̲n̲d̲ COEFFICIENT;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFERENCE SCHEME;
   b̲e̲g̲i̲n̲ DERIVATIVE(F); STEP := STEPSIZE;
      i̲f̲ ¬LINEAR ∨ N = 1 t̲h̲e̲n̲ JACOBIAN(J, Y);
      i̲f̲ ¬LIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ COEFFICIENT;
         C1 := STEP × MU1; D := STEP × STEP × MU2;
         f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
                    J1[K, L] := D × MATMAT(1, M, K, L, J, J) +
                    C1 × J[K, L];
               J1[K, K] := J1[K, K] + 1
            e̲n̲d̲;
         GSSELM(J1, M, AUX, RI, CI)
      e̲n̲d̲;
      C1 := STEP × STEP × MU0; D := STEP × 2 / 3;
      f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
         b̲e̲g̲i̲n̲ K0[K] := FK := F[K];
            LABDA[K] := D × FK + C1 × MATVEC(1, M, K, J, F)
         e̲n̲d̲;
      SOLELM(J1, M, RI, CI, LABDA);
      f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ F[K] := Y[K] + LABDA[K];
      DERIVATIVE(F);
      C1 := THETA0 × STEP; C2 := THETA1 × STEP; D := NU1 × STEP;
      f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
         b̲e̲g̲i̲n̲ YK := Y[K]; FK := F[K];
            LABDA[K] := YK + D × FK + NU2 × LABDA[K];
            Y[K] := F[K] := YK + C1 × K0[K] + C2 × FK
         e̲n̲d̲
   e̲n̲d̲ DIFFERENCE SCHEME;

   AUX[2] := @-14; AUX[4] := 8;
   f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ F[K] := Y[K];
   N := 0; OUTPUT; STEP := 0;
   NEXT STEP: N := N + 1;
   DIFFERENCE SCHEME; X := X + STEP; OUTPUT;
   i̲f̲ X < XE t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT STEP
e̲n̲d̲ EFSIRK;
c̲o̲m̲m̲e̲n̲t̲ ================== 33120 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EFERK(X, XE, M, Y, SIGMA, PHI, DERIVATIVE, J, JACOBIAN,
  K, L, AUT, AETA, RETA, HMIN, HMAX, LINEAR, OUTPUT);
   v̲a̲l̲u̲e̲ L; i̲n̲t̲e̲g̲e̲r̲ M, K, L;
     r̲e̲a̲l̲ X, XE, SIGMA, PHI, AETA, RETA, HMIN, HMAX; a̲r̲r̲a̲y̲ Y, J;
     B̲o̲o̲l̲e̲a̲n̲ AUT, LINEAR; p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, JACOBIAN, OUTPUT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M1, I;
   r̲e̲a̲l̲ H, B, B0, PHI0, COSPHI, SINPHI, ETA, DISCR, FAC, PI;
   B̲o̲o̲l̲e̲a̲n̲ CHANGE, LAST;
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P[1:L];
   r̲e̲a̲l̲ a̲r̲r̲a̲y̲ BETA, BETHA[0:L], BETAC[0:L + 3], K0, D, D1, D2[1:M],
     A[1:L, 1:L], AUX[1:3];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SUM(I, L, U, T); v̲a̲l̲u̲e̲ L, U; i̲n̲t̲e̲g̲e̲r̲ I, L, U;
        r̲e̲a̲l̲ T;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S; S := 0;
      f̲o̲r̲ I := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲ S := S + T;
      SUM := S
   e̲n̲d̲;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORMBETA;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ BETHA[1] := (·5-(1-(1-EXP(-B))/B)/B)/B;
         BETA[1] := (1/6-BETHA[1])/B
      e̲n̲d̲ e̲l̲s̲e̲
      i̲f̲ L = 2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EMIN1; E := EXP(-B); EMIN1 := E-1;
         BETHA[1] := (1-(3 + E + 4 × EMIN1/B)/B)/B;
         BETHA[2] := (·5-(2 + E + 3 × EMIN1/B)/B)/B/B;
         BETA[2] := (1/6-BETHA[1])/B/B;
         BETA[1] := (1/3-(1·5-(4 + E + 5 × EMIN1/B)/B)/B)/B
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ B0, B1, B2, A0, A1, A2, A3, C, D;
         BETAC[L-1] := C := D := EXP(-B)/FAC;
         f̲o̲r̲ I := L-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲
            b̲e̲g̲i̲n̲ C := I × B × C/(L-I); BETAC[I-1] := D := D × I + C e̲n̲d̲;
         B2 := ·5-BETAC[2];
         B1 := (1-BETAC[1]) × (L + 1)/B;
         B0 := (1-BETAC[0]) × (L + 2) × (L + 1) × ·5/B/B;
         A3 := 1/6-BETAC[3];
         A2 := B2 × (L + 1)/B;
         A1 := B1 × (L + 2) × ·5/B;
         A0 := B0 × (L + 3)/3/B;
         D := L/B;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L d̲o̲
            b̲e̲g̲i̲n̲ BETA[I] := (A3/I-A2/(I + 1) + A1/(I + 2)-A0/(I + 3)) × D + BETAC[I + 3];
               BETHA[I] := (B2/I-B1/(I + 1) + B0/(I + 2)) × D + BETAC[I + 2];
               D := D × (L-I)/I/B;
            e̲n̲d̲
      e̲n̲d̲ FORMBETA;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOLUTIONOFCOMPLEXEQUATIONS;
      i̲f̲ L = 2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ COS2PHI, COSA, SINA, E, ZI;
         PHI0 := PHI; COSPHI := COS(PHI0); SINPHI := SIN(PHI0);
         E := EXP(B × COSPHI); ZI := B × SINPHI-3 × PHI0;
         SINA := (i̲f̲ ABS(SINPHI) < @-6 t̲h̲e̲n̲ -E × (B + 3)
         e̲l̲s̲e̲ E × SIN(ZI)/SINPHI);
         COS2PHI := 2 × COSPHI × COSPHI-1;
         BETHA[2] := (·5 + (2 × COSPHI + (1 + 2 × COS2PHI + SINA)/B)/B)/B/B;
         SINA := (i̲f̲ ABS(SINPHI) < @-6 t̲h̲e̲n̲ E × (B + 4)
         e̲l̲s̲e̲ SINA × COSPHI-E × COS(ZI));
         BETHA[1] := -(COSPHI + (1 + 2 × COS2PHI + (4 × COSPHI × COS2PHI + SINA)
           /B)/B)/B;
         BETA[1] := BETHA[2] + 2 × COSPHI × (BETHA[1]-1/6)/B;
         BETA[2] := (1/6-BETHA[1])/B/B
      e̲n̲d̲ e̲l̲s̲e̲

      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J, C1;
         r̲e̲a̲l̲ C2, E, ZI, COSIPHI, SINIPHI, COSPHIL;
         r̲e̲a̲l̲ a̲r̲r̲a̲y̲ D[1:L];
         p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENTS OF MATRIX;
         b̲e̲g̲i̲n̲ PHI0 := PHI;
            COSPHI := COS(PHI0); SINPHI := SIN(PHI0);
            COSIPHI := 1; SINIPHI := 0;
            f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L-1 d̲o̲
               b̲e̲g̲i̲n̲ C1 := 4 + I; C2 := 1;
                  f̲o̲r̲ J := L-1 s̲t̲e̲p̲ -2 u̲n̲t̲i̲l̲ 1 d̲o̲
                     b̲e̲g̲i̲n̲  A[J, L-I] := C2 × COSIPHI;
                        A[J + 1, L-I] := C2 × SINIPHI;
                        C2 := C2 × C1; C1 := C1-1
                     e̲n̲d̲;
                  COSPHIL := COSIPHI × COSPHI-SINIPHI × SINPHI;
                  SINIPHI := COSIPHI × SINPHI + SINIPHI × COSPHI;
                  COSIPHI := COSPHIL
               e̲n̲d̲;
            AUX[2] := 0; DEC(A, L, AUX, P)
         e̲n̲d̲ EL OF MAT;
         p̲r̲o̲c̲e̲d̲u̲r̲e̲ RIGHT HAND SIDE;
         b̲e̲g̲i̲n̲ E := EXP(B × COSPHI);
            ZI := B × SINPHI-4 × PHI0;
            COSIPHI := E × COS(ZI); SINIPHI := E × SIN(ZI);
            ZI := 1/B/B/B;
            f̲o̲r̲ J := L s̲t̲e̲p̲ -2 u̲n̲t̲i̲l̲ 2 d̲o̲
               b̲e̲g̲i̲n̲ D[J] := ZI × SINIPHI;
                  D[J-1] := ZI × COSIPHI;
                  COSPHIL := COSIPHI × COSPHI-SINIPHI × SINPHI;
                  SINIPHI := COSIPHI × SINPHI + SINIPHI × COSPHI;
                  COSIPHI := COSPHIL; ZI := ZI × B
               e̲n̲d̲;
            SINIPHI := 2 × SINPHI × COSPHI;
            COSIPHI := 2 × COSPHI × COSPHI-1;
            COSPHIL := COSPHI × (2 × COSIPHI-1);
            D[L] := D[L] + SINPHI × (1/6 + (COSPHI + (1 + 2 × COSIPHI × (1 + 2 × COSPHI/B))
              /B)/B);
            D[L-1] := D[L-1]-COSPHI/6-(·5 × COSIPHI + (COSPHIL + (2 × COSIPHI ×
              COSIPHI-1)/B)/B)/B;
            D[L-2] := D[L-2] + SINPHI × (·5 + (2 × COSPHI + (2 × COSIPHI + 1)/B)/B);
            D[L-3] := D[L-3]-·5 × COSPHI-(COSIPHI + COSPHIL/B)/B;
            i̲f̲ L < 5 t̲h̲e̲n̲ g̲o̲t̲o̲ END;
            D[L-4] := D[L-4] + SINPHI + SINIPHI/B;
            D[L-5] := D[L-5]-COSPHI-COSIPHI/B;
            i̲f̲ L < 7 t̲h̲e̲n̲ g̲o̲t̲o̲ END;
            D[L-6] := D[L-6] + SINPHI;
            D[L-7] := D[L-7]-COSPHI;
            END:
         e̲n̲d̲ RHS;
         i̲f̲ PHI0 ≠ PHI t̲h̲e̲n̲ ELEMENTS OF MATRIX;
         RIGHT HAND SIDE;
         SOL(A, L, P, D);
         ZI := 1/B;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L d̲o̲
            b̲e̲g̲i̲n̲ BETA[I] := D[L + 1-I] × ZI;
               BETHA[I] := (I + 3) × BETA[I];
               ZI := ZI/B
            e̲n̲d̲
      e̲n̲d̲ SOLOFEQCOM;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COEFFICIENT;
   b̲e̲g̲i̲n̲ B0 := B := ABS(H × SIGMA);
      i̲f̲ B ≥ ·1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ PHI ≠ PI ∧ L = 2 ∨ ABS(PHI-PI) > ·01 t̲h̲e̲n̲
           SOLUTION OF COMPLEX EQUATIONS e̲l̲s̲e̲ FORMBETA
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L d̲o̲
            b̲e̲g̲i̲n̲ BETHA[I] := BETA[I-1];
               BETA[I] := BETA[I-1]/(I + 3);
            e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ COEFFICIENT;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCAL ERROR BOUND;
      ETA := AETA + RETA × SQRT(VECVEC(1, M1, 0, Y, Y));

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
   b̲e̲g̲i̲n̲ LOCAL ERROR BOUND;
      i̲f̲ K = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DISCR := SQRT(VECVEC(1, M1, 0, D, D)); H := ETA/DISCR
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ DISCR := H × SQRT(SUM(I, 1, M1, (D[I]-D2[I])⭡2))/ETA;
         H := H × (i̲f̲ LINEAR t̲h̲e̲n̲ 4/(4 + DISCR) + ·5
         e̲l̲s̲e̲ 4/(3 + DISCR) + 1/3)
      e̲n̲d̲;
      i̲f̲ H < HMIN t̲h̲e̲n̲ H := HMIN;
      i̲f̲ H > HMAX t̲h̲e̲n̲ H := HMAX;
      B := ABS(H × SIGMA);
      CHANGE := ABS(1-B/B0) > ·05 ∨ PHI ≠ PHI0;
      i̲f̲ 1·1 × H ≥ XE-X t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ CHANGE := LAST := t̲r̲u̲e̲; H := XE-X e̲n̲d̲;
      i̲f̲ ¬CHANGE t̲h̲e̲n̲ H := H × B0/B
   e̲n̲d̲ STEPSIZE;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFERENCE SCHEME;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K;
      r̲e̲a̲l̲ BETAI, BETHAI;
      i̲f̲ M1 < M t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D2[M] := 1; K0[M] := Y[M] + 2 × H/3; Y[M] := Y[M] + ·25 × H e̲n̲d̲;
      f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M1 d̲o̲
         b̲e̲g̲i̲n̲ K0[K] := Y[K] + 2 × H/3 × D[K];
            Y[K] := Y[K] + ·25 × H × D[K];
            D1[K] := H × MATVEC(1, M, K, J, D);
            D2[K] := D1[K] + D[K]
         e̲n̲d̲;
      f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L d̲o̲
         b̲e̲g̲i̲n̲ BETAI := 4 × BETA[I]/3; BETHAI := BETHA[I];
            f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M1 d̲o̲ D[K] := H × D1[K];
            f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M1 d̲o̲
               b̲e̲g̲i̲n̲ K0[K] := K0[K] + BETAI × D[K];
                  D1[K] := MATVEC(1, M1, K, J, D);
                  D2[K] := D2[K] + BETHAI × D1[K]
               e̲n̲d̲
         e̲n̲d̲;
      DERIVATIVE(K0);
      f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ Y[K] := Y[K] + ·75 × H × K0[K]
   e̲n̲d̲ DIFF SCHEME;

   B0 := PHI0 := -1; PI := 4 × ARCTAN(1);
   BETAC[L] := BETAC[L + 1] := BETAC[L + 2] := BETAC[L + 3] := 0;
   BETA[0] := 1/6; BETHA[0] := ·5;
   FAC := 1; f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ L-1 d̲o̲ FAC := I × FAC;
   M1 := i̲f̲ AUT t̲h̲e̲n̲ M e̲l̲s̲e̲ M-1;
   K := 0; LAST := f̲a̲l̲s̲e̲;
   NEXT LEVEL:
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ D[I] := Y[I];
   DERIVATIVE(D);
   i̲f̲ ¬LINEAR ∨ K = 0 t̲h̲e̲n̲ JACOBIAN(J, Y);
   STEPSIZE;
   i̲f̲ CHANGE t̲h̲e̲n̲ COEFFICIENT;
   OUTPUT;
   DIFFERENCE SCHEME;
   K := K + 1;
   X := X + H;
   i̲f̲ ¬LAST t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT LEVEL;
   END OF EFERK: OUTPUT;
e̲n̲d̲ EFERK;
c̲o̲m̲m̲e̲n̲t̲ ================== 33131 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LINIGER2(X, XE, M, Y, SIGMA1, SIGMA2, F, EVALUATE, J,
  JACOBIAN, K, ITMAX, STEP, AETA, RETA, OUTPUT);
   i̲n̲t̲e̲g̲e̲r̲ M, K, ITMAX;
     r̲e̲a̲l̲ X, XE, SIGMA1, SIGMA2, STEP, AETA, RETA;
     a̲r̲r̲a̲y̲ Y, J;
     B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EVALUATE;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ F;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ JACOBIAN, OUTPUT;

b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ H, HL, B1, B2, P, Q, C0, C1, C2, C3, C4;
   B̲o̲o̲l̲e̲a̲n̲ LAST;
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ PI[1:M];
   r̲e̲a̲l̲ a̲r̲r̲a̲y̲ DY, YL, FL[1:M], A[1:M, 1:M], AUX[1:3];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
   b̲e̲g̲i̲n̲ H := STEP;
      i̲f̲ 1·1 × H ≥ XE-X t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; H := XE-X; X := XE
      e̲n̲d̲ e̲l̲s̲e̲ X := X + H
   e̲n̲d̲ STEPSIZE;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COEFFICIENT;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R1, R2, EX, ZETA, ETA, SINL, COSL, SINH, COSH, D;
      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ R(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
         i̲f̲ X > 40 t̲h̲e̲n̲ R := X/(X-2) e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ EX := EXP(-X); R := X × (1-EX)/(X-2 + (X + 2) × EX) e̲n̲d̲;

      B1 := H × SIGMA1;
      B2 := H × SIGMA2;
      i̲f̲ B1 < ·1 t̲h̲e̲n̲ b̲e̲g̲i̲n̲ P := 0; Q := 1/3; g̲o̲t̲o̲ OUT e̲n̲d̲;
      i̲f̲ B2 < 0 t̲h̲e̲n̲ g̲o̲t̲o̲ COMPLEX;
      i̲f̲ B1 < 1 ∨ B2 < ·1 t̲h̲e̲n̲ g̲o̲t̲o̲ THIRDORDER;
      i̲f̲ ABS(B1-B2) < B1 × B1 × @-6 t̲h̲e̲n̲ g̲o̲t̲o̲ DOUBLEFIT;

      R1 := R(B1) × B1; R2 := R(B2) × B2;
      D := B2 × R1-B1 × R2;
      P := 2 × (R2-R1)/D;
      Q := 2 × (B2-B1)/D;
      g̲o̲t̲o̲ OUT;
      THIRDORDER: Q := 1/3;
      P := R(B1)/3-2/B1;
      g̲o̲t̲o̲ OUT;
      DOUBLEFIT: B1 := ·5 × (B1 + B2);
      R1 := R(B1);
      i̲f̲ B1 > 40 t̲h̲e̲n̲ EX := 0;
      R2 := B1/(1-EX); R2 := 1-EX × R2 × R2;
      Q := 1/(R1 × R1 × R2);
      P := R1 × Q-2/B1;
      g̲o̲t̲o̲ OUT;
      COMPLEX: ETA := ABS(B1 × SIN(SIGMA2));
      ZETA := ABS(B1 × COS(SIGMA2));
      i̲f̲ ETA < B1 × B1 × @-6 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ B1 := B2 := ZETA; g̲o̲t̲o̲ DOUBLEFIT e̲n̲d̲;
      i̲f̲ ZETA > 40 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ P := 1-4 × ZETA/B1/B1; Q := 4 × (1-ZETA)/B1/B1 + 1 e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ EX := EXP(ZETA);
         SINL := SIN(ETA); COSL := COS(ETA);
         SINH := ·5 × (EX-1/EX); COSH := ·5 × (EX + 1/EX);
         D := ETA × (COSH-COSL)-·5 × B1 × B1 × SINL;
         P := (ZETA × SINL + ETA × SINH-4 × ZETA × ETA/B1/B1 × (COSH-COSL))/D;
         Q := ETA × ((COSH-COSL-ZETA × SINH-ETA × SINL) × 4/B1/B1 + COSH + COSL)/D
      e̲n̲d̲;
      OUT:    C0 := ·25 × H × H × (P + Q);
      C1 := ·5 × H × (1 + P);
      C2 := H-C1;
      C3 := ·25 × H × H × (Q-P);
      C4 := ·5 × H × P;
      ELEMENTS OF MATRIX
   e̲n̲d̲ COEFFICIENT;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENTS OF MATRIX;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ K;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
                 A[I, K] := C0 × MATMAT(1, M, I, K, J, J)-C1 × J[I, K];
            A[I, I] := A[I, I] + 1
         e̲n̲d̲;
      AUX[2] := 0; DEC(A, M, AUX, PI)
   e̲n̲d̲ ELOFMAT;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ NEWTON ITERATION;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ ITNUM; r̲e̲a̲l̲ JFL, ETA, DISCR;
      ITNUM := 0;
      NEXT:   ITNUM := ITNUM + 1;
      i̲f̲ EVALUATE(ITNUM) t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ JACOBIAN(J, Y); COEFFICIENT e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ ITNUM = 1 ∧ H ≠ HL t̲h̲e̲n̲ COEFFICIENT;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ FL[I] := F(I);
      i̲f̲ ITNUM = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
            b̲e̲g̲i̲n̲ JFL := MATVEC(1, M, I, J, FL);
               DY[I] := H × (FL[I]-C4 × JFL);
               YL[I] := Y[I] + C2 × FL[I] + C3 × JFL
            e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲
        f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
           DY[I] := YL[I]-Y[I] + C1 × FL[I]-C0 × MATVEC(1, M, I, J, FL);
      SOL(A, M, PI, DY);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ Y[I] := Y[I] + DY[I];
      i̲f̲ ITNUM < ITMAX t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ ETA := SQRT(VECVEC(1, M, 0, Y, Y)) × RETA + AETA;
         DISCR := SQRT(VECVEC(1, M, 0, DY, DY));
         i̲f̲ ETA < DISCR t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT
      e̲n̲d̲
   e̲n̲d̲ NEWTON;

   LAST := f̲a̲l̲s̲e̲; K := 0; HL := 0;
   NEXT LEVEL:
   K := K + 1;
   STEPSIZE;
   NEWTON ITERATION;
   HL := H;
   OUTPUT;
   i̲f̲ ¬LAST t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT LEVEL
e̲n̲d̲ LINIGER2;
c̲o̲m̲m̲e̲n̲t̲ ================== 33040 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ MODIFIED TAYLOR(T, TE, M0, M, U, SIGMA, TAUMIN, I, DERIVATIVE, K,
  DATA, ALFA, NORM, AETA, RETA, ETA, RHO, OUT);
   i̲n̲t̲e̲g̲e̲r̲ M0, M, I, K, NORM;
     r̲e̲a̲l̲ T, TE, SIGMA, TAUMIN, ALFA, AETA, RETA, ETA, RHO;
     a̲r̲r̲a̲y̲ U, DATA;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, OUT;

b̲e̲g̲i̲n̲ I := 0;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N, P, Q;
      o̲w̲n̲ r̲e̲a̲l̲ EC0, EC1, EC2, TAU0, TAU1, TAU2, TAUS, T2;
      r̲e̲a̲l̲ T0, TAU, TAUI, TAUEC, ECL, BETAN, GAMMA;
      r̲e̲a̲l̲ a̲r̲r̲a̲y̲ C[M0:M], BETA, BETHA[1:DATA[-2]];
      B̲o̲o̲l̲e̲a̲n̲ START, STEP1, LAST;
      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ COEFFICIENT;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J; r̲e̲a̲l̲ IFAC;
         IFAC := 1; GAMMA := ·5; N := DATA[-2]; P := DATA[-1];
         BETAN := DATA[0]; Q := i̲f̲ P < N t̲h̲e̲n̲ P + 1 e̲l̲s̲e̲ N;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ BETA[J] := DATA[J]; IFAC := IFAC/J;
               BETHA[J] := IFAC-BETA[J]
            e̲n̲d̲;
         i̲f̲ P = N t̲h̲e̲n̲ BETHA[N] := IFAC
      e̲n̲d̲;

      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ NORMFUNCTION(NORM, W);
         i̲n̲t̲e̲g̲e̲r̲ NORM; a̲r̲r̲a̲y̲ W;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J; r̲e̲a̲l̲ S, X;
         S := 0;
         i̲f̲ NORM = 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
               b̲e̲g̲i̲n̲  X := ABS(W[J]); i̲f̲ X > S t̲h̲e̲n̲ S := X e̲n̲d̲
         e̲n̲d̲ e̲l̲s̲e̲
           S := SQRT(VECVEC(M0, M, 0, W, W));
         NORMFUNCTION := S
      e̲n̲d̲;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCAL ERROR BOUND;
         ETA := AETA + RETA × NORMFUNCTION(NORM, U);

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCAL ERROR CONSTRUCTION(I); i̲n̲t̲e̲g̲e̲r̲ I;
      b̲e̲g̲i̲n̲ i̲f̲ I = P t̲h̲e̲n̲ b̲e̲g̲i̲n̲ ECL := 0; TAUEC := 1 e̲n̲d̲;
         i̲f̲ I > P + 1 t̲h̲e̲n̲ TAUEC := TAUEC × TAU;
         ECL := ECL + ABS(BETHA[I]) × TAUEC × NORMFUNCTION(NORM, C);
         i̲f̲ I = N t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ EC0 := EC1; EC1 := EC2; EC2 := ECL;
            RHO := ECL × TAU⭡Q
         e̲n̲d̲
      e̲n̲d̲;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ TAUACC, TAUSTAB, AA, BB, CC, EC;
         LOCAL ERROR BOUND;
         i̲f̲ ETA > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ i̲f̲ START t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ i̲f̲ K = 0 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J;
                  f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ C[J] := U[J];
                  I := 1; DERIVATIVE(I, C);
                  TAUACC := ETA/NORMFUNCTION(NORM, C);
                  STEP1 := t̲r̲u̲e̲
               e̲n̲d̲ e̲l̲s̲e̲
               i̲f̲ STEP1 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ TAUACC := (ETA/RHO)⭡(1/Q) × TAU2;
                  i̲f̲ TAUACC > 10 × TAU2 t̲h̲e̲n̲
                    TAUACC := 10 × TAU2 e̲l̲s̲e̲ STEP1 := f̲a̲l̲s̲e̲
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ BB := (EC2-EC1)/TAU1; CC := EC2-BB × T2;
                  EC := BB × T + CC;
                  TAUACC := i̲f̲ EC < 0 t̲h̲e̲n̲ TAU2 e̲l̲s̲e̲
                    (ETA/EC)⭡(1/Q);
                  START := f̲a̲l̲s̲e̲
               e̲n̲d̲
            e̲n̲d̲ e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ AA := ((EC0-EC1)/TAU0 + (EC2-EC1)/TAU1)/
                 (TAU1 + TAU0);
               BB := (EC2-EC1)/TAU1-AA × (2 × T2-TAU1);
               CC := EC2-T2 × (BB + AA × T2); EC := CC + T × (BB + T × AA);
               TAUACC := i̲f̲ EC < 0 t̲h̲e̲n̲ TAUS
               e̲l̲s̲e̲ (ETA/EC)⭡(1/Q);
               i̲f̲ TAUACC > ALFA × TAUS t̲h̲e̲n̲ TAUACC := ALFA × TAUS;
               i̲f̲ TAUACC < GAMMA × TAUS t̲h̲e̲n̲ TAUACC := GAMMA × TAUS;
            e̲n̲d̲
         e̲n̲d̲ e̲l̲s̲e̲ TAUACC := TE-T;
         i̲f̲ TAUACC < TAUMIN t̲h̲e̲n̲ TAUACC := TAUMIN;
         TAUSTAB := BETAN/SIGMA;
         i̲f̲ TAUSTAB < @-12 × (T-T0) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ OUT; g̲o̲t̲o̲ END OF MODIFIED TAYLOR e̲n̲d̲;
         TAU := i̲f̲ TAUACC > TAUSTAB t̲h̲e̲n̲ TAUSTAB e̲l̲s̲e̲ TAUACC;
         TAUS := TAU; i̲f̲ TAU ≥ TE-T t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ TAU := TE-T; LAST := t̲r̲u̲e̲ e̲n̲d̲;
         TAU0 := TAU1; TAU1 := TAU2; TAU2 := TAU
      e̲n̲d̲;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFERENCE SCHEME;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J; r̲e̲a̲l̲ B;
         f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ C[J] := U[J]; TAUI := 1;
         NEXT TERM:
         I := I + 1; DERIVATIVE(I, C); TAUI := TAUI × TAU;
         B := BETA[I] × TAUI;
         i̲f̲ ETA > 0 ∧ I ≥ P t̲h̲e̲n̲ LOCAL ERROR CONSTRUCTION(I);
         f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ U[J] := U[J] + B × C[J];
         i̲f̲ I < N t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT TERM;
         T2 := T; i̲f̲ LAST t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ LAST := f̲a̲l̲s̲e̲; T := TE e̲n̲d̲
         e̲l̲s̲e̲ T := T + TAU
      e̲n̲d̲;

      START := K = 0; T0 := T;
      COEFFICIENT; LAST := f̲a̲l̲s̲e̲;
      NEXT LEVEL:
      STEPSIZE; K := K + 1; I := 0; DIFFERENCE SCHEME; OUT;
      i̲f̲ T ≠ TE t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT LEVEL
   e̲n̲d̲;
   END OF MODIFIED TAYLOR:
e̲n̲d̲ MODIFIED TAYLOR;
c̲o̲m̲m̲e̲n̲t̲ ================== 33050 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ EXPONENTIALLY FITTED TAYLOR(T, TE, M0, M, U, SIGMA, PHI, DIAMETER,
  DERIVATIVE, I, K, ALFA, NORM, AETA, RETA, ETA, RHO, HMIN, HSTART, OUTPUT);
   i̲n̲t̲e̲g̲e̲r̲ M0, M, I, K, NORM;
     r̲e̲a̲l̲ T, TE, SIGMA, PHI, DIAMETER, ALFA, AETA, RETA, ETA, RHO, HMIN, HSTART;
     a̲r̲r̲a̲y̲ U;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIVATIVE, OUTPUT;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ KL;
   r̲e̲a̲l̲ Q, EC0, EC1, EC2, H, HI, H0, H1, H2, BETAN, T2, SIGMAL, PHIL;
   r̲e̲a̲l̲ a̲r̲r̲a̲y̲ C, RO[M0:M], BETA, BETHA[1:3];
   B̲o̲o̲l̲e̲a̲n̲ LAST, START;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X); c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 31030;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X); c̲o̲d̲e̲ 34020;
   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ZEROIN(X, Y, FX, EPS); c̲o̲d̲e̲ 34150;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COEFFICIENT;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ B, B1, B2, BB, E, BETA2, BETA3;
      B := H × SIGMAL; B1 := B × COS(PHIL); BB := B × B;
      i̲f̲ ABS(B) < @-3 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ BETA2 := ·5-BB/24;
         BETA3 := 1/6 + B1/12;
         BETHA[3] := ·5 + B1/3
      e̲n̲d̲ e̲l̲s̲e̲
      i̲f̲ B1 < -40 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ BETA2 := (-2 × B1-4 × B1 × B1/BB + 1)/BB;
         BETA3 := (1 + 2 × B1/BB)/BB;
         BETHA[3] := 1/BB
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ E := EXP(B1)/BB; B2 := B × SIN(PHIL);
         BETA2 := (-2 × B1-4 × B1 × B1/BB + 1)/BB;
         BETA3 := (1 + 2 × B1/BB)/BB;
         i̲f̲ ABS(B2/B) < @-5 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ BETA2 := BETA2-E × (B1-3);
            BETA3 := BETA3 + E × (B1-2)/B1;
            BETHA[3] := 1/BB + E × (B1-1)
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ BETA2 := BETA2-E × SIN(B2-3 × PHIL)/B2 × B;
            BETA3 := BETA3 + E × SIN(B2-2 × PHIL)/B2;
            BETHA[3] := 1/BB + E × SIN(B2-PHIL)/B2 × B;
         e̲n̲d̲
      e̲n̲d̲;
      BETA[1] := BETHA[1] := 1;
      BETA[2] := BETA2; BETA[3] := BETA3;
      BETHA[2] := 1-BB × BETA3; B := ABS(B);
      Q := i̲f̲ B < 1·5 t̲h̲e̲n̲ 4-2 × B/3 e̲l̲s̲e̲ i̲f̲ B < 6 t̲h̲e̲n̲ (30-2 × B)/9
      e̲l̲s̲e̲ 2;
   e̲n̲d̲;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ NORMFUNCTION(NORM, W);
      i̲n̲t̲e̲g̲e̲r̲ NORM; a̲r̲r̲a̲y̲ W;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J; r̲e̲a̲l̲ S, X;
      S := 0;
      i̲f̲ NORM = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ J := M0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
            b̲e̲g̲i̲n̲ X := ABS(W[J]); i̲f̲ X > S t̲h̲e̲n̲ S := X e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲
        S := SQRT(VECVEC(M0, M, 0, W, W));
      NORMFUNCTION := S;
   e̲n̲d̲;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCAL ERROR BOUND;
      ETA := AETA + RETA × NORMFUNCTION(NORM, U);

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCAL ERROR CONSTRUCTION(I); i̲n̲t̲e̲g̲e̲r̲ I;
   b̲e̲g̲i̲n̲ i̲f̲ I = 1 t̲h̲e̲n̲ INIVEC(M0, M, RO, 0);
      i̲f̲ I < 4 t̲h̲e̲n̲ ELMVEC(M0, M, 0, RO, C, BETHA[I] × HI);
      i̲f̲ I = 4 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ ELMVEC(M0, M, 0, RO, C, -H);
         RHO := NORMFUNCTION(NORM, RO);
         EC0 := EC1; EC1 := EC2; EC2 := RHO/H⭡Q;
      e̲n̲d̲
   e̲n̲d̲;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ HACC, HSTAB, HCR, HMAX, A, B, C;
      i̲f̲ ¬START t̲h̲e̲n̲ LOCAL ERROR BOUND;
      i̲f̲ START t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ H1 := H2 := HACC := HSTART;
         EC2 := EC1 := 1; KL := 1; START := f̲a̲l̲s̲e̲
      e̲n̲d̲ e̲l̲s̲e̲
      i̲f̲ KL < 3 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ HACC := (ETA/RHO)⭡(1/Q) × H2;
         i̲f̲ HACC > 10 × H2 t̲h̲e̲n̲ HACC := 10 × H2 e̲l̲s̲e̲ KL := KL + 1
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ A := (H0 × (EC2-EC1)-H1 × (EC1-EC0))/(H2 × H0-H1 × H1);
         H := H2 × (i̲f̲ ETA < RHO t̲h̲e̲n̲ (ETA/RHO)⭡(1/Q) e̲l̲s̲e̲ ALFA);
         i̲f̲ A > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B := (EC2-EC1-A × (H2-H1))/H1;
            C := EC2-A × H2-B × T2; HACC := 0; HMAX := H;
            i̲f̲ ¬ZEROIN(HACC, H, HACC⭡Q × (A × HACC + B × T + C)-ETA,@-3 × H2) t̲h̲e̲n̲ HACC := HMAX
         e̲n̲d̲ e̲l̲s̲e̲ HACC := H;
         i̲f̲ HACC < ·5 × H2 t̲h̲e̲n̲ HACC := ·5 × H2;
      e̲n̲d̲;
      i̲f̲ HACC < HMIN t̲h̲e̲n̲ HACC := HMIN; H := HACC;
      i̲f̲ H × SIGMAL > 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ A := ABS(DIAMETER/SIGMAL + @-14)/2; B := 2 × ABS(SIN(PHIL));
         BETAN := (i̲f̲ A > B t̲h̲e̲n̲ 1/A e̲l̲s̲e̲ 1/B)/A;
         HSTAB := ABS(BETAN/SIGMAL);
         i̲f̲ HSTAB < @-14 × T t̲h̲e̲n̲ g̲o̲t̲o̲ ENDOFEFT;
         i̲f̲ H > HSTAB t̲h̲e̲n̲ H := HSTAB
      e̲n̲d̲;
      HCR := H2 × H2/H1;
      i̲f̲ KL > 2 ∧ ABS(H-HCR) < @-6 × HCR t̲h̲e̲n̲
        H := i̲f̲ H < HCR t̲h̲e̲n̲ HCR × (1-@-7) e̲l̲s̲e̲ HCR × (1 + @-7);
      i̲f̲ T + H > TE t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; HSTART := H; H := TE-T e̲n̲d̲;
      H0 := H1; H1 := H2; H2 := H;
   e̲n̲d̲;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DIFFERENCE SCHEME;
   b̲e̲g̲i̲n̲ HI := 1; SIGMAL := SIGMA; PHIL := PHI;
      STEPSIZE;
      COEFFICIENT;
      f̲o̲r̲ I := 1, 2, 3 d̲o̲
         b̲e̲g̲i̲n̲ HI := HI × H;
            i̲f̲ I > 1 t̲h̲e̲n̲ DERIVATIVE(I, C);
            LOCALERRORCONSTRUCTION(I);
            ELMVEC(M0, M, 0, U, C, BETA[I] × HI)
         e̲n̲d̲;
      T2 := T; K := K + 1;
      i̲f̲ LAST t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ LAST := f̲a̲l̲s̲e̲; T := TE; START := t̲r̲u̲e̲
      e̲n̲d̲ e̲l̲s̲e̲ T := T + H;
      DUPVEC(M0, M, 0, C, U);
      DERIVATIVE(1, C);
      LOCALERRORCONSTRUCTION(4);
      OUTPUT;
   e̲n̲d̲;

   START := t̲r̲u̲e̲; LAST := f̲a̲l̲s̲e̲;
   DUPVEC(M0, M, 0, C, U);
   DERIVATIVE(1, C);
   i̲f̲ K = 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LOCAL ERROR BOUND; HSTART := ETA/NORMFUNCTION(NORM, C)
   e̲n̲d̲;
   NEXT LEVEL:
   DIFFERENCE SCHEME;
   i̲f̲ T ≠ TE t̲h̲e̲n̲ g̲o̲t̲o̲ NEXT LEVEL;
   ENDOFEFT:
e̲n̲d̲ EXPONENTIAL FITTED TAYLOR;
c̲o̲m̲m̲e̲n̲t̲ ================== 33012 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK2(X, A, B, Y, YA, Z, ZA, FXYZ, E, D, FI);
   v̲a̲l̲u̲e̲ B, FI; r̲e̲a̲l̲ X, A, B, Y, YA, Z, ZA, FXYZ; B̲o̲o̲l̲e̲a̲n̲ FI;
     a̲r̲r̲a̲y̲ E, D;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E1, E2, E3, E4, XL, YL, ZL, H, INT, HMIN, HL,
     ABSH, K0, K1, K2, K3, K4, K5, DISCRY, DISCRZ, TOLY,
     TOLZ, MU, MU1, FHY, FHZ;
   B̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3] := A; D[4] := YA; D[5] := ZA e̲n̲d̲;
   D[1] := 0; XL := D[3]; YL := D[4]; ZL := D[5];
   i̲f̲ FI t̲h̲e̲n̲ D[2] := B - D[3]; ABSH := H := ABS(D[2]);
   i̲f̲ B - XL < 0 t̲h̲e̲n̲ H := - H; INT := ABS(B - XL);
   HMIN := INT × E[1] + E[2]; HL := INT × E[3] + E[4];
   i̲f̲ HL < HMIN t̲h̲e̲n̲ HMIN := HL; E1 := E[1] / INT;
   E2 := E[2] / INT; E3 := E[3] / INT; E4 := E[4] / INT;
   FIRST := t̲r̲u̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN; ABSH := HMIN
   e̲n̲d̲;
   i̲f̲ H ≥ B - XL ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[2] := H; LAST := t̲r̲u̲e̲; H := B - XL;
      ABSH := ABS(H)
   e̲n̲d̲
   e̲l̲s̲e̲ LAST := f̲a̲l̲s̲e̲;
   STEP: X := XL; Y := YL; Z := ZL; K0 := FXYZ × H;
   X := XL + H / 4·5;
   Y := YL + (ZL × 18 + K0 × 2) / 81 × H;
   Z := ZL + K0 / 4·5  ; K1 := FXYZ × H; X := XL + H / 3;
   Y := YL + (ZL × 6 + K0) / 18 × H;
   Z := ZL + (K0 + K1 × 3) / 12; K2 := FXYZ × H;
   X := XL + H × ·5;
   Y := YL + (ZL × 8 + K0 + K2) / 16 × H;
   Z := ZL + (K0 + K2 × 3) / 8; K3 := FXYZ × H;
   X := XL + H × ·8;
   Y := YL + (ZL × 100 + K0 × 12 + K3 × 28) / 125 × H;
   Z := ZL + (K0 × 53 - K1 × 135 + K2 × 126 + K3 × 56)
     / 125; K4 := FXYZ × H; X := i̲f̲ LAST t̲h̲e̲n̲ B e̲l̲s̲e̲ XL + H;
   Y := YL + (ZL × 336 + K0 × 21 + K2 × 92 + K4 × 55) /
     336 × H;
   Z := ZL + (K0 × 133 - K1 × 378 + K2 × 276 + K3 × 112
     + K4 × 25) / 168; K5 := FXYZ × H;
   DISCRY := ABS(( - K0 × 21 + K2 × 108 - K3 × 112 + K4× 25) / 56 × H);
   DISCRZ := ABS(K0 × 21 - K2 × 162 + K3 × 224 - K4 ×
     125 + K5 × 42) / 14;
   TOLY := ABSH × (ABS(ZL) × E1 + E2);
   TOLZ := ABS(K0) × E3 + ABSH × E4;
   REJECT := DISCRY > TOLY ∨ DISCRZ > TOLZ;
   FHY := DISCRY / TOLY; FHZ := DISCRZ / TOLZ;
   i̲f̲ FHZ > FHY t̲h̲e̲n̲ FHY := FHZ;
   MU := 1 / (1 + FHY) + ·45; i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[1] := D[1] + 1; Y := YL; Z := ZL;
         FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; HL := H; H := MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FHY := MU × H / HL + MU - MU1; HL := H; H := FHY × H;
   ACC: MU1 := MU;
   Y := YL + (ZL × 56 + K0 × 7 + K2 × 36 - K4 × 15) / 56× HL;
   Z := ZL + ( - K0 × 63 + K1 × 189 - K2 × 36 - K3 × 112
     + K4 × 50) / 28; K5 := FXYZ × HL;
   Y := YL + (ZL × 336 + K0 × 35 + K2 × 108 + K4 × 25)
     / 336 × HL;
   Z := ZL + (K0 × 35 + K2 × 162 + K4 × 125 + K5 × 14)
     / 336;
   NEXT: i̲f̲ B ≠ X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XL := X; YL := Y; ZL := Z; g̲o̲t̲o̲ TEST e̲n̲d̲;
   i̲f̲ ¬LAST t̲h̲e̲n̲ D[2] := H; D[3] := X; D[4] := Y; D[5] := Z
e̲n̲d̲ RK2;
c̲o̲m̲m̲e̲n̲t̲ ================== 33013 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK2N(X, A, B, Y, YA, Z, ZA, FXYZJ, J, E, D,
  FI, N); v̲a̲l̲u̲e̲ B, FI, N; i̲n̲t̲e̲g̲e̲r̲ J, N; r̲e̲a̲l̲ X, A, B, FXYZJ;
     B̲o̲o̲l̲e̲a̲n̲ FI; a̲r̲r̲a̲y̲ Y, YA, Z, ZA, E, D;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ JJ;
   r̲e̲a̲l̲ XL, H, INT, HMIN, HL, ABSH, FHM, DISCRY, DISCRZ,
     TOLY, TOLZ, MU, MU1, FHY, FHZ;
   B̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   a̲r̲r̲a̲y̲ YL, ZL, K0, K1, K2, K3, K4, K5[1:N], EE[1:4 ×
     N];
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3] := A;
      f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ D[JJ + 3] := YA[JJ]; D[N + JJ + 3] := ZA[JJ]
         e̲n̲d̲
   e̲n̲d̲;
   D[1] := 0; XL := D[3];
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ YL[JJ] := D[JJ + 3]; ZL[JJ] := D[N + JJ + 3] e̲n̲d̲;
   i̲f̲ FI t̲h̲e̲n̲ D[2] := B - D[3]; ABSH := H := ABS(D[2]);
   i̲f̲ B - XL < 0 t̲h̲e̲n̲ H := - H; INT := ABS(B - XL);
   HMIN := INT × E[1] + E[2];
   f̲o̲r̲ JJ := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 2 × N d̲o̲
      b̲e̲g̲i̲n̲ HL := INT × E[2 × JJ - 1] + E[2 × JJ];
         i̲f̲ HL < HMIN t̲h̲e̲n̲ HMIN := HL
      e̲n̲d̲;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 4 × N d̲o̲ EE[JJ] := E[JJ] / INT;
   FIRST := t̲r̲u̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN;
      ABSH := ABS(H)
   e̲n̲d̲;
   i̲f̲ H ≥ B - XL ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[2] := H; LAST := t̲r̲u̲e̲; H := B - XL;
      ABSH := ABS(H)
   e̲n̲d̲
   e̲l̲s̲e̲ LAST := f̲a̲l̲s̲e̲;
   STEP: X := XL;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ]; Z[JJ] := ZL[JJ] e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K0[J] := FXYZJ × H;
   X := XL + H / 4·5;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 18 + K0[JJ] × 2) /
           81 × H; Z[JJ] := ZL[JJ] + K0[JJ] / 4·5;
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K1[J] := FXYZJ × H;
   X := XL + H / 3;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 6 + K0[JJ]) / 18 × H;
         Z[JJ] := ZL[JJ] + (K0[JJ] + K1[JJ] × 3) / 12
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K2[J] := FXYZJ × H;
   X := XL + H × ·5;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 8 + K0[JJ] + K2[JJ])
           / 16 × H;
         Z[JJ] := ZL[JJ] + (K0[JJ] + K2[JJ] × 3) / 8
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K3[J] := FXYZJ × H;
   X := XL + H × ·8;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 100 + K0[JJ] × 12 +
           K3[JJ] × 28) / 125 × H;
         Z[JJ] := ZL[JJ] + (K0[JJ] × 53 - K1[JJ] × 135 +
           K2[JJ] × 126 + K3[JJ] × 56) / 125
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K4[J] := FXYZJ × H;
   X := i̲f̲ LAST t̲h̲e̲n̲ B e̲l̲s̲e̲ XL + H;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 336 + K0[JJ] × 21 +
           K2[JJ] × 92 + K4[JJ] × 55) / 336 × H;
         Z[JJ] := ZL[JJ] + (K0[JJ] × 133 - K1[JJ] × 378 +
           K2[JJ] × 276 + K3[JJ] × 112 + K4[JJ] × 25) / 168
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K5[J] := FXYZJ × H;
   REJECT := f̲a̲l̲s̲e̲; FHM := 0;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ DISCRY := ABS(( - K0[JJ] × 21 + K2[JJ] × 108 -
           K3[JJ] × 112 + K4[JJ] × 25) / 56 × H);
         DISCRZ := ABS(K0[JJ] × 21 - K2[JJ] × 162 + K3[JJ]× 224 - K4[JJ] × 125 + K5[JJ] × 42) / 14;
         TOLY := ABSH × (ABS(ZL[JJ]) × EE[2 × JJ - 1] +
           EE[2 × JJ]);
         TOLZ := ABS(K0[JJ]) × EE[2 × (JJ + N) - 1] + ABSH× EE[2 × (JJ + N)];
         REJECT := DISCRY > TOLY ∨ DISCRZ > TOLZ ∨ REJECT;
         FHY := DISCRY / TOLY; FHZ := DISCRZ / TOLZ;
         i̲f̲ FHZ > FHY t̲h̲e̲n̲ FHY := FHZ;
         i̲f̲ FHY > FHM t̲h̲e̲n̲ FHM := FHY
      e̲n̲d̲;
   MU := 1 / (1 + FHM) + ·45; i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[1] := D[1] + 1;
         f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ]; Z[JJ] := ZL[JJ] e̲n̲d̲;
         FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; HL := H; H := MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FHM := MU × H / HL + MU - MU1; HL := H; H := FHM × H;
   ACC: MU1 := MU;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 56 + K0[JJ] × 7 +
           K2[JJ] × 36 - K4[JJ] × 15) / 56 × HL;
         Z[JJ] := ZL[JJ] + ( - K0[JJ] × 63 + K1[JJ] × 189
           - K2[JJ] × 36 - K3[JJ] × 112 + K4[JJ] × 50) / 28
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K5[J] := FXYZJ × HL;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ] + (ZL[JJ] × 336 + K0[JJ] × 35 +
           K2[JJ] × 108 + K4[JJ] × 25) / 336 × HL;
         Z[JJ] := ZL[JJ] + (K0[JJ] × 35 + K2[JJ] × 162 +
           K4[JJ] × 125 + K5[JJ] × 14) / 336
      e̲n̲d̲;
   NEXT: i̲f̲ B ≠ X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XL := X;
      f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ YL[JJ] := Y[JJ]; ZL[JJ] := Z[JJ] e̲n̲d̲;
      g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ ¬LAST t̲h̲e̲n̲ D[2] := H; D[3] := X;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ D[JJ + 3] := Y[JJ]; D[N + JJ + 3] := Z[JJ] e̲n̲d̲
e̲n̲d̲ RK2N;
c̲o̲m̲m̲e̲n̲t̲ ================== 33014 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK3(X, A, B, Y, YA, Z, ZA, FXY, E, D, FI);
   v̲a̲l̲u̲e̲ B, FI; r̲e̲a̲l̲ X, A, B, Y, YA, Z, ZA, FXY; B̲o̲o̲l̲e̲a̲n̲ FI;
     a̲r̲r̲a̲y̲ E, D;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E1, E2, E3, E4, XL, YL, ZL, H, INT, HMIN, HL,
     ABSH, K0, K1, K2, K3, K4, K5, DISCRY, DISCRZ, TOLY,
     TOLZ, MU, MU1, FHY, FHZ;
   B̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3] := A; D[4] := YA; D[5] := ZA e̲n̲d̲;
   D[1] := 0; XL := D[3]; YL := D[4]; ZL := D[5];
   i̲f̲ FI t̲h̲e̲n̲ D[2] := B - D[3]; ABSH := H := ABS(D[2]);
   i̲f̲ B - XL < 0 t̲h̲e̲n̲ H := - H; INT := ABS(B - XL);
   HMIN := INT × E[1] + E[2]; HL := INT × E[3] + E[4];
   i̲f̲ HL < HMIN t̲h̲e̲n̲ HMIN := HL; E1 := E[1] / INT;
   E2 := E[2] / INT; E3 := E[3] / INT; E4 := E[4] / INT;
   FIRST := REJECT := t̲r̲u̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN; ABSH := HMIN
   e̲n̲d̲;
   i̲f̲ H ≥ B - XL ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[2] := H; LAST := t̲r̲u̲e̲; H := B - XL;
      ABSH := ABS(H)
   e̲n̲d̲
   e̲l̲s̲e̲ LAST := f̲a̲l̲s̲e̲;
   STEP: i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ X := XL; Y := YL; K0 := FXY × H e̲n̲d̲
   e̲l̲s̲e̲ K0 := K5 × H / HL; X := XL + ·276393202250021 × H;
   Y := YL + (ZL × ·2763932022 50021 + K0 ×·038196601125011) × H; K1 := FXY × H;
   X := XL + ·72360 6797749979 × H;
   Y := YL + (ZL × ·723606797749979 + K1 × ·26180
     3398874989) × H; K2 := FXY × H; X := XL + H × ·5;
   Y := YL + (ZL × ·5 + K0 × ·046875 + K1 ×·079824155839840 - K2 × ·001699155839840) × H;
   K4 := FXY × H; X := i̲f̲ LAST t̲h̲e̲n̲ B e̲l̲s̲e̲ XL + H;
   Y := YL + (ZL + K0 × ·309016994374947 + K2 ×·190983005625053) × H; K3 := FXY × H;
   Y := YL + (ZL + K0 × ·083333333333333 + K1 ×·301502832395825 + K2 × ·115163834270842) × H;
   K5 := FXY × H;
   DISCRY := ABS(( - K0 × ·5 + K1 × 1·809016994374947 +
     K2 × ·690983005625053 - K4 × 2) × H);
   DISCRZ := ABS((K0 - K3) × 2 - (K1 + K2) × 10 + K4 ×
     16 + K5 × 4); TOLY := ABSH × (ABS(ZL) × E1 + E2);
   TOLZ := ABS(K0) × E3 + ABSH × E4;
   REJECT := DISCRY > TOLY ∨ DISCRZ > TOLZ;
   FHY := DISCRY / TOLY; FHZ := DISCRZ / TOLZ;
   i̲f̲ FHZ > FHY t̲h̲e̲n̲ FHY := FHZ;
   MU := 1 / (1 + FHY) + ·45; i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[1] := D[1] + 1; Y := YL; Z := ZL;
         FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; HL := H; H := MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FHY := MU × H / HL + MU - MU1; HL := H; H := FHY × H;
   ACC: MU1 := MU;
   Z := ZL + (K0 + K3) × ·083333333333333 + (K1 + K2) ×·416666666666667;
   NEXT: i̲f̲ B ≠ X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XL := X; YL := Y; ZL := Z; g̲o̲t̲o̲ TEST e̲n̲d̲;
   i̲f̲ ¬LAST t̲h̲e̲n̲ D[2] := H; D[3] := X; D[4] := Y; D[5] := Z
e̲n̲d̲ RK3;
c̲o̲m̲m̲e̲n̲t̲ ================== 33015 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ RK3N(X, A, B, Y, YA, Z, ZA, FXYJ, J, E, D,
  FI, N); v̲a̲l̲u̲e̲ B, FI, N; i̲n̲t̲e̲g̲e̲r̲ J, N; r̲e̲a̲l̲ X, A, B, FXYJ;
     B̲o̲o̲l̲e̲a̲n̲ FI; a̲r̲r̲a̲y̲ Y, YA, Z, ZA, E, D;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ JJ;
   r̲e̲a̲l̲ XL, H, HMIN, INT, HL, ABSH, FHM, DISCRY, DISCRZ,
     TOLY, TOLZ, MU, MU1, FHY, FHZ;
   B̲o̲o̲l̲e̲a̲n̲ LAST, FIRST, REJECT;
   a̲r̲r̲a̲y̲ YL, ZL, K0, K1, K2, K3, K4, K5[1:N], EE[1:4 ×
     N];
   i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[3] := A;
      f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ D[JJ + 3] := YA[JJ]; D[N + JJ + 3] := ZA[JJ]
         e̲n̲d̲
   e̲n̲d̲;
   D[1] := 0; XL := D[3];
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ YL[JJ] := D[JJ + 3]; ZL[JJ] := D[N + JJ + 3] e̲n̲d̲;
   i̲f̲ FI t̲h̲e̲n̲ D[2] := B - D[3]; ABSH := H := ABS(D[2]);
   i̲f̲ B - XL < 0 t̲h̲e̲n̲ H := - H; INT := ABS(B - XL);
   HMIN := INT × E[1] + E[2];
   f̲o̲r̲ JJ := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 2 × N d̲o̲
      b̲e̲g̲i̲n̲ HL := INT × E[2 × JJ - 1] + E[2 × JJ];
         i̲f̲ HL < HMIN t̲h̲e̲n̲ HMIN := HL
      e̲n̲d̲;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 4 × N d̲o̲ EE[JJ] := E[JJ] / INT;
   FIRST := REJECT := t̲r̲u̲e̲; i̲f̲ FI t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ LAST := t̲r̲u̲e̲; g̲o̲t̲o̲ STEP e̲n̲d̲;
   TEST: ABSH := ABS(H); i̲f̲ ABSH < HMIN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ H := i̲f̲ H > 0 t̲h̲e̲n̲ HMIN e̲l̲s̲e̲ - HMIN; ABSH := HMIN
   e̲n̲d̲;
   i̲f̲ H ≥ B - XL ≡ H ≥ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ D[2] := H; LAST := t̲r̲u̲e̲; H := B - XL;
      ABSH := ABS(H)
   e̲n̲d̲
   e̲l̲s̲e̲ LAST := f̲a̲l̲s̲e̲;
   STEP: i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ X := XL;
      f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[JJ] := YL[JJ];
      f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K0[J] := FXYJ × H
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ FHY := H / HL;
      f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K0[JJ] := K5[JJ] × FHY
   e̲n̲d̲;
   X := XL + ·27639 3202250021 × H;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[JJ] := YL[JJ] + (ZL[JJ]× ·276393202250021 + K0[JJ] × ·038196601125011) × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K1[J] := FXYJ × H;
   X := XL + ·723606797749979 × H;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[JJ] := YL[JJ] + (ZL[JJ]× ·723606797749979 + K1[JJ] × ·261803398874989) × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K2[J] := FXYJ × H;
   X := XL + H × ·5;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[JJ] := YL[JJ] + (ZL[JJ]× ·5 + K0[JJ] × ·046875 + K1[JJ] × ·079824155839840
        - K2[JJ] × ·00169 9155839840) × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K4[J] := FXYJ × H;
   X := i̲f̲ LAST t̲h̲e̲n̲ B e̲l̲s̲e̲ XL + H;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[JJ] := YL[JJ] + (ZL[JJ]
        + K0[JJ] × ·309016994374947 + K2[JJ] ×·190983005625053) × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K3[J] := FXYJ × H;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Y[JJ] := YL[JJ] + (ZL[JJ]
        + K0[JJ] × ·083333333333333 + K1[JJ] × ·30150
        2832395825 + K2[JJ] × ·115163834270842) × H;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ K5[J] := FXYJ × H;
   REJECT := f̲a̲l̲s̲e̲; FHM := 0;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ DISCRY := ABS(( - K0[JJ] × ·5 + K1[JJ] ×
           1·809016994374947 + K2[JJ] × ·690983005625053 -
           K4[JJ] × 2) × H);
         DISCRZ := ABS((K0[JJ] - K3[JJ]) × 2 - (K1[JJ] +
           K2[JJ]) × 10 + K4[JJ] × 16 + K5[JJ] × 4);
         TOLY := ABSH × (ABS(ZL[JJ]) × EE[2 × JJ - 1] +
           EE[2 × JJ]);
         TOLZ := ABS(K0[JJ]) × EE[2 × (JJ + N) - 1] + ABSH× EE[2 × (JJ + N)];
         REJECT := DISCRY > TOLY ∨ DISCRZ > TOLZ ∨ REJECT;
         FHY := DISCRY / TOLY; FHZ := DISCRZ / TOLZ;
         i̲f̲ FHZ > FHY t̲h̲e̲n̲ FHY := FHZ;
         i̲f̲ FHY > FHM t̲h̲e̲n̲ FHM := FHY
      e̲n̲d̲;
   MU := 1 / (1 + FHM) + ·45; i̲f̲ REJECT t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ABSH ≤ HMIN t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ D[1] := D[1] + 1;
         f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ Y[JJ] := YL[JJ]; Z[JJ] := ZL[JJ] e̲n̲d̲;
         FIRST := t̲r̲u̲e̲; g̲o̲t̲o̲ NEXT
      e̲n̲d̲;
      H := MU × H; g̲o̲t̲o̲ TEST
   e̲n̲d̲ REJ;
   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := f̲a̲l̲s̲e̲; HL := H; H := MU × H; g̲o̲t̲o̲ ACC
   e̲n̲d̲;
   FHY := MU × H / HL + MU - MU1; HL := H; H := FHY × H;
   ACC: MU1 := MU;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Z[JJ] := ZL[JJ] + (K0[JJ]
        + K3[JJ]) × ·083333333333333 + (K1[JJ] + K2[JJ]) ×·416666666666667;
   NEXT: i̲f̲ B ≠ X t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XL := X;
      f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ YL[JJ] := Y[JJ]; ZL[JJ] := Z[JJ] e̲n̲d̲;
      g̲o̲t̲o̲ TEST
   e̲n̲d̲;
   i̲f̲ ¬LAST t̲h̲e̲n̲ D[2] := H; D[3] := X;
   f̲o̲r̲ JJ := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ D[JJ + 3] := Y[JJ]; D[N + JJ + 3] := Z[JJ] e̲n̲d̲
e̲n̲d̲ RK3N;
c̲o̲m̲m̲e̲n̲t̲ ================== 35120 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAN(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ U;
   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ OVERFLOW(X); c̲o̲d̲e̲ 30009;
   r̲e̲a̲l̲    p̲r̲o̲c̲e̲d̲u̲r̲e̲ GIANT;       c̲o̲d̲e̲ 30004;
   U := SIN(X)/COS(X);
   TAN := i̲f̲ OVERFLOW(U) t̲h̲e̲n̲ GIANT e̲l̲s̲e̲ U
e̲n̲d̲ TAN;
c̲o̲m̲m̲e̲n̲t̲ ================== 35111 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ SINH(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ AX, Y;
   AX := ABS(X);
   i̲f̲ AX < 0·3 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ Y := i̲f̲ AX < 0·1 t̲h̲e̲n̲ X × X e̲l̲s̲e̲ X × X/9;
      X :=  ((( 0·0001984540     × Y +
        0·0083333331783 ) × Y +
        0·16666666666675) × Y +
        1·0             ) × X ;
      SINH := i̲f̲ AX < 0·1 t̲h̲e̲n̲ X e̲l̲s̲e̲
        X × ( 1·0 + 0·14814814814815 × X × X )
   e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ AX < 17·5 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ AX := EXP( AX ); SINH := SIGN(X) × ·5 × ( AX -1/AX ) e̲n̲d̲
     e̲l̲s̲e̲ i̲f̲ AX > 742·36063037970 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ GIANT; c̲o̲d̲e̲ 30004;
      SINH := SIGN(X) × GIANT
   e̲n̲d̲ e̲l̲s̲e̲
     SINH := SIGN(X) × EXP(AX- ·69314 71805 59945)
e̲n̲d̲ SINH;
c̲o̲m̲m̲e̲n̲t̲ ================== 35115 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ARCCOSH(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
   ARCCOSH := i̲f̲ X ≤ 1 t̲h̲e̲n̲ 0 e̲l̲s̲e̲
     i̲f̲ X > @10 t̲h̲e̲n̲ 0·69314718055995 + LN(X) e̲l̲s̲e̲
     LN(X + SQRT((X-1) × (X + 1)));
c̲o̲m̲m̲e̲n̲t̲ ================== 35080 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EI(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ a̲r̲r̲a̲y̲ P, Q[0:7];
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHEPOLSER(N, X, A); c̲o̲d̲e̲ 31046;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ POL(N, X, A); c̲o̲d̲e̲ 31040;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ JFRAC(N, A, B); c̲o̲d̲e̲ 35083;

   i̲f̲ X > 24 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ P[0] := + 1·00000000000058   ; Q[1] := 1·99999999924131   ;
      P[1] := X-3·00000016782085   ; Q[2] := -2·99996432944446   ;
      P[2] := X-5·00140345515924   ; Q[3] := -7·90404992298926   ;
      P[3] := X-7·49289167792884   ; Q[4] := -4·31325836146628   ;
      P[4] := X-3·08336269051763@+1; Q[5] := 2·95999399486831@+2;
      P[5] := X-1·39381360364405   ; Q[6] := -6·74704580465832   ;
      P[6] := X + 8·91263822573708   ; Q[7] := 1·04745362652468@+3;
      P[7] := X-5·31686623494482@+1;
      EI := EXP(X) × (1 + JFRAC(7, Q, P)/X)/X
   e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ X > 12 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ P[0] := + 9·99994296074708@-1; Q[1] := 1·00083867402639   ;
      P[1] := X-1·95022321289660   ; Q[2] := -3·43942266899870   ;
      P[2] := X + 1·75656315469614   ; Q[3] := 2·89516727925135@+1;
      P[3] := X + 1·79601688769252@+1; Q[4] := 7·60761148007735@+2;
      P[4] := X-3·23467330305403@+1; Q[5] := 2·57776384238440@+1;
      P[5] := X-8·28561994140641   ; Q[6] := 5·72837193837324@+1;
      P[6] := X-1·86545454883399@+1; Q[7] := 6·95000655887434@+1;
      P[7] := X-3·48334653602853   ;
      EI := EXP(X) × JFRAC(7, Q, P)/X
   e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ X > 6 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ P[0] := + 1·00443109228078   ; Q[1] := 5·27468851962908@-1;
      P[1] := X-4·32531132878135@+1; Q[2] := 2·73624119889328@+3;
      P[2] := X + 6·01217990830080@+1; Q[3] := 1·43256738121938@+1;
      P[3] := X-3·31842531997221@+1; Q[4] := 1·00367439516726@+3;
      P[4] := X + 2·50762811293560@+1; Q[5] := -6·25041161671876   ;
      P[5] := X + 9·30816385662165   ; Q[6] := 3·00892648372915@+2;
      P[6] := X-2·19010233854880@+1; Q[7] := 3·93707701852715  ;
      P[7] := X-2·18086381520724   ;
      EI := EXP(X) × JFRAC(7, Q, P)/X
   e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ X > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ T, R, X0, XMX0;
      P[0] := -1·95773036904548@+8; Q[0] := -8·26271498626055@+7;
      P[1] := 3·89280421311201@+6; Q[1] := 8·91925767575612@+7;
      P[2] := -2·21744627758845@+7; Q[2] := -2·49033375740540@+7;
      P[3] := -1·19623669349247@+5; Q[3] := 4·28559624611749@+6;
      P[4] := -2·49301393458648@+5; Q[4] := -4·83547436162164@+5;
      P[5] := -4·21001615357070@+3; Q[5] := 3·57300298058508@+4;
      P[6] := -5·49142265521085@+2; Q[6] := -1·60708926587221@+3;
      P[7] := -8·66937339951070   ; Q[7] := 3·41718750000000@+1;
      X0 := ·372507410781367;
      T := X/3-1;
      R := CHEPOLSER(7, T, P)/CHEPOLSER(7, T, Q);
      XMX0 := (X-409576229586/1099511627776)-·767177250199394@-12;
      i̲f̲ ABS(XMX0) > ·037 t̲h̲e̲n̲ T := LN(X/X0) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Z, Z2;
         P[0] := ·837207933976075@+1; Q[0] := ·418603966988037@+1;
         P[1] := -·652268740837103@+1; Q[1] := -·465669026080814@+1;
         P[2] := ·569955700306720   ; Q[2] := ·1@+1;
         Z := XMX0/(X + X0); Z2 := Z × Z;
         T := Z × POL(2, Z2, P)/POL(2, Z2, Q)
      e̲n̲d̲;
      EI := T + XMX0 × R
   e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ X > -1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Y;
      P[0] := -4·41785471728217@+4; Q[0] := 7·65373323337614@+4;
      P[1] := 5·77217247139444@+4; Q[1] := 3·25971881290275@+4;
      P[2] := 9·93831388962037@+3; Q[2] := 6·10610794245759@+3;
      P[3] := 1·84211088668000@+3; Q[3] := 6·35419418378382@+2;
      P[4] := 1·01093806161906@+2; Q[4] := 3·72298352833327@+1;
      P[5] := 5·03416184097568   ; Q[5] := 1;
      Y := -X;
      EI := LN(Y)-POL(5, Y, P)/POL(5, Y, Q)
   e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ X > -4 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Y;
      P[0] := 8·67745954838444@-8; Q[0] := 1;
      P[1] := 9·99995519301390@-1; Q[1] := 1·28481935379157@+1;
      P[2] := 1·18483105554946@+1; Q[2] := 5·64433569561803@+1;
      P[3] := 4·55930644253390@+1; Q[3] := 1·06645183769914@+2;
      P[4] := 6·99279451291003@+1; Q[4] := 8·97311097125290@+1;
      P[5] := 4·25202034768841@+1; Q[5] := 3·14971849170441@+1;
      P[6] := 8·83671808803844   ; Q[6] := 3·79559003762122   ;
      P[7] := 4·01377664940665@-1; Q[7] := 9·08804569188869@-2;
      Y := -1/X;
      EI := -EXP(X) × POL(7, Y, P)/POL(7, Y, Q)
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Y;
      P[0] := -9·99999999998447@-1; Q[0] := 1;
      P[1] := -2·66271060431811@+1; Q[1] := 2·86271060422192@+1;
      P[2] := -2·41055827097015@+2; Q[2] := 2·92310039388533@+2;
      P[3] := -8·95927957772937@+2; Q[3] := 1·33278537748257@+3;
      P[4] := -1·29885688746484@+3; Q[4] := 2·77761949509163@+3;
      P[5] := -5·45374158883133@+2; Q[5] := 2·40401713225909@+3;
      P[6] := -5·66575206533869   ; Q[6] := 6·31657483280800@+2;
      Y := -1/X;
      EI := -EXP(X) × Y × (1 + Y × POL(6, Y, P)/POL(6, Y, Q))
   e̲n̲d̲
e̲n̲d̲ EI;
c̲o̲m̲m̲e̲n̲t̲ ================== 35086 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ENX(X, N1, N2, A);
   v̲a̲l̲u̲e̲ X, N1, N2;
   r̲e̲a̲l̲ X; i̲n̲t̲e̲g̲e̲r̲ N1, N2; a̲r̲r̲a̲y̲ A;
   i̲f̲ X ≤ 1·5 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲
      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ EI(X); c̲o̲d̲e̲ 35080;
      r̲e̲a̲l̲ W, E; i̲n̲t̲e̲g̲e̲r̲ I;
      W := -EI(-X);
      i̲f̲ N1 = 1 t̲h̲e̲n̲ A[1] := W;
      i̲f̲ N2 > 1 t̲h̲e̲n̲ E := EXP(-X);
      f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
         b̲e̲g̲i̲n̲
            W := (E - X × W)/(I - 1);
            i̲f̲ I ≥ N1 t̲h̲e̲n̲ A[I] := W
         e̲n̲d̲
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, N; r̲e̲a̲l̲ W, E, AN;
      N := ENTIER(X + ·5);
      i̲f̲ N ≤ 10 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ F, W1, T, H;
         r̲e̲a̲l̲ a̲r̲r̲a̲y̲ P[2:19];
         P[ 2] := ·37534261820491@-1; P[11] := ·135335283236613   ;
         P[ 3] := ·89306465560228@-2; P[12] := ·497870683678639@-1;
         P[ 4] := ·24233983686581@-2; P[13] := ·183156388887342@-1;
         P[ 5] := ·70576069342458@-3; P[14] := ·673794699908547@-2;
         P[ 6] := ·21480277819013@-3; P[15] := ·247875217666636@-2;
         P[ 7] := ·67375807781018@-4; P[16] := ·911881965554516@-3;
         P[ 8] := ·21600730159975@-4; P[17] := ·335462627902512@-3;
         P[ 9] := ·70411579854292@-5; P[18] := ·123409804086680@-3;
         P[10] := ·23253026570282@-5; P[19] := ·453999297624848@-4;
         F := W := P[N];
         E := P[N + 9];
         W1 := T := 1;
         H := X-N;
         f̲o̲r̲ I := N-1, I-1 w̲h̲i̲l̲e̲ ABS(W1) > @-15 × W d̲o̲
            b̲e̲g̲i̲n̲
               F := (E - I × F)/N;
               T := -H × T / (N-I);
               W1 := T × F; W := W + W1
            e̲n̲d̲
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲
         p̲r̲o̲c̲e̲d̲u̲r̲e̲ NONEXPENX(X, N1, N2, A); c̲o̲d̲e̲ 35087;
         a̲r̲r̲a̲y̲ B[N:N];
         NONEXPENX(X, N, N, B);
         W := B[N] × EXP(-X)
      e̲n̲d̲;
      i̲f̲ N1 = N2 ∧ N1 = N t̲h̲e̲n̲ A[N] := W e̲l̲s̲e̲
      b̲e̲g̲i̲n̲
         E := EXP(-X);
         AN := W;
         i̲f̲ N ≤ N2 ∧ N ≥ N1 t̲h̲e̲n̲ A[N] := W;
         f̲o̲r̲ I := N-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ N1 d̲o̲
            b̲e̲g̲i̲n̲
               W := (E - I × W)/X;
               i̲f̲ I ≤ N2 t̲h̲e̲n̲ A[I] := W
            e̲n̲d̲;
         W := AN;
         f̲o̲r̲ I := N + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
            b̲e̲g̲i̲n̲
               W := (E - X × W)/(I - 1);
               i̲f̲ I ≥ N1 t̲h̲e̲n̲ A[I] := W
            e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ ENX;
c̲o̲m̲m̲e̲n̲t̲ ================== 35087 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ NONEXPENX(X, N1, N2, A);
   v̲a̲l̲u̲e̲ X, N1, N2;
   r̲e̲a̲l̲ X; i̲n̲t̲e̲g̲e̲r̲ N1, N2; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, N; r̲e̲a̲l̲ W, AN;
   N := i̲f̲ X ≤ 1·5 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ ENTIER(X + ·5);
   i̲f̲ N ≤ 10 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ ENX(X, N1, N2, A); c̲o̲d̲e̲ 35086;
      a̲r̲r̲a̲y̲ B[N:N];
      ENX(X, N, N, B);
      W := B[N] × EXP(X)
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲
      i̲n̲t̲e̲g̲e̲r̲ K, K1;
      r̲e̲a̲l̲ UE, VE, WE, WE1, UO, VO, WO, WO1, R, S;
      UE := 1; VE := WE := 1/(X + N); WE1 := 0;
      UO := 1; VO := -N/(X × (X + N + 1)); WO1 := 1/X; WO := VO + WO1;
      W := (WE + WO)/2;
      K1 := 1;
      f̲o̲r̲ K := K1 w̲h̲i̲l̲e̲ WO-WE > @-15 × W ∧ WE > WE1 ∧ WO < WO1 d̲o̲
         b̲e̲g̲i̲n̲
            WE1 := WE; WO1 := WO;
            R := N + K; S := R + X + K;
            UE := 1/(1-K × (R-1) × UE/((S-2) × S));
            UO := 1/(1-K ×  R  × UO/( S × S-1));
            VE := VE × (UE-1);
            VO := VO × (UO-1);
            WE := WE + VE;
            WO := WO + VO;
            W := (WE + WO)/2;
            K1 := K1 + 1
         e̲n̲d̲
   e̲n̲d̲;
   AN := W;
   i̲f̲ N ≤ N2 ∧ N ≥ N1 t̲h̲e̲n̲ A[N] := W;
   f̲o̲r̲ I := N-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ N1 d̲o̲
      b̲e̲g̲i̲n̲
         W := (1 - I × W)/X;
         i̲f̲ I ≤ N2 t̲h̲e̲n̲ A[I] := W
      e̲n̲d̲;
   W := AN;
   f̲o̲r̲ I := N + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N2 d̲o̲
      b̲e̲g̲i̲n̲
         W := (1 - X × W)/(I - 1);
         i̲f̲ I ≥ N1 t̲h̲e̲n̲ A[I] := W
      e̲n̲d̲
e̲n̲d̲ EXPENX;
c̲o̲m̲m̲e̲n̲t̲ ================== 35084 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SINCOSINT(X, SI, CI); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, SI, CI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ABSX, Z, F, G;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SINCOSFG(X, F, G); c̲o̲d̲e̲ 35085;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHEPOLSER(N, X, A); c̲o̲d̲e̲ 31046;

   ABSX := ABS(X);
   i̲f̲ ABSX ≤ 4 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ a̲r̲r̲a̲y̲ A[0:10]; r̲e̲a̲l̲ Z2;
      A[0] := + 2·7368706803630@+00; A[1] := -1·1106314107894@+00;
      A[2] := + 1·4176562194666@-01; A[3] := -1·0252652579174@-02;
      A[4] := + 4·6494615619880@-04; A[5] := -1·4361730896642@-05;
      A[6] := + 3·2093684948229@-07; A[7] := -5·4251990770162@-09;
      A[8] := + 7·1776288639895@-11; A[9] := -7·6335493723482@-13;
      A[10] := + 6·6679958346983@-15;
      Z := X / 4; Z2 := Z × Z; G := Z2 + Z2 - 1;
      SI := Z × CHEPOLSER(10, G, A);
      A[0] := + 2·9659601400727@+00; A[1] := -9·4297198341830@-01;
      A[2] := + 8·6110342738169@-02; A[3] := -4·7776084547139@-03;
      A[4] := + 1·7529161205146@-04; A[5] := -4·5448727803752@-06;
      A[6] := + 8·7515839180060@-08; A[7] := -1·2998699938109@-09;
      A[8] := + 1·5338974898831@-11; A[9] := -1·4724256070277@-13;
      A[10] := + 1·1721420798429@-15;
      CI := ·577215664901533 + LN(ABSX) - Z2 × CHEPOLSER(10, G, A)
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ CX, SX;
      SINCOSFG(X, F, G);
      CX := COS(X); SX := SIN(X);
      SI := 1·570796326794897; i̲f̲ X < 0 t̲h̲e̲n̲ SI := -SI;
      SI := SI - F × CX - G × SX;
      CI := F × SX - G × CX
   e̲n̲d̲
e̲n̲d̲ SINCOSINT;
c̲o̲m̲m̲e̲n̲t̲ ================== 35085 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ SINCOSFG(X, F, G); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, F, G;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ABSX, SI, CI;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SINCOSINT(X, SI, CI); c̲o̲d̲e̲ 35084;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHEPOLSER(N, X, A); c̲o̲d̲e̲ 31046;

   ABSX := ABS(X);
   i̲f̲ ABSX ≤ 4 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ CX, SX;
      SINCOSINT(X, SI, CI);
      CX := COS(X); SX := SIN(X); SI := SI - 1·570796326794897;
      F := CI × SX - SI × CX;
      G := -CI × CX - SI × SX
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ a̲r̲r̲a̲y̲ A[0:23];
      A[0] := + 9·6578828035185@-01; A[1] := -4·3060837778597@-02;
      A[2] := -7·3143711748104@-03; A[3] := + 1·4705235789868@-03;
      A[4] := -9·8657685732702@-05; A[5] := -2·2743202204655@-05;
      A[6] := + 9·8240257322526@-06; A[7] := -1·8973430148713@-06;
      A[8] := + 1·0063435941558@-07; A[9] := + 8·0819364822241@-08;
      A[10] := -3·8976282875288@-08; A[11] := + 1·0335650325497@-08;
      A[12] := -1·4104344875897@-09; A[13] := -2·5232078399683@-10;
      A[14] := + 2·5699831325961@-10; A[15] := -1·0597889253948@-10;
      A[16] := + 2·8970031570214@-11; A[17] := -4·1023142563083@-12;
      A[18] := -1·0437693730018@-12; A[19] := + 1·0994184520547@-12;
      A[20] := -5·2214239401679@-13; A[21] := + 1·7469920787829@-13;
      A[22] := -3·8470012979279@-14;
      F := CHEPOLSER(22, 8/ABSX-1, A) / X;
      A[0] := + 2·2801220638241@-01; A[1] := -2·6869727411097@-02;
      A[2] := -3·5107157280958@-03; A[3] := + 1·2398008635186@-03;
      A[4] := -1·5672945116862@-04; A[5] := -1·0664141798094@-05;
      A[6] := + 1·1170629343574@-05; A[7] := -3·1754011655614@-06;
      A[8] := + 4·4317473520398@-07; A[9] := + 5·5108696874463@-08;
      A[10] := -5·9243078711743@-08; A[11] := + 2·2102573381555@-08;
      A[12] := -5·0256827540623@-09; A[13] := + 3·1519168259424@-10;
      A[14] := + 3·6306990848979@-10; A[15] := -2·2974764234591@-10;
      A[16] := + 8·5530309424048@-11; A[17] := -2·1183067724443@-11;
      A[18] := + 1·7133662645092@-12; A[19] := + 1·7238877517248@-12;
      A[20] := -1·2930281366811@-12; A[21] := + 5·7472339223731@-13;
      A[22] := -1·8415468268314@-13; A[23] := + 3·5937256571434@-14;
      G := 4 × CHEPOLSER(23, 8/ABSX-1, A) / ABSX /ABSX
   e̲n̲d̲
e̲n̲d̲ SINCOSFG;
c̲o̲m̲m̲e̲n̲t̲ ================== 35060 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ RECIP GAMMA(X, ODD, EVEN);
   v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, ODD, EVEN;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ ALFA, BETA, X2;
   a̲r̲r̲a̲y̲ B[1:12];
   B[ 1] := -·28387 65422 76024; B[ 2] := -·07685 28408 44786;
   B[ 3] := + ·00170 63050 71096; B[ 4] := + ·00127 19271 36655;
   B[ 5] := + ·00007 63095 97586; B[ 6] := -·00000 49717 36704;
   B[ 7] := -·00000 08659 20800; B[ 8] := -·00000 00331 26120;
   B[ 9] := + ·00000 00017 45136; B[10] := + ·00000 00002 42310;
   B[11] := + ·00000 00000 09161; B[12] := -·00000 00000 00170;
   X2 := X × X × 8;
   ALFA := -·00000 00000 00001; BETA := 0;
   f̲o̲r̲ I := 12 s̲t̲e̲p̲ - 2 u̲n̲t̲i̲l̲ 2 d̲o̲
      b̲e̲g̲i̲n̲ BETA := -(ALFA × 2 + BETA); ALFA := - BETA × X2 - ALFA + B[I]
      e̲n̲d̲;
   EVEN := (BETA / 2 + ALFA) × X2 - ALFA + ·92187 02936 50453;
   ALFA := -·00000 00000 00034; BETA := 0;
   f̲o̲r̲ I := 11 s̲t̲e̲p̲ - 2 u̲n̲t̲i̲l̲ 1 d̲o̲
      b̲e̲g̲i̲n̲ BETA := -(ALFA × 2 + BETA); ALFA := - BETA × X2 - ALFA + B[I]
      e̲n̲d̲;
   ODD := (ALFA + BETA) × 2;
   RECIP GAMMA := ODD × X + EVEN
e̲n̲d̲ RECIP GAMMA;
c̲o̲m̲m̲e̲n̲t̲ ================== 35061 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ GAMMA(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Y, S, F, G, ODD, EVEN;
   B̲o̲o̲l̲e̲a̲n̲ INV;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ RECIP GAMMA(X, ODD, EVEN);
      v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, ODD, EVEN;
      c̲o̲d̲e̲ 35060;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOG GAMMA(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
      c̲o̲d̲e̲ 35062;
   i̲f̲ X < ·5 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ Y := X - ENTIER(X / 2) × 2; S := 3·14159 26535 8979;
      i̲f̲ Y ≥ 1 t̲h̲e̲n̲ b̲e̲g̲i̲n̲ S := - S; Y := 2 - Y e̲n̲d̲;
      i̲f̲ Y ≥ ·5 t̲h̲e̲n̲ Y := 1 - Y; INV := t̲r̲u̲e̲; X := 1 - X;
      F := S / SIN(3·14159 26535 8979 × Y)
   e̲n̲d̲
   e̲l̲s̲e̲ INV := f̲a̲l̲s̲e̲;
   i̲f̲ X > 22 t̲h̲e̲n̲ G := EXP(LOG GAMMA(X)) e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ S := 1;
      NEXT: i̲f̲ X > 1·5 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ X := X - 1; S := S × X; g̲o̲t̲o̲ NEXT e̲n̲d̲;
      G := S / RECIP GAMMA(1 - X, ODD, EVEN)
   e̲n̲d̲;
   GAMMA := i̲f̲ INV t̲h̲e̲n̲ F / G e̲l̲s̲e̲ G
e̲n̲d̲ GAMMA;
c̲o̲m̲m̲e̲n̲t̲ ================== 35062 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOG GAMMA(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
   i̲f̲ X > 13 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, X2;
      R := 1;
      NEXT: i̲f̲ X ≤ 22 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ R := R / X; X := X + 1; g̲o̲t̲o̲ NEXT e̲n̲d̲;
      X2 := - 1 / (X × X); R := LN(R);
      LOG GAMMA := LN(X) × (X - ·5) - X + R + ·91893 85332 04672 +
        (((·59523 80952 38095@-3  × X2 + ·79365 07936 50794@-3) × X2 +·27777 77777 77778@-2) × X2 + ·83333 33333 33333@-1) / X
   e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ Y, F, U0, U1, U, Z;
      i̲n̲t̲e̲g̲e̲r̲ I;
      a̲r̲r̲a̲y̲ B[1:18];
      F := 1; U0 := U1 := 0;
      B[ 1] := -·07611 41616 704358; B[ 2] := + ·00843 23249 659328;
      B[ 3] := -·00107 94937 263286; B[ 4] := + ·00014 90074 800369;
      B[ 5] := -·00002 15123 998886; B[ 6] := + ·00000 31979 329861;
      B[ 7] := -·00000 04851 693012; B[ 8] := + ·00000 00747 148782;
      B[ 9] := -·00000 00116 382967; B[10] := + ·00000 00018 294004;
      B[11] := -·00000 00002 896918; B[12] := + ·00000 00000 461570;
      B[13] := -·00000 00000 073928; B[14] := + ·00000 00000 011894;
      B[15] := -·00000 00000 001921; B[16] := + ·00000 00000 000311;
      B[17] := -·00000 00000 000051; B[18] := + ·00000 00000 000008;
      i̲f̲ X < 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ F := 1 / X; X := X + 1 e̲n̲d̲
        e̲l̲s̲e̲
      NEXT: i̲f̲ X > 2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ X := X - 1; F := F × X; g̲o̲t̲o̲ NEXT e̲n̲d̲;
      F := LN(F); Y := X + X - 3; Z := Y + Y;
      f̲o̲r̲ I := 18 s̲t̲e̲p̲ - 1 u̲n̲t̲i̲l̲ 1 d̲o̲
         b̲e̲g̲i̲n̲ U := U0; U0 := Z × U0 + B[I] - U1; U1 := U e̲n̲d̲;
      LOG GAMMA := (U0 × Y + ·49141 53930 29387 - U1) × (X - 1) × (X - 2)
        + F
   e̲n̲d̲ LOG GAMMA;
c̲o̲m̲m̲e̲n̲t̲ ================== 35030 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ INCOMGAM(X, A, KLGAM, GRGAM, GAM, EPS);
   v̲a̲l̲u̲e̲ X, A, EPS; r̲e̲a̲l̲ X, A, KLGAM, GRGAM, GAM, EPS;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ C0, C1, C2, D0, D1, D2, X2, AX, P, Q, R, S, R1, R2, SCF;
   i̲n̲t̲e̲g̲e̲r̲ N;
   S := EXP(-X + A × LN(X)); SCF := @+300;
   i̲f̲ X ≤ (i̲f̲ A < 3 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ A) t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ X2 := X × X; AX := A × X; D0 := 1; P := A; C0 := S;
      D1 := (A + 1) × (A + 2-X); C1 := ((A + 1) × (A + 2) + X) × S;
      R2 := C1/D1;
      f̲o̲r̲ N := 1, N + 1 w̲h̲i̲l̲e̲ ABS((R2-R1)/R2) > EPS d̲o̲
         b̲e̲g̲i̲n̲ P := 2 + P; Q := (P + 1) × (P × (P + 2)-AX);
            R := N × (N + A) × (P + 2) × X2;
            C2 := (Q × C1 + R × C0)/P; D2 := (Q × D1 + R × D0)/P;
            R1 := R2; R2 := C2/D2;
            C0 := C1; C1 := C2; D0 := D1; D1 := D2;
            i̲f̲ ABS(C1) > SCF ∨ ABS(D1) > SCF t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ C0 := C0/SCF; C1 := C1/SCF;
               D0 := D0/SCF; D1 := D1/SCF
            e̲n̲d̲
         e̲n̲d̲; KLGAM := R2/A; GRGAM := GAM - KLGAM
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ C0 := A × S; C1 := (1 + X) × C0; Q := X + 2  - A;
      D0 := X; D1 := X × Q; R2 := C1/D1;
      f̲o̲r̲ N := 1, N + 1 w̲h̲i̲l̲e̲ ABS((R2-R1)/R2) > EPS d̲o̲
         b̲e̲g̲i̲n̲     Q := 2 + Q; R := N × (N + 1-A);
            C2 := Q × C1-R × C0; D2 := Q × D1-R × D0;
            R1 := R2; R2 := C2/D2;
            C0 := C1; C1 := C2; D0 := D1; D1 := D2;
            i̲f̲ ABS(C1) > SCF ∨ ABS(D1) > SCF t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ C0 := C0/SCF; C1 := C1/SCF;
               D0 := D0/SCF; D1 := D1/SCF
            e̲n̲d̲
         e̲n̲d̲; GRGAM := R2/A; KLGAM := GAM - GRGAM
   e̲n̲d̲
e̲n̲d̲ INCOMGAM;
c̲o̲m̲m̲e̲n̲t̲ ================== 35050 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INCBETA(X, P, Q, EPS);
   v̲a̲l̲u̲e̲ X, P, Q, EPS; r̲e̲a̲l̲ X, P, Q, EPS;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, N; r̲e̲a̲l̲ G, F, FN, FN1, FN2, GN, GN1, GN2, DN, PQ;
   B̲o̲o̲l̲e̲a̲n̲ N EVEN, RECUR;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ GAMMA(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
      c̲o̲d̲e̲  35061;

   i̲f̲ X = 0 ∨ X = 1 t̲h̲e̲n̲ INCBETA := X e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ X > ·5 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ F := P; P := Q; Q := F; X := 1-X; RECUR := t̲r̲u̲e̲ e̲n̲d̲
      e̲l̲s̲e̲ RECUR := f̲a̲l̲s̲e̲;
      G := FN2 := 0; M := 0; PQ := P + Q; F := FN1 := GN1 := GN2 := 1;
      N EVEN := f̲a̲l̲s̲e̲;
      f̲o̲r̲ N := 1, N + 1 w̲h̲i̲l̲e̲ ABS((F-G)/F) > EPS d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ N EVEN t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ M := M + 1; DN := M × X × (Q-M)/(P + N-1)/(P + N) e̲n̲d̲
            e̲l̲s̲e̲ DN := -X × (P + M) × (PQ + M)/(P + N-1)/(P + N);
            G := F; FN := FN1 + DN × FN2; GN := GN1 + DN × GN2;
            N EVEN := ¬N EVEN; F := FN/GN;
            FN2 := FN1; FN1 := FN; GN2 := GN1; GN1 := GN
         e̲n̲d̲;
      F := F × X⭡P × (1-X)⭡Q × GAMMA(P + Q)/GAMMA(P + 1)/GAMMA(Q);
      i̲f̲ RECUR t̲h̲e̲n̲ F := 1-F;
      INCBETA := F
   e̲n̲d̲
e̲n̲d̲ INCBETA;
c̲o̲m̲m̲e̲n̲t̲ ================== 35051 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ IBPPLUSN(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
   i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, EPS; a̲r̲r̲a̲y̲ I;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ IXQFIX(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
      r̲e̲a̲l̲ X, P, Q, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35053;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ IXPFIX(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
      r̲e̲a̲l̲ X, P, Q, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35054;

   i̲f̲ X = 0 ∨ X = 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ N := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲ I[N] := X e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ X ≤ ·5 t̲h̲e̲n̲ IXQFIX(X, P, Q, NMAX, EPS, I) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ IXPFIX(1-X, Q, P, NMAX, EPS, I);
         f̲o̲r̲ N := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲ I[N] := 1-I[N]
      e̲n̲d̲
   e̲n̲d̲
e̲n̲d̲ IBPPLUSN;
c̲o̲m̲m̲e̲n̲t̲ ================== 35052 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ IBQPLUSN(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
   i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, EPS; a̲r̲r̲a̲y̲ I;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ N;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ IXQFIX(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
      r̲e̲a̲l̲ X, P, Q, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35053;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ IXPFIX(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
      r̲e̲a̲l̲ X, P, Q, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35054;

   i̲f̲ X = 0 ∨ X = 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ N := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲ I[N] := X e̲n̲d̲
   e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ X ≤ ·5 t̲h̲e̲n̲ IXPFIX(X, P, Q, NMAX, EPS, I) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ IXQFIX(1-X, Q, P, NMAX, EPS, I);
         f̲o̲r̲ N := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲ I[N] := 1-I[N]
      e̲n̲d̲
   e̲n̲d̲
e̲n̲d̲ IBQPLUSN;
c̲o̲m̲m̲e̲n̲t̲ ================== 35053 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ IXQFIX(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
   r̲e̲a̲l̲ X, P, Q, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; a̲r̲r̲a̲y̲ I;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, MMAX; r̲e̲a̲l̲ S, IQ0, IQ1, Q0;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INCBETA(X, P, Q, EPS);
      v̲a̲l̲u̲e̲ X, P, Q, EPS; r̲e̲a̲l̲ X, P, Q, EPS;
      c̲o̲d̲e̲ 35050;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD(X, P, Q, I0, I1, NMAX, I);
      v̲a̲l̲u̲e̲ X, P, Q, I0, I1, NMAX; i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, I0, I1;
        a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35055;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD(X, P, Q, I0, NMAX, EPS, I);
      v̲a̲l̲u̲e̲ X, P, Q, I0, NMAX, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, I0, EPS;
        a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35056;

   M := ENTIER(Q); S := Q-M; Q0 := i̲f̲ S > 0 t̲h̲e̲n̲ S e̲l̲s̲e̲ S + 1;
   MMAX := i̲f̲ S > 0 t̲h̲e̲n̲ M e̲l̲s̲e̲ M-1;
   IQ0 := INCBETA(X, P, Q0, EPS);
   i̲f̲ MMAX > 0 t̲h̲e̲n̲ IQ1 := INCBETA(X, P, Q0 + 1, EPS);
   b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ IQ[0:MMAX];
      FORWARD(X, P, Q0, IQ0, IQ1, MMAX, IQ);
      BACKWARD(X, P, Q, IQ[MMAX], NMAX, EPS, I)
   e̲n̲d̲
e̲n̲d̲ IXQFIX;
c̲o̲m̲m̲e̲n̲t̲ ================== 35054 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ IXPFIX(X, P, Q, NMAX, EPS, I); v̲a̲l̲u̲e̲ X, P, Q, NMAX, EPS;
   r̲e̲a̲l̲ X, P, Q, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; a̲r̲r̲a̲y̲ I;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, MMAX; r̲e̲a̲l̲ S, P0, I0, I1, IQ0, IQ1;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INCBETA(X, P, Q, EPS);
      v̲a̲l̲u̲e̲ X, P, Q, EPS; r̲e̲a̲l̲ X, P, Q, EPS;
      c̲o̲d̲e̲ 35050;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD(X, P, Q, I0, I1, NMAX, I);
      v̲a̲l̲u̲e̲ X, P, Q, I0, I1, NMAX; i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, I0, I1;
        a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35055;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD(X, P, Q, I0, NMAX, EPS, I);
      v̲a̲l̲u̲e̲ X, P, Q, I0, NMAX, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, I0, EPS;
        a̲r̲r̲a̲y̲ I;
      c̲o̲d̲e̲ 35056;

   M := ENTIER(P); S := P-M; P0 := i̲f̲ S > 0 t̲h̲e̲n̲ S e̲l̲s̲e̲ S + 1;
   MMAX := i̲f̲ S > 0 t̲h̲e̲n̲ M e̲l̲s̲e̲ M-1;
   I0 := INCBETA(X, P0, Q, EPS);
   I1 := INCBETA(X, P0, Q + 1, EPS);
   b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ IP[0:MMAX];
      BACKWARD(X, P0, Q, I0, MMAX, EPS, IP); IQ0 := IP[MMAX];
      BACKWARD(X, P0, Q + 1, I1, MMAX, EPS, IP); IQ1 := IP[MMAX]
   e̲n̲d̲;
   FORWARD(X, P, Q, IQ0, IQ1, NMAX, I)
e̲n̲d̲ IXPFIX;
c̲o̲m̲m̲e̲n̲t̲ ================== 35055 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD(X, P, Q, I0, I1, NMAX, I);
   v̲a̲l̲u̲e̲ X, P, Q, I0, I1, NMAX; i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, I0, I1;
     a̲r̲r̲a̲y̲ I;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, N; r̲e̲a̲l̲ Y, R, S;
   I[0] := I0; i̲f̲ NMAX > 0 t̲h̲e̲n̲ I[1] := I1;
   M := NMAX-1; R := P + Q-1; Y := 1-X;
   f̲o̲r̲ N := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲
      b̲e̲g̲i̲n̲ S := (N + R) × Y;
         I[N + 1] := ((N + Q + S) × I[N]-S × I[N-1])/(N + Q)
      e̲n̲d̲
e̲n̲d̲ FORWARD;
c̲o̲m̲m̲e̲n̲t̲ ================== 35056 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD(X, P, Q, I0, NMAX, EPS, I);
   v̲a̲l̲u̲e̲ X, P, Q, I0, NMAX, EPS; i̲n̲t̲e̲g̲e̲r̲ NMAX; r̲e̲a̲l̲ X, P, Q, I0, EPS;
     a̲r̲r̲a̲y̲ I;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, N, NU; r̲e̲a̲l̲ R, PQ, Y, LOGX;
   a̲r̲r̲a̲y̲ IAPPROX[0:NMAX];
   I[0] := I0; i̲f̲ NMAX > 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ N := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲ IAPPROX[N] := 0;
      PQ := P + Q-1; LOGX := LN(X);
      R := NMAX + (LN(EPS) + Q × LN(NMAX))/LOGX;
      NU := ENTIER(R-Q × LN(R)/LOGX);
      L1:     N := NU; R := X;
      L2:     Y := (N + PQ) × X; R := Y/(Y + (N + P) × (1-R));
      i̲f̲ N ≤ NMAX t̲h̲e̲n̲ I[N] := R; N := N-1;
      i̲f̲ N ≥ 1 t̲h̲e̲n̲ g̲o̲t̲o̲ L2; R := I0;
      f̲o̲r̲ N := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲ R := I[N] := I[N] × R;
      f̲o̲r̲ N := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲
           i̲f̲ ABS((I[N]-IAPPROX[N])/I[N]) > EPS t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ M := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NMAX d̲o̲
                 IAPPROX[M] := I[M]; NU := NU + 5; g̲o̲t̲o̲ L1
         e̲n̲d̲
   e̲n̲d̲
e̲n̲d̲ BACKWARD;
c̲o̲m̲m̲e̲n̲t̲ ================== 34150 ================= ;
B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ZEROIN(X, Y, FX, TOLX);
   r̲e̲a̲l̲ X, Y, FX, TOLX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ EXT;
   r̲e̲a̲l̲ C, FC, B, FB, A, FA, D, FD, FDB, FDA, W, MB,
     TOL, M, P, Q, DW;
   DW := DWARF; B := X; FB := FX; A := X := Y; FA := FX;
   INTERPOLATE: C := A; FC := FA; EXT := 0;
   EXTRAPOLATE: i̲f̲ ABS(FC) < ABS(FB) t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ C ≠ A t̲h̲e̲n̲ b̲e̲g̲i̲n̲ D := A; FD := FA e̲n̲d̲;
      A := B; FA := FB; B := X := C; FB := FC; C := A; FC := FA
   e̲n̲d̲ INTERCHANGE;
   TOL := TOLX; M := (C + B) × 0·5; MB := M - B;
   i̲f̲ ABS(MB) > TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ EXT > 2 t̲h̲e̲n̲ W := MB e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ TOL := TOL × SIGN(MB);
         P := (B - A) × FB; i̲f̲ EXT ≤ 1 t̲h̲e̲n̲
           Q := FA - FB e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ FDB := (FD - FB) / (D - B);
            FDA := (FD - FA) / (D - A);
            P := FDA × P; Q := FDB × FA - FDA × FB
         e̲n̲d̲; i̲f̲ P < 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ P := -P; Q := -Q e̲n̲d̲;
         W := i̲f̲ P < DW ∨ P ≤ Q × TOL t̲h̲e̲n̲ TOL e̲l̲s̲e̲
           i̲f̲ P < MB × Q t̲h̲e̲n̲ P / Q e̲l̲s̲e̲ MB
      e̲n̲d̲; D := A; FD := FA; A := B; FA := FB;
      X := B := B + W; FB := FX;
      i̲f̲ (i̲f̲ FC ≥ 0 t̲h̲e̲n̲ FB ≥ 0 e̲l̲s̲e̲ FB ≤ 0) t̲h̲e̲n̲
        g̲o̲t̲o̲ INTERPOLATE e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ EXT := i̲f̲ W = MB t̲h̲e̲n̲ 0 e̲l̲s̲e̲ EXT + 1;
         g̲o̲t̲o̲ EXTRAPOLATE
      e̲n̲d̲
   e̲n̲d̲; Y := C;
   ZEROIN := i̲f̲ FC ≥ 0 t̲h̲e̲n̲ FB ≤ 0 e̲l̲s̲e̲ FB ≥ 0
e̲n̲d̲ ZEROIN;
c̲o̲m̲m̲e̲n̲t̲ ================== 34440 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ MARQUARDT(M, N, PAR, G, V, FUNCT, JACOBIAN, IN, OUT);
   v̲a̲l̲u̲e̲ M, N; i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ PAR, G, V, IN, OUT;
     B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT; p̲r̲o̲c̲e̲d̲u̲r̲e̲ JACOBIAN;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ MAXFE, FE, IT, I, J, ERR;
   r̲e̲a̲l̲ VV, WW, W, MU, RES, FPAR, FPARPRES, LAMBDA, LAMBDAMIN,
     P, PW, RELTOLRES, ABSTOLRES;
   a̲r̲r̲a̲y̲ EM[0:7], VAL, B, BB, PARPRES[1:N], JAC[1:M, 1:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULCOL(L, U, S, T, A, B, X); c̲o̲d̲e̲ 31022;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, S, A, B); c̲o̲d̲e̲ 31030;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, S, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, S, A, B); c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, S, A, B); c̲o̲d̲e̲ 34012;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, S, T, A, B); c̲o̲d̲e̲ 34015;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM);
      c̲o̲d̲e̲ 34273;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCFUNCT(M, N, PAR, G);
      i̲n̲t̲e̲g̲e̲r̲ M, N; a̲r̲r̲a̲y̲ PAR, G;
   b̲e̲g̲i̲n̲ FE := FE + 1; i̲f̲ FE ≥ MAXFE t̲h̲e̲n̲ ERR := 1 e̲l̲s̲e̲
      i̲f̲ ¬FUNCT(M, N, PAR, G) t̲h̲e̲n̲ ERR := 2;
      i̲f̲ ERR ≠ 0 t̲h̲e̲n̲ g̲o̲t̲o̲ EXIT
   e̲n̲d̲ LOCFUNCT;

   VV := 10; W := 0·5; MU := 0·01;
   WW := (i̲f̲ IN[6] < @-7 t̲h̲e̲n̲ @-8 e̲l̲s̲e̲ @-1 × IN[6]);
   EM[0] := EM[2] := EM[6] := IN[0]; EM[4] := 10 × N;
   RELTOLRES := IN[3]; ABSTOLRES := IN[4]⭡2; MAXFE := IN[5];
   ERR := 0; FE := IT := 1; P := FPAR := RES := 0;
   PW := -LN(WW × IN[0])/2·30;

   i̲f̲ ¬FUNCT(M, N, PAR, G) t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ ERR := 3; g̲o̲t̲o̲ ESCAPE e̲n̲d̲;
   FPAR := VECVEC(1, M, 0, G, G); OUT[3] := SQRT(FPAR);

   f̲o̲r̲ IT := 1, IT + 1 w̲h̲i̲l̲e̲ FPAR > ABSTOLRES ∧
     RES > RELTOLRES × FPAR + ABSTOLRES d̲o̲
      b̲e̲g̲i̲n̲ JACOBIAN(M, N, PAR, G, JAC, LOCFUNCT);
         I := QRISNGVALDEC(JAC, M, N, VAL, V, EM);
         i̲f̲ IT = 1 t̲h̲e̲n̲
           LAMBDA := IN[6] × VECVEC(1, N, 0, VAL, VAL) e̲l̲s̲e̲
         i̲f̲ P = 0 t̲h̲e̲n̲ LAMBDA := LAMBDA × W e̲l̲s̲e̲ P := 0;

         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              B[I] := VAL[I] × TAMVEC(1, M, I, JAC, G);

         L:   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              BB[I] := B[I]/(VAL[I] × VAL[I] + LAMBDA);
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
              PARPRES[I] := PAR[I] - MATVEC(1, N, I, V, BB);
         LOCFUNCT(M, N, PARPRES, G);
         FPARPRES := VECVEC(1, M, 0, G, G);
         RES := FPAR-FPARPRES;
         i̲f̲ RES < MU × VECVEC(1, N, 0, B, BB) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ P := P + 1; LAMBDA := VV × LAMBDA;
            i̲f̲ P = 1 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ LAMBDAMIN := WW × VECVEC(1, N, 0, VAL, VAL);
               i̲f̲ LAMBDA < LAMBDAMIN t̲h̲e̲n̲ LAMBDA := LAMBDAMIN
            e̲n̲d̲;
            i̲f̲ P < PW t̲h̲e̲n̲ g̲o̲t̲o̲ L e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ ERR := 4;
               g̲o̲t̲o̲ EXIT
            e̲n̲d̲;
         e̲n̲d̲;

         DUPVEC(1, N, 0, PAR, PARPRES);
         FPAR := FPARPRES
      e̲n̲d̲ ITERATION;

   EXIT:
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        MULCOL(1, N, I, I, JAC, V, 1/(VAL[I] + IN[0]));
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ I d̲o̲
           V[I, J] := V[J, I] := MATTAM(1, N, I, J, JAC, JAC);

   LAMBDA := LAMBDAMIN := VAL[1];
   f̲o̲r̲ I := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        i̲f̲ VAL[I] > LAMBDA    t̲h̲e̲n̲ LAMBDA   := VAL[I] e̲l̲s̲e̲
      i̲f̲ VAL[I] < LAMBDAMIN t̲h̲e̲n̲ LAMBDAMIN := VAL[I];

   OUT[7] := (LAMBDA/(LAMBDAMIN + IN[0]))⭡2;
   OUT[2] := SQRT(FPAR);
   OUT[6] := SQRT(RES + FPAR)-OUT[2];
   ESCAPE:
   OUT[4] := FE;
   OUT[5] := IT-1;
   OUT[1] := ERR
e̲n̲d̲ MARQUARDT;
c̲o̲m̲m̲e̲n̲t̲ ================== 33135 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ IMPEX (N, T0, TEND, Y0, DERIV, AVAILABLE, H0, HMAX,
  PRESCH, EPS, WEIGHTS, UPDATE, FAIL, CONTROL);
   v̲a̲l̲u̲e̲ N;
   i̲n̲t̲e̲g̲e̲r̲ N;
     r̲e̲a̲l̲ T0, TEND, H0, HMAX, EPS;
     B̲o̲o̲l̲e̲a̲n̲ PRESCH, FAIL;
     a̲r̲r̲a̲y̲ Y0, WEIGHTS;
     B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ AVAILABLE;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIV, UPDATE, CONTROL;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, K, ECI;
   r̲e̲a̲l̲ T, T1, T2, T3, TP, H, H2, HNEW, ALF, LQ;
   a̲r̲r̲a̲y̲ Y, Z, S1, S2, S3, U1, U3, W1, W2, W3, EHR[1:N], R, RF[1:5, 1:N],
     ERR[1:3], A1, A2[1:N, 1:N];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ PS1, PS2[1:N];
   B̲o̲o̲l̲e̲a̲n̲ START, TWO, HALV;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X);             c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIMAT(LR, UR, LC, UC, A, X);     c̲o̲d̲e̲ 31011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULVEC(L, U, SHIFT, A, B, X);     c̲o̲d̲e̲ 31020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULROW(L, U, I, J, A, B, X);       c̲o̲d̲e̲ 31021;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, SHIFT, A, B);       c̲o̲d̲e̲ 31030;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPROWVEC(L, U, I, A, B);        c̲o̲d̲e̲ 31032;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPMAT(L, U, I, J, A, B);         c̲o̲d̲e̲ 31035;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, SHIFT, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B);    c̲o̲d̲e̲ 34011;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B);  c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, SHIFT, A, B, X);     c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROW(L, U, I, J, A, B, X);       c̲o̲d̲e̲ 34024;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P);              c̲o̲d̲e̲ 34300;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B);                c̲o̲d̲e̲ 34051;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DFDY(T, Y, A); r̲e̲a̲l̲ T; a̲r̲r̲a̲y̲ Y, A;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J; r̲e̲a̲l̲ SL; a̲r̲r̲a̲y̲ F1, F2[1:N];
      DERIV(T, Y, F1, N);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲
            SL := @-6 × Y[I]; i̲f̲ ABS(SL) < @-6 t̲h̲e̲n̲ SL := @-6;
            Y[I] := Y[I] + SL; DERIV(T, Y, F2, N);
            f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 A[J, I] := (F2[J]-F1[J])/SL;
            Y[I] := Y[I]-SL;
         e̲n̲d̲
   e̲n̲d̲ DFDY;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STARTV(Y, T); v̲a̲l̲u̲e̲ T; r̲e̲a̲l̲ T; a̲r̲r̲a̲y̲ Y;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ A, B, C;
      A := (T-T1)/(T1-T2); B := (T-T2)/(T1-T3);
      C := (T-T1)/(T2-T3) × B; B := A × B;
      A := 1 + A + B; B := A + C-1;
      MULVEC(1, N, 0, Y, S1, A); ELMVEC(1, N, 0, Y, S2, -B);
      ELMVEC(1, N, 0, Y, S3, C)
   e̲n̲d̲ STARTV;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ITERATE(Z, Y, A, H, T, WEIGHTS, FAIL, PS);
      a̲r̲r̲a̲y̲ Z, Y, A, WEIGHTS; r̲e̲a̲l̲ H, T; l̲a̲b̲e̲l̲ FAIL;
        i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ PS;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ IT, LIT; r̲e̲a̲l̲ MAX, MAX1, CONV; a̲r̲r̲a̲y̲ DZ, F1[1:N];
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Z[I] := (Z[I] + Y[I])/2;
      IT := LIT := 1; CONV := 1;
      ATER: DERIV(T, Z, F1, N);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           F1[I] := DZ[I] := Z[I]-H × F1[I]/2-Y[I];
      SOL(A, N, PS, DZ);
      ELMVEC(1, N, 0, Z, DZ, -1);
      MAX := 0;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           MAX := MAX + (WEIGHTS[I] × DZ[I])⭡2;
      MAX := SQRT(MAX);
      i̲f̲ MAX × CONV < EPS/10 t̲h̲e̲n̲ g̲o̲t̲o̲ OUT;
      IT := IT + 1; i̲f̲ IT = 2 t̲h̲e̲n̲ g̲o̲t̲o̲ ASS;
      CONV := MAX/MAX1;
      i̲f̲ CONV > ·2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ i̲f̲ LIT = 0 t̲h̲e̲n̲ g̲o̲t̲o̲ FAIL;
         LIT := 0; CONV := 1; IT := 1;
         RECOMP(A, H, T, Z, FAIL, PS);
      e̲n̲d̲;
      ASS: MAX1 := MAX;
      g̲o̲t̲o̲ ATER;
      OUT: f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ Z[I] := 2 × Z[I]-Y[I];
   e̲n̲d̲ ITERATE;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ RECOMP(A, H, T, Y, FAIL, PS);
      r̲e̲a̲l̲ H, T; a̲r̲r̲a̲y̲ A, Y; l̲a̲b̲e̲l̲ FAIL; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ PS;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ SL; a̲r̲r̲a̲y̲ AUX[1:3];
      SL := H/2;
      i̲f̲ ¬AVAILABLE(T, Y, A, N) t̲h̲e̲n̲ DFDY(T, Y, A);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ MULROW(1, N, I, I, A, A, -SL); A[I, I] := 1 + A[I, I]
         e̲n̲d̲;
      AUX[2] := @-14;
      DEC(A, N, AUX, PS);
      i̲f̲ AUX[3] < N t̲h̲e̲n̲ g̲o̲t̲o̲ FAIL
   e̲n̲d̲ RECOMP;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INITIALIZATION;
   b̲e̲g̲i̲n̲ H2 := HNEW; H := H2/2;
      DUPVEC(1, N, 0, S1, Y0); DUPVEC(1, N, 0, S2, Y0); DUPVEC(1, N, 0, S3, Y0);
      DUPVEC(1, N, 0, W1, Y0); DUPROWVEC(1, N, 1, R, Y0);
      INIVEC(1, N, U1, 0); INIVEC(1, N, W2, 0);
      INIMAT(2, 5, 1, N, R, 0); INIMAT(1, 5, 1, N, RF, 0);
      T := T1 := T0; T2 := T0-2 × H-@6; T3 := 2 × T2 + 1;
      RECOMP(A1, H, T, S1, MISS, PS1); RECOMP(A2, H2, T, W1, MISS, PS2);
   e̲n̲d̲
     p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONE LARGE STEP;
   b̲e̲g̲i̲n̲ STARTV(Z, T + H);
      ITERATE(Z, S1, A1, H, T + H/2, WEIGHTS, MISS, PS1);
      DUPVEC(1, N, 0, Y, Z);
      STARTV(Z, T + H2);
      ITERATE(Z, Y, A1, H, T + 3 × H/2, WEIGHTS, MISS, PS1);
      DUPVEC(1, N, 0, U3, U1); DUPVEC(1, N, 0, U1, Y);
      DUPVEC(1, N, 0, S3, S2); DUPVEC(1, N, 0, S2, S1);
      DUPVEC(1, N, 0, S1, Z);
      ELMVEC(1, N, 0, Z, W1, 1); ELMVEC(1, N, 0, Z, S2, -1);
      ITERATE(Z, W1, A2, H2, T + H, WEIGHTS, MISS, PS2);
      T3 := T2; T2 := T1; T1 := T + H2;
      DUPVEC(1, N, 0, W3, W2); DUPVEC(1, N, 0, W2, W1); DUPVEC(1, N, 0, W1, Z);
   e̲n̲d̲;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHANGE OF INFORMATION;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ALF1, C1, C2, C3; a̲r̲r̲a̲y̲ KOF[2:4, 2:4], E, D[1:4];
      C1 := HNEW/H2; C2 := C1 × C1; C3 := C2 × C1;
      KOF[2, 2] := C1; KOF[2, 3] := (C1-C2)/2; KOF[2, 4] := C3/6-C2/2 + C1/3;
      KOF[3, 3] := C2; KOF[3, 4] := C2-C3; KOF[4, 4] := C3;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           U1[I] := R[2, I] + R[3, I]/2 + R[4, I]/3;
      ALF1 := MATVEC(1, N, 1, RF, U1)/VECVEC(1, N, 0, U1, U1);
      ALF := (ALF + ALF1) × C1;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲
            E[1] := RF[1, I]-ALF1 × U1[I];
            E[2] := RF[2, I]-ALF1 × 2 × R[3, I];
            E[3] := RF[3, I]-ALF1 × 4 × R[4, I];
            E[4] := RF[4, I];
            D[1] := R[1, I]; RF[1, I] := E[1] := E[1] × C2;
            f̲o̲r̲ K := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 4 d̲o̲
               b̲e̲g̲i̲n̲ R[K, I] := D[K] := MATMAT(K, 4, K, I, KOF, R);
                  RF[K, I] := E[K] := C2 × MATVEC(K, 4, K, KOF, E)
               e̲n̲d̲ K;
            S1[I] := D[1] + E[1]; W1[I] := D[1] + 4 × E[1];
            S2[I] := S1[I]-(D[2] + E[2]/2);
            S3[I] := S2[I]-(D[2] + E[2]) + (D[3] + E[3]/2);
         e̲n̲d̲ I;
      T3 := T-HNEW; T2 := T-HNEW/2; T1 := T;
      H2 := HNEW; H := H2/2; ERR[1] := 0;
      i̲f̲ HALV t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DUPVEC(1, N, 0, PS2, PS1); DUPMAT(1, N, 1, N, A2, A1) e̲n̲d̲;
      i̲f̲ TWO t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ DUPVEC(1, N, 0, PS1, PS2); DUPMAT(1, N, 1, N, A1, A2)
      e̲n̲d̲ e̲l̲s̲e̲ RECOMP(A1, HNEW/2, T, S1, MISS, PS1);
      i̲f̲ ¬HALV t̲h̲e̲n̲ RECOMP(A2, HNEW, T, W1, MISS, PS2);
   e̲n̲d̲;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD DIFFERENCES;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ r̲e̲a̲l̲ B0, B1, B2, B3;
            B1 := (U1[I] + 2 × S2[I] + U3[I])/4;
            B2 := (W1[I] + 2 × W2[I] + W3[I])/4;
            B3 := (S3[I] + 2 × U3[I] + S2[I])/4;
            B2 := (B2-B1)/3; B0 := B1-B2;
            B2 := B2-(S1[I]-2 × S2[I] + S3[I])/16;
            B1 := 2 × B3-(B2 + RF[1, I])-(B0 + R[1, I])/2;
            B3 := 0;
            f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 4 d̲o̲
               b̲e̲g̲i̲n̲ B1 := B1-B3; B3 := R[K, I]; R[K, I] := B0; B0 := B0-B1
               e̲n̲d̲; R[5, I] := B0;
            f̲o̲r̲ K := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 4 d̲o̲
               b̲e̲g̲i̲n̲ B3 := RF[K, I]; RF[K, I] := B2; B2 := B2-B3 e̲n̲d̲;
            RF[5, I] := B2;
         e̲n̲d̲;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ERROR ESTIMATES;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ C0, C1, C2, C3, B0, B1, B2, B3, W, SL1, SN, LR;
      C0 := C1 := C2 := C3 := 0;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ W := WEIGHTS[I]⭡2;
            B0 := RF[4, I]/36; C0 := C0 + B0 × B0 × W; LR := ABS(B0);
            B1 := RF[1, I] + ALF × R[2, I]; C1 := C1 + B1 × B1 × W;
            B2 := RF[3, I]; C2 := C2 + B2 × B2 × W;
            SL1 := ABS(RF[1, I]-RF[2, I]);
            SN := i̲f̲ SL1 < @-10 t̲h̲e̲n̲ 1 e̲l̲s̲e̲ ABS(RF[1, I]-R[4, I]/6)/SL1;
            i̲f̲ SN > 1 t̲h̲e̲n̲ SN := 1;
            i̲f̲ START t̲h̲e̲n̲ b̲e̲g̲i̲n̲ SN := SN⭡4; LR := LR × 4 e̲n̲d̲;
            EHR[I] := B3 := SN × EHR[I] + LR; C3 := C3 + B3 × B3 × W;
         e̲n̲d̲ I;
      B0 := ERR[1];
      ERR[1] := B1 := SQRT(C0); ERR[2] := SQRT(C1);
      ERR[3] := SQRT(C3) + SQRT(C2)/2;
      LQ := EPS/(i̲f̲ B0 < B1 t̲h̲e̲n̲ B1 e̲l̲s̲e̲ B0);
      i̲f̲ B0 < B1 ∧ LQ ≥ 80 t̲h̲e̲n̲ LQ := 10;
   e̲n̲d̲;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ REJECT;
      i̲f̲ START t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ HNEW := LQ⭡(1/5) × H/2; g̲o̲t̲o̲ INIT
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ f̲o̲r̲ K := 1, 2, 3, 4, 1, 2, 3 d̲o̲ ELMROW(1, N, K, K + 1, R, R, -1);
         f̲o̲r̲ K := 1, 2, 3, 4 d̲o̲ ELMROW(1, N, K, K + 1, RF, RF, -1);
         T := T-H2; HALV := t̲r̲u̲e̲; HNEW := H; g̲o̲t̲o̲ MSTP
      e̲n̲d̲;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ STEPSIZE;
      i̲f̲ LQ < 2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ HALV := t̲r̲u̲e̲; HNEW := H e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ i̲f̲ LQ > 80 t̲h̲e̲n̲
           HNEW := (i̲f̲ LQ > 5120 t̲h̲e̲n̲ (LQ/5)⭡(1/5) e̲l̲s̲e̲ 2) × H2;
         i̲f̲ HNEW > HMAX t̲h̲e̲n̲ HNEW := HMAX;
         i̲f̲ TEND > T ∧ TEND-T < HNEW t̲h̲e̲n̲ HNEW := TEND-T;
         TWO := HNEW = 2 × H2;
      e̲n̲d̲;

   i̲f̲ PRESCH t̲h̲e̲n̲ H := H0 e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ H0 > HMAX t̲h̲e̲n̲ H := HMAX e̲l̲s̲e̲ H := H0;
      i̲f̲ H > (TEND-T0)/4 t̲h̲e̲n̲ H := (TEND-T0)/4;
   e̲n̲d̲;
   HNEW := H;
   ALF := 0; T := TP := T0;
   INIVEC(1, 3, ERR, 0); INIVEC(1, N, EHR, 0);
   DUPROWVEC(1, N, 1, R, Y0);
   CONTROL(TP, T, H, HNEW, R, ERR, N);
   INIT: INITIALIZATION; START := t̲r̲u̲e̲;
   f̲o̲r̲ ECI := 0, 1, 2, 3 d̲o̲
      b̲e̲g̲i̲n̲ ONE LARGE STEP; T := T + H2;
         i̲f̲ ECI > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ BACKWARD DIFFERENCES; UPDATE(WEIGHTS, S2, N) e̲n̲d̲
      e̲n̲d̲;
   ECI := 4;
   MSTP: i̲f̲ HNEW ≠ H2 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ ECI := 1; CHANGE OF INFORMATION;
      ONE LARGE STEP; T := T + H2; ECI := 2;
   e̲n̲d̲;
   ONE LARGE STEP;
   BACKWARD DIFFERENCES;
   UPDATE(WEIGHTS, S2, N);
   ERROR ESTIMATES;
   i̲f̲ ECI < 4 ∧ LQ > 80 t̲h̲e̲n̲ LQ := 20;
   HALV := TWO := f̲a̲l̲s̲e̲;
   i̲f̲ PRESCH t̲h̲e̲n̲ g̲o̲t̲o̲ TRYCK;
   i̲f̲ LQ < 1 t̲h̲e̲n̲ REJECT e̲l̲s̲e̲ STEPSIZE;
   TRYCK: i̲f̲ TP ≤ T t̲h̲e̲n̲ CONTROL(TP, T, H, HNEW, R, ERR, N);
   i̲f̲ START t̲h̲e̲n̲ START := f̲a̲l̲s̲e̲;
   i̲f̲ HNEW = H2 t̲h̲e̲n̲ T := T + H2; ECI := ECI + 1;
   i̲f̲ T < TEND + H2 t̲h̲e̲n̲ g̲o̲t̲o̲ MSTP e̲l̲s̲e̲ g̲o̲t̲o̲ END;
   MISS: FAIL := PRESCH;
   i̲f̲ ¬FAIL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ ECI > 1 t̲h̲e̲n̲ T := T-H2;
      HALV := TWO := f̲a̲l̲s̲e̲; HNEW := H2/2;
      i̲f̲ START t̲h̲e̲n̲ g̲o̲t̲o̲ INIT e̲l̲s̲e̲ g̲o̲t̲o̲ TRYCK
   e̲n̲d̲;
   END:
e̲n̲d̲ IMPEX;
c̲o̲m̲m̲e̲n̲t̲ ================== 35021 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ERRORFUNCTION(X, ERF, ERFC);
   v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, ERF, ERFC;
   i̲f̲ X > 26   t̲h̲e̲n̲ b̲e̲g̲i̲n̲ ERF :=  1; ERFC := 0 e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ X < -5·5 t̲h̲e̲n̲ b̲e̲g̲i̲n̲ ERF := -1; ERFC := 2 e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ABSX, C, P, Q;
      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ NONEXPERFC(X); c̲o̲d̲e̲ 35022;

      ABSX := ABS(X);
      i̲f̲ ABSX ≤ 0·5 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ C := X × X;         P := ((-0·35609 84370 18154@-1  × C +
           0·69963 83488 61914@+1) × C + 0·21979 26161 82942@+2) × C +
           0·24266 79552 30532@+3;
         Q := ((C +
           0·15082 79763 04078@+2) × C + 0·91164 90540 45149@+2) × C +
           0·21505 88758 69861@+3;
         ERF :=  X × P / Q; ERFC := 1 - ERF
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ ERFC := EXP(-X × X) × NONEXPERFC(ABSX);
         ERF := 1 - ERFC;
         i̲f̲ X < 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ ERF := -ERF; ERFC := 2 - ERFC e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ ERRORFUNCTION;
c̲o̲m̲m̲e̲n̲t̲ ================== 35022 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ NONEXPERFC(X); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ABSX, ERF, ERFC, C, P, Q;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ERRORFUNCTION(X, ERF, ERFC); c̲o̲d̲e̲ 35021;

   ABSX := ABS(X);
   i̲f̲ ABSX ≤ 0·5 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ ERRORFUNCTION(X, ERF, ERFC);
      NONEXPERFC := EXP(X × X) × ERFC
   e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ ABSX < 4 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ C := ABSX;      P := ((((((-0·13686 48573 82717@-6  × C +
        0·56419 55174 78974@+0) × C + 0·72117 58250 88309@+1) × C +
        0·43162 22722 20567@+2) × C + 0·15298 92850 46940@+3) × C +
        0·33932 08167 34344@+3) × C + 0·45191 89537 11873@+3) × C +
        0·30045 92610 20162@+3;
      Q := ((((((C +
        0·12782 72731 96294@+2) × C + 0·77000 15293 52295@+2) × C +
        0·27758 54447 43988@+3) × C + 0·63898 02644 65631@+3) × C +
        0·93135 40948 50610@+3) × C + 0·79095 09253 27898@+3) × C +
        0·30045 92609 56983@+3;
      NONEXPERFC := i̲f̲ X > 0 t̲h̲e̲n̲ P / Q e̲l̲s̲e̲
        EXP(X × X) × 2 - P / Q
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ C := 1 / X / X;     P := (((0·22319 24597 34185@-1  × C +
        0·27866 13086 09648@-0) × C + 0·22695 65935 39687@-0) × C +
        0·49473 09106 23251@-1) × C + 0·29961 07077 03542@-2;
      Q := (((C +
        0·19873 32018 17135@+1) × C + 0·10516 75107 06793@+1) × C +
        0·19130 89261 07830@+0) × C + 0·10620 92305 28468@-1;
      C := (C × (-P) / Q + 0·56418 95835 47756) / ABSX;
      NONEXPERFC := i̲f̲ X > 0 t̲h̲e̲n̲ C e̲l̲s̲e̲ EXP(X × X) × 2 - C
   e̲n̲d̲
e̲n̲d̲ NONEXPERFC;
c̲o̲m̲m̲e̲n̲t̲ ================== 35027 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FRESNEL(X, C, S); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, C, S;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ABSX, X3, X4, A, P, Q, F, G, C1, S1;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FG(X, F, G); c̲o̲d̲e̲ 35028;
   ABSX := ABS(X);
   i̲f̲ ABSX ≤ 1·2 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ A := X × X; X3 := A × X; X4 := A × A;
      P := (((5·47711 38568 2687@-6  × X4 - 5·28079 65137 2623@-4)× X4 + 1·76193 95254 3491@-2) × X4 - 1·99460 89882 6184@-1)× X4 + 1;
      Q := (((1·18938 90142 2876@-7  × X4 + 1·55237 88527 6994@-5)× X4 + 1·09957 21502 5642@-3) × X4 + 4·72792 11201 0453@-2)× X4 + 1;
      C := X × P / Q;
      P := (((6·71748 46662 5141@-7  × X4 - 8·45557 28435 2777@-5)× X4 + 3·87782 12346 3683@-3) × X4 - 7·07489 91514 4523@-2)× X4 + 5·23598 77559 8299@-1;
      Q := (((5·95281 22767 8410@-8  × X4 + 9·62690 87593 9034@-6)× X4 + 8·17091 94215 2134@-4) × X4 + 4·11223 15114 2384@-2)× X4 + 1;
      S := X3 × P / Q
   e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ ABSX ≤ 1·6 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ A := X × X; X3 := A × X; X4 := A × A;
      P := ((((-5·68293 31012 1871@-8  × X4 + 1·02365 43505 6106@-5)× X4 - 6·71376 03469 4922@-4) × X4 + 1·91870 27943 1747@-2)× X4 - 2·07073 36033 5324@-1) × X4 + 1·00000 00000 0111@+0;
      Q := ((((4·41701 37406 5010@-10 × X4 + 8·77945 37789 2369@-8)× X4 + 1·01344 63086 6749@-5) × X4 + 7·88905 24505 2360@-4)× X4 + 3·96667 49695 2323@-2) × X4 + 1;
      C := X × P / Q;
      P := ((((-5·76765 81559 3089@-9  × X4 + 1·28531 04374 2725@-6)× X4 - 1·09540 02391 1435@-4) × X4 + 4·30730 52650 4367@-3)× X4 - 7·37766 91401 0191@-2) × X4 + 5·23598 77559 8344@-1;
      Q := ((((2·05539 12445 8580@-10 × X4 + 5·03090 58124 6612@-8)× X4 + 6·87086 26571 8620@-6) × X4 + 6·18224 62019 5473@-4)× X4 + 3·53398 34276 7472@-2) × X4 + 1;
      S := X3 × P / Q
   e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ ABSX < @15 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FG(X, F, G);
      A := X × X;
      A := (A - ENTIER(A / 4) × 4) × 1·57079 63267 9490;
      C1 := COS(A); S1 := SIN(A);
      A := i̲f̲ X < 0 t̲h̲e̲n̲ -0·5 e̲l̲s̲e̲ 0·5;
      C :=  F × S1 - G × C1 + A;
      S := -F × C1 - G × S1 + A
   e̲n̲d̲ e̲l̲s̲e̲ C := S := SIGN(X) × 0·5
e̲n̲d̲ FRESNEL;
c̲o̲m̲m̲e̲n̲t̲ ================== 35028 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FG(X, F, G); v̲a̲l̲u̲e̲ X; r̲e̲a̲l̲ X, F, G;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ABSX, C, S, C1, S1, A, XINV, X3INV, C4, P, Q;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FRESNEL(X, C, S); c̲o̲d̲e̲ 35027;

   ABSX := ABS(X);
   i̲f̲ ABSX ≤ 1·6 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FRESNEL(X, C, S);
      A := X × X × 1·57079 63267 9490; C1 := COS(A); S1 := SIN(A);
      A := i̲f̲ X < 0 t̲h̲e̲n̲ -0·5 e̲l̲s̲e̲ 0·5;
      P := A - C; Q := A - S;
      F := Q × C1 - P × S1;
      G := P × C1 + Q × S1
   e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ ABSX ≤ 1·9 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XINV := 1 / X; A := XINV × XINV;
      X3INV := A × XINV; C4 := A × A;
      P := (((1·35304 23554 0388@+1  × C4 + 6·98534 26160 1021@+1)× C4 + 4·80340 65557 7925@+1) × C4 + 8·03588 12280 3942@+0)× C4 + 3·18309 26850 4906@-1;
      Q := (((6·55630 64008 3916@+1  × C4 + 2·49561 99380 5172@+2)× C4 + 1·57611 00558 0123@+2) × C4 + 2·55491 61843 5795@+1)× C4 + 1;
      F := XINV × P / Q;
      P := ((((2·05421 43249 8501@+1  × C4 + 1·96232 03797 1663@+2)× C4 + 1·99182 81867 8903@+2) × C4 + 5·31122 81348 0989@+1)× C4 + 4·44533 82755 0512@+0) × C4 + 1·01320 61881 0275@-1;
      Q := ((((1·01379 48339 6003@+3  × C4 + 3·48112 14785 6545@+3)× C4 + 2·54473 13318 1822@+3) × C4 + 5·83590 57571 6429@+2)× C4 + 4·53925 01967 3689@+1) × C4 + 1;
      G := X3INV × P / Q
   e̲n̲d̲ e̲l̲s̲e̲
   i̲f̲ ABSX ≤ 2·4 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ XINV := 1 / X; A := XINV × XINV;
      X3INV := A × XINV; C4 := A × A;
      P := ((((7·17703 24936 5140@+2  × C4 + 3·09145 16157 4430@+3)× C4 + 1·93007 64078 6716@+3) × C4 + 3·39837 13492 6984@+2)× C4 + 1·95883 94102 1969@+1) × C4 + 3·18309 88182 2017@-1;
      Q := ((((3·36121 69918 0551@+3  × C4 + 1·09334 24898 8809@+4)× C4 + 6·33747 15585 1144@+3) × C4 + 1·08535 06750 0650@+3)× C4 + 6·18427 13817 2887@+1) × C4 + 1;
      F := XINV × P / Q;
      P := ((((3·13330 16306 8756@+2  × C4 + 1·59268 00608 5354@+3)× C4 + 9·08311 74952 9594@+2) × C4 + 1·40959 61791 1316@+2)× C4 + 7·11205 00178 9783@+0) × C4 + 1·01321 16176 1805@-1;
      Q := ((((1·15149 83237 6261@+4  × C4 + 2·41315 56721 3370@+4)× C4 + 1·06729 67803 0581@+4) × C4 + 1·49051 92279 7329@+3)× C4 + 7·17128 59693 9302@+1) × C4 + 1;
      G := X3INV × P / Q
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ XINV := 1 / X; A := XINV × XINV;
      X3INV := A × XINV; C4 := A × A;
      P := ((((2·61294 75322 5142@+4  × C4 + 6·13547 11361 4700@+4)× C4 + 1·34922 02817 1857@+4) × C4 + 8·16343 40178 4375@+2)× C4 + 1·64797 71284 1246@+1) × C4 + 9·67546 03296 7090@-2;
      Q := ((((1·37012 36481 7226@+6  × C4 + 1·00105 47890 0791@+6)× C4 + 1·65946 46262 1853@+5) × C4 + 9·01827 59623 1524@+3)× C4 + 1·73871 69067 3649@+2) × C4 + 1;
      F := (C4 × (-P) / Q + 0·31830 98861 83791) × XINV;
      P := (((((1·72590 22465 4837@+6  × C4 + 6·66907 06166 8636@+6)× C4 + 1·77758 95083 8030@+6) × C4 + 1·35678 86781 3756@+5)× C4 + 3·87754 14174 6378@+3) × C4 + 4·31710 15782 3358@+1)× C4 + 1·53989 73381 9769@-1;
      Q := (((((1·40622 44112 3580@+8  × C4 + 9·38695 86253 1635@+7)× C4 + 1·62095 60050 0232@+7) × C4 + 1·02878 69305 6688@+6)× C4 + 2·69183 18039 6243@+4) × C4 + 2·86733 19497 5899@+2)× C4 + 1;
      G := (C4 × (-P) / Q + 0·10132 11836 42338) × X3INV
   e̲n̲d̲
e̲n̲d̲ FG;
c̲o̲m̲m̲e̲n̲t̲ ================== 34453 ================= ;
B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ZEROINDER(X, Y, FX, DFX, TOLX);
   r̲e̲a̲l̲ X, Y, FX, DFX, TOLX;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ EXT;
   r̲e̲a̲l̲ B, FB, DFB, A, FA, DFA, C, FC, DFC, D, W, MB,
     TOL, M, P, Q, DW;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DWARF; c̲o̲d̲e̲ 30003;
   DW := DWARF;
   B := X; FB := FX; DFB := DFX; A := X := Y; FA := FX; DFA := DFX;
   INTERPOLATE: C := A; FC := FA; DFC := DFA; EXT := 0;
   EXTRAPOLATE: i̲f̲ ABS(FC) < ABS(FB) t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ A := B; FA := FB; DFA := DFB; B := X := C; FB := FC;
      DFB := DFC; C := A; FC := FA; DFC := DFA
   e̲n̲d̲ INTERCHANGE;
   TOL := TOLX; M := (C + B) × 0·5; MB := M - B;
   i̲f̲ ABS(MB) > TOL t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ EXT > 2 t̲h̲e̲n̲ W := MB e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ TOL := TOL × SIGN(MB);
         D := i̲f̲ EXT = 2 t̲h̲e̲n̲ DFA e̲l̲s̲e̲ (FB - FA) / (B - A);
         P := FB × D × (B - A);
         Q := FA × DFB - FB × D;
         i̲f̲ P < 0 t̲h̲e̲n̲ b̲e̲g̲i̲n̲ P := -P; Q := -Q e̲n̲d̲;
         W := i̲f̲ P < DW ∨ P ≤ Q × TOL t̲h̲e̲n̲ TOL e̲l̲s̲e̲
           i̲f̲ P < MB × Q t̲h̲e̲n̲ P / Q e̲l̲s̲e̲ MB;
      e̲n̲d̲; A := B; FA := FB; DFA := DFB;
      X := B := B + W; FB := FX; DFB := DFX;
      i̲f̲ (i̲f̲ FC ≥ 0 t̲h̲e̲n̲ FB ≥ 0 e̲l̲s̲e̲ FB ≤ 0) t̲h̲e̲n̲
        g̲o̲t̲o̲ INTERPOLATE e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ EXT := i̲f̲ W = MB t̲h̲e̲n̲ 0 e̲l̲s̲e̲ EXT + 1;
         g̲o̲t̲o̲ EXTRAPOLATE
      e̲n̲d̲
   e̲n̲d̲; Y := C;
   ZEROINDER := i̲f̲ FC ≥ 0 t̲h̲e̲n̲ FB ≤ 0 e̲l̲s̲e̲ FB ≥ 0
e̲n̲d̲ ZEROINDER;
c̲o̲m̲m̲e̲n̲t̲ ================== 34432 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ PRAXIS(N, X, FUNCT, IN, OUT);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N;
     a̲r̲r̲a̲y̲ X, IN, OUT;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT;
b̲e̲g̲i̲n̲
   c̲o̲m̲m̲e̲n̲t̲ THIS PROCEDURE MINIMIZES FUNCT(N, X), WITH THE
     PRINCIPAL AXIS METHOD (SEE BRENT, R.P, 1973, ALGORITHMS
     FOR MINIMIZATION WITHOUT DERIVATIVES, CH.7);

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X);                     c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIMAT(L, U, K, V, A, X);               c̲o̲d̲e̲ 31011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, K, A, X);                  c̲o̲d̲e̲ 31030;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPMAT(L, U, K, V, A, B);               c̲o̲d̲e̲ 31035;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPCOLVEC(L, U, K, A, B);               c̲o̲d̲e̲ 31034;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULROW(L, U, I, J, A, B, X);            c̲o̲d̲e̲ 31021;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULCOL(L, U, I, J, A, B, X);            c̲o̲d̲e̲ 31022;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, S, A, B);           c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B);        c̲o̲d̲e̲ 34014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B);        c̲o̲d̲e̲ 34015;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ICHROWCOL(L, U, I, J, A);               c̲o̲d̲e̲ 34033;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVECCOL(L, U, I, A, B, X);            c̲o̲d̲e̲ 34021;
   i̲n̲t̲e̲g̲e̲r̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ QRISNGVALDEC(A, M, N, VAL, V, EM); c̲o̲d̲e̲ 34273;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SETRANDOM(X);                           c̲o̲d̲e̲ 11014;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ RANDOM;                          c̲o̲d̲e̲ 11015;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DWARF;                           c̲o̲d̲e̲ 30003;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SORT;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, K; r̲e̲a̲l̲ S;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N - 1 d̲o̲
         b̲e̲g̲i̲n̲ K := I; S := D[I];
            f̲o̲r̲ J := I + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ i̲f̲ D[J] > S t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ K := J; S := D[J] e̲n̲d̲;
            i̲f̲ K > I t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ D[K] := D[I]; D[I] := S;
               f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                  b̲e̲g̲i̲n̲ S := V[J, I]; V[J, I] := V[J, K]; V[J, K] := S
                  e̲n̲d̲
            e̲n̲d̲
         e̲n̲d̲
   e̲n̲d̲ SORT;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MIN(J, NITS, D2, X1, F1, FK); v̲a̲l̲u̲e̲ J, NITS, FK;
      i̲n̲t̲e̲g̲e̲r̲ J, NITS; r̲e̲a̲l̲ D2, X1, F1; B̲o̲o̲l̲e̲a̲n̲ FK;
   b̲e̲g̲i̲n̲
      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FLIN(L); v̲a̲l̲u̲e̲ L; r̲e̲a̲l̲ L;
      b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I; a̲r̲r̲a̲y̲ T[1:N];
         i̲f̲ J > 0 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 T[I] := X[I] + L × V[I, J]
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ SEARCH ALONG PARABOLIC SPACE CURVE;
            QA := L × (L - QD1) / (QD0 × (QD0 + QD1));
            QB := (L + QD0) × (QD1 - L) /(QD0 × QD1);
            QC := L × (L + QD0) / (QD1 × (QD0 + QD1));
            f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                 T[I] := QA × Q0[I] + QB × X[I] + QC × Q1[I]
         e̲n̲d̲;
         NF := NF + 1; FLIN := FUNCT(N, T)
      e̲n̲d̲ FLIN;

      i̲n̲t̲e̲g̲e̲r̲ K; B̲o̲o̲l̲e̲a̲n̲ DZ;
      r̲e̲a̲l̲ X2, XM, F0, F2, FM, D1, T2, S, SF1, SX1;
      SF1 := F1; SX1 := X1;
      K := 0; XM := 0; F0 := FM := FX; DZ := D2 < RELTOL;
      S := SQRT(VECVEC(1, N, 0, X, X));
      T2 := M4 × SQRT(ABS(FX) / (i̲f̲ DZ t̲h̲e̲n̲ DMIN e̲l̲s̲e̲ D2)
        + S × LDT) + M2 × LDT; S := S × M4 + ABSTOL;
      i̲f̲ DZ ∧ T2 > S t̲h̲e̲n̲ T2 := S;
      i̲f̲ T2 < SMALL t̲h̲e̲n̲ T2 := SMALL;
      i̲f̲ T2 > 0·01 × H t̲h̲e̲n̲ T2 := 0·01 × H;
      i̲f̲ FK ∧ F1 ≤ FM t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ XM := X1; FM := F1 e̲n̲d̲;
      i̲f̲ ¬FK ∨ ABS(X1) < T2 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ X1 := i̲f̲ X1 > 0 t̲h̲e̲n̲ T2 e̲l̲s̲e̲ -T2;
         F1 := FLIN(X1)
      e̲n̲d̲;
      i̲f̲ F1 ≤ FM t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ XM := X1; FM := F1 e̲n̲d̲;
      L0: i̲f̲ DZ t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ EVALUATE FLIN AT ANOTHER POINT
           AND ESTIMATE THE SECOND DERIVATIVE;
         X2 := i̲f̲ F0 < F1 t̲h̲e̲n̲ -X1 e̲l̲s̲e̲ X1 × 2;
         F2 := FLIN(X2); i̲f̲ F2 ≤ FM t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ XM := X2; FM := F2 e̲n̲d̲;
         D2 := (X2 × (F1-F0)-X1 × (F2-F0))/(X1 × X2 × (X1-X2))
      e̲n̲d̲;
      c̲o̲m̲m̲e̲n̲t̲ ESTIMATE FIRST DERIVATIVE AT 0;
      D1 := (F1-F0)/X1-X1 × D2; DZ := t̲r̲u̲e̲;
      X2 := i̲f̲ D2 ≤ SMALL t̲h̲e̲n̲
        (i̲f̲ D1 < 0 t̲h̲e̲n̲ H e̲l̲s̲e̲ -H)
      e̲l̲s̲e̲ -0·5 × D1/D2;
      i̲f̲ ABS(X2) > H t̲h̲e̲n̲ X2 := i̲f̲ X2 > 0 t̲h̲e̲n̲ H e̲l̲s̲e̲ -H;
      L1: F2 := FLIN(X2);
      i̲f̲ K < NITS ∧ F2 > F0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ K := K + 1;
         i̲f̲ F0 < F1 ∧ X1 × X2 > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ L0;
         X2 := 0·5 × X2; g̲o̲t̲o̲ L1
      e̲n̲d̲;
      NL := NL + 1;
      i̲f̲ F2 > FM t̲h̲e̲n̲ X2 := XM e̲l̲s̲e̲ FM := F2;
      D2 := i̲f̲ ABS(X2 × (X2-X1)) > SMALL t̲h̲e̲n̲
        (X2 × (F1-F0)-X1 × (FM-F0))/(X1 × X2 × (X1-X2))
      e̲l̲s̲e̲ i̲f̲ K > 0 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ D2;
      i̲f̲ D2 ≤ SMALL t̲h̲e̲n̲ D2 := SMALL;
      X1 := X2; FX := FM;
      i̲f̲ SF1 < FX t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ FX := SF1; X1 := SX1 e̲n̲d̲;
      i̲f̲ J > 0 t̲h̲e̲n̲ ELMVECCOL(1, N, J, X, V, X1)
   e̲n̲d̲ MIN;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ QUAD;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I; r̲e̲a̲l̲ L, S;
      S := FX; FX := QF1; QF1 := S; QD1 := 0;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ S := X[I]; X[I] := L := Q1[I]; Q1[I] := S;
            QD1 := QD1 + (S - L) ⭡ 2
         e̲n̲d̲;
      L := QD1 := SQRT(QD1); S := 0;
      i̲f̲ (QD0 × QD1 > DWARF) ∧ NL ≥ 3 × N × N t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ MIN(0, 2, S, L, QF1, t̲r̲u̲e̲);
         QA := L × (L-QD1)/(QD0 × (QD0 + QD1));
         QB := (L + QD0) × (QD1-L)/(QD0 × QD1);
         QC := L × (L + QD0)/(QD1 × (QD0 + QD1))
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ FX := QF1; QA := QB := 0; QC := 1 e̲n̲d̲;
      QD0 := QD1; f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ S := Q0[I]; Q0[I] := X[I];
            X[I] := QA × S + QB × X[I] + QC × Q1[I]
         e̲n̲d̲
   e̲n̲d̲ QUAD;

   B̲o̲o̲l̲e̲a̲n̲ ILLC;
   i̲n̲t̲e̲g̲e̲r̲ I, J, K, K2, NL, MAXF, NF, KL, KT, KTM;
   r̲e̲a̲l̲ S, SL, DN, DMIN, FX, F1, LDS, LDT, SF, DF, QF1, QD0,
     QD1, QA, QB, QC, M2, M4, SMALL, VSMALL, LARGE, VLARGE, SCBD,
     LDFAC, T2, MACHEPS, RELTOL, ABSTOL, H;
   a̲r̲r̲a̲y̲ V[1:N, 1:N], D, Y, Z, Q0, Q1[1:N];

   MACHEPS := IN[0]; RELTOL := IN[1]; ABSTOL := IN[2]; MAXF := IN[5];
   H := IN[6]; SCBD := IN[7]; KTM := IN[8]; ILLC := IN[9] < 0;
   SMALL := MACHEPS ⭡ 2; VSMALL := SMALL ⭡ 2;
   LARGE := 1/SMALL; VLARGE := 1/VSMALL;
   M2 := RELTOL; M4 := SQRT(M2); SETRANDOM(0·5);
   LDFAC := i̲f̲ ILLC t̲h̲e̲n̲ 0·1 e̲l̲s̲e̲ 0·01;
   KT := NL := 0; NF := 1; OUT[3] := QF1 := FX := FUNCT(N, X);
   ABSTOL := T2 := SMALL + ABS(ABSTOL); DMIN := SMALL;
   i̲f̲ H < ABSTOL × 100 t̲h̲e̲n̲ H := ABSTOL × 100; LDT := H;
   INIMAT(1, N, 1, N, V, 0);
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ V[I, I] := 1;
   D[1] := QD0 := 0; DUPVEC(1, N, 0, Q1, X);
   INIVEC(1, N, Q0, 0);
   c̲o̲m̲m̲e̲n̲t̲ MAIN LOOP;
   L0: SF := D[1]; D[1] := S := 0;
   MIN(1, 2, D[1], S, FX, f̲a̲l̲s̲e̲);
   i̲f̲ S ≤ 0 t̲h̲e̲n̲ MULCOL(1, N, 1, 1, V, V, -1);
   i̲f̲ SF ≤ 0·9 × D[1] ∨ 0·9 × SF ≥ D[1] t̲h̲e̲n̲
     INIVEC(2, N, D, 0);
   f̲o̲r̲ K := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ DUPVEC(1, N, 0, Y, X); SF := FX;
         ILLC := ILLC ∨ KT > 0;
         L1: KL := K; DF := 0; i̲f̲ ILLC t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ RANDOM STOP TO GET OFF
              RESULTION VALLEY;
            f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ S := Z[I] := (0·1 × LDT + T2 × 10⭡KT)× (RANDOM-0·5);
                  ELMVECCOL(1, N, I, X, V, S)
               e̲n̲d̲;
            FX := FUNCT(N, X); NF := NF + 1
         e̲n̲d̲;
         f̲o̲r̲ K2 := K s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ SL := FX; S := 0;
               MIN (K2, 2, D[K2], S, FX, f̲a̲l̲s̲e̲);
               S := i̲f̲ ILLC t̲h̲e̲n̲ D[K2] × (S + Z[K2]) ⭡ 2
               e̲l̲s̲e̲ SL-FX; i̲f̲ DF < S t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ DF := S; KL := K2 e̲n̲d̲;
            e̲n̲d̲;
         i̲f̲ ¬ILLC ∧ DF < ABS(100 × MACHEPS × FX) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ ILLC := t̲r̲u̲e̲; g̲o̲t̲o̲ L1 e̲n̲d̲;
         f̲o̲r̲ K2 := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K-1 d̲o̲
            b̲e̲g̲i̲n̲ S := 0; MIN(K2, 2, D[K2], S, FX, f̲a̲l̲s̲e̲) e̲n̲d̲;
         F1 := FX; FX := SF; LDS := 0;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ SL := X[I]; X[I] := Y[I]; SL := Y[I] := SL - Y[I];
               LDS := LDS + SL × SL
            e̲n̲d̲; LDS := SQRT(LDS);
         i̲f̲ LDS > SMALL t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ f̲o̲r̲ I := KL - 1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ K d̲o̲
               b̲e̲g̲i̲n̲ f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                       V[J, I + 1] := V[J, I]; D[I + 1] := D[I]
               e̲n̲d̲;
            D[K] := 0; DUPCOLVEC(1, N, K, V, Y);
            MULCOL(1, N, K, K, V, V, 1 / LDS);
            MIN(K, 4, D[K], LDS, F1, t̲r̲u̲e̲); i̲f̲ LDS ≤ 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ LDS := LDS; MULCOL(1, N, K, K, V, V, -1) e̲n̲d̲
         e̲n̲d̲;
         LDT := LDFAC × LDT; i̲f̲ LDT < LDS t̲h̲e̲n̲ LDT := LDS;
         T2 := M2 × SQRT(VECVEC(1, N, 0, X, X)) + ABSTOL;
         KT := i̲f̲ LDT > 0·5 × T2 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ KT + 1;
         i̲f̲ KT > KTM t̲h̲e̲n̲ b̲e̲g̲i̲n̲ OUT[1] := 0; g̲o̲t̲o̲ L2 e̲n̲d̲
      e̲n̲d̲;
   QUAD;
   DN := 0; f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ D[I] := 1/SQRT(D[I]);
         i̲f̲ DN < D[I] t̲h̲e̲n̲ DN := D[I]
      e̲n̲d̲;
   f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ S := D[J]/DN; MULCOL(1, N, J, J, V, V, S) e̲n̲d̲;
   i̲f̲ SCBD > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ S := VLARGE; f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ SL := Z[I] := SQRT(MATTAM(1, N, I, I, V, V));
            i̲f̲ SL < M4 t̲h̲e̲n̲ Z[I] := M4;
            i̲f̲ S > SL t̲h̲e̲n̲ S := SL
         e̲n̲d̲;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ SL := S/Z[I]; Z[I] := 1/SL;
            i̲f̲ Z[I] > SCBD t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ SL := 1/SCBD; Z[I] := SCBD e̲n̲d̲;
            MULROW(1, N, I, I, V, V, SL)
         e̲n̲d̲
   e̲n̲d̲;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
        ICHROWCOL(I + 1, N, I, I, V);
   b̲e̲g̲i̲n̲ a̲r̲r̲a̲y̲ A[1:N, 1:N], EM[0:7];
      EM[0] := EM[2] := MACHEPS;
      EM[4] := 10 × N; EM[6] := VSMALL;
      DUPMAT(1, N, 1, N, A, V);
      i̲f̲ QRISNGVALDEC(A, N, N, D, V, EM) ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ OUT[1] := 2; g̲o̲t̲o̲ L2 e̲n̲d̲;
   e̲n̲d̲;
   i̲f̲ SCBD > 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           MULROW(1, N, I, I, V, V, Z[I]);
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ S := SQRT(TAMMAT(1, N, I, I, V, V));
            D[I] := S × D[I]; S := 1/S;
            MULCOL(1, N, I, I, V, V, S)
         e̲n̲d̲
   e̲n̲d̲;
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ S := DN × D[I];
         D[I] := i̲f̲ S > LARGE t̲h̲e̲n̲ VSMALL e̲l̲s̲e̲
           i̲f̲ S < SMALL t̲h̲e̲n̲ VLARGE e̲l̲s̲e̲ S ⭡ (-2)
      e̲n̲d̲;
   SORT;
   DMIN := D[N]; i̲f̲ DMIN < SMALL t̲h̲e̲n̲ DMIN := SMALL;
   ILLC := (M2 × D[1]) > DMIN;
   i̲f̲ NF < MAXF t̲h̲e̲n̲ g̲o̲t̲o̲ L0 e̲l̲s̲e̲ OUT[1] := 1;
   L2: OUT[2] := FX;
   OUT[4] := NF; OUT[5] := NL; OUT[6] := LDT
e̲n̲d̲ PRAXIS;
c̲o̲m̲m̲e̲n̲t̲ ================== 31061 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INFNRMVEC(L, U, K, A); v̲a̲l̲u̲e̲ L, U;
   i̲n̲t̲e̲g̲e̲r̲ L, U, K; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, MAX;
   MAX := 0; K := L;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ R := ABS(A[L]); i̲f̲ R > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := R; K := L e̲n̲d̲
      e̲n̲d̲;
   INFNRMVEC := MAX
e̲n̲d̲ INFNRMVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31062 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INFNRMROW(L, U, I, K, A); v̲a̲l̲u̲e̲ L, U, I;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I, K; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, MAX;
   MAX := 0; K := L;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ R := ABS(A[I, L]); i̲f̲ R > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := R; K := L e̲n̲d̲
      e̲n̲d̲;
   INFNRMROW := MAX
e̲n̲d̲ INFNRMROW;
c̲o̲m̲m̲e̲n̲t̲ ================== 31063 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INFNRMCOL(L, U, J, K, A); v̲a̲l̲u̲e̲ L, U, J;
   i̲n̲t̲e̲g̲e̲r̲ L, U, J, K; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, MAX;
   MAX := 0; K := L;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ R := ABS(A[L, J]); i̲f̲ R > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := R; K := L e̲n̲d̲
      e̲n̲d̲;
   INFNRMCOL := MAX
e̲n̲d̲ INFNRMCOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 31064 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INFNRMMAT(LR, UR, LC, UC, KR, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, KR; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, MAX;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONENRMROW(L, U, I, A); c̲o̲d̲e̲ 31066;
   MAX := 0; KR := LR;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
      b̲e̲g̲i̲n̲ R := ONENRMROW(LC, UC, LR, A); i̲f̲ R > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := R; KR := LR e̲n̲d̲
      e̲n̲d̲;
   INFNRMMAT := MAX
e̲n̲d̲ INFNRMMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 31065 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONENRMVEC(L, U, A); v̲a̲l̲u̲e̲ L, U;
   i̲n̲t̲e̲g̲e̲r̲ L, U; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ SUM;
   SUM := 0; f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
        SUM := SUM + ABS(A[L]);
   ONENRMVEC := SUM
e̲n̲d̲ ONENRMVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31066 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONENRMROW(L, U, I, A); v̲a̲l̲u̲e̲ L, U, I;
   i̲n̲t̲e̲g̲e̲r̲ L, U, I; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ SUM;
   SUM := 0; f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
        SUM := SUM + ABS(A[I, L]);
   ONENRMROW := SUM
e̲n̲d̲ ONENRMROW;
c̲o̲m̲m̲e̲n̲t̲ ================== 31067 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONENRMCOL(L, U, J, A); v̲a̲l̲u̲e̲ L, U, J;
   i̲n̲t̲e̲g̲e̲r̲ L, U, J; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ SUM;
   SUM := 0; f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
        SUM := SUM + ABS(A[L, J]);
   ONENRMCOL := SUM
e̲n̲d̲ ONENRMCOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 31068 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONENRMMAT(LR, UR, LC, UC, KC, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, KC; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ MAX, R;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ONENRMCOL(L, U, J, A); c̲o̲d̲e̲ 31067;
   MAX := 0; KC := LC;
   f̲o̲r̲ LC := LC s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UC d̲o̲
      b̲e̲g̲i̲n̲ R := ONENRMCOL(LR, UR, LC, A); i̲f̲ R > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := R; KC := LC e̲n̲d̲
      e̲n̲d̲;
   ONENRMMAT := MAX
e̲n̲d̲ ONENRMMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 31069 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ ABSMAXMAT(LR, UR, LC, UC, I, J, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, I, J; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ II; r̲e̲a̲l̲ MAX, R;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INFNRMCOL(L, U, I, K, A); c̲o̲d̲e̲ 31063;
   MAX := 0; I := LR; J := LC;
   f̲o̲r̲ LC := LC s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UC d̲o̲
      b̲e̲g̲i̲n̲ R := INFNRMCOL(LR, UR, LC, II, A); i̲f̲ R > MAX t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ MAX := R; I := II; J := LC e̲n̲d̲
      e̲n̲d̲;
   ABSMAXMAT := MAX
e̲n̲d̲ ABSMAXMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 35140 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ AIRY(Z, AI, AID, BI, BID, EXPON, FIRST);
   v̲a̲l̲u̲e̲ Z, FIRST; B̲o̲o̲l̲e̲a̲n̲ FIRST;
     r̲e̲a̲l̲ Z, AI, AID, BI, BID, EXPON;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S, T, U, V, SC, TC, UC, VC, X, K1, K2, K3, K4,
     C, ZT, SI, CO, EXPZT, SQRTZ, WWL, PL, PL1, PL2, PL3;
   o̲w̲n̲ r̲e̲a̲l̲ C1, C2, SQRT3, SQRT1OPI, PIO4;
   o̲w̲n̲ r̲e̲a̲l̲ a̲r̲r̲a̲y̲ XX, WW[1:10];
   i̲n̲t̲e̲g̲e̲r̲ N, L;

   i̲f̲ FIRST t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ SQRT3 :=    1·73205080756887729;
      SQRT1OPI := 0·56418958354775629;
      PIO4 :=     0·78539816339744831;
      C1 :=       0·35502 80538 87817;
      C2 :=       0·25881 94037 92807;
      XX[ 1] :=   1·40830 81072 180964@+1;
      XX[ 2] :=   1·02148 85479 197331@+1;
      XX[ 3] :=   7·44160 18450 450930    ;
      XX[ 4] :=   5·30709 43061 781927    ;
      XX[ 5] :=   3·63401 35029 132462    ;
      XX[ 6] :=   2·33106 52303 052450    ;
      XX[ 7] :=   1·34479 70824 609268    ;
      XX[ 8] :=   6·41888 58369 567296@-1;
      XX[ 9] :=   2·01003 45998 121046@-1;
      XX[10] :=   8·05943 59172 052833@-3;
      WW[ 1] :=   3·15425 15762 964787@-14;
      WW[ 2] :=   6·63942 10819 584921@-11;
      WW[ 3] :=   1·75838 89061 345669@-8;
      WW[ 4] :=   1·37123 92370 435815@-6;
      WW[ 5] :=   4·43509 66639 284350@-5;
      WW[ 6] :=   7·15550 10917 718255@-4;
      WW[ 7] :=   6·48895 66103 335381@-3;
      WW[ 8] :=   3·64404 15875 773282@-2;
      WW[ 9] :=   1·43997 92418 590999@-1;
      WW[10] :=   8·12311 41336 261486@-1;
   e̲n̲d̲;

   EXPON := 0;
   i̲f̲ Z ≥ -5·0 ∧ Z ≤ 8 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ U := V := T := UC := VC := TC := 1;
      S := SC := 0·5; N := 0; X := Z × Z × Z;
      f̲o̲r̲ N := N + 3 w̲h̲i̲l̲e̲ ABS(U) + ABS(V) + ABS(S) + ABS(T)> @-18 d̲o̲
         b̲e̲g̲i̲n̲ U := U × X/(N × (N-1)); V := V × X/(N × (N + 1));
            S := S × X/(N × (N + 2)); T := T × X/(N × (N-2));
            UC := UC + U; VC := VC + V; SC := SC + S; TC := TC + T
         e̲n̲d̲;
      BI := SQRT3 × (C1 × UC + C2 × Z × VC);
      BID := SQRT3 × (C1 × Z × Z × SC + C2 × TC);
      i̲f̲ Z < 2·5 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ AI := C1 × UC - C2 × Z × VC;
         AID := C1 × SC × Z × Z - C2 × TC;
         g̲o̲t̲o̲ END
      e̲n̲d̲
   e̲n̲d̲;

   K1 := K2 := K3 := K4 := 0;
   SQRTZ := SQRT(ABS(Z));
   ZT := 0·66666 66666 66667 × ABS(Z) × SQRTZ;
   C := SQRT1OPI/SQRT(SQRTZ);
   i̲f̲ Z < 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ Z := -Z; CO := COS(ZT-PIO4); SI := SIN(ZT-PIO4);
      f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 10 d̲o̲
         b̲e̲g̲i̲n̲ WWL := WW[L]; PL := XX[L]/ZT;
            PL2 := PL × PL; PL1 := 1 + PL2; PL3 := PL1 × PL1;
            K1 := K1 + WWL/PL1;
            K2 := K2 + WWL × PL/PL1;
            K3 := K3 + WWL × PL × (1 + PL × (2/ZT + PL))/PL3;
            K4 := K4 + WWL × (-1-PL × (1 + PL × (ZT-PL))/ZT)/PL3;
         e̲n̲d̲;
      AI := C × (CO × K1 + SI × K2);
      AID := 0·25 × AI/Z - C × SQRTZ × (CO × K3 + SI × K4);
      BI := C × (CO × K2-SI × K1);
      BID := 0·25 × BI/Z - C × SQRTZ × (CO × K4-SI × K3);
   e̲n̲d̲ e̲l̲s̲e̲
   b̲e̲g̲i̲n̲ i̲f̲ Z < 9 t̲h̲e̲n̲ EXPZT := EXP(ZT) e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ EXPZT := 1; EXPON := ZT e̲n̲d̲;
      f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ 10 d̲o̲
         b̲e̲g̲i̲n̲ WWL := WW[L]; PL := XX[L]/ZT;
            PL1 := 1 + PL; PL2 := 1-PL;
            K1 := K1 + WWL/PL1;
            K2 := K2 + WWL × PL/(ZT × PL1 × PL1);
            K3 := K3 + WWL/PL2;
            K4 := K4 + WWL × PL/(ZT × PL2 × PL2);
         e̲n̲d̲;
      AI := 0·5 × C × K1/EXPZT;
      AID := AI × (-·25/Z-SQRTZ) + 0·5 × C × SQRTZ × K2/EXPZT;
      i̲f̲ Z ≥ 8 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ BI := C × K3 × EXPZT;
         BID := BI × (SQRTZ-0·25/Z) - C × K4 × SQRTZ × EXPZT;
      e̲n̲d̲;
   e̲n̲d̲;
   END:
e̲n̲d̲ AIRY;
c̲o̲m̲m̲e̲n̲t̲ ================== 35145 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ AIRYZEROS(N, D, ZAI, VAI);
   v̲a̲l̲u̲e̲ N, D; i̲n̲t̲e̲g̲e̲r̲ N, D; a̲r̲r̲a̲y̲ ZAI, VAI;
b̲e̲g̲i̲n̲ B̲o̲o̲l̲e̲a̲n̲ A, FOUND; i̲n̲t̲e̲g̲e̲r̲ I;
   r̲e̲a̲l̲ C, E, R, ZAJ, ZAK, VAJ, DAJ, KAJ, ZZ;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ AIRY(A, B, C, D, E, F, G); c̲o̲d̲e̲ 35140;

   A :=  D = 0 ∨ D = 2;
   R := i̲f̲ D = 0 ∨ D = 3 t̲h̲e̲n̲ -1·1780 97245 09617
   e̲l̲s̲e̲ -3·5342 91735 28852;
   c̲o̲m̲m̲e̲n̲t̲  R := i̲f̲ D = 0 ∨ D = 3 t̲h̲e̲n̲ -3 × PI / 8
     e̲l̲s̲e̲ -9 × PI / 8;
   AIRY(0, ZAJ, VAJ, DAJ, KAJ, ZZ, t̲r̲u̲e̲);
   f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
      b̲e̲g̲i̲n̲ R := R + 4·7123 88980 38469; c̲o̲m̲m̲e̲n̲t̲ R := R + 3 × PI / 2;
         ZZ := R × R;
         ZAJ := i̲f̲ I = 1 ∧ D = 1 t̲h̲e̲n̲ -1·01879 297 e̲l̲s̲e̲
           i̲f̲ I = 1 ∧ D = 2 t̲h̲e̲n̲ -1·17371 322 e̲l̲s̲e̲
           R ⭡ 0·66666 66666 66667 × ( i̲f̲ A t̲h̲e̲n̲
           - ( 1 + ( 5/48 - ( 5/36 - ( 77125/82944 - (
           1080 56875 / 69 67296 - (16 23755 96875 / 3344 30208)
           /ZZ)/ZZ)/ZZ)/ZZ)/ZZ)
         e̲l̲s̲e̲
           - ( 1 - ( 7/48 - ( 35/288 - ( 1 81223 / 2 07360 - (
           186 83371 / 12 44160 - ( 9 11458 84361 / 1911 02976 )
           /ZZ)/ZZ)/ZZ)/ZZ)/ZZ));
         i̲f̲  D ≤ 1 t̲h̲e̲n̲ AIRY(ZAJ, VAJ, DAJ, C, E, ZZ, f̲a̲l̲s̲e̲)
         e̲l̲s̲e̲ AIRY(ZAJ, C, E, VAJ, DAJ, ZZ, f̲a̲l̲s̲e̲);
         FOUND := ABS( i̲f̲ A t̲h̲e̲n̲ VAJ e̲l̲s̲e̲ DAJ ) < @-12;
         f̲o̲r̲ C := C w̲h̲i̲l̲e̲ ¬FOUND d̲o̲
            b̲e̲g̲i̲n̲ i̲f̲ A t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ KAJ := VAJ / DAJ;
                  ZAK := ZAJ - KAJ × (1 + ZAJ × KAJ × KAJ)
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ KAJ := DAJ / (ZAJ × VAJ);
                  ZAK := ZAJ - KAJ × (1 + KAJ × (KAJ × ZAJ + 1 / ZAJ))
               e̲n̲d̲;
               i̲f̲  D ≤ 1 t̲h̲e̲n̲ AIRY(ZAK, VAJ, DAJ, C, E, ZZ, f̲a̲l̲s̲e̲)
               e̲l̲s̲e̲ AIRY(ZAK, C, E, VAJ, DAJ, ZZ, f̲a̲l̲s̲e̲);
               FOUND := ABS(ZAK - ZAJ) < @-14 × ABS(ZAK) ∨
                 ABS(i̲f̲ A t̲h̲e̲n̲ VAJ e̲l̲s̲e̲ DAJ) < @-12;
               ZAJ := ZAK
            e̲n̲d̲;
         VAI[I] := i̲f̲ A t̲h̲e̲n̲ DAJ e̲l̲s̲e̲ VAJ;
         ZAI[I] := ZAJ;
      e̲n̲d̲;
   AIRYZEROS := ZAI[N];
e̲n̲d̲ AIRYZEROS;
c̲o̲m̲m̲e̲n̲t̲ ================== 31040 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ POL(N, X, A);
   v̲a̲l̲u̲e̲ N, X; i̲n̲t̲e̲g̲e̲r̲ N; r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R;
   R := 0;
   f̲o̲r̲ N := N s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 0 d̲o̲
        R := R × X  + A[N];
   POL := R
e̲n̲d̲ POL;
c̲o̲m̲m̲e̲n̲t̲ ================== 31241 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAYPOL(N, K, X, A);
   v̲a̲l̲u̲e̲ N, K, X;
   i̲n̲t̲e̲g̲e̲r̲ N, K; r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A;
   i̲f̲ X ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, NM1;
      r̲e̲a̲l̲ XJ, AA, H;
      XJ := 1;
      f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ XJ := XJ × X; A[J] := A[J] × XJ e̲n̲d̲;
      AA := A[N]; NM1 := N-1;
      f̲o̲r̲ J := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
         b̲e̲g̲i̲n̲ H := AA;
            f̲o̲r̲ I := NM1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ J d̲o̲
                 H := A[ I] := A[I] + H
         e̲n̲d̲
   e̲n̲d̲ e̲l̲s̲e̲
     f̲o̲r̲ K := K s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 1 d̲o̲ A[K] := 0;
c̲o̲m̲m̲e̲n̲t̲ ================== 31242 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ NORDERPOL (N, K, X, A);
   v̲a̲l̲u̲e̲ N, K, X;
   i̲n̲t̲e̲g̲e̲r̲ N, K; r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A;
   i̲f̲ X ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, NM1;
      r̲e̲a̲l̲ XJ, AA, H;
      a̲r̲r̲a̲y̲ XX[0:N];
      XJ := 1;
      f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ XJ := XX[J] := XJ × X; A[J] := A[J] × XJ e̲n̲d̲;
      H := AA := A[N]; NM1 := N-1;
      f̲o̲r̲ I := NM1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ 0 d̲o̲ H := A[I] := A[I] + H;
      f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
         b̲e̲g̲i̲n̲ H := AA;
            f̲o̲r̲ I := NM1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ J d̲o̲
                 H := A[ I] := A[I] + H;
            A[J] := H/XX[J]
         e̲n̲d̲
   e̲n̲d̲ NORDERPOL ;
c̲o̲m̲m̲e̲n̲t̲ ================== 31243 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERPOL (N, K, X, A);
   v̲a̲l̲u̲e̲ N, K, X;
   i̲n̲t̲e̲g̲e̲r̲ N, K; r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ J; r̲e̲a̲l̲ FAC;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ NORDERPOL(N, K, X, A); c̲o̲d̲e̲ 31242;
   FAC := 1;
   NORDERPOL (N, K, X, A);
   f̲o̲r̲ J := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
      b̲e̲g̲i̲n̲ FAC := FAC × J; A[J] := A[J] × FAC e̲n̲d̲
e̲n̲d̲ DERPOL ;
c̲o̲m̲m̲e̲n̲t̲ ================== 32075 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TRICUB(XI, YI, XJ, YJ, XK, YK, G, RE, AE);
   v̲a̲l̲u̲e̲ XI, YI, XJ, YJ, XK, YK, RE, AE;
   r̲e̲a̲l̲ XI, YI, XJ, YJ, XK, YK, RE, AE; r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ G;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ SURF, SURFMIN, XZ, YZ, GI, GJ, GK;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INT(AX1, AY1, AF1, AX2, AY2, AF2, AX3, AY3, AF3,
     BX1, BY1, BF1, BX2, BY2, BF2, BX3, BY3, BF3,
     PX, PY, PF);
      v̲a̲l̲u̲e̲ BX1, BY1, BF1, BX2, BY2, BF2, BX3, BY3, BF3, PX, PY, PF;
      r̲e̲a̲l̲  BX1, BY1, BF1, BX2, BY2, BF2, BX3, BY3, BF3, PX, PY, PF,
        AX1, AY1, AF1, AX2, AY2, AF2, AX3, AY3, AF3;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, I3, I4, I5, A, B, C, SX1, SY1, SX2, SY2, SX3, SY3,
        CX1, CY1, CF1, CX2, CY2, CF2, CX3, CY3, CF3,
        DX1, DY1, DF1, DX2, DY2, DF2, DX3, DY3, DF3;

      A := AF1 + AF2 + AF3; B := BF1 + BF2 + BF3;
      I3 := 3 × A + 27 × PF + 8 × B;
      E := ABS(I3) × RE + AE;

      i̲f̲ SURF < SURFMIN ∨ ABS(5 × A + 45 × PF - I3) < E
        t̲h̲e̲n̲ INT := I3 × SURF e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ CX1 := AX1 + PX; CY1 := AY1 + PY; CF1 := G(CX1, CY1);
         CX2 := AX2 + PX; CY2 := AY2 + PY; CF2 := G(CX2, CY2);
         CX3 := AX3 + PX; CY3 := AY3 + PY; CF3 := G(CX3, CY3);
         C := CF1 + CF2 + CF3;
         I4 := A + 9 × PF + 4 × B + 12 × C;

         i̲f̲ ABS(I3 - I4) < E t̲h̲e̲n̲ INT := I4 × SURF e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ SX1 := ·5 × BX1; SY1 := ·5 × BY1;
            DX1 := AX1 + SX1; DY1 := AY1 + SY1; DF1 := G(DX1, DY1);
            SX2 :=  ·5 × BX2; SY2 :=  ·5 × BY2;
            DX2 := AX2 + SX2; DY2 := AY2 + SY2; DF2 := G(DX2, DY2);
            SX3 :=  ·5 × BX3; SY3 :=  ·5 × BY3;
            DX3 := AX3 + SX3; DY3 := AY3 + SY3; DF3 := G(DX3, DY3);

            I5 := (51 × A + 2187 × PF + 276 × B + 972 × C -
              768 × (DF1 + DF2 + DF3))/63;
            i̲f̲ ABS(I4 - I5) < E t̲h̲e̲n̲ INT := I5 × SURF e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ SURF := ·25 × SURF;

               INT :=

                 INT(SX1, SY1, BF1, SX2, SY2, BF2, SX3, SY3, BF3,
                 DX1, DY1, DF1, DX2, DY2, DF2, DX3, DY3, DF3,
                 PX, PY, PF) +

                 INT(AX1, AY1, AF1, SX3, SY3, BF3, SX2, SY2, BF2, DX1, DY1, DF1,
                 AX1 + SX2, AY1 + SY2, G(AX1 + SX2, AY1 + SY2),
                 AX1 + SX3, AY1 + SY3, G(AX1 + SX3, AY1 + SY3),·5 × CX1, ·5 × CY1, CF1) +
                 INT(AX2, AY2, AF2, SX3, SY3, BF3, SX1, SY1, BF1, DX2, DY2, DF2,
                 AX2 + SX1, AY2 + SY1, G(AX2 + SX1, AY2 + SY1),
                 AX2 + SX3, AY2 + SY3, G(AX2 + SX3, AY2 + SY3),·5 × CX2, ·5 × CY2, CF2) +
                 INT(AX3, AY3, AF3, SX1, SY1, BF1, SX2, SY2, BF2, DX3, DY3, DF3,
                 AX3 + SX2, AY3 + SY2, G(AX3 + SX2, AY3 + SY2),
                 AX3 + SX1, AY3 + SY1, G(AX3 + SX1, AY3 + SY1),·5 × CX3, ·5 × CY3, CF3);

               SURF := 4 × SURF
            e̲n̲d̲
         e̲n̲d̲
      e̲n̲d̲
   e̲n̲d̲ INT;

   SURF := 0·5 × ABS(XJ × YK - XK × YJ + XI × YJ -
     XJ × YI + XK × YI - XI × YK);
   SURFMIN := SURF × RE; RE := 30 × RE; AE := 30 × AE/SURF;
   XZ := (XI + XJ + XK)/3; YZ := (YI + YJ + YK)/3;
   GI := G(XI, YI); GJ := G(XJ, YJ); GK := G(XK, YK);
   XI := XI × ·5; YI := YI × ·5; XJ := XJ × ·5;
   YJ := YJ × ·5; XK := XK × ·5; YK := YK × ·5;

   TRICUB := INT(XI, YI, GI, XJ, YJ, GJ, XK, YK, GK,
     XJ + XK, YJ + YK, G(XJ + XK, YJ + YK),
     XK + XI, YK + YI, G(XK + XI, YK + YI),
     XI + XJ, YI + YJ, G(XI + XJ, YI + YJ),·5 × XZ, ·5 × YZ, G(XZ, YZ))/60
e̲n̲d̲ TRICUB;
c̲o̲m̲m̲e̲n̲t̲ ================== 34444 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ PEIDE(N, M, NOBS, NBP, PAR, RES, BP, JTJINV, IN, OUT, DERIV, JAC DFDY,
  JAC DFDP, CALL YSTART, DATA, MONITOR);
   v̲a̲l̲u̲e̲ N, M, NOBS; i̲n̲t̲e̲g̲e̲r̲ N, M, NOBS, NBP;
     a̲r̲r̲a̲y̲ PAR, RES, JTJINV, IN, OUT;
     i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ BP;
     p̲r̲o̲c̲e̲d̲u̲r̲e̲  CALL YSTART, DATA, MONITOR;
     B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ DERIV, JAC DFDY, JACDFDP;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I, J, EXTRA, WEIGHT, NCOL, NROW, AWAY, NPAR, II, JJ, MAX,
     NFE, NIS;
   r̲e̲a̲l̲ EPS, EPS1, XEND, C, X, T, HMIN, HMAX, RES1, IN3, IN4, FAC3, FAC4;
   a̲r̲r̲a̲y̲ AUX[1:3], OBS[1:NOBS], SAVE[-38:6 × N], TOBS[0:NOBS],
     YP[1:NBP + NOBS, 1:NBP + M], YMAX[1:N], Y[1:6 × N × (NBP + M + 1)], FY[1:N, 1:N],
     FP[1:N, 1:M + NBP];
   i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ COBS[1:NOBS];
   B̲o̲o̲l̲e̲a̲n̲ FIRST, SEC, CLEAN;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIVEC(L, U, A, X); c̲o̲d̲e̲ 31010;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ INIMAT(L1, U1, L2, U2, A, X); c̲o̲d̲e̲ 31011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULVEC(L, U, S, A, B, X); c̲o̲d̲e̲ 31020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MULROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 31021;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, S, A, B); c̲o̲d̲e̲ 31030;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPMAT(L1, U1, L2, U2, A, B); c̲o̲d̲e̲ 31035;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ VECVEC(L, U, S, A, B); c̲o̲d̲e̲ 34010;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMVEC(L, U, S, A, B, X); c̲o̲d̲e̲ 34020;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ SOL(A, N, P, B); c̲o̲d̲e̲ 34051;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DEC(A, N, AUX, P); c̲o̲d̲e̲ 34300;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ MARQUARDT(M, N, P, R, C, F, J, I, O); c̲o̲d̲e̲ 34440;

   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ INTERPOL(STARTINDEX, JUMP, K, TOBSDIF);
      v̲a̲l̲u̲e̲ STARTINDEX, JUMP, K, TOBSDIF;
      i̲n̲t̲e̲g̲e̲r̲ STARTINDEX, JUMP, K; r̲e̲a̲l̲ TOBSDIF;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ I; r̲e̲a̲l̲ S, R; S := Y[STARTINDEX]; R := TOBSDIF;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
         b̲e̲g̲i̲n̲ STARTINDEX := STARTINDEX + JUMP;
            S := S + Y[STARTINDEX] × R; R := R × TOBSDIF
         e̲n̲d̲; INTERPOL := S
   e̲n̲d̲ INTERPOL;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ JAC DYDP(NROW, NCOL, PAR, RES, JAC, LOCFUNCT);
      v̲a̲l̲u̲e̲ NROW, NCOL; i̲n̲t̲e̲g̲e̲r̲ NROW, NCOL;
        a̲r̲r̲a̲y̲ PAR, RES, JAC; p̲r̲o̲c̲e̲d̲u̲r̲e̲ LOCFUNCT;
   b̲e̲g̲i̲n̲
      DUPMAT(1, NROW, 1, NCOL, JAC, YP)
   e̲n̲d̲ JACOBIAN;
   B̲o̲o̲l̲e̲a̲n̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ FUNCT(NROW, NCOL, PAR, RES);
      v̲a̲l̲u̲e̲ NROW, NCOL; i̲n̲t̲e̲g̲e̲r̲ NROW, NCOL; a̲r̲r̲a̲y̲ PAR, RES;
   b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, K, KNEW, FAILS, SAME, KPOLD, N6, NNPAR, J5N,
        COBSII;
      r̲e̲a̲l̲ XOLD, HOLD, A0, TOLUP, TOL, TOLDWN, TOLCONV, H, CH, CHNEW,
        ERROR, DFI, TOBSDIF;
      B̲o̲o̲l̲e̲a̲n̲ EVALUATE, EVALUATED, DECOMPOSE, CONV;
      a̲r̲r̲a̲y̲ A[0:5], DELTA, LAST DELTA, DF, Y0[1:N], JACOB[1:N, 1:N];
      i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ P[1:N];

      r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ NORM2(AI); r̲e̲a̲l̲ AI;
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ S, A; S := @-100;
         f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ A := AI/YMAX[I]; S := S + A × A e̲n̲d̲;
         NORM2 := S
      e̲n̲d̲ NORM2;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ RESET;
      b̲e̲g̲i̲n̲ i̲f̲ CH < HMIN/HOLD t̲h̲e̲n̲ CH := HMIN/HOLD e̲l̲s̲e̲
         i̲f̲ CH > HMAX/HOLD t̲h̲e̲n̲ CH := HMAX/HOLD;
         X := XOLD; H := HOLD × CH; C := 1;
         f̲o̲r̲ J := 0 s̲t̲e̲p̲ N u̲n̲t̲i̲l̲ K × N d̲o̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                    Y[J + I] := SAVE[J + I] × C;
               C := C × CH
            e̲n̲d̲;
         DECOMPOSE := t̲r̲u̲e̲
      e̲n̲d̲ RESET;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ ORDER;
      b̲e̲g̲i̲n̲ C := EPS × EPS; J := (K-1) × (K + 8)/2 - 38;
         f̲o̲r̲ I := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲ A[I] := SAVE[I + J];
         J := J + K + 1;
         TOLUP  := C × SAVE[J];
         TOL    := C × SAVE[J + 1];
         TOLDWN := C × SAVE[J + 2];
         TOLCONV := EPS/(2 × N × (K + 2));
         A0 := A[0];  DECOMPOSE := t̲r̲u̲e̲;
      e̲n̲d̲ ORDER;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ EVALUATE JACOBIAN;
      b̲e̲g̲i̲n̲ EVALUATE := f̲a̲l̲s̲e̲;
         DECOMPOSE := EVALUATED := t̲r̲u̲e̲;
         i̲f̲ ¬JAC DFDY(PAR, Y, X, FY) t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ SAVE[-3] := 4; g̲o̲t̲o̲ RETURN e̲n̲d̲;
      e̲n̲d̲ EVALUATE JACOBIAN;
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ DECOMPOSE JACOBIAN;
      b̲e̲g̲i̲n̲ DECOMPOSE := f̲a̲l̲s̲e̲;
         C := -A0 × H;
         f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                    JACOB[I, J] := FY[I, J] × C;
               JACOB[J, J] := JACOB[J, J] + 1
            e̲n̲d̲;
         DEC(JACOB, N, AUX, P)
      e̲n̲d̲ DECOMPOSE JACOBIAN;

      p̲r̲o̲c̲e̲d̲u̲r̲e̲ CALCULATE STEP AND ORDER;
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ A1, A2, A3;
         A1 := i̲f̲ K ≤ 1 t̲h̲e̲n̲ 0 e̲l̲s̲e̲
           0·75 × (TOLDWN/NORM2(Y[K × N + I])) ⭡ (0·5/K);
         A2 := 0·80 × (TOL/ERROR) ⭡ (0·5/(K + 1));
         A3 := i̲f̲ K ≥ 5 ∨ FAILS ≠ 0
           t̲h̲e̲n̲ 0 e̲l̲s̲e̲
           0·70 × (TOLUP/NORM2(DELTA[I] - LAST DELTA[I]))⭡
           (0·5/(K + 2));

         i̲f̲ A1 > A2 ∧ A1 > A3 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ KNEW := K-1; CHNEW := A1 e̲n̲d̲ e̲l̲s̲e̲
         i̲f̲ A2 > A3 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ KNEW := K  ; CHNEW := A2 e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲ KNEW := K + 1; CHNEW := A3 e̲n̲d̲
      e̲n̲d̲ CALCULATE STEP AND ORDER;

      i̲f̲ SEC t̲h̲e̲n̲ b̲e̲g̲i̲n̲ SEC := f̲a̲l̲s̲e̲; g̲o̲t̲o̲ RETURN e̲n̲d̲;
      NPAR := M; EXTRA := NIS := 0; II := 1;
      JJ := i̲f̲ NBP = 0 t̲h̲e̲n̲ 0 e̲l̲s̲e̲ 1;
      N6 := N × 6;
      INIVEC(-3, -1, SAVE, 0);
      INIVEC(N6 + 1, (6 + M) × N, Y, 0);
      INIMAT(1, NOBS + NBP, 1, M + NBP, YP, 0);
      T := TOBS[1]; X := TOBS[0];
      CALL YSTART(PAR, Y, YMAX);
      HMAX := TOBS[1]-TOBS[0]; HMIN := HMAX × IN[1];
      EVALUATE JACOBIAN; NNPAR := N × NPAR;

      NEW START:
      K := 1; KPOLD := 0; SAME := 2; ORDER;
      i̲f̲ ¬DERIV(PAR, Y, X, DF) t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ SAVE[-3] := 3; g̲o̲t̲o̲ RETURN e̲n̲d̲;
      H := SQRT(2 × EPS/SQRT(NORM2 (MATVEC(1, N, I, FY, DF))));
      i̲f̲ H > HMAX t̲h̲e̲n̲ H := HMAX e̲l̲s̲e̲
      i̲f̲ H < HMIN t̲h̲e̲n̲ H := HMIN;
      XOLD := X; HOLD := H; CH := 1;
      f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ SAVE[I] := Y[I]; SAVE[N + I] := Y[N + I] := DF[I] × H e̲n̲d̲;
      FAILS := 0;
      f̲o̲r̲ L := 0 w̲h̲i̲l̲e̲ X < XEND d̲o̲
         b̲e̲g̲i̲n̲ i̲f̲ X + H ≤ XEND t̲h̲e̲n̲ X := X + H e̲l̲s̲e̲
            b̲e̲g̲i̲n̲ H := XEND-X; X := XEND; CH := H/HOLD; C := 1;
               f̲o̲r̲ J := N s̲t̲e̲p̲ N u̲n̲t̲i̲l̲ K × N d̲o̲
                  b̲e̲g̲i̲n̲ C := C × CH;
                     f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J + N d̲o̲
                          Y[I] := Y[I] × C
                  e̲n̲d̲;
               SAME := i̲f̲ SAME < 3 t̲h̲e̲n̲ 3 e̲l̲s̲e̲ SAME + 1;
            e̲n̲d̲;

            c̲o̲m̲m̲e̲n̲t̲ PREDICTION;
            f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ f̲o̲r̲ I := L s̲t̲e̲p̲ N u̲n̲t̲i̲l̲ (K-1) × N + L d̲o̲
                       f̲o̲r̲ J := (K-1) × N + L s̲t̲e̲p̲ -N u̲n̲t̲i̲l̲ I d̲o̲
                          Y[J] := Y[J] + Y[J + N];
                  DELTA[L] := 0
               e̲n̲d̲;  EVALUATED := f̲a̲l̲s̲e̲;

            c̲o̲m̲m̲e̲n̲t̲ CORRECTION AND ESTIMATION LOCAL ERROR;
            f̲o̲r̲ L := 1, 2, 3 d̲o̲
               b̲e̲g̲i̲n̲ i̲f̲ ¬DERIV(PAR, Y, X, DF) t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ SAVE[-3] := 3; g̲o̲t̲o̲ RETURN e̲n̲d̲;
                  f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                       DF[I] := DF[I] × H - Y[N + I];
                  i̲f̲ EVALUATE t̲h̲e̲n̲ EVALUATE JACOBIAN;
                  i̲f̲ DECOMPOSE t̲h̲e̲n̲ DECOMPOSE JACOBIAN;
                  SOL(JACOB, N, P, DF);

                  CONV := t̲r̲u̲e̲;
                  f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                     b̲e̲g̲i̲n̲ DFI := DF[I];
                        Y[  I] := Y[  I] + A0 × DFI;
                        Y[N + I] := Y[N + I] +      DFI;
                        DELTA[I] := DELTA[I] +  DFI;
                        CONV := CONV ∧ ABS(DFI) < TOLCONV × YMAX[I]
                     e̲n̲d̲;
                  i̲f̲ CONV t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ ERROR := NORM2(DELTA[I]);
                     g̲o̲t̲o̲ CONVERGENCE
                  e̲n̲d̲
               e̲n̲d̲;

            c̲o̲m̲m̲e̲n̲t̲ ACCEPTANCE OR REJECTION;
            i̲f̲ ¬CONV t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ i̲f̲ ¬EVALUATED t̲h̲e̲n̲ EVALUATE := t̲r̲u̲e̲
               e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ CH := CH/4; i̲f̲ H < 4 × HMIN t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ SAVE[-1] := SAVE[-1] + 10;
                     HMIN := HMIN/10;
                     i̲f̲ SAVE[-1] > 40 t̲h̲e̲n̲ g̲o̲t̲o̲ RETURN
                  e̲n̲d̲
               e̲n̲d̲;
               RESET
            e̲n̲d̲ e̲l̲s̲e̲ CONVERGENCE:

            i̲f̲ ERROR > TOL t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ FAILS := FAILS + 1;
               i̲f̲ H > 1·1 × HMIN t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ i̲f̲ FAILS > 2 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲  RESET; g̲o̲t̲o̲ NEW START
                  e̲n̲d̲ e̲l̲s̲e̲
                  b̲e̲g̲i̲n̲ CALCULATE STEP AND ORDER;
                     i̲f̲ KNEW ≠ K t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ K := KNEW; ORDER e̲n̲d̲;
                     CH := CH × CHNEW; RESET
                  e̲n̲d̲
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ i̲f̲ K = 1 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ VIOLATE EPS CRITERION;
                     SAVE[-2] := SAVE[-2] + 1;
                     SAME := 4; g̲o̲t̲o̲ ERROR TEST OK
                  e̲n̲d̲;
                  K := 1; RESET; ORDER; SAME := 2
               e̲n̲d̲
            e̲n̲d̲ e̲l̲s̲e̲ ERROR TEST OK:

            b̲e̲g̲i̲n̲ FAILS := 0;
               f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                  b̲e̲g̲i̲n̲ C := DELTA[I];
                     f̲o̲r̲ L := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
                          Y[L × N + I] := Y[L × N + I] + A[L] × C;
                     i̲f̲ ABS(Y[I]) > YMAX[I] t̲h̲e̲n̲
                       YMAX[I] :=  ABS(Y[I])
                  e̲n̲d̲;

               SAME := SAME-1;
               i̲f̲ SAME = 1 t̲h̲e̲n̲
                 DUPVEC(1, N, 0, LAST DELTA, DELTA) e̲l̲s̲e̲
               i̲f̲ SAME = 0 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ CALCULATE STEP AND ORDER;
                  i̲f̲ CHNEW > 1·1 t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲
                     i̲f̲ K ≠ KNEW t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ i̲f̲ KNEW > K t̲h̲e̲n̲
                          MULVEC(KNEW × N + 1, KNEW × N + N, -KNEW × N, Y, DELTA,
                          A[K]/KNEW);
                        K := KNEW; ORDER
                     e̲n̲d̲;
                     SAME := K + 1;
                     i̲f̲ CHNEW × H > HMAX
                       t̲h̲e̲n̲ CHNEW := HMAX/H;
                     H := H × CHNEW; C := 1;
                     f̲o̲r̲ J := N s̲t̲e̲p̲ N u̲n̲t̲i̲l̲ K × N d̲o̲
                        b̲e̲g̲i̲n̲ C := C × CHNEW;
                           MULVEC(J + 1, J + N, 0, Y, Y, C)
                        e̲n̲d̲; DECOMPOSE := t̲r̲u̲e̲
                  e̲n̲d̲
                  e̲l̲s̲e̲ SAME := 10
               e̲n̲d̲ OF A SINGLE INTEGRATION STEP OF Y;
               NIS := NIS + 1;

               c̲o̲m̲m̲e̲n̲t̲ START OF A INTEGRATION STEP OF YP;
               i̲f̲ CLEAN t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ HOLD := H; XOLD := X; KPOLD := K; CH := 1;
                  DUPVEC(1, K × N + N, 0, SAVE, Y)
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ i̲f̲ H ≠ HOLD t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ CH := H/HOLD; C := 1;
                     f̲o̲r̲ J := N6 + NNPAR s̲t̲e̲p̲ NNPAR u̲n̲t̲i̲l̲
                       KPOLD × NNPAR + N6 d̲o̲
                        b̲e̲g̲i̲n̲ C := C × CH;
                           f̲o̲r̲ I := J + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ J + NNPAR d̲o̲
                                Y[I] := Y[I] × C
                        e̲n̲d̲; HOLD := H
                  e̲n̲d̲;
                  i̲f̲ K > KPOLD t̲h̲e̲n̲
                    INIVEC(N6 + K × NNPAR + 1, N6 + K × NNPAR + NNPAR, Y, 0);
                  XOLD := X; KPOLD := K; CH := 1;
                  DUPVEC(1, K × N + N, 0, SAVE, Y);
                  EVALUATE JACOBIAN;
                  DECOMPOSE JACOBIAN;
                  i̲f̲ ¬JAC DFDP(PAR, Y, X, FP) t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ SAVE[-3] := 5; g̲o̲t̲o̲ RETURN e̲n̲d̲;
                  i̲f̲ NPAR > M t̲h̲e̲n̲ INIMAT(1, N, M + 1, NPAR, FP, 0);

                  c̲o̲m̲m̲e̲n̲t̲ PREDICTION;
                  f̲o̲r̲ L := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K-1 d̲o̲
                       f̲o̲r̲ J := K-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ L d̲o̲
                          ELMVEC(J × NNPAR + N6 + 1, J × NNPAR + N6 + NNPAR, NNPAR, Y, Y, 1);

                  c̲o̲m̲m̲e̲n̲t̲ CORRECTION;
                  f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NPAR d̲o̲
                     b̲e̲g̲i̲n̲ J5N := (J + 5) × N;
                        DUPVEC(1, N, J5N, Y0, Y);
                        f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ DF[I] :=
                             H × (FP[I, J] + MATVEC(1, N, I, FY, Y0))
                             -Y[NNPAR + J5N + I];
                        SOL(JACOB, N, P, DF);
                        f̲o̲r̲ L := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K d̲o̲
                           b̲e̲g̲i̲n̲ I := L × NNPAR + J5N;
                              ELMVEC(I + 1, I + N, -I, Y, DF, A[L])
                           e̲n̲d̲
                     e̲n̲d̲
               e̲n̲d̲;
               f̲o̲r̲ L := 0 w̲h̲i̲l̲e̲ X ≥ T d̲o̲
                  b̲e̲g̲i̲n̲
                     c̲o̲m̲m̲e̲n̲t̲ CALCULATION OF A ROW OF THE JACOBIAN
                       MATRIX AND AN ELEMENT OF THE RESIDUAL
                       VECTOR;
                     TOBSDIF := (TOBS[II]-X)/H; COBSII := COBS[II];
                     RES[II] := INTERPOL(COBSII, N, K, TOBSDIF)-OBS[II];
                     i̲f̲ ¬CLEAN t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NPAR d̲o̲
                             YP[II, I] := INTERPOL(COBSII + (I + 5) × N, NNPAR, K,
                             TOBSDIF);

                        c̲o̲m̲m̲e̲n̲t̲ INTRODUCING OF BREAK-POINTS;
                        i̲f̲ BP[JJ] ≠ II t̲h̲e̲n̲ e̲l̲s̲e̲
                        i̲f̲ FIRST ∧ ABS(RES[II]) < EPS1 t̲h̲e̲n̲
                        b̲e̲g̲i̲n̲ NBP := NBP-1; DUPVEC(JJ, NBP, 1, BP, BP);
                           BP[NBP + 1] := 0
                        e̲n̲d̲ e̲l̲s̲e̲
                        b̲e̲g̲i̲n̲ EXTRA := EXTRA + 1;
                           i̲f̲ FIRST t̲h̲e̲n̲ PAR[M + JJ] := OBS[II];
                           c̲o̲m̲m̲e̲n̲t̲ INTRODUCING A JACOBIAN ROW AND A
                             RESIDUAL VECTOR ELEMENT FOR
                             CONTINUITY REQUIREMENTS;
                           YP[NOBS + JJ, M + JJ] := -WEIGHT;
                           MULROW(1, NPAR, NOBS + JJ, II, YP, YP, WEIGHT);
                           RES[NOBS + JJ] := WEIGHT × (RES[II] + OBS[II]-
                             PAR[M + JJ])
                        e̲n̲d̲
                     e̲n̲d̲;

                     i̲f̲ II = NOBS t̲h̲e̲n̲ g̲o̲t̲o̲ RETURN e̲l̲s̲e̲
                     b̲e̲g̲i̲n̲ T := TOBS[II + 1];
                        i̲f̲ BP[JJ] = II ∧ JJ < NBP t̲h̲e̲n̲ JJ := JJ + 1;
                        HMAX := T-TOBS[II]; HMIN := HMAX × IN[1]; II := II + 1
                     e̲n̲d̲;
                  e̲n̲d̲;

               c̲o̲m̲m̲e̲n̲t̲ BREAK-POINTS INTRODUCE NEW INITIAL VALUES
                 FOR Y AND YP;
               i̲f̲ EXTRA > 0 t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
                     b̲e̲g̲i̲n̲ Y[I] := INTERPOL(I, N, K, TOBSDIF);
                        f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NPAR d̲o̲
                             Y[I + (J + 5) × N] := INTERPOL(I + (J + 5) × N, NNPAR, K,
                             TOBSDIF)
                     e̲n̲d̲;
                  f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ EXTRA d̲o̲
                     b̲e̲g̲i̲n̲  COBSII := COBS[BP[NPAR-M + L]];
                        Y[COBSII] := PAR[NPAR + L];
                        f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NPAR + EXTRA d̲o̲
                             Y[COBSII + (5 + I) × N] := 0;
                        INIVEC(1 + NNPAR + (L + 5) × N, NNPAR + (L + 6) × N, Y, 0);
                        Y[COBSII + (5 + NPAR + L) × N] := 1
                     e̲n̲d̲;
                  NPAR := NPAR + EXTRA; EXTRA := 0;
                  X := TOBS[II-1]; EVALUATE JACOBIAN; NNPAR := N × NPAR;
                  g̲o̲t̲o̲ NEW START
               e̲n̲d̲
            e̲n̲d̲
         e̲n̲d̲ STEP;

      RETURN:
      i̲f̲ SAVE[-2] > MAX t̲h̲e̲n̲ MAX := SAVE[-2];
      FUNCT := SAVE[-1] ≤ 40 ∧ SAVE[-3] = 0;
      i̲f̲ ¬FIRST t̲h̲e̲n̲
        MONITOR(1, NCOL, NROW, PAR, RES, WEIGHT, NIS)
   e̲n̲d̲ FUNCT;
   I := -39;
   f̲o̲r̲ C := 1, 1, 9, 4, 0, 2/3, 1, 1/3, 36, 20·25, 1, 6/11,
     1, 6/11, 1/11, 84·028, 53·778, 0·25, ·48, 1, ·7, ·2, ·02,
     156·25, 108·51, ·027778, 120/274, 1, 225/274,
     85/274, 15/274, 1/274, 0, 187·69, ·0047361
     d̲o̲ b̲e̲g̲i̲n̲ I := I + 1; SAVE[I] := C e̲n̲d̲;

   DATA(NOBS, TOBS, OBS, COBS); WEIGHT := 1;
   FIRST := SEC := f̲a̲l̲s̲e̲; CLEAN := NBP > 0;
   AUX[2] := @-12; EPS := IN[2]; EPS1 := @10;
   XEND := TOBS[NOBS]; OUT[1] := 0; BP[0] := MAX := 0;

   c̲o̲m̲m̲e̲n̲t̲ SMOOTH INTEGRATION WITHOUT BREAK-POINTS;
   i̲f̲ ¬FUNCT(NOBS, M, PAR, RES) t̲h̲e̲n̲ g̲o̲t̲o̲ ESCAPE;
   RES1 := SQRT(VECVEC(1, NOBS, 0, RES, RES)); NFE := 1;
   i̲f̲ IN[5] = 1 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ OUT[1] := 1; g̲o̲t̲o̲ ESCAPE e̲n̲d̲;

   i̲f̲ CLEAN t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ FIRST := t̲r̲u̲e̲; CLEAN := f̲a̲l̲s̲e̲;
      FAC3 := SQRT(SQRT(IN[3]/RES1)); FAC4 := SQRT(SQRT(IN[4]/RES1));
      EPS1 := RES1 × FAC4;
      i̲f̲ ¬FUNCT(NOBS, M, PAR, RES) t̲h̲e̲n̲ g̲o̲t̲o̲ ESCAPE;
      FIRST := f̲a̲l̲s̲e̲
   e̲n̲d̲ e̲l̲s̲e̲ NFE := 0;

   NCOL := M + NBP; NROW := NOBS + NBP;
   SEC := t̲r̲u̲e̲;
   IN3 := IN[3]; IN4 := IN[4]; IN[3] := RES1;

   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ W; a̲r̲r̲a̲y̲ AID[1:NCOL, 1:NCOL];
      WEIGHT := AWAY := 0;
      OUT[4] := OUT[5] := W := 0;
      f̲o̲r̲ WEIGHT := (SQRT(WEIGHT) + 1)⭡2 w̲h̲i̲l̲e̲
        WEIGHT ≠ 16 ∧ NBP > 0 d̲o̲

         b̲e̲g̲i̲n̲ i̲f̲ AWAY = 0 ∧ W ≠ 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ c̲o̲m̲m̲e̲n̲t̲ IF NO BREAK-POINTS WERE OMITTED THEN ONE
                 FUNCTION EVALUATION IS SAVED;
               W := WEIGHT/W;
               f̲o̲r̲ I := NOBS + 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NROW d̲o̲
                  b̲e̲g̲i̲n̲ f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NCOL d̲o̲
                          YP[I, J] := W × YP[I, J];
                     RES[I] := W × RES[I]
                  e̲n̲d̲; SEC := t̲r̲u̲e̲; NFE := NFE-1
            e̲n̲d̲;

            IN[3] := IN[3] × FAC3 × WEIGHT; IN[4] := EPS1;
            MONITOR(2, NCOL, NROW, PAR, RES, WEIGHT, NIS);
            MARQUARDT(NROW, NCOL, PAR, RES, AID, FUNCT, JAC DYDP, IN, OUT);
            i̲f̲ OUT[1] > 0 t̲h̲e̲n̲ g̲o̲t̲o̲ ESCAPE;

            c̲o̲m̲m̲e̲n̲t̲ THE RELATIVE STARTING VALUE OF LAMBDA IS
              ADJUSTED TO THE LAST VALUE OF LAMBDA USED;
            AWAY := OUT[4]-OUT[5]-1;
            IN[6] := IN[6] × 5⭡AWAY × 2⭡(AWAY-OUT[5]);

            NFE := NFE + OUT[4];
            W := WEIGHT; EPS1 := (SQRT(WEIGHT) + 1)⭡2 × IN[4] × FAC4;
            AWAY := 0;

            c̲o̲m̲m̲e̲n̲t̲ USELESS BREAK-POINTS ARE OMITTED;
            f̲o̲r̲ J := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NBP d̲o̲
               b̲e̲g̲i̲n̲ i̲f̲ ABS(OBS[BP[J]] + RES[BP[J]]-PAR[J + M]) < EPS1
                    t̲h̲e̲n̲
                  b̲e̲g̲i̲n̲ NBP := NBP-1; DUPVEC(J, NBP, 1, BP, BP);
                     DUPVEC(J + M, NBP + M, 1, PAR, PAR);
                     J := J-1; AWAY := AWAY + 1; BP[NBP + 1] := 0
                  e̲n̲d̲
               e̲n̲d̲;
            NCOL := NCOL-AWAY; NROW := NROW-AWAY
         e̲n̲d̲;

      IN[3] := IN3; IN[4] := IN4; NBP := 0; WEIGHT := 1;
      MONITOR(2, M, NOBS, PAR, RES, WEIGHT, NIS);
      MARQUARDT(NOBS, M, PAR, RES, JTJINV, FUNCT, JAC DYDP, IN, OUT);
      NFE := OUT[4] + NFE
   e̲n̲d̲;
   ESCAPE: i̲f̲ OUT[1] = 3 t̲h̲e̲n̲ OUT[1] := 2 e̲l̲s̲e̲
   i̲f̲ OUT[1] = 4 t̲h̲e̲n̲ OUT[1] := 6;
   i̲f̲ SAVE[-3] ≠ 0 t̲h̲e̲n̲ OUT[1] := SAVE[-3];
   OUT[3] := RES1;
   OUT[4] := NFE;
   OUT[5] := MAX
e̲n̲d̲ PEIDE;
c̲o̲m̲m̲e̲n̲t̲ ================== 33300 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FEM LAG SYM(X, Y, N, P, R, F, ORDER, E);
   i̲n̲t̲e̲g̲e̲r̲ N, ORDER;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ P, R, F;
     a̲r̲r̲a̲y̲ X, Y, E;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, L1;
   r̲e̲a̲l̲ XL1, XL, H, A12, B1, B2, TAU1, TAU2, CH, TL, G, YL, PP,
     P1, P2, P3, P4, R1, R2, R3, R4, F1, F2, F3, F4,
     E1, E2, E3, E4, E5, E6;
   a̲r̲r̲a̲y̲ T, SUB, CHI, GI[0:N-1];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 1;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲  H2;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ P2 := P(XL1); R2 := R(XL1); F2 := F(XL1) e̲n̲d̲;
      P1 := P2; P2 := P(XL); R1 := R2; R2 := R(XL); F1 := F2; F2 := F(XL);
      H2 := H/2; B1 := H2 × F1; B2 := H2 × F2; TAU1 := H2 × R1; TAU2 := H2 × R2;
      A12 := -0·5 × (P1 + P2)/H
   e̲n̲d̲ ELAN. M.V. EV.;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 2;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲  X2, H6, H15, B3, TAU3, C12, C32, A13, A22, A23;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ P3 := P(XL1); R3 := R(XL1); F3 := F(XL1) e̲n̲d̲;
      X2 := (XL1 + XL)/2; H6 := H/6; H15 := H/1·5;
      P1 := P3; P2 := P(X2); P3 := P(XL);
      R1 := R3; R2 := R(X2); R3 := R(XL);
      F1 := F3; F2 := F(X2); F3 := F(XL);
      B1 := H6 × F1; B2 := H15 × F2; B3 := H6 × F3;
      TAU1 := H6 × R1; TAU2 := H15 × R2; TAU3 := H6 × R3;
      A12 := -(2 × P1 + P3/1·5)/H; A13 := (0·5 × (P1 + P3) - P2/1·5)/H;
      A22 := (P1 + P3)/H/0·375 + TAU2; A23 := -(P1/3 + P3) × 2/H;
      c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
      C12 := - A12/A22; C32 := - A23/A22; A12 := A13 + C32 × A12;
      B1 := B1 + C12 × B2; B2 := B3 + C32 × B2;
      TAU1 := TAU1 + C12 × TAU2; TAU2 := TAU3 + C32 × TAU2
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION 2;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 3;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲  X2, X3, H12, H24, DET, C12, C13, C42, C43,
        A13, A14, A22, A23, A24, A33, A34, B3, B4, TAU3, TAU4;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ P4 := P(XL1); R4 := R(XL1); F4 := F(XL1) e̲n̲d̲;
      X2 := XL1 + 0·27639320225 × H; X3 := XL - X2 + XL1;
      H12 := H/12; H24 := H/2·4;
      P1 := P4; P2 := P(X2); P3 := P(X3); P4 := P(XL);
      R1 := R4; R2 := R(X2); R3 := R(X3); R4 := R(XL);
      F1 := F4; F2 := F(X2); F3 := F(X3); F4 := F(XL);
      B1 := H12 × F1; B2 := H24 × F2; B3 := H24 × F3; B4 := H12 × F4;
      TAU1 := H12 × R1; TAU2 := H24 × R2; TAU3 := H24 × R3; TAU4 := H12 × R4;
      A12 := -( + 4·04508497187450 × P1
        + 0·57581917135425 × P3
        + 0·25751416197911 × P4)/H;
      A13 := ( + 1·5450849718747 × P1
        - 1·5075141619791 × P2
        + 0·6741808286458 × P4)/H;
      A14 := ((P2 + P3)/2·4 - (P1 + P4)/2)/H;
      A22 := (5·454237476562 × P1 + P3/·48 + ·79576252343762 × P4)/H + TAU2;
      A23 := - (P1 + P4)/(H × 0·48);
      A24 := ( + 0·67418082864575 × P1
        - 1·50751416197910 × P3
        + 1·54508497187470 × P4)/H;
      A33 := (·7957625234376 × P1 + P2/·48 + 5·454237476562 × P4)/H + TAU3;
      A34 := -( + 0·25751416197911 × P1
        + 0·57581917135418 × P2
        + 4·0450849718747 × P4)/H;
      c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
      DET := A22 × A33 - A23 × A23;
      C12 := (A13 × A23 - A12 × A33)/DET;
      C13 := (A12 × A23 - A13 × A22)/DET;
      C42 := (A23 × A34 - A24 × A33)/DET;
      C43 := (A24 × A23 - A34 × A22)/DET;
      TAU1 := TAU1 + C12 × TAU2 + C13 × TAU3;
      TAU2 := TAU4 + C42 × TAU2 + C43 × TAU3;
      A12 := A14 + C42 × A12 + C43 × A13;
      B1 := B1 + C12 × B2 + C13 × B3;
      B2 := B4 + C42 × B2 + C43 × B3
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION 3;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BOUNDARY CONDITIONS;
      i̲f̲ L = 1 ∧ E2 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲  TAU1 := 1; B1 := E3/E1; B2 := B2 - A12 × B1;
         TAU2 := TAU2 - A12; A12 := 0 e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ L = 1 ∧ E2 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ AUX; AUX := P1/E2; TAU1 := TAU1 - AUX × E1 ;
         B1 := B1 - E3 × AUX
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := 1; B2 := E6/E4;
         B1 := B1 - A12 × B2; TAU1 := TAU1 - A12; A12 := 0
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ AUX; AUX := P2/E5;
         TAU2 := TAU2 + AUX × E4; B2 := B2 + AUX × E6
      e̲n̲d̲ B.C.1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD BABUSHKA;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ CHI[0] := CH := TL := TAU1; T[0] := TL;
         GI[0] :=  G := YL :=   B1; Y[0] := YL;
         SUB[0] := A12; PP := A12/(CH - A12);
         CH := TAU2 - CH × PP; G := B2 - G × PP; TL := TAU2; YL := B2
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ CHI[L1] := CH := CH + TAU1;
         GI[L1] :=  G :=  G + B1;
         SUB[L1] := A12; PP := A12/(CH - A12);
         CH := TAU2 - CH × PP; G := B2 - G × PP;
         T[L1] := TL + TAU1; TL := TAU2;
         Y[L1] := YL +   B1; YL :=   B2
      e̲n̲d̲ FORWARD BABUSHKA 1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD BABUSHKA;
   b̲e̲g̲i̲n̲ PP := YL; Y[N] := G/CH;
      G := PP; CH := TL; L := N;
      f̲o̲r̲ L := L - 1 w̲h̲i̲l̲e̲ L ≥ 0 d̲o̲
         b̲e̲g̲i̲n̲ PP := SUB[L]; PP := PP/(CH - PP);
            TL := T[L]; CH := TL - CH × PP;
            YL := Y[L]; G := YL - G × PP;
            Y[L] := (GI[L] + G - YL)/(CHI[L] + CH - TL)
         e̲n̲d̲
   e̲n̲d̲ BACKWARD BABUSHKA;

   L := 0; XL := X[0];
   E1 := E[1]; E2 := E[2]; E3 := E[3]; E4 := E[4]; E5 := E[5]; E6 := E[6];
   f̲o̲r̲ L := L + 1 w̲h̲i̲l̲e̲ L ≤ N d̲o̲
      b̲e̲g̲i̲n̲ L1 := L - 1; XL1 := XL; XL := X[L]; H := XL - XL1;
         i̲f̲ ORDER = 2 t̲h̲e̲n̲ ELEMENT MAT VEC EVALUATION 1 e̲l̲s̲e̲
         i̲f̲ ORDER = 4 t̲h̲e̲n̲ ELEMENT MAT VEC EVALUATION 2 e̲l̲s̲e̲
           ELEMENT MAT VEC EVALUATION 3;
         i̲f̲ L = 1 ∨ L = N t̲h̲e̲n̲ BOUNDARY CONDITIONS;
         FORWARD BABUSHKA
      e̲n̲d̲;
   BACKWARD BABUSHKA;
e̲n̲d̲ FEM LAG SYM;
c̲o̲m̲m̲e̲n̲t̲ ================== 33301 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FEM LAG(X, Y, N, R, F, ORDER, E);
   v̲a̲l̲u̲e̲ N, ORDER; i̲n̲t̲e̲g̲e̲r̲ N, ORDER;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ R, F;
     a̲r̲r̲a̲y̲ X, Y, E;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, L1;
   r̲e̲a̲l̲ XL1, XL, H, A12, B1, B2, TAU1, TAU2, CH, TL, G, YL, PP,
     E1, E2, E3, E4, E5, E6;
   a̲r̲r̲a̲y̲ T, SUB, CHI, GI[0: N-1];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 1;
   b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ F2, R2; r̲e̲a̲l̲ R1, F1, H2;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ F2 := F(XL1); R2 := R(XL1) e̲n̲d̲;
      A12 := - 1/H; H2 := H/2;
      R1 := R2; R2 := R(XL); F1 := F2; F2 := F(XL);
      B1 := H2 × F1; B2 := H2 × F2; TAU1 := H2 × R1; TAU2 := H2 × R2
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION 1;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 2;
   b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ R3, F3;
      r̲e̲a̲l̲ R1, R2, F1, F2, X2, H6, H15,
        B3, TAU3, C12, A13, A22, A23;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ R3 := R(XL1); F3 := F(XL1) e̲n̲d̲;
      X2 := (XL1 + XL)/2; H6 := H/6; H15 := H/1·5;
      R1 := R3; R2 := R(X2); R3 := R(XL);
      F1 := F3; F2 := F(X2); F3 := F(XL);
      B1 := H6 × F1; B2 := H15 × F2; B3 := H6 × F3;
      TAU1 := H6 × R1; TAU2 := H15 × R2; TAU3 := R3 × H6;
      A12 := A23 := -8/H/3; A13 := - A12/8; A22 := -2 × A12 + TAU2;
      c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
      C12 := - A12/A22; A12 := A13 + C12 × A12;
      B2 := C12 × B2; B1 := B1 + B2; B2 := B3 + B2;
      TAU2 := C12 × TAU2; TAU1 := TAU1 + TAU2; TAU2 := TAU3 + TAU2
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION2;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 3;
   b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ R4, F4;
      r̲e̲a̲l̲ R1, R2, R3, F1, F2, F3, X2, X3, H12, H24,
        DET, C12, C13, C42, C43, A13, A14, A22, A23, A24,
        A33, A34, B3, B4, TAU3, TAU4;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ R4 := R(XL1); F4 := F(XL1) e̲n̲d̲;
      X2 := XL1 + 0·27639320225 × H; X3 := XL - X2 + XL1;
      R1 := R4; R2 := R(X2); R3 := R(X3); R4 := R(XL);
      F1 := F4; F2 := F(X2); F3 := F(X3); F4 := F(XL);
      H12 := H/12; H24 := H/2·4;
      B1 := F1 × H12; B2 := F2 × H24; B3 := F3 × H24; B4 := F4 × H12;
      TAU1 := R1 × H12; TAU2 := R2 × H24; TAU3 := R3 × H24; TAU4 := R4 × H12;
      A12 := A34 := -4·8784183052078/H; A13 := A24 := 0·7117516385412/H;
      A14 := -0·16666666666667/H; A23 := 25 × A14;
      A22 := -2 × A23 + TAU2; A33 := -2 × A23 + TAU3;
      c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
      DET := A22 × A33 - A23 × A23;
      C12 := (A13 × A23 - A12 × A33)/DET;
      C13 := (A12 × A23 - A13 × A22)/DET;
      C42 := (A23 × A34 - A24 × A33)/DET;
      C43 := (A24 × A23 - A34 × A22)/DET;
      TAU1 := TAU1 + C12 × TAU2 + C13 × TAU3;
      TAU2 := TAU4 + C42 × TAU2 + C43 × TAU3;
      A12 := A14 + C42 × A12 + C43 × A13;
      B1 := B1 + C12 × B2 + C13 × B3;
      B2 := B4 + C42 × B2 + C43 × B3
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION3;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BOUNDARY CONDITIONS;
      i̲f̲ L = 1 ∧ E2 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := 1; B1 := E3/E1; B2 := B2 - A12 × B1;
         TAU2 := TAU2 - A12; A12 := 0 e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ L = 1 ∧ E2 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := TAU1 - E1/E2;
         B1 := B1 - E3/E2
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := 1; B2 := E6/E4; B1 := B1 - A12 × B2;
         TAU1 := TAU1 - A12; A12 := 0
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := TAU2 + E4/E5;
         B2 := B2 + E6/E5
      e̲n̲d̲ BOUNDARY CONDITIONS;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD BABUSHKA;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ CHI[0] := CH := TL := TAU1; T[0] := TL;
         GI[0] := G := YL := B1; Y[0] := YL;
         SUB[0] := A12; PP := A12/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; TL := TAU2; YL := B2
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ CHI[L1] := CH := CH + TAU1;
         GI[L1] := G := G + B1; SUB[L1] := A12; PP := A12/(CH - A12);
         CH := TAU2 - CH × PP; G := B2 - G × PP;
         T[L1] := TL + TAU1; TL := TAU2;
         Y[L1] := YL +   B1; YL :=   B2
      e̲n̲d̲ FORWARD BABUSHKA 1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD BABUSHKA;
   b̲e̲g̲i̲n̲ PP := YL; Y[N] := G/CH;
      G := PP; CH := TL; L := N;
      f̲o̲r̲ L := L - 1 w̲h̲i̲l̲e̲ L ≥ 0 d̲o̲
         b̲e̲g̲i̲n̲ PP := SUB[L]; PP := PP/(CH - PP);
            TL := T[L]; CH := TL - CH × PP;
            YL := Y[L]; G := YL - G × PP;
            Y[L] := ((GI[L] + G) - YL)/((CHI[L] + CH) - TL)
         e̲n̲d̲
   e̲n̲d̲ BACKWARD BABUSHKA;

   L := 0; XL := X[0];
   E1 := E[1]; E2 := E[2]; E3 := E[3]; E4 := E[4]; E5 := E[5]; E6 := E[6];
   f̲o̲r̲ L := L + 1 w̲h̲i̲l̲e̲ L ≤ N d̲o̲
      b̲e̲g̲i̲n̲ L1 := L - 1; XL1 := XL; XL := X[L]; H := XL - XL1;
         i̲f̲ ORDER = 2 t̲h̲e̲n̲ ELEMENT MAT VEC EVALUATION 1 e̲l̲s̲e̲
         i̲f̲ ORDER = 4 t̲h̲e̲n̲ ELEMENT MAT VEC EVALUATION 2 e̲l̲s̲e̲
           ELEMENT MAT VEC EVALUATION 3;
         i̲f̲ L = 1 ∨ L = N t̲h̲e̲n̲ BOUNDARY CONDITIONS;
         FORWARD BABUSHKA
      e̲n̲d̲;
   BACKWARD BABUSHKA;
e̲n̲d̲ FEM LAGR;
c̲o̲m̲m̲e̲n̲t̲ ================== 33302 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ FEM LAG SKEW(X, Y, N, Q, R, F, ORDER, E);
   i̲n̲t̲e̲g̲e̲r̲ N, ORDER;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ Q, R, F;
     a̲r̲r̲a̲y̲ X, Y, E;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, L1;
   r̲e̲a̲l̲ XL1, XL, H, A12, A21, B1, B2, TAU1, TAU2, CH, TL, G, YL, PP,
     E1, E2, E3, E4, E5, E6;
   a̲r̲r̲a̲y̲ T, SUPER, SUB, CHI, GI[0:N-1];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 1;
   b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ Q2, R2, F2;
      r̲e̲a̲l̲ Q1, R1, F1, H2, S12;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ Q2 := Q(XL1); R2 := R(XL1); F2 := F(XL1) e̲n̲d̲;
      H2 := H/2; S12 := - 1/H;
      Q1 := Q2; Q2 := Q(XL);
      R1 := R2; R2 := R(XL);
      F1 := F2; F2 := F(XL);
      B1 := H2 × F1; B2 := H2 × F2;
      TAU1 := H2 × R1; TAU2 := H2 × R2;
      A12 := S12 + Q1/2; A21 := S12 - Q2/2
   e̲n̲d̲ ELEMENT MAT VEC EV.;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 2;
   b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ Q3, R3, F3;
      r̲e̲a̲l̲ Q1, Q2, R1, R2, F1, F2, S12, S13, S22, X2, H6, H15,
        C12, C32, A13, A31, A22, A23, A32, B3, TAU3;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ Q3 := Q(XL1); R3 := R(XL1); F3 := F(XL1) e̲n̲d̲;

      X2 := (XL1 + XL)/2; H6 := H/6; H15 := H/1·5;
      Q1 := Q3; Q2 := Q(X2); Q3 := Q(XL);
      R1 := R3; R2 := R(X2); R3 := R(XL);
      F1 := F3; F2 := F(X2); F3 := F(XL);
      B1 := H6 × F1; B2 := H15 × F2; B3 := H6 × F3;
      TAU1 := H6 × R1; TAU2 := H15 × R2; TAU3 := H6 × R3;
      S12 := - 1/H/0·375; S13 := - S12/8; S22 := - 2 × S12;
      A12 := S12 + Q1/1·5; A13 := S13 - Q1/6;
      A21 := S12 - Q2/1·5; A23 := S12 + Q2/1·5; A22 := S22 + TAU2;
      A31 := S13 + Q3/6; A32 := S12 - Q3/1·5;
      c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
      C12 := - A12/A22; C32 := - A32/A22;
      A12 := A13 + C12 × A23; A21 := A31 + C32 × A21;
      B1 := B1 + C12 × B2; B2 := B3 + C32 × B2;
      TAU1 := TAU1 + C12 × TAU2; TAU2 := TAU3 + C32 × TAU2
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION 2;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 3;
   b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ Q4, R4, F4;
      r̲e̲a̲l̲ Q1, Q2, Q3, R1, R2, R3, F1, F2, F3,
        S12, S13, S14, S22, S23, X2, X3, H12, H24,
        DET, C12, C13, C42, C43, A13, A14, A22, A23,
        A24, A31, A32, A33, A34, A41, A42, A43,
        B3, B4, TAU3, TAU4;

      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ Q4 := Q(XL1); R4 := R(XL1); F4 := F(XL1) e̲n̲d̲;
      X2 := XL1 + 0·27639320225 × H; X3 := XL - X2 + XL1;
      H12 := H/12; H24 := H/2·4;
      Q1 := Q4; Q2 := Q(X2); Q3 := Q(X3); Q4 := Q(XL);
      R1 := R4; R2 := R(X2); R3 := R(X3); R4 := R(XL);
      F1 := F4; F2 := F(X2); F3 := F(X3); F4 := F(XL);
      S12 := -4·8784183052080/H; S13 := 0·7117516385414/H;
      S14 := -·16666666666667/H; S23 := 25 × S14; S22 := -2 × S23;
      B1 := H12 × F1; B2 := H24 × F2; B3 := H24 × F3; B4 := H12 × F4;
      TAU1 := H12 × R1; TAU2 := H24 × R2; TAU3 := H24 × R3; TAU4 := H12 × R4;
      A12 := S12 + 0·67418082864578 × Q1;
      A13 := S13 - 0·25751416197912 × Q1;
      A14 := S14 + Q1/12;
      A21 := S12 - 0·67418082864578 × Q2;
      A22 := S22 + TAU2;
      A23 := S23 + 0·93169499062490 × Q2;
      A24 := S13 - 0·25751416197912 × Q2;
      A31 := S13 + 0·25751416197912 × Q3;
      A32 := S23 - 0·93169499062490 × Q3;
      A33 := S22 + TAU3;
      A34 := S12 + 0·67418082864578 × Q3;
      A41 := S14 - Q4/12;
      A42 := S13 + 0·25751416197912 × Q4;
      A43 := S12 - 0·67418082864578 × Q4;
      c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
      DET := A22 × A33 - A23 × A32;
      C12 := (A13 × A32 - A12 × A33)/DET;
      C13 := (A12 × A23 - A13 × A22)/DET;
      C42 := (A32 × A43 - A42 × A33)/DET;
      C43 := (A42 × A23 - A43 × A22)/DET;
      TAU1 := TAU1 + C12 × TAU2 + C13 × TAU3 ;
      TAU2 := TAU4 + C42 × TAU2 + C43 × TAU3;
      A12 := A14 + C12 × A24 + C13 × A34;
      A21 := A41 + C42 × A21 + C43 × A31;
      B1 := B1 + C12 × B2 + C13 × B3;
      B2 := B4 + C42 × B2 + C43 × B3
   e̲n̲d̲ ELEMENT MAT VEC EVALUATION 3;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BOUNDARY CONDITIONS;
      i̲f̲ L = 1 ∧ E2 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := 1; B1 := E3/E1; A12 := 0 e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ L = 1 ∧ E2 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := TAU1 - E1/E2; B1 := B1 - E3/E2
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := 1; A21 := 0; B2 := E6/E4;
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := TAU2 + E4/E5; B2 := B2 + E6/E5
      e̲n̲d̲ B.C.1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD BABUSKA;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ CHI[0] := CH := TL := TAU1; T[0] := TL;
         GI[0] := G := YL := B1; Y[0] := YL;
         SUB[0] := A21; SUPER[0] := A12;
         PP := A21/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; TL := TAU2; YL := B2
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ CHI[L1] := CH := CH + TAU1;
         GI[L1] := G := G + B1;
         SUB[L1] := A21; SUPER[L1] := A12;
         PP := A21/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; T[L1] := TL + TAU1; TL := TAU2;
         Y[L1] := YL +  B1; YL := B2
      e̲n̲d̲ FORWARD BABUSKA;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD BABUSKA;
   b̲e̲g̲i̲n̲ PP := YL; Y[N] := G/CH;
      G := PP; CH := TL; L := N;
      f̲o̲r̲ L := L - 1 w̲h̲i̲l̲e̲ L ≥ 0 d̲o̲
         b̲e̲g̲i̲n̲ PP := SUPER[L]/(CH - SUB[L]);
            TL := T[L]; CH := TL - CH × PP;
            YL := Y[L]; G := YL - G × PP;
            Y[L] := (GI[L] + G - YL)/(CHI[L] + CH - TL)  ;
         e̲n̲d̲
   e̲n̲d̲ BACKWARD BABUSKA;

   L := 0; XL := X[0];
   E1 := E[1]; E2 := E[2]; E3 := E[3]; E4 := E[4]; E5 := E[5]; E6 := E[6];
   c̲o̲m̲m̲e̲n̲t̲ ELEMENTWISE ASSEMBLAGE OF MATRIX AND VECTOR
     COMBINED WITH FORWARD BABUSKA SUBSTITUTION;
   f̲o̲r̲ L := L + 1 w̲h̲i̲l̲e̲ L ≤ N d̲o̲
      b̲e̲g̲i̲n̲ XL1 := XL; L1 := L - 1; XL := X[L]; H := XL - XL1;
         i̲f̲ ORDER = 2 t̲h̲e̲n̲ ELEMENT MAT VEC EVALUATION 1 e̲l̲s̲e̲
         i̲f̲ ORDER = 4 t̲h̲e̲n̲ ELEMENT MAT VEC EVALUATION 2 e̲l̲s̲e̲
           ELEMENT MAT VEC EVALUATION 3;
         i̲f̲ L = 1 ∨ L = N t̲h̲e̲n̲ BOUNDARY CONDITIONS;
         FORWARD BABUSKA
      e̲n̲d̲;
   BACKWARD BABUSKA;
e̲n̲d̲ FEM LAGR;
c̲o̲m̲m̲e̲n̲t̲ ================== 33303 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲  FEM HERM SYM(X, Y, N, P, Q, R, F, ORDER, E);
   v̲a̲l̲u̲e̲ N, ORDER; i̲n̲t̲e̲g̲e̲r̲ N, ORDER;
     a̲r̲r̲a̲y̲ X, Y, E;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲  P, Q, R, F;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, N2, V, W;
   a̲r̲r̲a̲y̲ A[1:8 × (N - 1)], EM[2:3];
   r̲e̲a̲l̲ A11, A12, A13, A14, A22, A23, A24, A33, A34, A44,
     YA, YB, ZA, ZB,
     B1, B2, B3, B4, D1, D2, E1, R1, R2, XL1, XL;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHLDECSOLBND(A, N, W, AUX, B); c̲o̲d̲e̲ 34333;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENTMATVECEVALUATION;
      i̲f̲ ORDER = 4 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ r̲e̲a̲l̲ X2, H, H2, H3, P1, P2,
           Q1, Q2, R1, R2, F1, F2,
           B11, B12, B13, B14, B22, B23, B24, B33, B34, B44,
           S11, S12, S13, S14, S22, S23, S24, S33, S34, S44,
           M11, M12, M13, M14, M22, M23, M24, M33, M34, M44;
         o̲w̲n̲ r̲e̲a̲l̲ P3, Q3, R3, F3;

         H := XL - XL1; H2 := H × H; H3 := H × H2;
         X2 := (XL1 + XL)/2;
         i̲f̲ L = 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ P3 := P(XL1); Q3 := Q(XL1); R3 := R(XL1); F3 := F(XL1)
         e̲n̲d̲;

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT BENDING MATRIX;
         P1 := P3; P2 := P(X2); P3 := P(XL);
         B11 := 6 × (P1 + P3); B12 := 4 × P1 + 2 × P3;
         B13 := - B11; B14 := B11 - B12;
         B22 := (4 × P1 + P2 + P3)/1·5; B23 := - B12; B24 := B12 - B22;
         B33 := B11; B34 := - B14; B44 := B14 - B24;

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT STIFFNESS MATRIX;
         Q1 := Q3; Q2 := Q(X2); Q3 := Q(XL);
         S11 := 1·5 × Q2; S12 := Q2/4; S13 := - S11; S14 := S12;
         S24 := Q2/24; S22 := Q1/6 + S24; S23 := - S12;
         S33 := S11; S34 := - S12; S44 := S24 + Q3/6;

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT MASS MATRIX;
         R1 := R3; R2 := R(X2); R3 := R(XL);
         M11 := (R1 + R2)/6; M12 := R2/24; M13 := R2/6; M14 := - M12;
         M22 := R2/96; M23 := - M14; M24 := - M22;
         M33 := (R2 + R3)/6; M34 := M14; M44 := M22;

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT LOAD VECTOR;
         F1 := F3; F2 := F(X2); F3 := F(XL);
         B1 := H × (F1 + 2 × F2)/6; B3 := H × (F3 + 2 × F2)/6;
         B2 := H2 × F2/12; B4 := - B2;
         A11 := B11/H3 + S11/H + M11 × H; A12 := B12/H2 + S12 + M12 × H2;
         A13 := B13/H3 + S13/H + M13 × H; A14 := B14/H2 + S14 + M14 × H2;
         A22 := B22/H + S22 × H + M22 × H3; A23 := B23/H2 + S23 + M23 × H2;
         A24 := B24/H + S24 × H + M24 × H3; A34 := B34/H2 + S34 + M34 × H2;
         A33 := B33/H3 + S33/H + M33 × H; A44 := B44/H + S44 × H + M44 × H3
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ ORDER = 6 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ P4, Q4, R4, F4;
         r̲e̲a̲l̲ H, H2, H3, X2, X3,
           P1, P2, P3, Q1, Q2, Q3,
           R1, R2, R3, F1, F2, F3,
           B11, B12, B13, B14, B15, B22, B23, B24, B25,
           B33, B34, B35, B44, B45, B55,
           S11, S12, S13, S14, S15, S22, S23, S24, S25,
           S33, S34, S35, S44, S45, S55,
           M11, M12, M13, M14, M15, M22, M23, M24, M25,
           M33, M34, M35, M44, M45, M55,
           A15, A25, A35, A45, A55, C1, C2, C3, C4, B5;
         i̲f̲ L = 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ P4 := P(XL1); Q4 := Q(XL1); R4 := R(XL1); F4 := F(XL1)
         e̲n̲d̲;

         H := XL - XL1; H2 := H × H; H3 := H × H2;
         X2 := 0·27639320225 × H + XL1; X3 := XL1 + XL - X2;

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT BENDING MATRIX;
         P1 := P4; P2 := P(X2); P3 := P(X3); P4 := P(XL);
         B11 := + 4·0333333333333@+1 × P1 + 1·1124913866738@-1 × P2
           + 1·4422084194664@+1 × P3 + 8·3333333333333@+0 × P4;
         B12 := + 1·4666666666667@+1 × P1 - 3·3191425091659@-1 × P2
           + 2·7985809175818@+0 × P3 + 1·6666666666667@+0 × P4;
         B13 := + 1·8333333333333@+1 × (P1 + P4)
           + 1·2666666666667@+0 × (P2 + P3);
         B15 := - (B11 + B13); B14 := - (B12 + B13 + B15/2);
         B22 := + 5·3333333333333@+0 × P1 + 9·9027346441674@-1 × P2
           + 5·4305986891624@-1 × P3 + 3·3333333333333@-1 × P4;
         B23 := + 6·6666666666667@+0 × P1 - 3·7791278464167@+0 × P2
           + 2·4579451308295@-1 × P3 + 3·6666666666667@+0 × P4;
         B25 := - (B12 + B23); B24 := - (B22 + B23 + B25/2);
         B33 := + 8·3333333333333@+0 × P1 + 1·4422084194666@+1 × P2
           + 1·1124913866726@-1 × P3 + 4·0333333333333@+1 × P4;
         B35 := - (B13 + B33); B34 := - (B23 + B33 + B35/2);
         B45 := - (B14 + B34); B44 := - (B24 + B34 + B45/2);
         B55 := - (B15 + B35);
         c̲o̲m̲m̲e̲n̲t̲ ELEMENT STIFFNESS MATRIX;
         Q1 := Q4; Q2 := Q(X2); Q3 := Q(X3); Q4 := Q(XL);
         S11 := + 2·8844168389330@+0 × Q2 + 2·2249827733448@-2 × Q3;
         S12 := + 2·5671051872498@-1 × Q2 + 3·2894812749994@-3 × Q3;
         S13 := + 2·5333333333333@-1 × (Q2 + Q3);
         S14 := - 3·7453559925005@-2 × Q2 - 2·2546440074988@-2 × Q3;
         S15 := - (S13 + S11);
         S22 := + 8·3333333333333@-2 × Q1 + 2·2847006554164@-2 × Q2
           + 4·8632677916445@-4 × Q3;
         S23 := + 2·2546440075002@-2 × Q2 + 3·7453559924873@-2 × Q3;
         S24 := - 3·3333333333333@-3 × (Q2 + Q3);
         S25 := - (S12 + S23);
         S33 := + 2·2249827733471@-2 × Q2 + 2·8844168389330@+0 × Q3;
         S34 := - 3·2894812750127@-3 × Q2 - 2·5671051872496@-1 × Q3;
         S35 := - (S13 + S33);
         S44 := + 4·8632677916788@-4 × Q2
           + 2·2847006554161@-2 × Q3 + 8·3333333333338@-2 × Q4;
         S45 := - (S14 + S34);
         S55 := - (S15 + S35);

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT MASS MATRIX;
         R1 := R4; R2 := R(X2); R3 := R(X3); R4 := R(XL);
         M11 := + 8·3333333333333@-2 × R1 + 1·0129076086083@-1 × R2
           + 7·3759058058380@-3 × R3;
         M12 := + 1·3296181273333@-2 × R2 + 1·3704853933353@-3 × R3;
         M13 := - 2·7333333333333@-2 × (R2 + R3);
         M14 := + 5·0786893258335@-3 × R2 + 3·5879773408333@-3 × R3;
         M15 := + 1·3147987115999@-1 × R2 - 3·5479871159991@-2 × R3;
         M22 := + 1·7453559925000@-3 × R2 + 2·5464400750059@-4 × R3;
         M23 := - 3·5879773408336@-3 × R2 - 5·0786893258385@-3 × R3;
         M24 := + 6·6666666666667@-4 × (R2 + R3);
         M25 := + 1·7259029213333@-2 × R2 - 6·5923625466719@-3 × R3;
         M33 := + 7·3759058058380@-3 × R2
           + 1·0129076086083@-1 × R3 + 8·3333333333333@-2 × R4;
         M34 := - 1·3704853933333@-3 × R2 - 1·3296181273333@-2 × R3;
         M35 := - 3·5479871159992@-2 × R2 + 1·3147987115999@-1 × R3;
         M44 := + 2·5464400750008@-4 × R2 + 1·7453559924997@-3 × R3;
         M45 := + 6·5923625466656@-3 × R2 - 1·7259029213330@-2 × R3;
         M55 := + ·17066666666667@+0 × (R2 + R3);
         c̲o̲m̲m̲e̲n̲t̲ ELEMENT LOAD VECTOR;
         F1 := F4; F2 := F(X2); F3 := F(X3); F4 := F(XL);
         B1 := + 8·3333333333333@-2 × F1 + 2·0543729868749@-1 × F2
           - 5·5437298687489@-2 × F3;
         B2 := + 2·6967233145832@-2 × F2 - 1·0300566479175@-2 × F3;
         B3 := - 5·5437298687489@-2 × F2
           + 2·0543729868749@-1 × F3 + 8·3333333333333@-2 × F4;
         B4 := + 1·0300566479165@-2 × F2 - 2·6967233145830@-2 × F3;
         B5 := + 2·6666666666667@-1 × (F2 + F3);

         A11 := H2 × (H2 × M11 + S11) + B11; A12 := H2 × (H2 × M12 + S12) + B12;
         A13 := H2 × (H2 × M13 + S13) + B13; A14 := H2 × (H2 × M14 + S14) + B14;
         A15 := H2 × (H2 × M15 + S15) + B15; A22 := H2 × (H2 × M22 + S22) + B22;
         A23 := H2 × (H2 × M23 + S23) + B23; A24 := H2 × (H2 × M24 + S24) + B24;
         A25 := H2 × (H2 × M25 + S25) + B25; A33 := H2 × (H2 × M33 + S33) + B33;
         A34 := H2 × (H2 × M34 + S34) + B34; A35 := H2 × (H2 × M35 + S35) + B35;
         A44 := H2 × (H2 × M44 + S44) + B44; A45 := H2 × (H2 × M45 + S45) + B45;
         A55 := H2 × (H2 × M55 + S55) + B55;

         c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
         C1 := A15/A55; C2 := A25/A55; C3 := A35/A55; C4 := A45/A55;
         B1 := (B1 - C1 × B5) × H; B2 := (B2 - C2 × B5) × H2;
         B3 := (B3 - C3 × B5) × H; B4 := (B4 - C4 × B5) × H2;
         A11 := (A11 - C1 × A15)/H3; A12 := (A12 - C1 × A25)/H2;
         A13 := (A13 - C1 × A35)/H3; A14 := (A14 - C1 × A45)/H2;
         A22 := (A22 - C2 × A25)/H; A23 := (A23 - C2 × A35)/H2;
         A24 := (A24 - C2 × A45)/H; A33 := (A33 - C3 × A35)/H3;
         A34 := (A34 - C3 × A45)/H2; A44 := (A44 - C4 × A45)/H;
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ o̲w̲n̲ r̲e̲a̲l̲ P5, Q5, R5, F5;
         r̲e̲a̲l̲ X2, X3, X4, H, H2, H3,
           P1, P2, P3, P4, Q1, Q2, Q3, Q4,
           R1, R2, R3, R4, F1, F2, F3, F4,
           B11, B12, B13, B14, B15, B16, B22, B23, B24, B25, B26,
           B33, B34, B35, B36, B44, B45, B46, B55, B56, B66,
           S11, S12, S13, S14, S15, S16, S22, S23, S24, S25, S26,
           S33, S34, S35, S36, S44, S45, S46, S55, S56, S66,
           M11, M12, M13, M14, M15, M16, M22, M23, M24, M25, M26,
           M33, M34, M35, M36, M44, M45, M46, M55, M56, M66,
           C15, C16, C25, C26, C35, C36, C45, C46, B5, B6,
           A15, A16, A25, A26, A35, A36, A45, A46, A55, A56, A66, DET;
         i̲f̲ L = 1 t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ P5 := P(XL1); Q5 := Q(XL1); R5 := R(XL1); F5 := F(XL1)
         e̲n̲d̲;
         H := XL - XL1; H2 := H × H; H3 := H × H2;
         X2 := XL1 + H × ·172673164646; X3 := XL1 + H/2; X4 := XL1 + XL - X2;
         c̲o̲m̲m̲e̲n̲t̲ ELEMENT BENDING MATRIX;
         P1 := P5; P2 := P(X2); P3 := P(X3); P4 := P(X4); P5 := P(XL);
         B11 := + 105·8 × P1 + 9·8 × P5     + 7·3593121303513@-2 × P2
           + 2·2755555555556@+1 × P3 + 7·0565656088553@+0 × P4;
         B12 := + 27·6 × P1  + 1·4 × P5     - 3·41554824811@-1 × P2
           + 2·8444444444444@+0 × P3 + 1·0113960946522@+0 × P4;
         B13 := - 32·2 × (P1 + P5)        - 7·2063492063505@-1 × (P2 + P4)
           + 2·2755555555556@+1 × P3;
         B14 := + 4·6 × P1 + 8·4 × P5       + 1·0328641222944@-1 × P2
           - 2·8444444444444@+0 × P3 - 3·3445562534992@+0 × P4;
         B15 := - (B11 + B13); B16 := - (B12 + B13 + B14 + B15/2);
         B22 := + 7·2 × P1 + 0·2 × P5       + 1·5851984028581@+0 × P2
           + 3·5555555555556@-1 × P3 + 1·4496032730059@-1 × P4;
         B23 := - 8·4 × P1 - 4·6 × P5       + 3·3445562534992@+0 × P2
           + 2·8444444444444@+0 × P3 - 1·0328641222944@-1 × P4;
         B24 := + 1·2 × (P1 + P5)         - 4·7936507936508@-1 × (P2 + P4)
           - 3·5555555555556@-1 × P3;
         B25 := - (B12 + B23); B26 := - (B22 + B23 + B24 + B25/2);
         B33 := + 7·0565656088553@+0 × P2 + 2·2755555555556@+1 × P3
           + 7·3593121303513@-2 × P4 + 105·8 × P5 + 9·8 × P1;
         B34 := - 1·4 × P1 - 27·6 × P5      - 1·0113960946522@+0 × P2
           - 2·8444444444444@+0 × P3 + 3·4155482481100@-1 × P4;
         B35 := - (B13 + B33); B36 := - (B23 + B33 + B34 + B35/2);
         B44 := + 7·2 × P5 + P1/5          + 1·4496032730059@-1 × P2
           + 3·5555555555556@-1 × P3 + 1·5851984028581@+0 × P4;
         B45 := - (B14 + B34); B46 := - (B24 + B34 + B44 + B45/2);
         B55 := - (B15 + B35); B56 := - (B16 + B36);
         B66 := - (B26 + B36 + B46 + B56/2);

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT STIFFNESS MATRIX;
         Q1 := Q5; Q2 := Q(X2); Q3 := Q(X3); Q4 := Q(X4); Q5 := Q(XL);
         S11 := + 3·0242424037951@+0 × Q2 + 3·1539909130065@-2 × Q4;
         S12 := + 1·2575525581744@-1 × Q2 + 4·1767169716742@-3 × Q4;
         S13 := - 3·0884353741496@-1 × (Q2 + Q4);
         S14 := + 4·0899041243062@-2 × Q2 + 1·2842455355577@-2 × Q4;
         S15 := - (S13 + S11);
         S16 := + 5·9254861177068@-1 × Q2 + 6·0512612719116@-2 × Q4;
         S22 := + 5·2292052865422@-3 × Q2 + 5·5310763862796@-4 × Q4 + Q1/20;
         S23 := - 1·2842455355577@-2 × Q2 - 4·0899041243062@-2 × Q4;
         S24 := + 1·7006802721088@-3 × (Q2 + Q4);
         S25 := - (S12 + S23);
         S26 := + 2·4639593097426@-2 × Q2 + 8·0134681270641@-3 × Q4;
         S33 := + 3·1539909130065@-2 × Q2 + 3·0242424037951@+0 × Q4;
         S34 := - 4·1767169716742@-3 × Q2 - 1·2575525581744@-1 × Q4;
         S35 := - (S13 + S33);
         S36 := - 6·0512612719116@-2 × Q2 - 5·9254861177068@-1 × Q4;
         S44 := + 5·5310763862796@-4 × Q2 + 5·2292052865422@-3 × Q4 + Q5/20;
         S45 := - (S14 + S34);
         S46 := + 8·0134681270641@-3 × Q2 + 2·4639593097426@-2 × Q4;
         S55 := - (S15 + S35); S56 := -(S16 + S36);
         S66 := + 1·1609977324263@-1 × (Q2 + Q4) + 3·5555555555556@-1 × Q3;
         c̲o̲m̲m̲e̲n̲t̲ ELEMENT MASS MATRIX;
         R1 := R5; R2 := R(X2); R3 := R(X3); R4 := R(X4); R5 := R(XL);
         M11 := + 9·7107020727310@-2 × R2 + 1·5810259199180@-3 × R4 + R1/20;
         M12 := + 8·2354889460254@-3 × R2 + 2·1932154960071@-4 × R4;
         M13 := + 1·2390670553936@-2 × (R2 + R4);
         M14 := - 1·7188466249968@-3 × R2 - 1·0508326752939@-3 × R4;
         M15 := + 5·3089789712119@-2 × R2 + 6·7741558661060@-3 × R4;
         M16 := - 1·7377712856076@-2 × R2 + 2·2173630018466@-3 × R4;
         M22 := + 6·9843846173145@-4 × R2 + 3·0424512029349@-5 × R4;
         M23 := + 1·0508326752947@-3 × R2 + 1·7188466249936@-3 × R4;
         M24 := - 1·4577259475206@-4 × (R2 + R4);
         M25 := + 4·5024589679127@-3 × R2 + 9·3971790283374@-4 × R4;
         M26 := - 1·4737756452780@-3 × R2 + 3·0759488725998@-4 × R4;
         M33 := + 1·5810259199209@-3 × R2 + 9·7107020727290@-2 × R4 + R5/20;
         M34 := - 2·1932154960131@-4 × R2 - 8·2354889460254@-3 × R4;
         M35 := + 6·7741558661123@-3 × R2 + 5·3089789712112@-2 × R4;
         M36 := - 2·2173630018492@-3 × R2 + 1·7377712856071@-2 × R4;
         M44 := + 3·0424512029457@-5 × R2 + 6·9843846173158@-4 × R4;
         M45 := - 9·3971790283542@-4 × R2 - 4·5024589679131@-3 × R4;
         M46 := + 3·0759488726060@-4 × R2 - 1·4737756452778@-3 × R4;
         M55 := + 2·9024943310657@-2 × (R2 + R4) + 3·5555555555556@-1 × R3;
         M56 := + 9·5006428402050@-3 × (R4-R2);
         M66 := + 3·1098153547125@-3 × (R2 + R4);

         c̲o̲m̲m̲e̲n̲t̲ ELEMENT LOAD VECTOR;
         F1 := F5; F2 := F(X2); F3 := F(X3); F4 := F(X4); F5 := F(XL);
         B1 := + 1·6258748099336@-1 × F2 + 2·0745852339969@-2 × F4 + F1/20;
         B2 := + 1·3788780589233@-2 × F2 + 2·8778860774335@-3 × F4;
         B3 := + 2·0745852339969@-2 × F2 + 1·6258748099336@-1 × F4 + F5/20;
         B4 := - 2·8778860774335@-3 × F2 - 1·3788780589233@-2 × F4;
         B5 := + (F2 + F4)/11·25       + 3·5555555555556@-1 × F3;
         B6 := + 2·9095718698132@-2 × (F4-F2);

         A11 := H2 × (H2 × M11 + S11) + B11; A12 := H2 × (H2 × M12 + S12) + B12;
         A13 := H2 × (H2 × M13 + S13) + B13; A14 := H2 × (H2 × M14 + S14) + B14;
         A15 := H2 × (H2 × M15 + S15) + B15; A16 := H2 × (H2 × M16 + S16) + B16;
         A22 := H2 × (H2 × M22 + S22) + B22; A23 := H2 × (H2 × M23 + S23) + B23;
         A24 := H2 × (H2 × M24 + S24) + B24; A25 := H2 × (H2 × M25 + S25) + B25;
         A26 := H2 × (H2 × M26 + S26) + B26; A33 := H2 × (H2 × M33 + S33) + B33;
         A34 := H2 × (H2 × M34 + S34) + B34; A35 := H2 × (H2 × M35 + S35) + B35;
         A36 := H2 × (H2 × M36 + S36) + B36; A44 := H2 × (H2 × M44 + S44) + B44;
         A45 := H2 × (H2 × M45 + S45) + B45; A46 := H2 × (H2 × M46 + S46) + B46;
         A55 := H2 × (H2 × M55 + S55) + B55; A56 := H2 × (H2 × M56 + S56) + B56;
         A66 := H2 × (H2 × M66 + S66) + B66;
         c̲o̲m̲m̲e̲n̲t̲ STATIC CONDENSATION;
         DET := - A55 × A66 + A56 × A56;
         C15 := (A15 × A66 - A16 × A56)/DET; C16 := (A16 × A55 - A15 × A56)/DET;
         C25 := (A25 × A66 - A26 × A56)/DET; C26 := (A26 × A55 - A25 × A56)/DET;
         C35 := (A35 × A66 - A36 × A56)/DET; C36 := (A36 × A55 - A35 × A56)/DET;
         C45 := (A45 × A66 - A46 × A56)/DET; C46 := (A46 × A55 - A45 × A56)/DET;
         A11 := (A11 + C15 × A15 + C16 × A16)/H3;
         A12 := (A12 + C15 × A25 + C16 × A26)/H2;
         A13 := (A13 + C15 × A35 + C16 × A36)/H3;
         A14 := (A14 + C15 × A45 + C16 × A46)/H2;
         A22 := (A22 + C25 × A25 + C26 × A26)/H;
         A23 := (A23 + C25 × A35 + C26 × A36)/H2;
         A24 := (A24 + C25 × A45 + C26 × A46)/H;
         A33 := (A33 + C35 × A35 + C36 × A36)/H3;
         A34 := (A34 + C35 × A45 + C36 × A46)/H2;
         A44 := (A44 + C45 × A45 + C46 × A46)/H;
         B1 := (B1 + C15 × B5 + C16 × B6) × H; B2 := (B2 + C25 × B5 + C26 × B6) × H2;
         B3 := (B3 + C35 × B5 + C36 × B6) × H; B4 := (B4 + C45 × B5 + C46 × B6) × H2;
      e̲n̲d̲ EL.MATVECEVAL.;
   L := 1; W := V := 0; N2 := N + N - 2; XL1 := X[0]; XL := X[1];
   YA := E[1]; ZA := E[2]; YB := E[3]; ZB := E[4];
   ELEMENTMATVECEVALUATION; EM[2] := @-12;
   R1 := B3 - A13 × YA - A23 × ZA; D1 := A33; D2 := A44;
   R2 := B4 - A14 × YA - A24 × ZA; E1 := A34;
   f̲o̲r̲ L := L + 1 w̲h̲i̲l̲e̲ L < N d̲o̲
      b̲e̲g̲i̲n̲ XL1 := XL; XL := X[L];
         ELEMENTMATVECEVALUATION;
         A[W + 1] := D1 + A11; A[W + 4] := E1 + A12;
         A[W + 7] := A13; A[W + 10] := A14;
         A[W + 5] := D2 + A22; A[W + 8] := A23;
         A[W + 11] := A24; A[W + 14] := 0;
         Y[V + 1] := R1 + B1; Y[V + 2] := R2 + B2;
         R1 := B3; R2 := B4; V := V + 2; W := W + 8;
         D1 := A33; D2 := A44; E1 := A34
      e̲n̲d̲;
   L := N; XL1 := XL; XL := X[L]; ELEMENTMATVECEVALUATION;
   Y[N2      - 1] := R1 + B1 - A13 × YB - A14 × ZB;
   Y[N2] := R2 + B2 - A23 × YB - A24 × ZB;
   A[W + 1] := D1 + A11; A[W + 4] := E1 + A12; A[W + 5] := D2 + A22;
   CHLDECSOLBND(A, N2, 3, EM, Y)
e̲n̲d̲ FEMHERM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34600 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ QZIVAL(N, A, B, ALFR, ALFI, BETA, ITER, EM);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, B, ALFR, ALFI, BETA, EM;
     i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ ITER;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ DWARF, EPS, EPSA, EPSB;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHDECMUL(N, A, B, DWARF); c̲o̲d̲e̲ 34602;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HESTGL2(N, A, B); c̲o̲d̲e̲ 34604;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2ROW2(LA, LB, UA, UB, J, A1, A2, A, B); c̲o̲d̲e̲ 34608;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3ROW2(LA, LB, U, J, A1, A2, A3, A, B); c̲o̲d̲e̲ 34610;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2COL(LA, LB, U, I, A1, A2, A, B); c̲o̲d̲e̲ 34605;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3COL(LA, LB, U, I, A1, A2, A3, A, B); c̲o̲d̲e̲ 34606;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHSH2(A1R, A1I, A2R, A2I, C, SR, SI); c̲o̲d̲e̲ 34611;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECMAT(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31070;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECTAM(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31073;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ QZIT(N, A, B, EPS, EPSA, EPSB, ITER); v̲a̲l̲u̲e̲ N, EPS;
      r̲e̲a̲l̲ EPS, EPSA, EPSB; i̲n̲t̲e̲g̲e̲r̲ N; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ ITER; a̲r̲r̲a̲y̲ A, B;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ANORM, BNORM, ANI, BNI, CONST, A10, A20, A30, B11,
        B22, B33, B44, A11, A12, A21, A22, A33, A34, A43, A44, B12, B34, OLD1, OLD2;
      i̲n̲t̲e̲g̲e̲r̲ I, Q, M, M1, Q1, J, K, K1, K2, K3, KM1; B̲o̲o̲l̲e̲a̲n̲ STATIONARY;
      ANORM := BNORM := 0; f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ BNI := 0; ITER[I] := 0; ANI := i̲f̲ I > 1 t̲h̲e̲n̲ ABS(A[I, I-1]) e̲l̲s̲e̲ 0;
            f̲o̲r̲ J := I s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ ANI := ANI + ABS(A[I, J]); BNI := BNI + ABS(B[I, J])
               e̲n̲d̲; i̲f̲ ANI > ANORM t̲h̲e̲n̲ ANORM := ANI; i̲f̲ BNI > BNORM t̲h̲e̲n̲
              BNORM := BNI
         e̲n̲d̲; i̲f̲ ANORM = 0 t̲h̲e̲n̲ ANORM := EPS; i̲f̲ BNORM = 0 t̲h̲e̲n̲ BNORM := EPS;
      EPSA := EPS × ANORM; EPSB := EPS × BNORM;
      f̲o̲r̲ M := N, M w̲h̲i̲l̲e̲ M ≥ 3 d̲o̲
         b̲e̲g̲i̲n̲
            f̲o̲r̲ I := M + 1, I-1 w̲h̲i̲l̲e̲ (i̲f̲ I > 1 t̲h̲e̲n̲ ABS(A[I, I-1]) > EPSA e̲l̲s̲e̲
              f̲a̲l̲s̲e̲) d̲o̲ Q := I-1;
            i̲f̲ Q > 1 t̲h̲e̲n̲ A[Q, Q-1] := 0;
            L: i̲f̲ Q ≥ M-1 t̲h̲e̲n̲ M := Q-1 e̲l̲s̲e̲
            b̲e̲g̲i̲n̲
               i̲f̲ ABS(B[Q, Q]) ≤ EPSB t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ B[Q, Q] := 0; Q1 := Q + 1;
                  HSH2COL(Q, Q, M, Q, A[Q, Q], A[Q1, Q], A, B); A[Q1, Q] := 0;
                  Q := Q1; g̲o̲t̲o̲ L
               e̲n̲d̲ e̲l̲s̲e̲ M1 := M-1; Q1 := Q + 1; CONST := 0·75; ITER[M] := ITER[M] + 1;
               STATIONARY := i̲f̲ ITER[M] = 1 t̲h̲e̲n̲ t̲r̲u̲e̲ e̲l̲s̲e̲
                 ABS(A[M, M-1]) ≥ CONST × OLD1 ∧ ABS(A[M-1, M-2]) ≥ CONST × OLD2;
               i̲f̲ ITER[M] > 30 ∧ STATIONARY t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ ITER[I] := -1;
                  g̲o̲t̲o̲ OUT
               e̲n̲d̲;
               i̲f̲ ITER[M] = 10 ∧ STATIONARY t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ A10 := 0; A20 := 1; A30 := 1·1605
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ B11 := B[Q, Q]; B22 := i̲f̲ ABS(B[Q1, Q1]) < EPSB t̲h̲e̲n̲ EPSB
                  e̲l̲s̲e̲ B[Q1, Q1];
                  B33 := i̲f̲ ABS(B[M1, M1]) < EPSB t̲h̲e̲n̲ EPSB e̲l̲s̲e̲ B[M1, M1];
                  B44 := i̲f̲ ABS(B[M, M]) < EPSB t̲h̲e̲n̲ EPSB e̲l̲s̲e̲ B[M, M] ;
                  A11 := A[Q, Q]/B11; A12 := A[Q, Q1]/B22; A21 := A[Q1, Q]/B11;
                  A22 := A[Q1, Q1]/B22; A33 := A[M1, M1]/B33; A34 := A[M1, M]/B44;
                  A43 := A[M, M1]/B33; A44 := A[M, M]/B44; B12 := B[Q, Q1]/B22;
                  B34 := B[M1, M]/B44;
                  A10 := ((A33-A11) × (A44-A11)-A34 × A43 + A43 × B34 × A11)/A21
                    + A12-A11 × B12;
                  A20 := (A22-A11-A21 × B12)-(A33-A11)-(A44-A11) + A43 × B34;
                  A30 := A[Q + 2, Q1]/B22
               e̲n̲d̲; OLD1 := ABS(A[M, M-1]); OLD2 := ABS(A[M-1, M-2]);
               f̲o̲r̲ K := Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M1 d̲o̲
                  b̲e̲g̲i̲n̲ K1 := K + 1; K2 := K + 2; K3 := i̲f̲ K + 3 > M t̲h̲e̲n̲ M e̲l̲s̲e̲ K + 3;
                     KM1 := i̲f̲ K-1 < Q t̲h̲e̲n̲ Q e̲l̲s̲e̲ K-1;
                     i̲f̲ K ≠ M1 t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ i̲f̲ K = Q t̲h̲e̲n̲
                        b̲e̲g̲i̲n̲
                           HSH3COL(KM1, KM1, M, K, A[K, KM1], A[K1, KM1], A[K2, KM1], A, B);
                           A[K1, KM1] := A[K2, KM1] := 0
                        e̲n̲d̲;
                        HSH3ROW2(Q, Q, K3, K, B[K2, K2], B[K2, K1], B[K2, K], A, B);
                        B[K2, K] := B[K2, K1] := 0 ;
                     e̲n̲d̲ e̲l̲s̲e̲
                     b̲e̲g̲i̲n̲ HSH2COL(KM1, KM1, M, K, A[K, KM1], A[K1, KM1], A, B);
                        A[K1, KM1] := 0
                     e̲n̲d̲;
                     HSH2ROW2(Q, Q, K3, K3, K, B[K1, K1], B[K1, K], A, B); B[K1, K] := 0
                  e̲n̲d̲
            e̲n̲d̲;
            OUT:
         e̲n̲d̲
   e̲n̲d̲ QZIT;
e̲n̲d̲ QZI;
c̲o̲m̲m̲e̲n̲t̲ ================== 34601 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ QZI(N, A, B, X, ALFR, ALFI, BETA, ITER, EM);
   v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, B, X, ALFR, ALFI, BETA, EM;
     i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ ITER;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ DWARF, EPS, EPSA, EPSB;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHDECMUL(N, A, B, DWARF); c̲o̲d̲e̲ 34602;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HESTGL3(N, A, B, X); c̲o̲d̲e̲ 34603;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2ROW3(L, UA, UB, UX, J, A1, A2, A, B, X); c̲o̲d̲e̲ 34607;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3ROW3(L, U, UX, J, A1, A2, A3, A, B, X); c̲o̲d̲e̲ 34609;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2COL(LA, LB, U, I, A1, A2, A, B); c̲o̲d̲e̲ 34605;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3COL(LA, LB, U, I, A1, A2, A3, A, B); c̲o̲d̲e̲ 34606;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHSH2(A1R, A1I, A2R, A2I, C, SR, SI); c̲o̲d̲e̲ 34611;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ COMDIV(XR, XI, YR, YI, ZR, ZI); c̲o̲d̲e̲ 34342;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ QZIT(N, A, B, X, EPS, EPSA, EPSB, ITER); v̲a̲l̲u̲e̲ N, EPS;
      r̲e̲a̲l̲ EPS, EPSA, EPSB; i̲n̲t̲e̲g̲e̲r̲ N; i̲n̲t̲e̲g̲e̲r̲ a̲r̲r̲a̲y̲ ITER; a̲r̲r̲a̲y̲ A, B, X;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ANORM, BNORM, ANI, BNI, CONST, A10, A20, A30, B11,
        B22, B33, B44, A11, A12, A21, A22, A33, A34, A43, A44, B12, B34, OLD1, OLD2;
      i̲n̲t̲e̲g̲e̲r̲ I, Q, M, M1, Q1, J, K, K1, K2, K3, KM1; B̲o̲o̲l̲e̲a̲n̲ STATIONARY;
      ANORM := BNORM := 0; f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
         b̲e̲g̲i̲n̲ BNI := 0; ITER[I] := 0; ANI := i̲f̲ I > 1 t̲h̲e̲n̲ ABS(A[I, I-1]) e̲l̲s̲e̲ 0;
            f̲o̲r̲ J := I s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
               b̲e̲g̲i̲n̲ ANI := ANI + ABS(A[I, J]); BNI := BNI + ABS(B[I, J])
               e̲n̲d̲; i̲f̲ ANI > ANORM t̲h̲e̲n̲ ANORM := ANI; i̲f̲ BNI > BNORM t̲h̲e̲n̲
              BNORM := BNI
         e̲n̲d̲; i̲f̲ ANORM = 0 t̲h̲e̲n̲ ANORM := EPS; i̲f̲ BNORM = 0 t̲h̲e̲n̲ BNORM := EPS;
      EPSA := EPS × ANORM; EPSB := EPS × BNORM;
      f̲o̲r̲ M := N, M w̲h̲i̲l̲e̲ M ≥ 3 d̲o̲
         b̲e̲g̲i̲n̲
            f̲o̲r̲ I := M + 1, I-1 w̲h̲i̲l̲e̲ (i̲f̲ I > 1 t̲h̲e̲n̲ ABS(A[I, I-1]) > EPSA e̲l̲s̲e̲
              f̲a̲l̲s̲e̲) d̲o̲ Q := I-1;
            i̲f̲ Q > 1 t̲h̲e̲n̲ A[Q, Q-1] := 0;
            L: i̲f̲ Q ≥ M-1 t̲h̲e̲n̲ M := Q-1 e̲l̲s̲e̲
            b̲e̲g̲i̲n̲
               i̲f̲ ABS(B[Q, Q]) ≤ EPSB t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ B[Q, Q] := 0; Q1 := Q + 1;
                  HSH2COL(Q, Q, N, Q, A[Q, Q], A[Q1, Q], A, B); A[Q1, Q] := 0;
                  Q := Q1; g̲o̲t̲o̲ L
               e̲n̲d̲ e̲l̲s̲e̲ M1 := M-1; Q1 := Q + 1; CONST := 0·75; ITER[M] := ITER[M] + 1;
               STATIONARY := i̲f̲ ITER[M] = 1 t̲h̲e̲n̲ t̲r̲u̲e̲ e̲l̲s̲e̲
                 ABS(A[M, M-1]) ≥ CONST × OLD1 ∧ ABS(A[M-1, M-2]) ≥ CONST × OLD2;
               i̲f̲ ITER[M] > 30 ∧ STATIONARY t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ f̲o̲r̲ I := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M d̲o̲ ITER[I] := -1;
                  g̲o̲t̲o̲ OUT
               e̲n̲d̲;
               i̲f̲ ITER[M] = 10 ∧ STATIONARY t̲h̲e̲n̲
               b̲e̲g̲i̲n̲ A10 := 0; A20 := 1; A30 := 1·1605
               e̲n̲d̲ e̲l̲s̲e̲
               b̲e̲g̲i̲n̲ B11 := B[Q, Q]; B22 := i̲f̲ ABS(B[Q1, Q1]) < EPSB t̲h̲e̲n̲ EPSB
                  e̲l̲s̲e̲ B[Q1, Q1];
                  B33 := i̲f̲ ABS(B[M1, M1]) < EPSB t̲h̲e̲n̲ EPSB e̲l̲s̲e̲ B[M1, M1];
                  B44 := i̲f̲ ABS(B[M, M]) < EPSB t̲h̲e̲n̲ EPSB e̲l̲s̲e̲ B[M, M] ;
                  A11 := A[Q, Q]/B11; A12 := A[Q, Q1]/B22; A21 := A[Q1, Q]/B11;
                  A22 := A[Q1, Q1]/B22; A33 := A[M1, M1]/B33; A34 := A[M1, M]/B44;
                  A43 := A[M, M1]/B33; A44 := A[M, M]/B44; B12 := B[Q, Q1]/B22;
                  B34 := B[M1, M]/B44;
                  A10 := ((A33-A11) × (A44-A11)-A34 × A43 + A43 × B34 × A11)/A21
                    + A12-A11 × B12;
                  A20 := (A22-A11-A21 × B12)-(A33-A11)-(A44-A11) + A43 × B34;
                  A30 := A[Q + 2, Q1]/B22
               e̲n̲d̲; OLD1 := ABS(A[M, M-1]); OLD2 := ABS(A[M-1, M-2]);
               f̲o̲r̲ K := Q s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ M1 d̲o̲
                  b̲e̲g̲i̲n̲ K1 := K + 1; K2 := K + 2; K3 := i̲f̲ K + 3 > M t̲h̲e̲n̲ M e̲l̲s̲e̲ K + 3;
                     KM1 := i̲f̲ K-1 < Q t̲h̲e̲n̲ Q e̲l̲s̲e̲ K-1;
                     i̲f̲ K ≠ M1 t̲h̲e̲n̲
                     b̲e̲g̲i̲n̲ i̲f̲ K = Q t̲h̲e̲n̲
                          HSH3COL(KM1, KM1, N, K, A10, A20, A30, A, B) e̲l̲s̲e̲
                        b̲e̲g̲i̲n̲
                           HSH3COL(KM1, KM1, N, K, A[K, KM1], A[K1, KM1], A[K2, KM1], A, B);
                           A[K1, KM1] := A[K2, KM1] := 0
                        e̲n̲d̲;
                        HSH3ROW3(1, K3, N, K, B[K2, K2], B[K2, K1], B[K2, K], A, B, X);
                        B[K2, K] := B[K2, K1] := 0 ;
                     e̲n̲d̲ e̲l̲s̲e̲
                     b̲e̲g̲i̲n̲ HSH2COL(KM1, KM1, N, K, A[K, KM1], A[K1, KM1], A, B);
                        A[K1, KM1] := 0
                     e̲n̲d̲;
                     HSH2ROW3(1, K3, K3, N, K, B[K1, K1], B[K1, K], A, B, X); B[K1, K] := 0
                  e̲n̲d̲
            e̲n̲d̲
         e̲n̲d̲; OUT:
   e̲n̲d̲ QZIT;
e̲n̲d̲ QZI;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ QZVAL(N, A, B, X, EPSA, EPSB, ALFR, ALFI, BETA); v̲a̲l̲u̲e̲ N;
   r̲e̲a̲l̲ EPSA, EPSB; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ ALFR, ALFI, BETA, A, B, X;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ M, L, J; r̲e̲a̲l̲ AN, BN, A11, A12, A21, A22, B11, B12, B22, E, C, D,
     ER, EI, A11R, A11I, A12R, A12I, A21R, A21I, A22R, A22I, CZ, SZR, SZI,
     CQ, SQR, SQI, SSR, SSI, TR, TI, BDR, BDI, R;
   f̲o̲r̲ M := N, M w̲h̲i̲l̲e̲ M > 0 d̲o̲
        i̲f̲ (i̲f̲ M > 1 t̲h̲e̲n̲ A[M, M-1] = 0 e̲l̲s̲e̲ t̲r̲u̲e̲) t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ ALFR[M] := A[M, M]; BETA[M] := B[M, M]; ALFI[M] := 0; M := M-1
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ L := M-1; i̲f̲ ABS(B[L, L]) ≤ EPSB t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B[L, L] := 0; HSH2COL(L, L, N, L, A[L, L], A[M, L], A, B);
            A[M, L] := B[M, L] := 0; ALFR[L] := A[L, L]; ALFR[M] := A[M, M];
            BETA[L] := B[L, L]; BETA[M] := B[M, M]; ALFI[M] := ALFI[L] := 0;
         e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ ABS(B[M, M]) ≤ EPSB t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ B[M, M] := 0; HSH2ROW3(1, M, M, N, L, A[M, M], A[M, L], A, B, X);
            A[M, L] := B[M, L] := 0; ALFR[L] := A[L, L]; ALFR[M] := A[M, M];
            BETA[L] := B[L, L]; BETA[M] := B[M, M]; ALFI[M] := ALFI[L] := 0;
         e̲n̲d̲ e̲l̲s̲e̲
         b̲e̲g̲i̲n̲
            AN := ABS(A[L, L]) + ABS(A[L, M]) + ABS(A[M, L]) + ABS(A[M, M]);
            BN := ABS(B[L, L]) + ABS(B[L, M]) + ABS(B[M, M]);
            A11 := A[L, L]/AN; A12 := A[L, M]/AN; A21 := A[M, L]/AN; A22 := A[M, M]/AN;
            B11 := B[L, L]/BN; B12 := B[L, M]/BN; B22 := B[M, M]/BN;
            E := A11/B11;
            C := ((A22-E × B22)/B22-(A21 × B12)/(B11 × B22))/2;
            D := C × C + (A21 × (A12-E × B12))/(B11 × B22);
            i̲f̲ D ≥ 0 t̲h̲e̲n̲
            b̲e̲g̲i̲n̲ E := E + (i̲f̲ C < 0 t̲h̲e̲n̲ C-SQRT(D) e̲l̲s̲e̲ C + SQRT(D));
               A11 := A11-E × B11; A12 := A12-E × B12; A22 := A22-E × B22;
               i̲f̲ ABS(A11) + ABS(A12) ≥ ABS(A21) + ABS(A22) t̲h̲e̲n̲
                 HSH2ROW3(1, M, M, N, L, A12, A11, A, B, X) e̲l̲s̲e̲
                 HSH2ROW3(1, M, M, N, L, A22, A21, A, B, X);
               i̲f̲ AN ≥ ABS(E) × BN t̲h̲e̲n̲
                 HSH2COL(L, L, N, L, B[L, L], B[M, L], A, B) e̲l̲s̲e̲
                 HSH2COL(L, L, N, L, A[L, L], A[M, L], A, B);
               A[M, L] := B[M, L] := 0;
               ALFR[L] := A[L, L]; ALFR[M] := A[M, M]; BETA[L] := B[L, L];
               BETA[M] := B[M, M]; ALFI[M] := ALFI[L] := 0;
            e̲n̲d̲ e̲l̲s̲e̲
            b̲e̲g̲i̲n̲
               ER := E + C; EI := SQRT(-D); A11R := A11-ER × B11; A11I := EI × B11;
               A12R := A12-ER × B12; A12I := EI × B12; A21R := A21; A21I := 0;
               A22R := A22-ER × B22; A22I := EI × B22;
               i̲f̲ ABS(A11R) + ABS(A11I) + ABS(A12R) + ABS(A12I) ≥
                 ABS(A21R) + ABS(A22R) + ABS(A22I) t̲h̲e̲n̲
                 CHSH2(A12R, A12I, -A11R, -A11I, CZ, SZR, SZI) e̲l̲s̲e̲
                 CHSH2(A22R, A22I, -A21R, -A21I, CZ, SZR, SZI);
               i̲f̲ AN ≥ (ABS(ER) + ABS(EI)) × BN t̲h̲e̲n̲
                 CHSH2(CZ × B11 + SZR × B12, SZI × B12, SZR × B22, SZI × B22, CQ, SQR, SQI)
               e̲l̲s̲e̲ CHSH2(CZ × A11 + SZR × A12, SZI × A12, CZ × A21 + SZR × A22, SZI × A22,
                 CQ, SQR, SQI); SSR := SQR × SZR + SQI × SZI; SSI := SQR × SZI-SQI × SZR;
               TR := CQ × CZ × A11 + CQ × SZR × A12 + SQR × CZ × A21 + SSR × A22;
               TI := CQ × SZI × A12-SQI × CZ × A21 + SSI × A22;
               BDR := CQ × CZ × B11 + CQ × SZR × B12 + SSR × B22;
               BDI := CQ × SZI × B12 + SSI × B22;
               R := SQRT(BDR × BDR + BDI × BDI); BETA[L] := BN × R;
               ALFR[L] := AN × (TR × BDR + TI × BDI)/R;
               ALFI[L] := AN × (TR × BDI-TI × BDR)/R;
               TR := SSR × A11-SQR × CZ × A12-CQ × SZR × A21 + CQ × CZ × A22;
               TI := -SSI × A11-SQI × CZ × A12 + CQ × SZI × A21;
               BDR := SSR × B11-SQR × CZ × B12 + CQ × CZ × B22;
               BDI := -SSI × B11-SQI × CZ × B12;
               R := SQRT(BDR × BDR + BDI × BDI); BETA[M] := BN × R;
               ALFR[M] := AN × (TR × BDR + TI × BDI)/R;
               ALFI[M] := AN × (TR × BDI-TI × BDR)/R;
            e̲n̲d̲
         e̲n̲d̲; M := M-2
      e̲n̲d̲
e̲n̲d̲ QZVAL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34602 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHDECMUL(N, A, B, DWARF); v̲a̲l̲u̲e̲ N, DWARF; i̲n̲t̲e̲g̲e̲r̲ N;
     r̲e̲a̲l̲ DWARF; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲  a̲r̲r̲a̲y̲ V[1:N]; i̲n̲t̲e̲g̲e̲r̲ J, K, K1, N1; r̲e̲a̲l̲ R, T, C;
   r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECMAT(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31070;
   K := 1; N1 := N + 1;
   f̲o̲r̲ K1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N1 d̲o̲
      b̲e̲g̲i̲n̲ R := TAMMAT(K1, N, K, K, B, B);
         i̲f̲ R > DWARF t̲h̲e̲n̲
         b̲e̲g̲i̲n̲ R := i̲f̲ B[K, K] < 0 t̲h̲e̲n̲ -SQRT(R + B[K, K] × B[K, K])
            e̲l̲s̲e̲ SQRT(R + B[K, K] × B[K, K]); T := B[K, K] + R; C := -T/R;
            B[K, K] := -R; V[K] := 1;
            f̲o̲r̲ J := K1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲ V[J] := B[J, K]/T;
            HSHVECMAT(K, N, K1, N, C, V, B); HSHVECMAT(K, N, 1, N, C, V, A)
         e̲n̲d̲; K := K1
      e̲n̲d̲
e̲n̲d̲ HSHDECMUL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34603 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HESTGL3(N, A, B, X); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, B, X;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ NM1, K, L, K1, L1;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2COL(LA, LB, U, I, A1, A2, A, B); c̲o̲d̲e̲ 34605;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2ROW3(L, UA, UB, UX, J, A1, A2, A, B, X); c̲o̲d̲e̲ 34607;
   i̲f̲ N > 2 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ K := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K-1 d̲o̲ B[K, L] := 0;
      NM1 := N-1; K := 1;
      f̲o̲r̲ K1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NM1 d̲o̲
         b̲e̲g̲i̲n̲ L1 := N;
            f̲o̲r̲ L := N-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ K1 d̲o̲
               b̲e̲g̲i̲n̲
                  HSH2COL(K, L, N, L, A[L, K], A[L1, K], A, B); A[L1, K] := 0;
                  HSH2ROW3(1, N, L1, N, L, B[L1, L1], B[L1, L], A, B, X);
                  B[L1, L] := 0; L1 := L
               e̲n̲d̲; K := K1
         e̲n̲d̲
   e̲n̲d̲
e̲n̲d̲ HESTGL3;
c̲o̲m̲m̲e̲n̲t̲ ================== 34604 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HESTGL2(N, A, B); v̲a̲l̲u̲e̲ N; i̲n̲t̲e̲g̲e̲r̲ N; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ NM1, K, L, K1, L1;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2COL(LA, LB, U, I, A1, A2, A, B); c̲o̲d̲e̲ 34605;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2ROW2(LA, LB, UA, UB, A1, A2, A, B); c̲o̲d̲e̲ 34608;
   i̲f̲ N > 2 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ f̲o̲r̲ K := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
           f̲o̲r̲ L := 1 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ K-1 d̲o̲ B[K, L] := 0;
      NM1 := N-1; K := 1;
      f̲o̲r̲ K1 := 2 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ NM1 d̲o̲
         b̲e̲g̲i̲n̲ L1 := N;
            f̲o̲r̲ L := N-1 s̲t̲e̲p̲ -1 u̲n̲t̲i̲l̲ K1 d̲o̲
               b̲e̲g̲i̲n̲
                  HSH2COL(K, L, N, L, A[L, K], A[L1, K], A, B); A[L1, K] := 0;
                  HSH2ROW2(1, 1, N, L1, L, B[L1, L1], B[L1, L], A, B);
                  B[L1, L] := 0; L1 := L
               e̲n̲d̲; K := K1
         e̲n̲d̲
   e̲n̲d̲
e̲n̲d̲ HESTGL2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34605 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2COL(LA, LB, U, I, A1, A2, A, B); v̲a̲l̲u̲e̲ LA, LB, U, I, A1, A2;
   i̲n̲t̲e̲g̲e̲r̲ LA, LB, U, I; r̲e̲a̲l̲ A1, A2; a̲r̲r̲a̲y̲ A, B;
   i̲f̲ A2 ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, T, C; a̲r̲r̲a̲y̲ V[I:I + 1];
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECMAT(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31070;
      R := i̲f̲ A1 < 0 t̲h̲e̲n̲ -SQRT(A1 × A1 + A2 × A2) e̲l̲s̲e̲ SQRT(A1 × A1 + A2 × A2);
      T := A1 + R; C := -T/R; V[I] := 1; V[I + 1] := A2/T;
      HSHVECMAT(I, I + 1, LA, U, C, V, A); HSHVECMAT(I, I + 1, LB, U, C, V, B)
   e̲n̲d̲ HSH2COL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34606 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3COL(LA, LB, U, I, A1, A2, A3, A, B);
   v̲a̲l̲u̲e̲ LA, LB, U, I, A1, A2, A3; i̲n̲t̲e̲g̲e̲r̲ LA, LB, I, U; r̲e̲a̲l̲ A1, A2, A3; a̲r̲r̲a̲y̲ A, B;
   i̲f̲ A2 ≠ 0 ∨ A3 ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, T, C; a̲r̲r̲a̲y̲ V[I:I + 2];
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECMAT(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31070;
      R := i̲f̲ A1 < 0 t̲h̲e̲n̲ -SQRT(A1 × A1 + A2 × A2 + A3 × A3)
      e̲l̲s̲e̲ SQRT(A1 × A1 + A2 × A2 + A3 × A3);
      T := A1 + R; C := -T/R; V[I] := 1; V[I + 1] := A2/T; V[I + 2] := A3/T;
      HSHVECMAT(I, I + 2, LA, U, C, V, A); HSHVECMAT(I, I + 2, LB, U, C, V, B)
   e̲n̲d̲ HSH3COL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34607 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2ROW3(L, UA, UB, UX, J, A1, A2, A, B, X); v̲a̲l̲u̲e̲ L, UA, UB, UX,
     J, A1, A2; i̲n̲t̲e̲g̲e̲r̲ L, UA, UB, UX, J; r̲e̲a̲l̲ A1, A2; a̲r̲r̲a̲y̲ A, B, X;
   i̲f̲ A2 ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, T, C; i̲n̲t̲e̲g̲e̲r̲ K; a̲r̲r̲a̲y̲ V[J:J + 1];
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECTAM(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31073;
      R := i̲f̲ A1 < 0 t̲h̲e̲n̲ -SQRT(A1 × A1 + A2 × A2) e̲l̲s̲e̲ SQRT(A1 × A1 + A2 × A2);
      T := A1 + R; C := -T/R; V[J + 1] := 1; V[J] := A2/T;
      HSHVECTAM(L, UA, J, J + 1, C, V, A); HSHVECTAM(L, UB, J, J + 1, C, V, B);
      HSHVECTAM(1, UX, J, J + 1, C, V, X)
   e̲n̲d̲ HSH2ROW3;
c̲o̲m̲m̲e̲n̲t̲ ================== 34608 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH2ROW2(LA, LB, UA, UB, J, A1, A2, A, B); v̲a̲l̲u̲e̲ LA, LB, UA, UB,
     J, A1, A2; i̲n̲t̲e̲g̲e̲r̲ LA, LB, UA, UB, J; r̲e̲a̲l̲ A1, A2; a̲r̲r̲a̲y̲ A, B;
   i̲f̲ A2 ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, T, C; i̲n̲t̲e̲g̲e̲r̲ K; a̲r̲r̲a̲y̲ V[J:J + 1];
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECTAM(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31073;
      R := i̲f̲ A1 < 0 t̲h̲e̲n̲ -SQRT(A1 × A1 + A2 × A2) e̲l̲s̲e̲ SQRT(A1 × A1 + A2 × A2);
      T := A1 + R; C := -T/R; V[J + 1] := 1; V[J] := A2/T;
      HSHVECTAM(LA, UA, J, J + 1, C, V, A); HSHVECTAM(LB, UB, J, J + 1, C, V, B)
   e̲n̲d̲ HSH2ROW2;
c̲o̲m̲m̲e̲n̲t̲ ================== 34609 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3ROW3(L, U, UX, J, A1, A2, A3, A, B, X);
   v̲a̲l̲u̲e̲ L, U, UX, J, A1, A2, A3; i̲n̲t̲e̲g̲e̲r̲ L, J, U, UX; r̲e̲a̲l̲ A1, A2, A3; a̲r̲r̲a̲y̲ A, B, X;
   i̲f̲ A2 ≠ 0 ∨ A3 ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, T, C; a̲r̲r̲a̲y̲ V[J:J + 2]; i̲n̲t̲e̲g̲e̲r̲ K;
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECTAM(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31073;
      R := i̲f̲ A1 < 0 t̲h̲e̲n̲ -SQRT(A1 × A1 + A2 × A2 + A3 × A3)
      e̲l̲s̲e̲ SQRT(A1 × A1 + A2 × A2 + A3 × A3);
      T := A1 + R; C := -T/R; V[J + 2] := 1; V[J + 1] := A2/T; V[J] := A3/T;
      HSHVECTAM(L, U, J, J + 2, C, V, A); HSHVECTAM(L, U, J, J + 2, C, V, B);
      HSHVECTAM(L, UX, J, J + 2, C, V, X)
   e̲n̲d̲ HSH3ROW3;
c̲o̲m̲m̲e̲n̲t̲ ================== 34610 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSH3ROW2(LA, LB, U, J, A1, A2, A3, A, B);
   v̲a̲l̲u̲e̲ LA, LB, U, J, A1, A2, A3; i̲n̲t̲e̲g̲e̲r̲ LA, LB, U, J; r̲e̲a̲l̲ A1, A2, A3; a̲r̲r̲a̲y̲ A, B;
   i̲f̲ A2 ≠ 0 ∨ A3 ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R, T, C; a̲r̲r̲a̲y̲ V[J:J + 2];
      p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECTAM(LR, UR, LC, UC, X, U, A); c̲o̲d̲e̲ 31073;
      R := i̲f̲ A1 < 0 t̲h̲e̲n̲ -SQRT(A1 × A1 + A2 × A2 + A3 × A3)
      e̲l̲s̲e̲ SQRT(A1 × A1 + A2 × A2 + A3 × A3);
      T := A1 + R; C := -T/R; V[J + 2] := 1; V[J + 1] := A2/T; V[J] := A3/T;
      HSHVECTAM(LA, U, J, J + 2, C, V, A); HSHVECTAM(LB, U, J, J + 2, C, V, B)
   e̲n̲d̲ HSH3ROW2;
c̲o̲m̲m̲e̲n̲t̲ ================== 31070 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECMAT(LR, UR, LC, UC, X, U, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ U, A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMVEC(L, U, I, A, B); c̲o̲d̲e̲ 34012;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34022;
   f̲o̲r̲ LC := LC s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UC d̲o̲
        ELMCOLVEC(LR, UR, LC, A, U, TAMVEC(LR, UR, LC, A, U) × X)
e̲n̲d̲;
c̲o̲m̲m̲e̲n̲t̲ ================== 31071 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOLMAT(LR, UR, LC, UC, I, X, U, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, I, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, I;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ U, A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ TAMMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34014;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34023;
   f̲o̲r̲ LC := LC s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UC d̲o̲
        ELMCOL(LR, UR, LC, I, A, U, TAMMAT(LR, UR, LC, I, A, U) × X)
e̲n̲d̲;
c̲o̲m̲m̲e̲n̲t̲ ================== 31072 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHROWMAT(LR, UR, LC, UC, I, X, U, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, I, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, I;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ U, A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMCOLROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34029;
   f̲o̲r̲ LC := LC s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UC d̲o̲
        ELMCOLROW(LR, UR, LC, I, A, U, MATMAT(LR, UR, I, LC, U, A) × X)
e̲n̲d̲;
c̲o̲m̲m̲e̲n̲t̲ ================== 31073 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHVECTAM(LR, UR, LC, UC, X, U, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ U, A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATVEC(L, U, I, A, B); c̲o̲d̲e̲ 34011;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWVEC(L, U, I, A, B, X); c̲o̲d̲e̲ 34027;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        ELMROWVEC(LC, UC, LR, A, U, MATVEC(LC, UC, LR, A, U) × X)
e̲n̲d̲;
c̲o̲m̲m̲e̲n̲t̲ ================== 31074 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHCOLTAM(LR, UR, LC, UC, I, X, U, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, I, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, I;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ U, A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATMAT(L, U, I, J, A, B); c̲o̲d̲e̲ 34013;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROWCOL(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34028;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        ELMROWCOL(LC, UC, LR, I, A, U, MATMAT(LC, UC, LR, I, A, U) × X)
e̲n̲d̲;
c̲o̲m̲m̲e̲n̲t̲ ================== 31075 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ HSHROWTAM(LR, UR, LC, UC, I, X, U, A);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, I, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC, I;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ U, A;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ MATTAM(L, U, I, J, A, B); c̲o̲d̲e̲ 34015;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELMROW(L, U, I, J, A, B, X); c̲o̲d̲e̲ 34024;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
        ELMROW(LC, UC, LR, I, A, U, MATTAM(LC, UC, LR, I, A, U) × X)
e̲n̲d̲;
c̲o̲m̲m̲e̲n̲t̲ ================== 30006 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ PI;
   PI := 3·14159265358979;
c̲o̲m̲m̲e̲n̲t̲ ================== 30007 ================= ;
r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ E;
   E := 2·71828182845905;
c̲o̲m̲m̲e̲n̲t̲ ================== 34410 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGVECVEC(L, U, SHIFT, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, SHIFT, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, SHIFT;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[L], B[L + SHIFT], E, EE);
         LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGVECVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34411 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGMATVEC(L, U, I, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, I, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, I;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[I, L], B[I], E, EE); LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGMATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34412 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGTAMVEC(L, U, I, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, I, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, I;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[L, I], B[I], E, EE); LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGTAMVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34413 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGMATMAT(L, U, I, J, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, I, J, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[I, L], B[L, J], E, EE); LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGMATMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34414 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGTAMMAT(L, U, I, J, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, I, J, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[L, I], B[L, J], E, EE); LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGTAMMAT;
c̲o̲m̲m̲e̲n̲t̲ ================== 34415 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGMATTAM(L, U, I, J, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, I, J, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[I, L], B[J, L], E, EE); LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGMATTAM;
c̲o̲m̲m̲e̲n̲t̲ ================== 34416 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGSEQVEC(L, U, IL, SHIFT, A, B, C, CC, D, DD);
   v̲a̲l̲u̲e̲ L, U, IL, SHIFT, C, CC; i̲n̲t̲e̲g̲e̲r̲ L, U, IL, SHIFT;
     r̲e̲a̲l̲ C, CC, D, DD; a̲r̲r̲a̲y̲ A, B;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(A, B, C, CC); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGADD(A, AA, B, BB, C, CC); c̲o̲d̲e̲ 31105;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(A[IL], B[L + SHIFT], E, EE); IL := IL + L;
         LNGADD(C, CC, E, EE, C, CC)
      e̲n̲d̲;
   D := C; DD := CC
e̲n̲d̲ LNGSEQVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31507 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGFULSYMMATVEC(LR, UR, LC, UC, A, B, C);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, B; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ D, DD;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGSYMMATVEC(L, U, I, A, B, C, CC, D, DD);
      c̲o̲d̲e̲ 34418;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
      b̲e̲g̲i̲n̲ LNGSYMMATVEC(LC, UC, LR, A, B, 0, 0, D, DD);
         C[LR] := D + DD
      e̲n̲d̲
e̲n̲d̲ LNGFULSYMMATVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31508 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGRESVEC(LR, UR, LC, UC, A, B, C, X);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ D, DD, E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(X, Y, E, EE); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGMATVEC(L, U, I, A, B, C, CC, D, DD); c̲o̲d̲e̲ 34411;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(C[LR], X, E, EE);
         LNGMATVEC(LC, UC, LR, A, B, E, EE, D, DD); C[LR] := D + DD
      e̲n̲d̲
e̲n̲d̲ LNGRESVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 31509 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGSYMRESVEC(LR, UR, LC, UC, A, B, C, X);
   v̲a̲l̲u̲e̲ LR, UR, LC, UC, B, X; i̲n̲t̲e̲g̲e̲r̲ LR, UR, LC, UC;
     r̲e̲a̲l̲ X; a̲r̲r̲a̲y̲ A, B, C;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ D, DD, E, EE;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DPMUL(X, Y, E, EE); c̲o̲d̲e̲ 31103;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ LNGSYMMATVEC(L, U, I, A, B, C, CC, D, DD);
      c̲o̲d̲e̲ 34418;
   f̲o̲r̲ LR := LR s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ UR d̲o̲
      b̲e̲g̲i̲n̲ DPMUL(C[LR], X, E, EE);
         LNGSYMMATVEC(LC, UC, LR, A, B, E, EE, D, DD); C[LR] := D + DD
      e̲n̲d̲
e̲n̲d̲ LNGSYMRESVEC;
c̲o̲m̲m̲e̲n̲t̲ ================== 34357 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOMCOL(L, U, I, J, AR, AI, CR, CI, S);
   v̲a̲l̲u̲e̲ L, U, I, J, CR, CI, S; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J;
     r̲e̲a̲l̲ CR, CI, S; a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ARLI, AILI, ARLJ, AILJ;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ ARLI := AR[L, I]; AILI := AI[L, I]; ARLJ := AR[L, J];
         AILJ := AI[L, J];
         AR[L, I] := CR × ARLI + CI × AILI - S × ARLJ;
         AI[L, I] := CR × AILI - CI × ARLI - S × AILJ;
         AR[L, J] := CR × ARLJ - CI × AILJ + S × ARLI;
         AI[L, J] := CR × AILJ + CI × ARLJ + S × AILI;
      e̲n̲d̲
e̲n̲d̲ ROTCOMCOL;
c̲o̲m̲m̲e̲n̲t̲ ================== 34358 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ ROTCOMROW(L, U, I, J, AR, AI, CR, CI, S);
   v̲a̲l̲u̲e̲ L, U, I, J, CR, CI, S; i̲n̲t̲e̲g̲e̲r̲ L, U, I, J;
     r̲e̲a̲l̲ CR, CI, S; a̲r̲r̲a̲y̲ AR, AI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ ARIL, AIIL, ARJL, AIJL;
   f̲o̲r̲ L := L s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ U d̲o̲
      b̲e̲g̲i̲n̲ ARIL := AR[I, L]; AIIL := AI[I, L]; ARJL := AR[J, L];
         AIJL := AI[J, L];
         AR[I, L] := CR × ARIL + CI × AIIL + S × ARJL;
         AI[I, L] := CR × AIIL - CI × ARIL + S × AIJL;
         AR[J, L] := CR × ARJL - CI × AIJL - S × ARIL;
         AI[J, L] := CR × AIJL + CI × ARJL - S × AIIL;
      e̲n̲d̲
e̲n̲d̲ ROTCOMROW;
c̲o̲m̲m̲e̲n̲t̲ ================== 34611 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ CHSH2(A1R, A1I, A2R, A2I, C, SR, SI);
   v̲a̲l̲u̲e̲ A1R, A1I, A2R, A2I; r̲e̲a̲l̲ A1R, A1I, A2R, A2I, C, SR, SI;
b̲e̲g̲i̲n̲ r̲e̲a̲l̲ R;
   i̲f̲ A2R ≠ 0 ∨ A2I ≠ 0 t̲h̲e̲n̲
   b̲e̲g̲i̲n̲ i̲f̲ A1R ≠ 0 ∨ A1I ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ R := SQRT(A1R × A1R + A1I × A1I); C := R;
         SR := (A1R × A2R + A1I × A2I)/R; SI := (A1R × A2I-A1I × A2R)/R;
         R := SQRT(C × C + SR × SR + SI × SI); C := C/R; SR := SR/R; SI := SI/R
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ SI := C := 0; SR := 1 e̲n̲d̲
   e̲n̲d̲ e̲l̲s̲e̲ b̲e̲g̲i̲n̲ C := 1; SR := SI := 0 e̲n̲d̲
e̲n̲d̲ CHSH2;
c̲o̲m̲m̲e̲n̲t̲ ================== 33314 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ NONLIN FEM LAG SKEW(X, Y, N, F, FY, FZ, NC, E);
   i̲n̲t̲e̲g̲e̲r̲ N, NC;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ F, FY, FZ;
     a̲r̲r̲a̲y̲ X, Y, E;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, L1, IT;
   r̲e̲a̲l̲ XL1, XL, H, A12, A21, B1, B2, TAU1, TAU2, CH, TL, G, YL, PP,
     PLM, PRM, PL1, PL3, PL1PL2, PL1PL3, PL2PL2, PL2PL3,
     PR1PR2, PR1PR3, PR2PR3, PL1QL2, PL1QL3, PL2QL1, PL2QL2, PL2QL3,
     PL3QL1, PL3QL2, PR1QR2, PR1QR3, PR2QR1, PR2QR2, PR2QR3, PR3QR1,
     PR3QR2, H2RM, ZL1, ZL, E1, E2, E3, E4, E5, E6, EPS, RHO;
   a̲r̲r̲a̲y̲ T, SUPER, SUB, CHI, GI[0:N-1], Z[0:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, S, A, B); c̲o̲d̲e̲ 31030;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 1;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲  XM, VL, VR, WL, WR, PR, QM, RM, FM, XL12, XL1XL, XL2, ZM, ZACCM;
      i̲f̲ NC = 0 t̲h̲e̲n̲ VL := VR := 0·5 e̲l̲s̲e̲ i̲f̲ NC = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ VL := (XL1 × 2 + XL)/6; VR := (XL1 + XL × 2)/6 e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ XL12 := XL1 × XL1/12; XL1XL := XL1 × XL/6; XL2 := XL × XL/12;
         VL := 3 × XL12 + XL1XL + XL2;
         VR := 3 × XL2 + XL1XL + XL12
      e̲n̲d̲;
      WL := H × VL; WR := H × VR; PR := VR/(VL + VR);
      XM := XL1 + H × PR; ZM := PR × ZL + (1 - PR) × ZL1;
      ZACCM := (ZL - ZL1)/H ; QM := FZ(XM, ZM, ZACCM);
      RM := FY(XM, ZM, ZACCM); FM := F(XM, ZM, ZACCM);
      TAU1 := WL × RM; TAU2 := WR × RM;
      B1 := WL × FM - ZACCM × (VL + VR); B2 := WR × FM + ZACCM × (VL + VR);
      A12 := - (VL + VR)/H + VL × QM + (1 - PR) × PR × RM × (WL + WR);
      A21 := - (VL + VR)/H - VR × QM + (1 - PR) × PR × RM × (WL + WR);
   e̲n̲d̲ ELEM. M.V. EV.;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BOUNDARY CONDITIONS;
      i̲f̲ L = 1 ∧ E2 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := 1; B1 := A12 := 0 e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ L = 1 ∧ E2 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := TAU1 - E1/E2
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := 1; B2 := A21 := 0
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := TAU2 + E4/E5
      e̲n̲d̲ B.C.1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD BABUSKA;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ CHI[0] := CH := TL := TAU1; T[0] := TL;
         GI[0] := G := YL := B1; Y[0] := YL;
         SUB[0] := A21; SUPER[0] := A12;
         PP := A21/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; TL := TAU2; YL := B2
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ CHI[L1] := CH := CH + TAU1;
         GI[L1] := G := G + B1;
         SUB[L1] := A21; SUPER[L1] := A12;
         PP := A21/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; T[L1] := TL + TAU1; TL := TAU2;
         Y[L1] := YL +  B1; YL := B2
      e̲n̲d̲ FORWARD BABUSKA;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD BABUSKA;
   b̲e̲g̲i̲n̲ PP := YL; Y[N] := G/CH;
      G := PP; CH := TL; L := N;
      f̲o̲r̲ L := L - 1 w̲h̲i̲l̲e̲ L ≥ 0 d̲o̲
         b̲e̲g̲i̲n̲ PP := SUPER[L]/(CH - SUB[L]);
            TL := T[L]; CH := TL - CH × PP;
            YL := Y[L]; G := YL - G × PP;
            Y[L] := (GI[L] + G - YL)/(CHI[L] + CH - TL)  ;
         e̲n̲d̲
   e̲n̲d̲ BACKWARD BABUSKA;

   DUPVEC(0, N, 0, Z, Y);
   E1 := E[1]; E2 := E[2]; E3 := E[3]; E4 := E[4]; E5 := E[5]; E6 := E[6];
   f̲o̲r̲ IT := 1, IT + 1 w̲h̲i̲l̲e̲ EPS > RHO d̲o̲
      b̲e̲g̲i̲n̲ L := 0; XL := X[0]; ZL := Z[0];
         f̲o̲r̲ L := L + 1 w̲h̲i̲l̲e̲ L ≤ N d̲o̲
            b̲e̲g̲i̲n̲ XL1 := XL; L1 := L - 1; XL := X[L]; H := XL - XL1;
               ZL1 := ZL; ZL := Z[L];
               ELEMENT MAT VEC EVALUATION 1;
               i̲f̲ L = 1 ∨ L = N t̲h̲e̲n̲ BOUNDARY CONDITIONS;
               FORWARD BABUSKA
            e̲n̲d̲;
         BACKWARD BABUSKA;
         EPS := 0; RHO := 1;
         f̲o̲r̲ L := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ RHO := RHO + ABS(Z[L]);
               EPS := EPS + ABS(Y[L]); Z[L] := Z[L] - Y[L]
            e̲n̲d̲;
         RHO := @-14 × RHO
      e̲n̲d̲;
   DUPVEC(0, N, 0, Y, Z)
e̲n̲d̲ NONLIN FEM LAG SKEW;

e̲o̲p̲
