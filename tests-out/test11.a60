c̲o̲m̲m̲e̲n̲t̲ ================== 33314 ================= ;
p̲r̲o̲c̲e̲d̲u̲r̲e̲ NONLIN FEM LAG SKEW(X, Y, N, F, FY, FZ, NC, E);
   i̲n̲t̲e̲g̲e̲r̲ N, NC;
     r̲e̲a̲l̲ p̲r̲o̲c̲e̲d̲u̲r̲e̲ F, FY, FZ;
     a̲r̲r̲a̲y̲ X, Y, E;
b̲e̲g̲i̲n̲ i̲n̲t̲e̲g̲e̲r̲ L, L1, IT;
   r̲e̲a̲l̲ XL1, XL, H, A12, A21, B1, B2, TAU1, TAU2, CH, TL, G, YL, PP,
     PLM, PRM, PL1, PL3, PL1PL2, PL1PL3, PL2PL2, PL2PL3,
     PR1PR2, PR1PR3, PR2PR3, PL1QL2, PL1QL3, PL2QL1, PL2QL2, PL2QL3,
     PL3QL1, PL3QL2, PR1QR2, PR1QR3, PR2QR1, PR2QR2, PR2QR3, PR3QR1,
     PR3QR2, H2RM, ZL1, ZL, E1, E2, E3, E4, E5, E6, EPS, RHO;
   a̲r̲r̲a̲y̲ T, SUPER, SUB, CHI, GI[0:N-1], Z[0:N];

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ DUPVEC(L, U, S, A, B); c̲o̲d̲e̲ 31030;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ ELEMENT MAT VEC EVALUATION 1;
   b̲e̲g̲i̲n̲ r̲e̲a̲l̲  XM, VL, VR, WL, WR, PR, QM, RM, FM, XL12, XL1XL, XL2, ZM, ZACCM;
      i̲f̲ NC = 0 t̲h̲e̲n̲ VL := VR := 0·5 e̲l̲s̲e̲ i̲f̲ NC = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ VL := (XL1 × 2 + XL)/6; VR := (XL1 + XL × 2)/6 e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ XL12 := XL1 × XL1/12; XL1XL := XL1 × XL/6; XL2 := XL × XL/12;
         VL := 3 × XL12 + XL1XL + XL2;
         VR := 3 × XL2 + XL1XL + XL12
      e̲n̲d̲;
      WL := H × VL; WR := H × VR; PR := VR/(VL + VR);
      XM := XL1 + H × PR; ZM := PR × ZL + (1 - PR) × ZL1;
      ZACCM := (ZL - ZL1)/H ; QM := FZ(XM, ZM, ZACCM);
      RM := FY(XM, ZM, ZACCM); FM := F(XM, ZM, ZACCM);
      TAU1 := WL × RM; TAU2 := WR × RM;
      B1 := WL × FM - ZACCM × (VL + VR); B2 := WR × FM + ZACCM × (VL + VR);
      A12 := - (VL + VR)/H + VL × QM + (1 - PR) × PR × RM × (WL + WR);
      A21 := - (VL + VR)/H - VR × QM + (1 - PR) × PR × RM × (WL + WR);
   e̲n̲d̲ ELEM. M.V. EV.;
   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BOUNDARY CONDITIONS;
      i̲f̲ L = 1 ∧ E2 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := 1; B1 := A12 := 0 e̲n̲d̲
        e̲l̲s̲e̲ i̲f̲ L = 1 ∧ E2 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU1 := TAU1 - E1/E2
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 = 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := 1; B2 := A21 := 0
      e̲n̲d̲ e̲l̲s̲e̲ i̲f̲ L = N ∧ E5 ≠ 0 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ TAU2 := TAU2 + E4/E5
      e̲n̲d̲ B.C.1;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ FORWARD BABUSKA;
      i̲f̲ L = 1 t̲h̲e̲n̲
      b̲e̲g̲i̲n̲ CHI[0] := CH := TL := TAU1; T[0] := TL;
         GI[0] := G := YL := B1; Y[0] := YL;
         SUB[0] := A21; SUPER[0] := A12;
         PP := A21/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; TL := TAU2; YL := B2
      e̲n̲d̲ e̲l̲s̲e̲
      b̲e̲g̲i̲n̲ CHI[L1] := CH := CH + TAU1;
         GI[L1] := G := G + B1;
         SUB[L1] := A21; SUPER[L1] := A12;
         PP := A21/(CH - A12); CH := TAU2 - CH × PP;
         G := B2 - G × PP; T[L1] := TL + TAU1; TL := TAU2;
         Y[L1] := YL +  B1; YL := B2
      e̲n̲d̲ FORWARD BABUSKA;

   p̲r̲o̲c̲e̲d̲u̲r̲e̲ BACKWARD BABUSKA;
   b̲e̲g̲i̲n̲ PP := YL; Y[N] := G/CH;
      G := PP; CH := TL; L := N;
      f̲o̲r̲ L := L - 1 w̲h̲i̲l̲e̲ L ≥ 0 d̲o̲
         b̲e̲g̲i̲n̲ PP := SUPER[L]/(CH - SUB[L]);
            TL := T[L]; CH := TL - CH × PP;
            YL := Y[L]; G := YL - G × PP;
            Y[L] := (GI[L] + G - YL)/(CHI[L] + CH - TL)  ;
         e̲n̲d̲
   e̲n̲d̲ BACKWARD BABUSKA;

   DUPVEC(0, N, 0, Z, Y);
   E1 := E[1]; E2 := E[2]; E3 := E[3]; E4 := E[4]; E5 := E[5]; E6 := E[6];
   f̲o̲r̲ IT := 1, IT + 1 w̲h̲i̲l̲e̲ EPS > RHO d̲o̲
      b̲e̲g̲i̲n̲ L := 0; XL := X[0]; ZL := Z[0];
         f̲o̲r̲ L := L + 1 w̲h̲i̲l̲e̲ L ≤ N d̲o̲
            b̲e̲g̲i̲n̲ XL1 := XL; L1 := L - 1; XL := X[L]; H := XL - XL1;
               ZL1 := ZL; ZL := Z[L];
               ELEMENT MAT VEC EVALUATION 1;
               i̲f̲ L = 1 ∨ L = N t̲h̲e̲n̲ BOUNDARY CONDITIONS;
               FORWARD BABUSKA
            e̲n̲d̲;
         BACKWARD BABUSKA;
         EPS := 0; RHO := 1;
         f̲o̲r̲ L := 0 s̲t̲e̲p̲ 1 u̲n̲t̲i̲l̲ N d̲o̲
            b̲e̲g̲i̲n̲ RHO := RHO + ABS(Z[L]);
               EPS := EPS + ABS(Y[L]); Z[L] := Z[L] - Y[L]
            e̲n̲d̲;
         RHO := @-14 × RHO
      e̲n̲d̲;
   DUPVEC(0, N, 0, Y, Z)
e̲n̲d̲ NONLIN FEM LAG SKEW;
