<html><head><meta charset="utf-8"></head><body><pre><u>comment</u> ================== 33314 ================= ;
<u>procedure</u> NONLIN FEM LAG SKEW(X, Y, N, F, FY, FZ, NC, E);
   <u>integer</u> N, NC;
     <u>real</u> <u>procedure</u> F, FY, FZ;
     <u>array</u> X, Y, E;
<u>begin</u> <u>integer</u> L, L1, IT;
   <u>real</u> XL1, XL, H, A12, A21, B1, B2, TAU1, TAU2, CH, TL, G, YL, PP,
     PLM, PRM, PL1, PL3, PL1PL2, PL1PL3, PL2PL2, PL2PL3,
     PR1PR2, PR1PR3, PR2PR3, PL1QL2, PL1QL3, PL2QL1, PL2QL2, PL2QL3,
     PL3QL1, PL3QL2, PR1QR2, PR1QR3, PR2QR1, PR2QR2, PR2QR3, PR3QR1,
     PR3QR2, H2RM, ZL1, ZL, E1, E2, E3, E4, E5, E6, EPS, RHO;
   <u>array</u> T, SUPER, SUB, CHI, GI[0:N-1], Z[0:N];

   <u>procedure</u> DUPVEC(L, U, S, A, B); <u>code</u> 31030;

   <u>procedure</u> ELEMENT MAT VEC EVALUATION 1;
   <u>begin</u> <u>real</u>  XM, VL, VR, WL, WR, PR, QM, RM, FM, XL12, XL1XL, XL2, ZM, ZACCM;
      <u>if</u> NC = 0 <u>then</u> VL ≔ VR ≔ 0·5 <u>else</u> <u>if</u> NC = 1 <u>then</u>
      <u>begin</u> VL ≔ (XL1 × 2 + XL)/6; VR ≔ (XL1 + XL × 2)/6 <u>end</u> <u>else</u>
      <u>begin</u> XL12 ≔ XL1 × XL1/12; XL1XL ≔ XL1 × XL/6; XL2 ≔ XL × XL/12;
         VL ≔ 3 × XL12 + XL1XL + XL2;
         VR ≔ 3 × XL2 + XL1XL + XL12
      <u>end</u>;
      WL ≔ H × VL; WR ≔ H × VR; PR ≔ VR/(VL + VR);
      XM ≔ XL1 + H × PR; ZM ≔ PR × ZL + (1 - PR) × ZL1;
      ZACCM ≔ (ZL - ZL1)/H ; QM ≔ FZ(XM, ZM, ZACCM);
      RM ≔ FY(XM, ZM, ZACCM); FM ≔ F(XM, ZM, ZACCM);
      TAU1 ≔ WL × RM; TAU2 ≔ WR × RM;
      B1 ≔ WL × FM - ZACCM × (VL + VR); B2 ≔ WR × FM + ZACCM × (VL + VR);
      A12 ≔ - (VL + VR)/H + VL × QM + (1 - PR) × PR × RM × (WL + WR);
      A21 ≔ - (VL + VR)/H - VR × QM + (1 - PR) × PR × RM × (WL + WR);
   <u>end</u> ELEM. M.V. EV.;
   <u>procedure</u> BOUNDARY CONDITIONS;
      <u>if</u> L = 1 ∧ E2 = 0 <u>then</u>
      <u>begin</u> TAU1 ≔ 1; B1 ≔ A12 ≔ 0 <u>end</u>
        <u>else</u> <u>if</u> L = 1 ∧ E2 ≠ 0 <u>then</u>
      <u>begin</u> TAU1 ≔ TAU1 - E1/E2
      <u>end</u> <u>else</u> <u>if</u> L = N ∧ E5 = 0 <u>then</u>
      <u>begin</u> TAU2 ≔ 1; B2 ≔ A21 ≔ 0
      <u>end</u> <u>else</u> <u>if</u> L = N ∧ E5 ≠ 0 <u>then</u>
      <u>begin</u> TAU2 ≔ TAU2 + E4/E5
      <u>end</u> B.C.1;

   <u>procedure</u> FORWARD BABUSKA;
      <u>if</u> L = 1 <u>then</u>
      <u>begin</u> CHI[0] ≔ CH ≔ TL ≔ TAU1; T[0] ≔ TL;
         GI[0] ≔ G ≔ YL ≔ B1; Y[0] ≔ YL;
         SUB[0] ≔ A21; SUPER[0] ≔ A12;
         PP ≔ A21/(CH - A12); CH ≔ TAU2 - CH × PP;
         G ≔ B2 - G × PP; TL ≔ TAU2; YL ≔ B2
      <u>end</u> <u>else</u>
      <u>begin</u> CHI[L1] ≔ CH ≔ CH + TAU1;
         GI[L1] ≔ G ≔ G + B1;
         SUB[L1] ≔ A21; SUPER[L1] ≔ A12;
         PP ≔ A21/(CH - A12); CH ≔ TAU2 - CH × PP;
         G ≔ B2 - G × PP; T[L1] ≔ TL + TAU1; TL ≔ TAU2;
         Y[L1] ≔ YL +  B1; YL ≔ B2
      <u>end</u> FORWARD BABUSKA;

   <u>procedure</u> BACKWARD BABUSKA;
   <u>begin</u> PP ≔ YL; Y[N] ≔ G/CH;
      G ≔ PP; CH ≔ TL; L ≔ N;
      <u>for</u> L ≔ L - 1 <u>while</u> L &GreaterSlantEqual; 0 <u>do</u>
         <u>begin</u> PP ≔ SUPER[L]/(CH - SUB[L]);
            TL ≔ T[L]; CH ≔ TL - CH × PP;
            YL ≔ Y[L]; G ≔ YL - G × PP;
            Y[L] ≔ (GI[L] + G - YL)/(CHI[L] + CH - TL)  ;
         <u>end</u>
   <u>end</u> BACKWARD BABUSKA;

   DUPVEC(0, N, 0, Z, Y);
   E1 ≔ E[1]; E2 ≔ E[2]; E3 ≔ E[3]; E4 ≔ E[4]; E5 ≔ E[5]; E6 ≔ E[6];
   <u>for</u> IT ≔ 1, IT + 1 <u>while</u> EPS &gt; RHO <u>do</u>
      <u>begin</u> L ≔ 0; XL ≔ X[0]; ZL ≔ Z[0];
         <u>for</u> L ≔ L + 1 <u>while</u> L &LessSlantEqual; N <u>do</u>
            <u>begin</u> XL1 ≔ XL; L1 ≔ L - 1; XL ≔ X[L]; H ≔ XL - XL1;
               ZL1 ≔ ZL; ZL ≔ Z[L];
               ELEMENT MAT VEC EVALUATION 1;
               <u>if</u> L = 1 ∨ L = N <u>then</u> BOUNDARY CONDITIONS;
               FORWARD BABUSKA
            <u>end</u>;
         BACKWARD BABUSKA;
         EPS ≔ 0; RHO ≔ 1;
         <u>for</u> L ≔ 0 <u>step</u> 1 <u>until</u> N <u>do</u>
            <u>begin</u> RHO ≔ RHO + ABS(Z[L]);
               EPS ≔ EPS + ABS(Y[L]); Z[L] ≔ Z[L] - Y[L]
            <u>end</u>;
         RHO ≔ &#9192;-14 × RHO
      <u>end</u>;
   DUPVEC(0, N, 0, Y, Z)
<u>end</u> NONLIN FEM LAG SKEW;
</pre></body></html>
